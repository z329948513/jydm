.版本 2

.程序集 类_内存, , 公开
.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 置属性, 整数型, 公开
    .参数 进程句柄, 整数型, , 目标进程句柄,用 进程_打开() 命令取得
    .参数 内存地址, 整数型
    .参数 内存属性, 整数型, , 2 = 只读; 4 = 可读写
    .局部变量 虚拟保护, 精易_内存属性
    .局部变量 原保护, 整数型
    .局部变量 返回, 整数型

    .如果真 (VirtualQueryEx (进程句柄, 内存地址, 虚拟保护, 28) ≠ 0)
        返回 ＝ VirtualProtectEx (进程句柄, 虚拟保护.区域地址, 虚拟保护.区域长度, 内存属性, 虚拟保护.当前属性)
    .如果真结束
    返回 (返回)

.子程序 取属性, 逻辑型, 公开, 取内存属性信息;成功返回真,失败返回假;
    .参数 进程句柄, 整数型, , 目标进程句柄,用 进程_打开() 命令取得
    .参数 内存地址, 整数型, , 要取内存属性的内存地址;
    .参数 内存属性, 精易_内存属性, 参考, 内存属性.当前属性 0=未分配,不可访问=1,2=可读,读写=4,可写与拷贝=8,可运行=16,可运行与可读=32,可运行可读写=64,可运行读写拷贝=128,guard=256,非物理内存=512

    返回 (VirtualQueryEx (进程句柄, 内存地址, 内存属性, 28) ≠ 0)

.子程序 搜索, 整数型, 公开, 某些情况，需提升权限(返回结果数目,失败返回0)
    .参数 进程ID, 整数型, , 进程ID
    .参数 搜索内容, 字节集, , 欲搜索的内容  其他类型-需自行转换为字节集类型
    .参数 结果数组, 整数型, 参考 数组, 用来保存搜索的结果
    .局部变量 长度, 整数型
    .局部变量 操作句柄, 整数型
    .局部变量 内存地址, 整数型
    .局部变量 内存块信息, 精易_内存属性
    .局部变量 数据缓冲区, 字节集
    .局部变量 读取结果, 逻辑型
    .局部变量 找到地址, 整数型

    清除数组 (结果数组)
    长度 ＝ 取字节集长度 (搜索内容)
    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    .判断循环首 (VirtualQueryEx (操作句柄, 内存地址, 内存块信息, 28) ≠ 0) ' 取得内存块信息
        .如果真 (内存块信息.当前属性 ≠ 16 且 内存块信息.当前属性 ≠ 1 且 内存块信息.当前属性 ≠ 512) ' 判断保护状态
            数据缓冲区 ＝ 取空白字节集 (内存块信息.区域长度)
            读取结果 ＝ ReadProcessMemory_字节集 (操作句柄, 内存地址, 数据缓冲区, 内存块信息.区域长度, 0)
            .判断循环首 (读取结果 ≠ 假)
                找到地址 ＝ 寻找字节集 (数据缓冲区, 搜索内容, 找到地址) ' 查找是否有要搜索的数据
                .如果 (找到地址 ＝ -1) ' 跳出 进行下次循环
                    跳出循环 ()
                    
                .否则
                    加入成员 (结果数组, 到数值 (内存地址 ＋ 找到地址 － 1))
                .如果结束
                找到地址 ＝ 找到地址 ＋ 长度
            .判断循环尾 ()
        .如果真结束
        内存地址 ＝ 内存地址 ＋ 内存块信息.区域长度 ' 搜索下一内存块
        处理事件 ()
    .判断循环尾 ()
    CloseHandle (操作句柄)
    返回 (取数组成员数 (结果数组))

.子程序 读小数, 小数型, 公开, 失败返回0。
    .参数 进程ID, 整数型
    .参数 内存地址, 整数型
    .局部变量 临时整数, 小数型
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_小数型 (操作句柄, 内存地址, 临时整数, 4, 0)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 临时整数, 0))

.子程序 写小数, 逻辑型, 公开
    .参数 进程ID, 整数型, , 进程ID
    .参数 内存地址, 整数型, , 内存地址
    .参数 要写入的值, 小数型, , 写入数据
    .局部变量 局_进程句柄, 整数型
    .局部变量 bool, 逻辑型
    .局部变量 局_长度, 整数型

    局_进程句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID)
    bool ＝ WriteProcessMemory_小数型 (局_进程句柄, 内存地址, 要写入的值, 4, 0)
    CloseHandle (局_进程句柄)
    返回 (bool)

.子程序 读文本, 文本型, 公开, 读取文本型数据(返回文本 失败返回空文本)
    .参数 进程ID, 整数型
    .参数 内存地址, 整数型
    .参数 长度, 整数型, 可空, 默认为20 欲读取的长度字节
    .局部变量 临时字节集, 字节集
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    .如果真 (长度 ＝ 0)
        长度 ＝ 20
    .如果真结束
    ' --读文本---------------------------------
    临时字节集 ＝ 取空白字节集 (长度)
    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_字节集 (操作句柄, 内存地址, 临时字节集, 长度, 长度)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 到文本 (临时字节集), “”))

.子程序 读文本1, 文本型, 公开, 读取文本型数据(返回文本 失败返回空文本)
    .参数 进程句柄, 整数型, , 进程句柄
    .参数 内存地址, 整数型
    .参数 长度, 整数型, 可空, 默认为20 欲读取的长度字节
    .局部变量 临时文本, 文本型
    .局部变量 bool, 逻辑型

    .如果真 (长度 ＝ 0)
        长度 ＝ 20
    .如果真结束
    ' --读文本---------------------------------
    临时文本 ＝ 取空白文本 (长度)
    bool ＝ ReadProcessMemory_文本型 (进程句柄, 内存地址, 临时文本, 长度, 长度)
    返回 (选择 (bool, 临时文本, “”))

.子程序 写文本, 逻辑型, 公开, 写入文本数据(成功返回真,失败返回假)
    .参数 进程ID, 整数型, , 进程ID
    .参数 内存地址, 整数型, , 内存地址
    .参数 要写入的数据, 文本型, , 写入数据
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID)
    bool ＝ WriteProcessMemory_文本型 (操作句柄, 内存地址, 要写入的数据, 取文本长度 (要写入的数据), 0)
    CloseHandle (操作句柄)
    返回 (bool)

.子程序 读整数, 整数型, 公开, 读取整数型数据(返回整数内容 失败返回0) 
    .参数 进程ID, 整数型
    .参数 内存地址, 整数型
    .局部变量 临时整数, 整数型
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_整数型 (操作句柄, 内存地址, 临时整数, 4, 0)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 临时整数, 0))

.子程序 读整数1, 整数型, 公开, 读取整数型数据(返回整数内容 失败返回0) 
    .参数 进程句柄, 整数型, , 进程句柄
    .参数 内存地址, 整数型
    .局部变量 临时整数, 整数型
    .局部变量 bool, 逻辑型

    bool ＝ ReadProcessMemory_整数型 (进程句柄, 内存地址, 临时整数, 4, 0)
    返回 (选择 (bool, 临时整数, 0))

.子程序 读短整数, 短整数型, 公开, 读取短整数型数据(返回整数内容 失败返回0)
    .参数 进程ID, 整数型
    .参数 内存地址, 整数型
    .局部变量 临时整数, 短整数型
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_短整数型 (操作句柄, 内存地址, 临时整数, 2, 0)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 临时整数, 0))

.子程序 读短整数1, 短整数型, 公开, 读取短整数型数据(返回整数内容 失败返回0)
    .参数 进程句柄, 整数型, , 进程句柄
    .参数 内存地址, 整数型
    .局部变量 临时整数, 整数型
    .局部变量 bool, 逻辑型

    bool ＝ ReadProcessMemory_短整数型 (进程句柄, 内存地址, 临时整数, 2, 0)
    返回 (选择 (bool, 临时整数, 0))

.子程序 读长整数, 长整数型, 公开, 失败返回0
    .参数 进程ID, 整数型
    .参数 内存地址, 整数型
    .局部变量 临时整数, 长整数型
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_长整数型 (操作句柄, 内存地址, 临时整数, 8, 0)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 临时整数, 0))

.子程序 读长整数1, 长整数型, 公开, 失败返回0
    .参数 进程句柄, 整数型, , 进程句柄
    .参数 内存地址, 整数型
    .局部变量 临时整数, 长整数型

    .如果真 (ReadProcessMemory_长整数型 (进程句柄, 内存地址, 临时整数, 8, 0))
        返回 (临时整数)
    .如果真结束
    返回 (0)

.子程序 写整数, 逻辑型, 公开, 写入四字节数据(成功返回真,失败返回假) 
    .参数 进程ID, 整数型, , 进程ID
    .参数 内存地址, 整数型, , 内存地址
    .参数 要写入的值, 整数型, , 写入数据
    .参数 写入类型, 整数型, 可空, 默认为1  0=短整数型 1=整数型 2=长整数型
    .局部变量 局_进程句柄, 整数型
    .局部变量 bool, 逻辑型
    .局部变量 局_长度, 整数型

    .如果 (是否为空 (写入类型))
        局_长度 ＝ 4
    .否则
        .判断开始 (写入类型 ＝ 0)
            局_长度 ＝ 2
        .判断 (写入类型 ＝ 1)
            局_长度 ＝ 4
        .判断 (写入类型 ＝ 2)
            局_长度 ＝ 8
        .默认
            
        .判断结束
        
    .如果结束
    局_进程句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID)
    bool ＝ WriteProcessMemory_整数3 (局_进程句柄, 内存地址, 要写入的值, 局_长度, 0)
    CloseHandle (局_进程句柄)
    返回 (bool)

.子程序 读字节, 字节型, 公开, 读取字节型数据(返回整数内容 失败返回0) 
    .参数 进程ID, 整数型
    .参数 内存地址, 整数型
    .局部变量 临时整数, 字节型
    .局部变量 长度, 整数型
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    ' ---读整数----------------------------------
    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_字节型 (操作句柄, 内存地址, 临时整数, 1, 0)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 临时整数, 0))

.子程序 写字节, 逻辑型, 公开, 写入1字节数据(成功返回真,失败返回假) 
    .参数 进程ID, 整数型, , 进程ID
    .参数 内存地址, 整数型, , 内存地址
    .参数 要写入的数据, 字节型, , 写入数据
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID)
    bool ＝ WriteProcessMemory_写字节 (操作句柄, 内存地址, 要写入的数据, 1, 0)
    CloseHandle (操作句柄)
    返回 (bool)

.子程序 读字节集, 字节集, 公开, 读取字节集数据(返回字节集 失败返回空) 
    .参数 进程ID, 整数型, , 进程ID
    .参数 内存地址, 整数型, , 内存地址
    .参数 长度, 整数型, 可空, 可空。留空将智能读取长度
    .局部变量 变量, 字节集, , , 保存读取数据的变量
    .局部变量 操作句柄, 整数型
    .局部变量 内存块信息, 精易_内存属性
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    .如果真 (长度 ＝ 0)
        VirtualQueryEx (操作句柄, 内存地址, 内存块信息, 28)
        长度 ＝ 内存块信息.区域长度 ＋ 内存块信息.区域地址 － 内存地址
    .如果真结束
    变量 ＝ 取空白字节集 (长度)
    bool ＝ ReadProcessMemory_字节集 (操作句柄, 内存地址, 变量, 长度, 0)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 变量, { }))

.子程序 写字节集, 逻辑型, 公开, 写入字节集数据(成功返回真,失败返回假)
    .参数 进程ID, 整数型, , 进程ID
    .参数 内存地址, 整数型, , 内存地址
    .参数 要写入的数据, 字节集, , 写入数据
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型

    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID)
    bool ＝ WriteProcessMemory_字节集 (操作句柄, 内存地址, 要写入的数据, 取字节集长度 (要写入的数据), 0)
    CloseHandle (操作句柄)
    返回 (bool)

.子程序 读日期时间, 日期时间型, 公开, 从内存中读取日期时间型数据,失败将返回100年1月1日
    .参数 进程ID, 整数型, , 进程ID
    .参数 地址, 整数型, , 内存地址
    .局部变量 局_变量, 字节集, , , 保存读取数据的变量
    .局部变量 局_进程句柄, 整数型
    .局部变量 bool, 逻辑型
    .局部变量 局_长度, 整数型

    局_长度 ＝ 8
    局_变量 ＝ 取空白字节集 (局_长度)
    局_进程句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_字节集 (局_进程句柄, 地址, 局_变量, 局_长度, 0)
    CloseHandle (局_进程句柄)
    .如果真 (bool ＝ 假)
        返回 (到时间 (0))
    .如果真结束
    返回 (取字节集数据 (局_变量, #日期时间型, ))

.子程序 读子程序指针, 子程序指针, 公开, 从内存中读取子程序指针,失败返回空指针
    .参数 进程ID, 整数型, , 进程ID
    .参数 地址, 整数型, , 内存地址
    .局部变量 空指针, 子程序指针
    .局部变量 变量, 字节集, , , 保存读取数据的变量
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型
    .局部变量 长度, 整数型

    长度 ＝ 4
    变量 ＝ 取空白字节集 (长度)
    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_字节集 (操作句柄, 地址, 变量, 长度, 0)
    CloseHandle (操作句柄)
    .如果真 (bool ＝ 假)
        返回 (空指针)
    .如果真结束
    返回 (取字节集数据 (变量, #子程序指针型, ))

.子程序 读双精度小数, 双精度小数型, 公开, 从内存中读取双精度小数型数据,失败返回0
    .参数 进程ID, 整数型, , 进程ID
    .参数 地址, 整数型, , 内存地址
    .局部变量 变量, 字节集, , , 保存读取数据的变量
    .局部变量 操作句柄, 整数型
    .局部变量 bool, 逻辑型
    .局部变量 长度, 整数型

    长度 ＝ 8
    变量 ＝ 取空白字节集 (长度)
    操作句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, 进程ID) ' 取得操作句柄
    bool ＝ ReadProcessMemory_字节集 (操作句柄, 地址, 变量, 长度, 0)
    CloseHandle (操作句柄)
    返回 (选择 (bool, 取字节集数据 (变量, #双精度小数型, ), 0))

.子程序 写代码, 逻辑型, 公开, 写内存整数型,写内存字节集。[坛友：“啄啄”提供]
    .参数 进程ID, 整数型, , 要操作的进程ID
    .参数 地址表达式, 文本型, , 比如“0A1574D+1D45+547=999”
    .局部变量 局部_写入的值, 文本型
    .局部变量 局部_一级分割, 文本型, , "0"
    .局部变量 局部_二级分割, 文本型, , "0"
    .局部变量 局部_计次, 整数型
    .局部变量 局部_读取的值, 整数型

    局部_一级分割 ＝ 分割文本 (地址表达式, “=”, )
    局部_写入的值 ＝ 局部_一级分割 [2]
    局部_二级分割 ＝ 分割文本 (局部_一级分割 [1], “+”, )
    .判断开始 (取文本左边 (局部_写入的值, 1) ＝ “{”)
        .计次循环首 (取数组成员数 (局部_二级分割), 局部_计次)
            .判断开始 (局部_计次 ＝ 取数组成员数 (局部_二级分割))
                局部_读取的值 ＝ 进制_十六到十 (局部_二级分割 [局部_计次]) ＋ 局部_读取的值
            .默认
                局部_读取的值 ＝ 到整数 (读字节集 (进程ID, 进制_十六到十 (局部_二级分割 [局部_计次]) ＋ 局部_读取的值, ))
            .判断结束
            
        .计次循环尾 ()
        返回 (写字节集 (进程ID, 局部_读取的值, 字节集_十六进制到字节集 (局部_写入的值)))
    .默认
        .计次循环首 (取数组成员数 (局部_二级分割), 局部_计次)
            .判断开始 (局部_计次 ＝ 取数组成员数 (局部_二级分割))
                局部_读取的值 ＝ 进制_十六到十 (局部_二级分割 [局部_计次]) ＋ 局部_读取的值
            .默认
                局部_读取的值 ＝ 读整数 (进程ID, 进制_十六到十 (局部_二级分割 [局部_计次]) ＋ 局部_读取的值)
            .判断结束
            
        .计次循环尾 ()
        返回 (写整数 (进程ID, 局部_读取的值, 到整数 (局部_写入的值)))
    .判断结束
    

.子程序 读代码_整数, 整数型, 公开, 读内存整数型。[坛友：“啄啄”提供]
    .参数 进程ID, 整数型, , 要操作的进程ID
    .参数 地址表达式, 文本型, , 比如“0A1574D+1D45+547”
    .局部变量 局部_计次, 整数型
    .局部变量 局部_读取的值, 整数型
    .局部变量 局部_分割, 文本型, , "0"

    局部_分割 ＝ 分割文本 (地址表达式, “+”, )
    .计次循环首 (取数组成员数 (局部_分割), 局部_计次)
        .判断开始 (局部_计次 ＝ 取数组成员数 (局部_分割))
            局部_读取的值 ＝ 进制_十六到十 (局部_分割 [局部_计次]) ＋ 局部_读取的值
        .默认
            局部_读取的值 ＝ 读整数 (进程ID, 进制_十六到十 (局部_分割 [局部_计次]) ＋ 局部_读取的值)
        .判断结束
        
    .计次循环尾 ()
    返回 (读整数 (进程ID, 局部_读取的值))

.子程序 读代码_字节集, 字节集, 公开, 读内存字节集。[坛友：“啄啄”提供]
    .参数 进程ID, 整数型, , 要操作的进程ID
    .参数 地址表达式, 文本型, , 比如“0A1574D+1D45+547”
    .局部变量 局部_计次, 整数型
    .局部变量 局部_读取的值, 整数型
    .局部变量 局部_分割, 文本型, , "0"

    局部_分割 ＝ 分割文本 (地址表达式, “+”, )
    .计次循环首 (取数组成员数 (局部_分割), 局部_计次)
        .判断开始 (局部_计次 ＝ 取数组成员数 (局部_分割))
            局部_读取的值 ＝ 进制_十六到十 (局部_分割 [局部_计次]) ＋ 局部_读取的值
        .默认
            局部_读取的值 ＝ 读整数 (进程ID, 进制_十六到十 (局部_分割 [局部_计次]) ＋ 局部_读取的值)
        .判断结束
        
    .计次循环尾 ()
    返回 (读字节集 (进程ID, 局部_读取的值, ))

.子程序 读字节集_64, 字节集, 公开, 成功返回字节集数据，如果失败返回空字节集。不同类型的内存值可用 取字节集数据(字节集，#整数型) 取得对应的内存值。
    .参数 PID, 整数型, , 进程PID
    .参数 表达式地址, 文本型, , 如：141FF33E8+170+7EC
    .参数 读入长度, 长整数型, , 4，一般内存地址都是4个字节为一个地址{xxx,xxx,xxx,xxx}
    .局部变量 分割的文本, 文本型, , "0"
    .局部变量 成员数, 整数型
    .局部变量 n, 整数型
    .局部变量 进程句柄, 整数型
    .局部变量 返回字节集, 字节集
    .局部变量 指针, 长整数型
    .局部变量 地址, 长整数型

    ' 该命令收录自：https://bbs.125.la/thread-14405734-1-1.html
    分割的文本 ＝ 分割文本 (表达式地址, “+”, )
    成员数 ＝ 取数组成员数 (分割的文本)
    .计次循环首 (成员数, n)
        .判断开始 (n ＝ 成员数)
            进程句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, PID)
            返回字节集 ＝ 取空白字节集 (读入长度)
            地址 ＝ 进制_十六到十 (分割的文本 [n])
            指针 ＝ 指针 ＋ 地址
            ZwWow64ReadVirtualMemory64 (进程句柄, 指针, 返回字节集, 读入长度, 0)
            CloseHandle (进程句柄)
            返回 (返回字节集)
        .默认
            进程句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, PID) ' 打开进程，返回进程句柄，每次打开都回返回不同句柄
            返回字节集 ＝ 取空白字节集 (读入长度) ' 得先初始化，不然读内存传参不过来
            地址 ＝ 进制_十六到十 (分割的文本 [n])
            指针 ＝ 指针 ＋ 地址
            ZwWow64ReadVirtualMemory64 (进程句柄, 指针, 返回字节集, 读入长度, 0)
            CloseHandle (进程句柄) ' 读完一次内存，及时关闭进程句柄，防止对象没关闭照成程序崩溃
            指针 ＝ 取字节集数据 (返回字节集, #整数型, ) ' 有偏移的内存地址，中间过程都是返回地址的，所以要取出字节集数据，得到偏移后的指针
            .如果真 (指针 ＝ 0) ' 通常指针为0就是取不出来了
                ' 调试输出 (分割的文本 [n], 指针, n, “此处失败”)
                返回 ({ })
            .如果真结束
            
        .判断结束
        
    .计次循环尾 ()
    返回 ({ })

.子程序 写字节集_64, 整数型, 公开, 写内存字节集，失败返回-1，成功非-1
    .参数 PID, 整数型, , 进程PID
    .参数 表达式地址, 文本型, , 如：141FF33E8+170+7EC
    .参数 写入长度, 长整数型, , 4，一般内存地址都是4个字节为一个地址{xxx,xxx,xxx,xxx}
    .参数 写入数据, 字节集, , 用法：如果写入整数型数据→到字节集(123) 文本型数据→到字节集（“123”）。
    .局部变量 分割的文本, 文本型, , "0"
    .局部变量 成员数, 整数型
    .局部变量 n, 整数型
    .局部变量 进程句柄, 整数型
    .局部变量 返回字节集, 字节集
    .局部变量 指针, 长整数型
    .局部变量 地址, 长整数型
    .局部变量 结果, 整数型

    分割的文本 ＝ 分割文本 (表达式地址, “+”, )
    成员数 ＝ 取数组成员数 (分割的文本)
    .计次循环首 (成员数, n)
        .判断开始 (n ＝ 成员数)
            进程句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, PID)
            返回字节集 ＝ 取空白字节集 (写入长度)
            地址 ＝ 进制_十六到十 (分割的文本 [n])
            指针 ＝ 指针 ＋ 地址
            结果 ＝ ZwWow64WriteVirtualMemory64 (进程句柄, 指针, 写入数据, 写入长度, 0)
            CloseHandle (进程句柄)
            返回 (结果)
        .默认
            进程句柄 ＝ OpenProcess (#PROCESS_ALL_ACCESS, 0, PID) ' 打开进程，返回进程句柄，每次打开都回返回不同句柄
            返回字节集 ＝ 取空白字节集 (写入长度) ' 得先初始化，不然读内存传参不过来
            地址 ＝ 进制_十六到十 (分割的文本 [n])
            指针 ＝ 指针 ＋ 地址
            ZwWow64ReadVirtualMemory64 (进程句柄, 指针, 返回字节集, 写入长度, 0)
            CloseHandle (进程句柄) ' 读完一次内存，及时关闭进程句柄，防止对象没关闭照成程序崩溃
            指针 ＝ 取字节集数据 (返回字节集, #整数型, ) ' 有偏移的内存地址，中间过程都是返回地址的，所以要取出字节集数据，得到偏移后的指针
            .如果真 (指针 ＝ 0) ' 通常指针为0就是取不出来了
                ' 调试输出 (分割的文本 [n], 指针, n, “此处失败”)
                返回 (-1)
            .如果真结束
            
        .判断结束
        
    .计次循环尾 ()
    返回 (-1)

