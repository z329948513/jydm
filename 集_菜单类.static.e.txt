.版本 2

.程序集 集_菜单类
.子程序 菜单_点击, , 公开, 后台点击指定窗口上的指定菜单；
    .参数 窗口句柄, 整数型, , 要点击菜单的窗口句柄；
    .参数 菜单句柄, 整数型, , 要点击的菜单句柄；
    .参数 菜单索引, 整数型, , 菜单索引从0开始；

    投递消息整数 (窗口句柄, 273, 菜单_取ID (菜单句柄, 菜单索引), 0)

.子程序 菜单_取ID, 整数型, 公开
    .参数 菜单句柄, 整数型
    .参数 菜单索引, 整数型

    返回 (GetMenuItemID (菜单句柄, 菜单索引))

.子程序 菜单_清除, 逻辑型, 公开, 成功返回真，失败返回假；
    .参数 菜单句柄, 整数型

    返回 (DestroyMenu (菜单句柄))

.子程序 菜单_重画, 逻辑型, 公开, 为指定的窗口重画菜单,成功返回真，失败返回假；
    .参数 窗口句柄, 整数型, , 窗口句柄

    返回 (DrawMenuBar (窗口句柄))

.子程序 菜单_删除, 逻辑型, 公开, 成功返回真，失败返回假；
    .参数 菜单句柄, 整数型, , 同级的菜单句柄；
    .参数 菜单索引, 整数型, , 索引从0开始；

    返回 (RemoveMenu (菜单句柄, 菜单索引, 1024))

.子程序 菜单_取数量, 整数型, 公开, 成功返回菜单数量，失败返回0；
    .参数 菜单句柄, 整数型, , 要取数量的菜单句柄；

    返回 (GetMenuItemCount (菜单句柄))

.子程序 菜单_取句柄, 整数型, 公开, 成功返回菜单句柄，失败返回0；
    .参数 窗口句柄, 整数型, , 要取菜单的窗口句柄；

    返回 (GetMenu (窗口句柄))

.子程序 菜单_取标题, 文本型, 公开, 成功返回菜单标题；失败返回空文本；
    .参数 菜单句柄, 整数型, , 菜单句柄
    .参数 菜单索引, 整数型, , 索引从0开始；
    .局部变量 文本, 文本型
    .局部变量 返回, 整数型
    .局部变量 菜单ID, 整数型

    文本 ＝ 取空白文本 (255)
    菜单ID ＝ GetMenuItemID (菜单句柄, 菜单索引)
    GetMenuStringA (菜单句柄, 菜单索引, 文本, 255, 0)
    .如果真 (文本 ＝ “”)
        GetMenuStringA (菜单句柄, 菜单索引, 文本, 255, 3)
    .如果真结束
    .如果真 (文本 ＝ “”)
        GetMenuStringA (菜单句柄, 菜单索引, 文本, 255, 8)
    .如果真结束
    .如果真 (文本 ＝ “”)
        GetMenuStringA (菜单句柄, 菜单索引, 文本, 255, 1024)
    .如果真结束
    .如果真 (文本 ＝ “”)
        GetMenuStringA (菜单句柄, 菜单索引, 文本, 255, 2048)
    .如果真结束
    .如果真 (文本 ＝ “” 且 菜单ID ≠ -1)
        文本 ＝ “分隔符”
    .如果真结束
    返回 (文本)

.子程序 菜单_枚举, 整数型, 公开, 成功返回菜单的数量和标题数组；失败返回0；
    .参数 窗口句柄, 整数型, , 要枚举菜单标题的窗口句柄；
    .参数 菜单标题数组, 文本型, 参考 可空 数组, 用于存放菜单标题的文本数组；
    .参数 菜单ID数组, 整数型, 参考 可空 数组, 用于存放菜单ID的整数数组；
    .局部变量 一级菜单, 整数型
    .局部变量 二级菜单, 整数型
    .局部变量 三级菜单, 整数型
    .局部变量 局_一级计次, 整数型
    .局部变量 局_二级计次, 整数型
    .局部变量 局_三级计次, 整数型
    .局部变量 局_一级数量, 整数型
    .局部变量 局_二级数量, 整数型
    .局部变量 局_三级数量, 整数型
    .局部变量 局_菜单索引, 整数型

    一级菜单 ＝ 菜单_取句柄 (窗口句柄)
    局_菜单索引 ＝ 19999
    .如果真 (一级菜单 ＞ 0)
        局_一级数量 ＝ 菜单_取数量 (一级菜单)
        .计次循环首 (局_一级数量, 局_一级计次)
            加入成员 (菜单标题数组, 菜单_取标题 (一级菜单, 局_一级计次 － 1))
            局_菜单索引 ＝ 局_菜单索引 ＋ 1
            加入成员 (菜单ID数组, 局_菜单索引)
            二级菜单 ＝ 菜单_取子句柄 (一级菜单, 局_一级计次 － 1)
            局_二级数量 ＝ 菜单_取数量 (二级菜单)
            .如果真 (局_二级数量 ＞ 0)
                .计次循环首 (局_二级数量, 局_二级计次)
                    加入成员 (菜单标题数组, 菜单_取标题 (二级菜单, 局_二级计次 － 1))
                    局_菜单索引 ＝ 局_菜单索引 ＋ 1
                    加入成员 (菜单ID数组, 局_菜单索引)
                    三级菜单 ＝ 菜单_取子句柄 (二级菜单, 局_二级计次 － 1)
                    局_三级数量 ＝ 菜单_取数量 (三级菜单)
                    .如果真 (局_三级数量 ＞ 0)
                        .计次循环首 (局_三级数量, 局_三级计次)
                            加入成员 (菜单标题数组, 菜单_取标题 (三级菜单, 局_三级计次 － 1))
                            局_菜单索引 ＝ 局_菜单索引 ＋ 1
                            加入成员 (菜单ID数组, 局_菜单索引)
                            处理事件 ()
                        .计次循环尾 ()
                    .如果真结束
                    处理事件 ()
                .计次循环尾 ()
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
    .如果真结束
    返回 (取数组成员数 (菜单ID数组))

.子程序 菜单_枚举子级菜单, 整数型, 公开, 枚举子级菜单标题，成功返回子菜单数量，失败返回0；
    .参数 窗口句柄, 整数型
    .参数 父菜单标题, 文本型
    .参数 子菜单标题数组, 文本型, 可空 数组, 用于装载子菜单标题的数组变量；
    .局部变量 局_菜单句柄, 整数型
    .局部变量 局_子菜单句柄, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_菜单数量, 整数型

    局_菜单句柄 ＝ 菜单_取句柄 (窗口句柄)
    局_菜单数量 ＝ 菜单_取数量 (局_菜单句柄)
    .计次循环首 (局_菜单数量, 局_计次)
        .如果真 (菜单_取标题 (局_菜单句柄, 局_计次 － 1) ＝ 父菜单标题)
            局_子菜单句柄 ＝ 菜单_取子句柄 (局_菜单句柄, 局_计次 － 1)
            跳出循环 ()
        .如果真结束
        处理事件 ()
    .计次循环尾 ()
    局_菜单数量 ＝ 菜单_取数量 (局_子菜单句柄)
    .计次循环首 (局_菜单数量, 局_计次)
        加入成员 (子菜单标题数组, 菜单_取标题 (局_子菜单句柄, 局_计次 － 1))
        处理事件 ()
    .计次循环尾 ()
    返回 (取数组成员数 (子菜单标题数组))

.子程序 菜单_修改标题, 逻辑型, 公开, 成功返回真，失败返回假；只能改子菜单标题；
    .参数 菜单句柄, 整数型, , 要修改标题的菜单句柄；
    .参数 菜单新标题, 文本型, , 菜单新标题
    .参数 菜单索引, 整数型, , 索引从0开始；
    .局部变量 局_菜单ID, 整数型

    局_菜单ID ＝ GetMenuItemID (菜单句柄, 菜单索引)
    .判断开始 (ModifyMenuA (菜单句柄, 局_菜单ID, 0, 0, 菜单新标题))
        返回 (真)
    .判断 (ModifyMenuA (菜单句柄, 局_菜单ID, 0, 3, 菜单新标题))
        返回 (真)
    .判断 (ModifyMenuA (菜单句柄, 局_菜单ID, 0, 8, 菜单新标题))
        返回 (真)
    .判断 (ModifyMenuA (菜单句柄, 局_菜单ID, 0, 1024, 菜单新标题))
        返回 (真)
    .默认
        返回 (ModifyMenuA (菜单句柄, 局_菜单ID, 0, 2048, 菜单新标题))
    .判断结束
    返回 (假)
    

.子程序 菜单_取子句柄, 整数型, 公开, 成功返回子菜单句柄，失败返回0；
    .参数 父菜单句柄, 整数型, , 菜单句柄
    .参数 父菜单索引, 整数型, , 索引从0开始；

    返回 (GetSubMenu (父菜单句柄, 父菜单索引))

.子程序 菜单_添加, 逻辑型, 公开, 成功返回真，失败返回假；
    .参数 菜单句柄, 整数型, , 同级的菜单句柄；
    .参数 菜单标题, 文本型, 可空, 如果 菜单类型 为4时，此参数留空；
    .参数 菜单类型, 整数型, 可空, 0=普通；1=禁止；2=打勾；3=分支菜单；4=菜单分隔条；
    .参数 弹出式菜单句柄, 整数型, 参考 可空, 如果  菜单类型 为3时，引参数用来接收弹出式菜单句柄的；
    .参数 菜单数值, 整数型, 可空, 用于辩认菜单的数值，跟列表框的数值一样；

    .判断开始 (菜单类型 ＝ 0)
        返回 (AppendMenuA (菜单句柄, 0, 菜单数值, 菜单标题))
    .判断 (菜单类型 ＝ 1)
        返回 (AppendMenuA (菜单句柄, 3, 菜单数值, 菜单标题))
    .判断 (菜单类型 ＝ 2)
        返回 (AppendMenuA (菜单句柄, 8, 菜单数值, 菜单标题))
    .判断 (菜单类型 ＝ 3)
        弹出式菜单句柄 ＝ CreatePopupMenu ()
        返回 (AppendMenuA (菜单句柄, 16, 弹出式菜单句柄, 菜单标题))
    .默认
        返回 (AppendMenuA (菜单句柄, 2048, 菜单数值, “”))
    .判断结束
    返回 (假)

.子程序 菜单_点击按标题, , 公开, 如果有的菜单点击不了,请将参数4设置为真试试；
    .参数 窗口句柄, 整数型, , 要点击菜单的窗口；
    .参数 菜单标题, 文本型, , 要点击的菜单标题；
    .参数 模糊点击, 逻辑型, 可空, 可空，默认为假，精确点击；真=模糊点击；
    .参数 备用参数, 逻辑型, 可空, 如果有的菜单点击不了请将些参数设置为真试试；
    .局部变量 临时文本, 文本型
    .局部变量 计次, 整数型
    .局部变量 菜单数量, 整数型
    .局部变量 菜单标题数组, 文本型, , "0"
    .局部变量 菜单ID数组, 整数型, , "0"

    菜单数量 ＝ 菜单_枚举 (窗口句柄, 菜单标题数组, 菜单ID数组)
    .计次循环首 (菜单数量, 计次)
        临时文本 ＝ 菜单标题数组 [计次]
        .如果 (模糊点击)
            .如果真 (寻找文本 (临时文本, 菜单标题, , 假) ≠ -1)
                .如果 (备用参数)
                    投递消息整数 (窗口句柄, 273, 菜单ID数组 [计次] － 20000, 0)
                .否则
                    投递消息整数 (窗口句柄, 273, 菜单ID数组 [计次] － 20001, 0)
                .如果结束
                跳出循环 ()
            .如果真结束
            
        .否则
            .如果真 (临时文本 ＝ 菜单标题)
                .如果 (备用参数)
                    投递消息整数 (窗口句柄, 273, 菜单ID数组 [计次] － 20000, 0)
                .否则
                    投递消息整数 (窗口句柄, 273, 菜单ID数组 [计次] － 20001, 0)
                .如果结束
                跳出循环 ()
            .如果真结束
            
        .如果结束
        处理事件 ()
    .计次循环尾 ()

.子程序 菜单_添加图标, 逻辑型, 公开, 在指定定的菜单处加入图标
    .参数 子菜单索引, 整数型, , 子菜单索引
    .参数 菜单句柄, 整数型, , 欲添加图标的菜单句柄
    .参数 未选图标句柄, 整数型, , 未选中时的图标句柄,可以用 图标_取句柄() 命令取得 图标句柄
    .参数 已选图标句柄, 整数型, , 已选中时的图标句柄,可以用 图标_取句柄() 命令取得 图标句柄

    返回 (SetMenuItemBitmaps (菜单句柄, 子菜单索引, 1024, 未选图标句柄, 已选图标句柄))

