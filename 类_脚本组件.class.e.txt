.版本 2

.程序集 类_脚本组件, , 公开
.程序集变量 脚本对象, 对象

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    线程_初始化COM库 ()
    脚本对象.创建 (“ScriptControl”, )
    脚本对象.写属性 (“Language”, “JScript”)

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    脚本对象.清除 ()
    线程_取消COM库 ()

.子程序 执行, 逻辑型, 公开, 执行指定的代码文本。返回真为执行正常，返回假为出错，错误信息可以从“错误信息”属性中取得。
    .参数 脚本代码, 文本型
    .局部变量 脚本代码_变体型, 变体型

    ' 脚本代码_变体型.赋值 (编码_Ansi到Unicode (脚本代码), )
    ' 脚本对象.数值方法 (“ExecuteStatement”, 脚本代码_变体型)
    脚本对象.数值方法 (“ExecuteStatement”, 脚本代码)
    返回 (脚本对象.取错误 () ＝ “”)

.子程序 计算表达式, 文本型, 公开, 计算表达式并返回结果。
    .参数 表达式, 文本型

    返回 (脚本对象.文本方法 (“Eval”, 表达式))

.子程序 清除, , 公开
    脚本对象.数值方法 (“Reset”, )

.子程序 取错误, 文本型, 公开, 返回错误信息
    返回 (脚本对象.取错误 ())

.子程序 取脚本对象, 对象, 公开
    返回 (脚本对象)

.子程序 运行, 文本型, 公开, 在添加的代码中运行指定的过程，如有返回值返回相应文本，否则返回空文本。
    .参数 过程或函数名, 文本型
    .参数 参数1, 文本型, 可空
    .参数 参数2, 文本型, 可空
    .参数 参数3, 文本型, 可空
    .参数 参数4, 文本型, 可空
    .参数 参数5, 文本型, 可空
    .参数 参数6, 文本型, 可空

    .判断开始 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假 且 是否为空 (参数4) ＝ 假 且 是否为空 (参数5) ＝ 假 且 是否为空 (参数6) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3, 参数4, 参数5, 参数6))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假 且 是否为空 (参数4) ＝ 假 且 是否为空 (参数5) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3, 参数4, 参数5))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假 且 是否为空 (参数4) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3, 参数4))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2))
    .判断 (是否为空 (参数1) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1))
    .默认
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名))
    .判断结束
    

.子程序 运行2, 文本型, 公开, 在添加的代码中运行指定的过程，如有返回值返回相应文本，否则返回空文本。
    .参数 过程或函数名, 文本型
    .参数 参数1, 整数型, 可空
    .参数 参数2, 整数型, 可空
    .参数 参数3, 整数型, 可空
    .参数 参数4, 整数型, 可空
    .参数 参数5, 整数型, 可空
    .参数 参数6, 整数型, 可空

    .判断开始 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假 且 是否为空 (参数4) ＝ 假 且 是否为空 (参数5) ＝ 假 且 是否为空 (参数6) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3, 参数4, 参数5, 参数6))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假 且 是否为空 (参数4) ＝ 假 且 是否为空 (参数5) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3, 参数4, 参数5))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假 且 是否为空 (参数4) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3, 参数4))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假 且 是否为空 (参数3) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2, 参数3))
    .判断 (是否为空 (参数1) ＝ 假 且 是否为空 (参数2) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1, 参数2))
    .判断 (是否为空 (参数1) ＝ 假)
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名, 参数1))
    .默认
        返回 (脚本对象.文本方法 (“Run”, 过程或函数名))
    .判断结束
    

.子程序 属性_语言, 文本型, 公开, 设置或返回正在使用的脚本语言名称。
    .参数 新语言属性, 整数型, 可空, 默认为为 JScript   0. JScript   1. VBScript

    .判断开始 (新语言属性 ＝ 0)
        脚本对象.写属性 (“Language”, “JScript”)
    .判断 (新语言属性 ＝ 1)
        脚本对象.写属性 (“Language”, “vbs”)
    .默认
        
    .判断结束
    返回 (脚本对象.读文本属性 (“Language”, ))

.子程序 属性_超时, 整数型, 公开, 设置或返回时间（毫秒），此时间后用户可选择中止脚本代码的执行或允许代码继续执行。
    .参数 新超时属性值, 整数型, 可空, 默认为-1，执行无超时

    .如果真 (是否为空 (新超时属性值))
        新超时属性值 ＝ -1
    .如果真结束
    脚本对象.写属性 (“Timeout”, 新超时属性值)
    返回 (脚本对象.读数值属性 (“Timeout”, ))

.子程序 属性_状态, 整数型
    .参数 新状态值, 整数型, 可空, 参数值为以下值之一： 0、Initialized； 1、Connected

    .如果真 (是否为空 (新状态值) ＝ 假)
        脚本对象.写属性 (“State”, 新状态值)
    .如果真结束
    返回 (脚本对象.读数值属性 (“State”, ))

.子程序 属性_是否使用图形界面, 逻辑型
    .参数 新是否使用图形界面属性, 逻辑型, 可空

    .如果真 (是否为空 (新是否使用图形界面属性) ＝ 假)
        脚本对象.写属性 (“AllowUI”, 新是否使用图形界面属性)
    .如果真结束
    返回 (脚本对象.读逻辑属性 (“AllowUI”, ))

.子程序 属性_图形界面父窗口句柄, 整数型
    .参数 新父窗口句柄, 整数型, 可空

    .如果真 (是否为空 (新父窗口句柄) ＝ 假)
        脚本对象.写属性 (“SitehWnd”, 新父窗口句柄)
    .如果真结束
    返回 (脚本对象.读数值属性 (“SitehWnd”, ))

.子程序 添加代码
    .参数 代码文本, 文本型

    脚本对象.数值方法 (“AddCode”, 代码文本)

.子程序 未封装的属性及方法
    ' 类型: IScriptControl  10个属性，6个方法，标记: {0E59F1D3-1FBE-11D0-8FF2-00A0D10038BC}
    ' -------------------------------- 属性列表 --------------------------------
    ' 属性读取格式： 〈逻辑型〉 对象．读逻辑属性 （"UseSafeSubset"）
    ' 属性写入格式： 〈逻辑型〉 对象．写属性 （"UseSafeSubset"，欲写入属性值）
    ' 参数<1>的名称为“欲写入属性值”，类型为“逻辑型”。
    ' 解释：Force script to execute in safe mode and disallow potentially harmful actions
    
    
    ' 属性读取格式： 〈对象(IScriptModuleCollection)〉 对象．读对象型属性 （"Modules"）
    ' 解释：Collection of modules for the ScriptControl
    
    ' 属性读取格式： 〈对象〉 对象．读对象型属性 （"CodeObject"）
    ' 解释：Object exposed by the scripting engine that contains methods and properties defined in the code added to the global module
    
    
    ' 属性读取格式： 〈对象(IScriptProcedureCollection)〉 对象．读对象型属性 （"Procedures"）
    ' 解释：Collection of procedures that are defined in the global module
    
    
    ' -------------------------------- 方法列表 --------------------------------
    ' 方法调用格式： 〈整数型〉 对象．数值方法 （"AddObject"，Name，Object，AddMembers）
    ' 参数<1>的名称为“Name”，类型为“文本型”。
    ' 参数<2>的名称为“Object”，类型为“对象”。
    ' 参数<3>的名称为“AddMembers”，类型为“逻辑型”，参数默认值为“假”。
    ' 解释：Add an object to the global namespace of the scripting engine
    

