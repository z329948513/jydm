.版本 2

.程序集 集_时间
.子程序 时间_秒到时分秒格式, 文本型, 公开, 返回指定格式的时间文本
    .参数 参_秒, 整数型
    .参数 参_格式, 文本型, 可空, d天h小时m分s秒
    .局部变量 局_天, 整数型
    .局部变量 局_小时, 整数型
    .局部变量 局_分, 整数型
    .局部变量 局_秒, 整数型
    .局部变量 局_位置, 整数型

    局_秒 ＝ 参_秒
    .如果真 (是否为空 (参_格式) 或 参_格式 ＝ “”)
        参_格式 ＝ “d天h小时m分s秒”
    .如果真结束
    
    局_天 ＝ 局_秒 ÷ 86400
    局_小时 ＝ 局_秒 ％ 86400 ÷ 3600
    局_分 ＝ 局_秒 ％ 86400 ％ 3600 ÷ 60
    局_秒 ＝ 局_秒 ％ 86400 ％ 3600 ％ 60
    
    局_位置 ＝ 倒找文本 (参_格式, “d”, , 假)
    .如果真 (局_位置 ≠ -1)
        参_格式 ＝ 文本替换 (参_格式, 局_位置, 1, 到文本 (局_天))
    .如果真结束
    局_位置 ＝ 倒找文本 (参_格式, “h”, , 假)
    .如果真 (局_位置 ≠ -1)
        参_格式 ＝ 文本替换 (参_格式, 局_位置, 1, 到文本 (局_小时))
    .如果真结束
    局_位置 ＝ 倒找文本 (参_格式, “m”, , 假)
    .如果真 (局_位置 ≠ -1)
        参_格式 ＝ 文本替换 (参_格式, 局_位置, 1, 到文本 (局_分))
    .如果真结束
    局_位置 ＝ 倒找文本 (参_格式, “s”, , 假)
    .如果真 (局_位置 ≠ -1)
        参_格式 ＝ 文本替换 (参_格式, 局_位置, 1, 到文本 (局_秒))
    .如果真结束
    
    返回 (参_格式)

.子程序 时间_十八位时间戳转北京, 日期时间型, 公开, 将【18位的时间戳】转换成【北京标准时间】
    .参数 十八位时间戳, 文本型, , 要转换的18位时间时间戳
    .局部变量 十三位时间戳, 长整数型

    ' 例程来自：https://bbs.125.la/thread-14319889-1-1.html
    十三位时间戳 ＝ (到长整数 (十八位时间戳) － 到长整数 (“621355968000000000”)) ÷ 10000
    返回 (时间_格林威治转北京 (十三位时间戳))

.子程序 时间_北京转十八位时间戳, 文本型, 公开, 将【北京标准时间】转换成【18位的时间戳】
    .参数 时间, 日期时间型, , 要转换的时间
    .局部变量 十三位时间戳, 长整数型
    .局部变量 十八位时间戳, 长整数型

    ' 例程来自：https://bbs.125.la/thread-14319889-1-1.html
    十三位时间戳 ＝ 到长整数 (时间_到时间戳 (时间, 假, ))
    十八位时间戳 ＝ 十三位时间戳 × 10000 ＋ 到长整数 (“621355968000000000”)
    返回 (到文本 (十八位时间戳))

.子程序 时间_取时间间隔, 文本型, 公开, 返回一个文本，表示“时间1”减去“时间2”之后间隔的天，时，分，秒。返回示例：3天13时56分27秒
    .参数 时间1, 日期时间型
    .参数 时间2, 日期时间型
    .局部变量 剩余秒数A, 整数型
    .局部变量 剩余秒数B, 整数型
    .局部变量 剩余天, 整数型
    .局部变量 剩余小时, 整数型
    .局部变量 剩余分钟, 整数型
    .局部变量 剩余秒, 整数型
    .局部变量 返回时间, 文本型

    ' 源码出处：https://bbs.125.la/thread-14315424-1-1.html
    剩余秒数A ＝ 取时间间隔 (时间1, 时间2, 8)
    .如果真 (剩余秒数A ＜ 0)
        剩余秒数A ＝ 剩余秒数A × -1
    .如果真结束
    剩余秒数B ＝ 剩余秒数A ％ 86400
    剩余天 ＝ (剩余秒数A － 剩余秒数B) ÷ 86400
    剩余秒数A ＝ 剩余秒数B
    剩余秒数B ＝ 剩余秒数A ％ 3600
    剩余小时 ＝ (剩余秒数A － 剩余秒数B) ÷ 3600
    剩余秒数A ＝ 剩余秒数B
    剩余秒数B ＝ 剩余秒数A ％ 60
    剩余分钟 ＝ (剩余秒数A － 剩余秒数B) ÷ 60
    剩余秒 ＝ 剩余秒数B
    .如果真 (剩余天 ≠ 0)
        返回时间 ＝ 到文本 (剩余天) ＋ “天”
    .如果真结束
    .如果真 (剩余小时 ≠ 0)
        返回时间 ＝ 返回时间 ＋ 到文本 (剩余小时) ＋ “时”
    .如果真结束
    .如果真 (剩余分钟 ≠ 0)
        返回时间 ＝ 返回时间 ＋ 到文本 (剩余分钟) ＋ “分”
    .如果真结束
    .如果真 (剩余秒 ≠ 0)
        返回时间 ＝ 返回时间 ＋ 到文本 (剩余秒) ＋ “秒”
    .如果真结束
    返回 (返回时间)

.子程序 时间_转为GMT格式, 文本型, 公开, 将日期时间型数据转换为GMT格式 Greenwich Mean Time
    .参数 待转换时间, 日期时间型
    .局部变量 整数容器, 整数型
    .局部变量 GMT时间, 文本型

    整数容器 ＝ 取星期几 (待转换时间)
    .判断开始 (整数容器 ＝ 1)
        GMT时间 ＝ “Sun, ”
    .判断 (整数容器 ＝ 2)
        GMT时间 ＝ “Mon, ”
    .判断 (整数容器 ＝ 3)
        GMT时间 ＝ “Tue, ”
    .判断 (整数容器 ＝ 4)
        GMT时间 ＝ “Wed, ”
    .判断 (整数容器 ＝ 5)
        GMT时间 ＝ “Thu, ”
    .判断 (整数容器 ＝ 6)
        GMT时间 ＝ “Fri, ”
    .判断 (整数容器 ＝ 7)
        GMT时间 ＝ “Sat, ”
    .默认
        
    .判断结束
    GMT时间 ＝ GMT时间 ＋ 到文本 (取日 (待转换时间)) ＋ “-”
    整数容器 ＝ 取月份 (待转换时间)
    .判断开始 (整数容器 ＝ 1)
        GMT时间 ＝ GMT时间 ＋ “Jan” ＋ “-”
    .判断 (整数容器 ＝ 2)
        GMT时间 ＝ GMT时间 ＋ “Feb” ＋ “-”
    .判断 (整数容器 ＝ 3)
        GMT时间 ＝ GMT时间 ＋ “Mar” ＋ “-”
    .判断 (整数容器 ＝ 4)
        GMT时间 ＝ GMT时间 ＋ “Apr” ＋ “-”
    .判断 (整数容器 ＝ 5)
        GMT时间 ＝ GMT时间 ＋ “May” ＋ “-”
    .判断 (整数容器 ＝ 6)
        GMT时间 ＝ GMT时间 ＋ “Jun” ＋ “-”
    .判断 (整数容器 ＝ 7)
        GMT时间 ＝ GMT时间 ＋ “Jul” ＋ “-”
    .判断 (整数容器 ＝ 8)
        GMT时间 ＝ GMT时间 ＋ “Aug” ＋ “-”
    .判断 (整数容器 ＝ 9)
        GMT时间 ＝ GMT时间 ＋ “Sep” ＋ “-”
    .判断 (整数容器 ＝ 10)
        GMT时间 ＝ GMT时间 ＋ “Oct” ＋ “-”
    .判断 (整数容器 ＝ 11)
        GMT时间 ＝ GMT时间 ＋ “Nov” ＋ “-”
    .判断 (整数容器 ＝ 12)
        GMT时间 ＝ GMT时间 ＋ “Dec” ＋ “-”
    .默认
        
    .判断结束
    
    
    
    GMT时间 ＝ GMT时间 ＋ 到文本 (取年份 (待转换时间)) ＋ “ ” ＋ 到文本 (内部_补全 (取小时 (待转换时间))) ＋ “:” ＋ 到文本 (内部_补全 (取分钟 (待转换时间))) ＋ “:” ＋ 到文本 (内部_补全 (取秒 (待转换时间))) ＋ “ GMT”
    返回 (GMT时间)

.子程序 内部_补全, 文本型
    .参数 参_原数据, 整数型
    .局部变量 局_新数据, 文本型

    .如果真 (参_原数据 ＜ 10)
        局_新数据 ＝ “0” ＋ 到文本 (参_原数据)
        返回 (局_新数据)
    .如果真结束
    返回 (到文本 (参_原数据))

.子程序 时间_转为GMT格式1, 文本型, 公开, 感谢会员shituo提供代码，将日期时间型数据转换为GMT格式 Greenwich Mean Time
    .参数 待转换时间, 日期时间型, 可空, 可为空，默认即现行时间。
    .局部变量 GMT时间, 文本型
    .局部变量 time, 精易_时间
    .局部变量 Success, 逻辑型

    .如果 (是否为空 (待转换时间))
        ' GetLocalTime (time)
        GetSystemTime (time)
    .否则
        time.年 ＝ 取年份 (待转换时间)
        time.月 ＝ 取月份 (待转换时间)
        time.日 ＝ 取日 (待转换时间)
        time.时 ＝ 取小时 (待转换时间)
        time.分 ＝ 取分钟 (待转换时间)
        time.秒 ＝ 取秒 (待转换时间)
    .如果结束
    
    GMT时间 ＝ 取空白文本 (128)
    Success ＝ InternetTimeFromSystemTime (time, 0, GMT时间, 128)
    返回 (选择 (Success, GMT时间, “”))

.子程序 时间_转为GMT格式2, 文本型, 公开, 将日期时间型数据转换为GMT格式 Greenwich Mean Time
    .参数 Time, 日期时间型, 可空, 可空，默认即现行时间。
    .局部变量 Success, 逻辑型
    .局部变量 pwszTimeStr, 字节集
    .局部变量 sTime, 精易_时间
    .局部变量 Retrun, 文本型

    .如果 (是否为空 (Time))
        ' GetLocalTime (sTime)
        GetSystemTime (sTime)
    .否则
        sTime.年 ＝ 取年份 (Time)
        sTime.月 ＝ 取月份 (Time)
        sTime.日 ＝ 取日 (Time)
        sTime.时 ＝ 取小时 (Time)
        sTime.分 ＝ 取分钟 (Time)
        sTime.秒 ＝ 取秒 (Time)
        sTime.星期 ＝ 取星期几 (Time)
    .如果结束
    
    pwszTimeStr ＝ 取空白字节集 (100)
    Success ＝ WinHttpTimeFromSystemTime (sTime, pwszTimeStr)
    Retrun ＝ 编码_Unicode到Ansi (pwszTimeStr)
    返回 (Retrun)

.子程序 时间_转为GMT格式3, 文本型, 公开, 将日期时间型数据转换为GMT格式 Greenwich Mean Time
    .参数 lpDateTime, 日期时间型, 可空
    .局部变量 time, 精易_时间
    .局部变量 lpDateStr, 文本型
    .局部变量 lpTimeStr, 文本型

    ' 例程收录自：https://bbs.125.la/thread-14396648-1-1.html
    .判断开始 (是否为空 (lpDateTime))
        GetSystemTime (time)
    .判断 (VariantTimeToSystemTime (lpDateTime, time) ≠ 1)
        返回 (“”)
    .默认
        
    .判断结束
    ' Fri, 21 Jun 2019 09:18:38 GMT
    lpDateStr ＝ 取空白文本 (50)
    lpTimeStr ＝ 取空白文本 (24)
    GetDateFormatA (#en_US, 0, time, “ddd, dd MMM yyyy”, lpDateStr, 50)
    GetTimeFormatA (#en_US, 0, time, “HH:mm:ss GMT”, lpTimeStr, 24)
    返回 (lpDateStr ＋ “ ” ＋ lpTimeStr)

.子程序 时间_GMT转为时间, 日期时间型, 公开, 感谢会员shituo提供代码，将GMT文本时间 转换成日期时间型
    .参数 GMT时间, 文本型
    .局部变量 stime, 精易_时间
    .局部变量 DateFormat, 日期时间型

    InternetTimeToSystemTime (GMT时间, stime, 0)
    DateFormat ＝ 指定时间 (stime.年, stime.月, stime.日, stime.时, stime.分, stime.秒)
    DateFormat ＝ 增减时间 (DateFormat, #小时, 8)
    返回 (DateFormat)

.子程序 时间_GMT转为时间1, 日期时间型, 公开, 将GMT文本时间 转换成日期时间型
    .参数 GMT时间, 文本型
    .局部变量 Success, 逻辑型
    .局部变量 pwszTimeStr, 字节集
    .局部变量 sTime, 精易_时间
    .局部变量 DateFormat, 日期时间型

    pwszTimeStr ＝ 编码_Ansi到Unicode (GMT时间)
    Success ＝ WinHttpTimeToSystemTime (pwszTimeStr, sTime)
    DateFormat ＝ 指定时间 (sTime.年, sTime.月, sTime.日, sTime.时, sTime.分, sTime.秒)
    DateFormat ＝ 增减时间 (DateFormat, #小时, 8)
    返回 (DateFormat)

.子程序 时间_取现行时间戳, 文本型, 公开, 生成一个13位或者10位的现行时间戳，默认生成13位时间戳。13位：1325252169718，10位：1325252169
    .参数 是否取十位时间戳, 逻辑型, 可空, 本参数为真时，将生成10位时间戳返回
    .局部变量 时间b, 精易_时间
    .局部变量 返回值, 文本型
    .局部变量 毫秒数, 文本型
    .局部变量 时间a, 文本型

    GetLocalTime (时间b)
    毫秒数 ＝ 取重复文本 (3 － 取文本长度 (到文本 (时间b.毫)), “0”) ＋ 到文本 (时间b.毫)
    时间a ＝ 到文本 (取时间间隔 (取现行时间 (), 到时间 (“1970-01-01 08:00:00”), #秒))
    返回值 ＝ 时间a ＋ 毫秒数
    ' 返回值 ＝ 选择 (是否取十位时间戳, 取文本左边 (时间a ＋ 毫秒数, 10), 时间a ＋ 毫秒数)
    .判断开始 (是否取十位时间戳)
        返回值 ＝ 取文本左边 (返回值, 10)
        .判断循环首 (取文本长度 (返回值) ＜ 10)
            返回值 ＝ 返回值 ＋ “0”
        .判断循环尾 ()
        返回 (返回值)
    .默认
        
        .判断循环首 (取文本长度 (返回值) ＜ 13)
            返回值 ＝ 返回值 ＋ “0”
        .判断循环尾 ()
        返回 (返回值)
    .判断结束
    

.子程序 时间_取北京时间戳, 文本型, 公开, 生成一个13位或者10位的北京时间戳，默认生成13位时间戳。13位：1325252169718，10位：1325252169。使用网站：【http://www.114time.com/】接口。
    .参数 是否取十位时间戳, 逻辑型, 可空, 本参数为真时，将生成10位时间戳返回
    .局部变量 局_时间, 日期时间型

    局_时间 ＝ 时间_取北京时间 ()
    返回 (时间_到时间戳 (局_时间, 是否取十位时间戳, ))
    

.子程序 时间_取北京时间, 日期时间型, 公开, 获取北京时间，超稳定，长久不失效，代码提供者：aqwvwv
    .参数 参_网址, 文本型, 可空
    .局部变量 局_返回协议头, 文本型
    .局部变量 局_时间, 日期时间型

    .如果真 (是否为空 (参_网址))
        参_网址 ＝ “http://www.baidu.com/”
    .如果真结束
    网页_访问_对象 (参_网址, 2, , , , , 局_返回协议头, , , , , , , , , , , , )
    局_返回协议头 ＝ 文本_取出中间文本 (局_返回协议头, “Date:”, #换行符)
    局_时间 ＝ 时间_GMT转为时间 (局_返回协议头)
    返回 (局_时间)

.子程序 时间_到时间戳, 文本型, 公开, 把指定日期时间转换为10位或13位时间戳,默认生成13位时间戳。13位：1325252169718，10位：1325252169
    .参数 参_时间, 日期时间型, 可空, 可为空，默认为现行时间
    .参数 参_十位时间戳, 逻辑型, 可空, 本参数为真时，将生成10位时间戳返回
    .参数 参_是否取满10位, 逻辑型, 可空, 如果参数为真，则保证一定取出至少10位，如果不足10位，在前面补0. 如果为假，则直接取出
    .局部变量 局_时间, 精易_时间
    .局部变量 返回值, 文本型
    .局部变量 毫秒数, 文本型
    .局部变量 时间a, 文本型

    参_时间 ＝ 选择 (是否为空 (参_时间), 取现行时间 (), 参_时间)
    VariantTimeToSystemTime (参_时间, 局_时间)
    毫秒数 ＝ 取重复文本 (3 － 取文本长度 (到文本 (局_时间.毫)), “0”) ＋ 到文本 (局_时间.毫)
    时间a ＝ 到文本 (取时间间隔 (参_时间, 到时间 (“1970-01-01 08:00:00”), #秒))
    .如果真 (参_是否取满10位)
        .如果真 (取文本长度 (时间a) ＜ 10)
            时间a ＝ 取重复文本 (10 － 取文本长度 (时间a), “0”) ＋ 时间a
        .如果真结束
        
    .如果真结束
    返回 (选择 (参_十位时间戳, 取文本左边 (时间a, 10), 时间a ＋ 毫秒数))

.子程序 时间_取随机时间戳, 文本型, 公开, 生成一个随机的时间戳。如：0.842703761170252
    .局部变量 VBscript, 对象
    .局部变量 局_文本, 文本型

    线程_初始化COM库 ()
    VBscript.创建 (“ScriptControl”, )
    VBscript.写属性 (“Language”, “JScript”)
    VBscript.数值方法 (“ExecuteStatement”, “function time(){return Math.random()}”)
    .判断循环首 (取文本长度 (局_文本) ≠ 17)
        局_文本 ＝ VBscript.通用方法 (“Run”, “time”, ).取文本 ()
        .如果真 (取文本左边 (局_文本, 1) ≠ “0”)
            局_文本 ＝ “0” ＋ 局_文本
        .如果真结束
        系统_处理事件 ()
    .判断循环尾 ()
    VBscript.清除 ()
    线程_取消COM库 ()
    返回 (局_文本)

.子程序 时间_时间戳转文本, 文本型, 公开, 把一个13位的时间戳，如：1325252169718 转成 2011-12-30 21:36:09
    .参数 时间戳, 文本型
    .局部变量 VBscript, 对象
    .局部变量 局_文本, 文本型
    .局部变量 局_数组, 文本型, , "0"
    .局部变量 局_时间, 日期时间型

    CoInitialize (0)
    .如果 (取文本长度 (时间戳) ＝ 10)
        局_时间 ＝ 增减时间 (到时间 (“1970.01.01 08:00:00”), #秒, 到整数 (时间戳))
        返回 (选择 (时间_取制式 () ＝ 0, 时间_格式化 (局_时间, “yyyy-MM-dd ”, “hh:mm:ss”, ), 时间_格式化 (局_时间, “yyyy-MM-dd ”, “HH:mm:ss”, )))
    .否则
        .如果真 (VBscript.创建 (“ScriptControl”, ))
            VBscript.写属性 (“Language”, “JScript”)
            VBscript.数值方法 (“ExecuteStatement”, “function timea(){var d,s;d=new Date();d.setTime('” ＋ 时间戳 ＋ “');return(d);}”)
            局_文本 ＝ VBscript.通用方法 (“Run”, “timea”, ).取文本 ()
            VBscript.清除 ()
            CoUninitialize ()
            .如果真 (取文本长度 (局_文本) ＞ 0)
                局_数组 ＝ 分割文本 (局_文本, “ ”, )
                .如果真 (取数组成员数 (局_数组) ≥ 6)
                    .判断开始 (局_数组 [2] ＝ “Jan”)
                        局_数组 [2] ＝ “1”
                    .判断 (局_数组 [2] ＝ “Feb”)
                        局_数组 [2] ＝ “2”
                    .判断 (局_数组 [2] ＝ “Mar”)
                        局_数组 [2] ＝ “3”
                    .判断 (局_数组 [2] ＝ “Apr”)
                        局_数组 [2] ＝ “4”
                    .判断 (局_数组 [2] ＝ “May”)
                        局_数组 [2] ＝ “5”
                    .判断 (局_数组 [2] ＝ “Jun”)
                        局_数组 [2] ＝ “6”
                    .判断 (局_数组 [2] ＝ “Jul”)
                        局_数组 [2] ＝ “7”
                    .判断 (局_数组 [2] ＝ “Aug”)
                        局_数组 [2] ＝ “8”
                    .判断 (局_数组 [2] ＝ “Sep”)
                        局_数组 [2] ＝ “9”
                    .判断 (局_数组 [2] ＝ “Oct”)
                        局_数组 [2] ＝ “10”
                    .判断 (局_数组 [2] ＝ “Nov”)
                        局_数组 [2] ＝ “11”
                    .判断 (局_数组 [2] ＝ “Dec”)
                        局_数组 [2] ＝ “12”
                    .默认
                        
                    .判断结束
                    
                    返回 (局_数组 [6] ＋ “-” ＋ 局_数组 [2] ＋ “-” ＋ 局_数组 [3] ＋ “ ” ＋ 局_数组 [4])
                .如果真结束
                
            .如果真结束
            
        .如果真结束
        
    .如果结束
    返回 (“”)

.子程序 时间_取公历节日, 文本型, 公开, 根据公历年月日,返回公历的节日,某些节日需要有年代才能确定是星期几.
    .参数 公历年, 整数型, 可空
    .参数 公历月, 整数型, 可空
    .参数 公历日, 整数型, 可空
    .局部变量 节日文本, 文本型
    .局部变量 星期几, 整数型

    星期几 ＝ 取星期几 (到时间 (到文本 (公历年) ＋ “年” ＋ 到文本 (公历月) ＋ “月” ＋ 到文本 (公历日) ＋ “日”))
    节日文本 ＝ “”
    .如果真 (公历月 ＝ 1 且 公历日 ＝ 1)
        节日文本 ＝ “元旦”
    .如果真结束
    .如果真 (公历月 ＝ 2 且 公历日 ＝ 14)
        节日文本 ＝ “情人节”
    .如果真结束
    .如果真 (公历月 ＝ 3 且 公历日 ＝ 5)
        节日文本 ＝ “雷峰纪念日”
    .如果真结束
    .如果真 (公历月 ＝ 3 且 公历日 ＝ 8)
        节日文本 ＝ “国际劳动妇女节”
    .如果真结束
    .如果真 (公历月 ＝ 3 且 公历日 ＝ 12)
        节日文本 ＝ “植树节”
    .如果真结束
    .如果真 (公历月 ＝ 3 且 公历日 ＝ 15)
        节日文本 ＝ “国际消费者权益日”
    .如果真结束
    .如果真 (公历月 ＝ 3 且 公历日 ＝ 23)
        节日文本 ＝ “国际气象日”
    .如果真结束
    .如果真 (公历月 ＝ 3 且 公历日 ＝ 31)
        节日文本 ＝ “安全教育日”
    .如果真结束
    .如果真 (公历月 ＝ 4 且 公历日 ＝ 1)
        节日文本 ＝ “愚人节”
    .如果真结束
    .如果真 (公历月 ＝ 4 且 公历日 ＝ 7)
        节日文本 ＝ “世界卫生日”
    .如果真结束
    .如果真 (公历月 ＝ 4 且 公历日 ＝ 22)
        节日文本 ＝ “世界地球日”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 1)
        节日文本 ＝ “国际劳动节”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 4)
        节日文本 ＝ “青年节”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 8)
        节日文本 ＝ “世界红十字日”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 12)
        节日文本 ＝ “国际护士节”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 15)
        节日文本 ＝ “国际家庭日”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 16)
        节日文本 ＝ “全国助残日”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 17)
        节日文本 ＝ “国际电信日”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＝ 31)
        节日文本 ＝ “世界无烟日”
    .如果真结束
    .如果真 (公历月 ＝ 6 且 公历日 ＝ 1)
        节日文本 ＝ “国际儿童节”
    .如果真结束
    .如果真 (公历月 ＝ 6 且 公历日 ＝ 5)
        节日文本 ＝ “国际环境保护日”
    .如果真结束
    .如果真 (公历月 ＝ 6 且 公历日 ＝ 6)
        节日文本 ＝ “全国爱眼日”
    .如果真结束
    .如果真 (公历月 ＝ 6 且 公历日 ＝ 25)
        节日文本 ＝ “全国土地日”
    .如果真结束
    .如果真 (公历月 ＝ 6 且 公历日 ＝ 26)
        节日文本 ＝ “国际禁毒日”
    .如果真结束
    .如果真 (公历月 ＝ 7 且 公历日 ＝ 1)
        节日文本 ＝ “党的生日、香港回归日”
    .如果真结束
    .如果真 (公历月 ＝ 7 且 公历日 ＝ 7)
        节日文本 ＝ “抗日战争胜利日”
    .如果真结束
    .如果真 (公历月 ＝ 7 且 公历日 ＝ 11)
        节日文本 ＝ “世界人口日”
    .如果真结束
    .如果真 (公历月 ＝ 8 且 公历日 ＝ 1)
        节日文本 ＝ “中国建军节”
    .如果真结束
    .如果真 (公历月 ＝ 8 且 公历日 ＝ 8)
        节日文本 ＝ “父亲节”
    .如果真结束
    .如果真 (公历月 ＝ 9 且 公历日 ＝ 8)
        节日文本 ＝ “国际扫盲日”
    .如果真结束
    .如果真 (公历月 ＝ 9 且 公历日 ＝ 9)
        节日文本 ＝ “毛泽东逝世”
    .如果真结束
    .如果真 (公历月 ＝ 9 且 公历日 ＝ 10)
        节日文本 ＝ “中国教师节”
    .如果真结束
    .如果真 (公历月 ＝ 9 且 公历日 ＝ 20)
        节日文本 ＝ “中国爱牙日”
    .如果真结束
    .如果真 (公历月 ＝ 9 且 公历日 ＝ 27)
        节日文本 ＝ “世界旅游日”
    .如果真结束
    .如果真 (公历月 ＝ 9 且 公历日 ＝ 28)
        节日文本 ＝ “世界聋人日、孔子诞辰”
    .如果真结束
    .如果真 (公历月 ＝ 10 且 公历日 ＝ 1)
        节日文本 ＝ “世界音乐日、国庆节”
    .如果真结束
    .如果真 (公历月 ＝ 10 且 公历日 ＝ 4)
        节日文本 ＝ “世界动物日”
    .如果真结束
    .如果真 (公历月 ＝ 10 且 公历日 ＝ 6)
        节日文本 ＝ “老人节”
    .如果真结束
    .如果真 (公历月 ＝ 10 且 公历日 ＝ 8)
        节日文本 ＝ “全国高血压日”
    .如果真结束
    .如果真 (公历月 ＝ 10 且 公历日 ＝ 9)
        节日文本 ＝ “世界邮政日”
    .如果真结束
    .如果真 (公历月 ＝ 10 且 公历日 ＝ 14)
        节日文本 ＝ “世界标准日”
    .如果真结束
    .如果真 (公历月 ＝ 10 且 公历日 ＝ 24)
        节日文本 ＝ “联合国日”
    .如果真结束
    .如果真 (公历月 ＝ 11 且 公历日 ＝ 1)
        节日文本 ＝ “万圣节”
    .如果真结束
    .如果真 (公历月 ＝ 11 且 公历日 ＝ 2)
        节日文本 ＝ “万灵节”
    .如果真结束
    .如果真 (公历月 ＝ 11 且 公历日 ＝ 12)
        节日文本 ＝ “孙中山诞辰”
    .如果真结束
    .如果真 (公历月 ＝ 11 且 公历日 ＝ 17)
        节日文本 ＝ “国际学生日”
    .如果真结束
    .如果真 (公历月 ＝ 12 且 公历日 ＝ 5)
        节日文本 ＝ “国际志愿人员日”
    .如果真结束
    .如果真 (公历月 ＝ 12 且 公历日 ＝ 20)
        节日文本 ＝ “澳门回归”
    .如果真结束
    .如果真 (公历月 ＝ 12 且 公历日 ＝ 25)
        节日文本 ＝ “圣诞节”
    .如果真结束
    .如果真 (公历月 ＝ 12 且 公历日 ＝ 26)
        节日文本 ＝ “毛泽东诞辰”
    .如果真结束
    .如果真 (公历月 ＝ 5 且 公历日 ＞ 7 且 公历日 ＜ 15 且 星期几 ＝ 1)
        节日文本 ＝ “母亲节”
    .如果真结束
    .如果真 (公历月 ＝ 6 且 公历日 ＞ 14 且 公历日 ＜ 22 且 星期几 ＝ 1)
        节日文本 ＝ “父亲节”
    .如果真结束
    .如果真 (公历月 ＝ 11 且 公历日 ＞ 21 且 星期几 ＝ 1)
        节日文本 ＝ “感恩节”
    .如果真结束
    返回 (节日文本)

.子程序 时间_取节气文本, 文本型, 公开, 立春,雨水,惊蛰,春分,清明,谷雨；立夏,小满,芒种,夏至,小暑,大暑；立秋,处暑,白露,秋分,寒露,霜降；立冬,小雪,大雪,冬至,小寒,大寒
    .参数 节气顺序, 整数型, , 根据24个节气返回节气名称.

    .判断开始 (节气顺序 ＝ 1)
        返回 (“立春”)
    .判断 (节气顺序 ＝ 2)
        返回 (“雨水”)
    .判断 (节气顺序 ＝ 3)
        返回 (“惊蛰”)
    .判断 (节气顺序 ＝ 4)
        返回 (“春分”)
    .判断 (节气顺序 ＝ 5)
        返回 (“清明”)
    .判断 (节气顺序 ＝ 6)
        返回 (“谷雨”)
    .判断 (节气顺序 ＝ 7)
        返回 (“立夏”)
    .判断 (节气顺序 ＝ 8)
        返回 (“小满”)
    .判断 (节气顺序 ＝ 9)
        返回 (“芒种”)
    .判断 (节气顺序 ＝ 10)
        返回 (“夏至”)
    .判断 (节气顺序 ＝ 11)
        返回 (“小暑”)
    .判断 (节气顺序 ＝ 12)
        返回 (“大暑”)
    .判断 (节气顺序 ＝ 13)
        返回 (“立秋”)
    .判断 (节气顺序 ＝ 14)
        返回 (“处暑”)
    .判断 (节气顺序 ＝ 15)
        返回 (“白露”)
    .判断 (节气顺序 ＝ 16)
        返回 (“秋分”)
    .判断 (节气顺序 ＝ 17)
        返回 (“寒露”)
    .判断 (节气顺序 ＝ 18)
        返回 (“霜降”)
    .判断 (节气顺序 ＝ 19)
        返回 (“立冬”)
    .判断 (节气顺序 ＝ 20)
        返回 (“小雪”)
    .判断 (节气顺序 ＝ 21)
        返回 (“大雪”)
    .判断 (节气顺序 ＝ 22)
        返回 (“冬至”)
    .判断 (节气顺序 ＝ 23)
        返回 (“小寒”)
    .判断 (节气顺序 ＝ 24)
        返回 (“大寒”)
    .默认
        返回 (“”)
    .判断结束
    

.子程序 时间_取美式日期, 文本型, 公开, 转换日期时间表达为美式表达方式
    .参数 日期时间, 日期时间型
    .局部变量 日期, 日期时间型
    .局部变量 美式日期, 文本型

    日期 ＝ 日期时间
    美式日期 ＝ 到文本 (取年份 (日期))
    美式日期 ＝ 美式日期 ＋ “/” ＋ 到文本 (取月份 (日期))
    美式日期 ＝ 美式日期 ＋ “/” ＋ 到文本 (取日 (日期))
    返回 (美式日期)
    

.子程序 时间_取农历节日, 文本型, 公开, 取农历所对应该的节日，需要事先知道农历日期的月份，与日子，
    .参数 农历月份, 整数型
    .参数 农历天数, 整数型
    .局部变量 节日文本, 文本型

    节日文本 ＝ “”
    .如果真 (农历月份 ＝ 1 且 农历天数 ＝ 1)
        节日文本 ＝ “春节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 1 且 农历天数 ＝ 15)
        节日文本 ＝ “元宵节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 2 且 农历天数 ＝ 2)
        节日文本 ＝ “中和节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 5 且 农历天数 ＝ 5)
        节日文本 ＝ “端午节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 7 且 农历天数 ＝ 7)
        节日文本 ＝ “七巧节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 8 且 农历天数 ＝ 15)
        节日文本 ＝ “中秋节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 9 且 农历天数 ＝ 9)
        节日文本 ＝ “重阳节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 12 且 农历天数 ＝ 8)
        节日文本 ＝ “腊八节”
        
    .如果真结束
    .如果真 (农历月份 ＝ 12 且 农历天数 ＝ 24)
        节日文本 ＝ “小年”
        
    .如果真结束
    返回 (节日文本)

.子程序 时间_取身份证比较, 逻辑型, 公开, 对身份证号和一个日期进行比较,看两者是否一致
    .参数 身份证号, 文本型
    .参数 比较日期, 日期时间型
    .局部变量 长度, 整数型

    长度 ＝ 取文本长度 (身份证号)
    .如果真 (长度 ＝ 15)
        .如果 (到数值 (“19” ＋ 取文本中间 (身份证号, 7, 2)) ＝ 取年份 (比较日期) 且 到数值 (取文本中间 (身份证号, 9, 2)) ＝ 取月份 (比较日期) 且 到数值 (取文本中间 (身份证号, 11, 2)) ＝ 取日 (比较日期))
            返回 (真)
        .否则
            返回 (假)
        .如果结束
        
    .如果真结束
    .如果真 (长度 ＝ 18)
        .如果 (到数值 (取文本中间 (身份证号, 7, 4)) ＝ 取年份 (比较日期) 且 到数值 (取文本中间 (身份证号, 11, 2)) ＝ 取月份 (比较日期) 且 到数值 (取文本中间 (身份证号, 13, 2)) ＝ 取日 (比较日期))
            返回 (真)
        .否则
            返回 (假)
        .如果结束
        
    .如果真结束
    返回 (假)
    

.子程序 时间_取中国星期几, 文本型, 公开, 返回一个星期文本
    .参数 目标时间, 日期时间型, 可空, 如果星期数不为零,本参数不处理,否则将本参数内的时间进行处理.
    .局部变量 星期数, 整数型, , , 如果空或为零时取下面时间的返回星期几

    目标时间 ＝ 选择 (是否为空 (目标时间), 取现行时间 (), 目标时间)
    星期数 ＝ 取星期几 (目标时间)
    返回 (多项选择 (星期数, “星期日”, “星期一”, “星期二”, “星期三”, “星期四”, “星期五”, “星期六”))

.子程序 时间_取身份证日期, 文本型, 公开, 取一个身份证号中的出生日期,在本程序前对身份证是否正确进行简单判断,应该用真正的判断程序进行判断
    .参数 身份证号, 文本型
    .局部变量 长度, 整数型

    长度 ＝ 取文本长度 (身份证号)
    .如果真 (长度 ＝ 15)
        返回 (“19” ＋ 取文本中间 (身份证号, 7, 6))
    .如果真结束
    .如果真 (长度 ＝ 18)
        返回 (取文本中间 (身份证号, 7, 8))
    .如果真结束
    返回 (“”)
    

.子程序 时间_取身份证性别, 文本型, 公开, 判断身份证的性别
    .参数 身份证号, 文本型
    .局部变量 长度, 整数型

    长度 ＝ 取文本长度 (身份证号)
    .如果真 (长度 ＝ 15)
        .如果 (到数值 (取文本中间 (身份证号, 15, 1)) ％ 2 ＝ 1)
            返回 (“男”)
        .否则
            返回 (“女”)
        .如果结束
        
    .如果真结束
    .如果真 (长度 ＝ 18)
        .如果 (到数值 (取文本中间 (身份证号, 17, 1)) ％ 2 ＝ 1)
            返回 (“男”)
        .否则
            返回 (“女”)
        .如果结束
        
    .如果真结束
    返回 (“身份证有误”)

.子程序 时间_取身份证判断, 逻辑型, 公开, 判断身份证号是否正确，检测1900年到2030年中15位或18位身份证，正确返回真，错误返回假。
    .参数 身份证号, 文本型
    .局部变量 长度, 整数型
    .局部变量 局_年
    .局部变量 局_月
    .局部变量 局_日
    .局部变量 局_乘数, 整数型, , "0"
    .局部变量 局_计次
    .局部变量 和, 整数型

    长度 ＝ 取文本长度 (身份证号)
    .如果真 (长度 ＝ 15 或 长度 ＝ 18)
        .如果真 (长度 ＝ 15)
            .如果真 (到数值 (取文本中间 (身份证号, 9, 2)) ≥ 13 或 到数值 (取文本中间 (身份证号, 9, 2)) ＝ 0)
                ' 输出调试文本 (取文本中间 (身份证号, 9, 2))
                返回 (假) ' 月份大于12个月3201057302
            .如果真结束
            .如果真 (到数值 (取文本中间 (身份证号, 11, 2)) ≥ 32 或 到数值 (取文本中间 (身份证号, 11, 2)) ＝ 0)
                ' 输出调试文本 (取文本中间 (身份证号, 11, 2))
                返回 (假) ' 日子大于32天
            .如果真结束
            返回 (真)
        .如果真结束
        
        .如果真 (长度 ＝ 18)
            局_年 ＝ 到数值 (取文本中间 (身份证号, 7, 4))
            .如果真 (局_年 ≤ 1900 或 局_年 ≥ 2030)
                ' 输出调试文本 (取文本中间 (身份证号, 9, 2))
                返回 (假) ' 年份小于1900年,大于2030年
            .如果真结束
            局_月 ＝ 到数值 (取文本中间 (身份证号, 11, 2))
            .如果真 (局_月 ≥ 13 或 局_月 ＝ 0)
                ' 输出调试文本 (取文本中间 (身份证号, 11, 2))
                返回 (假) ' 月份大于12个月
            .如果真结束
            局_日 ＝ 到数值 (取文本中间 (身份证号, 13, 2))
            .如果真 (局_日 ≥ 32 或 局_日 ＝ 0)
                ' 输出调试文本 (取文本中间 (身份证号, 13, 2))
                返回 (假) ' 日子大于32天
            .如果真结束
            ' 校验码计算公式 由易友【小爬虫】提供。
            局_乘数 ＝ { 7, 9, 10, 5, 8, 4, 2, 1, 6, 3, 7, 9, 10, 5, 8, 4, 2 }
            .计次循环首 (17, 局_计次)
                和 ＝ 和 ＋ 到整数 (取文本中间 (身份证号, 局_计次, 1)) × 局_乘数 [局_计次]
            .计次循环尾 ()
            和 ＝ 和 ％ 11
            返回 (多项选择 (和 ＋ 1, “1”, “0”, “x”, “9”, “8”, “7”, “6”, “5”, “4”, “3”, “2”) ＝ 到小写 (取文本右边 (身份证号, 1)))
        .如果真结束
        
    .如果真结束
    返回 (假)

.子程序 时间_身份证号转换, 文本型, 公开, 从15位转换到18位身份证号
    .参数 身份证号, 文本型
    .局部变量 加权因子, 整数型, , "17"
    .局部变量 校验码串, 文本型, , "11"
    .局部变量 位, 文本型
    .局部变量 累加, 整数型
    .局部变量 校验码, 文本型
    .局部变量 计数, 整数型
    .局部变量 长度, 整数型

    加权因子 ＝ { 7, 9, 10, 5, 8, 4, 2, 1, 6, 3, 7, 9, 10, 5, 8, 4, 2 }
    校验码串 ＝ { “1”, “0”, “X”, “9”, “8”, “7”, “6”, “5”, “4”, “3”, “2” }
    长度 ＝ 取文本长度 (身份证号)
    .如果真 (长度 ≠ 18)
        .如果真 (长度 ≠ 15)
            返回 (“”) ' 信息框 (“身份证号码长度非法！”, #错误图标, )
        .如果真结束
        
    .如果真结束
    .如果真 (长度 ＝ 15)
        身份证号 ＝ 取文本左边 (身份证号, 6) ＋ “19” ＋ 取文本右边 (身份证号, 9)
    .如果真结束
    .计次循环首 (17, 计数)
        位 ＝ 取文本中间 (身份证号, 计数, 1)
        累加 ＝ 累加 ＋ 到数值 (位) × 加权因子 [计数]
    .计次循环尾 ()
    校验码 ＝ 校验码串 [累加 ％ 11 ＋ 1]
    身份证号 ＝ 取文本左边 (身份证号, 17) ＋ 校验码
    返回 (身份证号)

.子程序 时间_到文本, 文本型, 公开, 返回文本格式的现行时间，如：2005053107123
    .参数 操作时间, 日期时间型, 可空, 可空。留空为现行时间
    .参数 输出格式, 整数型, 可空, 可空。0=N年N月N日 N时N分N秒  1=年-月-日 时:分:秒  2=年/月/日 时/分/秒  3=年月日时分秒
    .参数 取出类型, 整数型, 可空, 可空。留空为全部，1=只取年月日，2=取月日，3=只取时分秒，4=时分，5=分秒
    .局部变量 局_日期格式, 文本型
    .局部变量 局_时间格式, 文本型
    .局部变量 局_格式, 文本型
    .局部变量 局_制式, 整数型

    操作时间 ＝ 选择 (是否为空 (操作时间), 取现行时间 (), 操作时间)
    输出格式 ＝ 选择 (输出格式 ≥ 0 且 输出格式 ≤ 3, 输出格式, 0)
    局_日期格式 ＝ 多项选择 (输出格式 ＋ 1, “yyyy年MM月dd日 ”, “yyyy-MM-dd ”, “yyyy/MM/dd ”, “yyyyMMdd”)
    局_日期格式 ＝ 选择 (取出类型 ≥ 3 且 取出类型 ≤ 5, “”, 局_日期格式)
    局_时间格式 ＝ 多项选择 (输出格式 ＋ 1, “hh时mm分ss秒”, “hh:mm:ss”, “hh/mm/ss”, “hhmmss”)
    局_时间格式 ＝ 选择 (取出类型 ≥ 1 且 取出类型 ≤ 2, “”, 局_时间格式)
    .如果真 (取出类型 ＝ 1)
        局_日期格式 ＝ 删首尾空 (局_日期格式)
    .如果真结束
    .如果真 (取出类型 ＝ 2)
        局_日期格式 ＝ 多项选择 (输出格式 ＋ 1, “MM月dd日”, “MM-dd”, “MM/dd”, “MMdd”)
    .如果真结束
    .如果真 (取出类型 ＝ 4)
        局_时间格式 ＝ 多项选择 (输出格式 ＋ 1, “hh时mm分”, “hh:mm”, “hh/mm”, “hhmm”)
    .如果真结束
    .如果真 (取出类型 ＝ 5)
        局_时间格式 ＝ 多项选择 (输出格式 ＋ 1, “mm分ss秒”, “mm:ss”, “mm/ss”, “mmss”)
    .如果真结束
    局_制式 ＝ 时间_取制式 ()
    局_时间格式 ＝ 选择 (局_制式 ＝ 1, 子文本替换 (局_时间格式, “hh”, “HH”, , , 真), 局_时间格式)
    局_格式 ＝ 时间_格式化 (操作时间, 局_日期格式, 局_时间格式)
    返回 (局_格式)

.子程序 时间_身份证号15到18, 文本型, 公开, 将15位身份证转换为18位
    .参数 身份证号15, 文本型
    .局部变量 计数, 整数型
    .局部变量 加权因子, 整数型
    .局部变量 加权码, 文本型
    .局部变量 转换后号, 文本型

    转换后号 ＝ 取文本左边 (身份证号15, 6) ＋ “19” ＋ 取文本右边 (身份证号15, 9)
    .变量循环首 (18, 2, -1, 计数)
        加权因子 ＝ 加权因子 ＋ 求次方 (2, 计数 － 1) ％ 11 × 到数值 (取文本中间 (转换后号, 19 － 计数, 1))
    .变量循环尾 ()
    加权因子 ＝ 加权因子 ％ 11
    .判断开始 (加权因子 ＝ 0)
        加权码 ＝ “1”
    .判断 (加权因子 ＝ 1)
        加权码 ＝ “0”
    .判断 (加权因子 ＝ 2)
        加权码 ＝ “X”
    .默认
        加权码 ＝ 删首尾空 (到文本 (12 － 加权因子))
    .判断结束
    返回 (转换后号 ＋ 加权码)

.子程序 时间_北京转格林威治, 长整数型, 公开, 将【北京标准时间】转换成【格林威治的标准时间】，13位的时间戳可用“时间_取现行时间戳（）”获得
    .参数 欲转换的时间, 日期时间型, 可空, 如果为空则取当前时间，如: 1970-01-01 08:00:00 或 2009年2月9日12时36分36秒

    .如果真 (是否为空 (欲转换的时间))
        欲转换的时间 ＝ 取现行时间 ()
    .如果真结束
    返回 (取时间间隔 (欲转换的时间, [1970年1月1日8时], #秒))

.子程序 时间_格林威治转北京, 日期时间型, 公开, 将【格林威治的标准时间】转换成【北京标准时间】,源码修改至官方用户
    .参数 要转换的格林威治时间, 长整数型, , 要转换的格林威治时间
    .局部变量 time, 日期时间型
    .局部变量 待转数据, 整数型
    .局部变量 局_时间, 整数型

    time ＝ 指定时间 (1970, 1, 1, 8, 0, 0)
    局_时间 ＝ 取文本长度 (到文本 (要转换的格林威治时间))
    .如果 (局_时间 ≠ 10)
        待转数据 ＝ 到整数 (取文本左边 (到文本 (要转换的格林威治时间), 10))
    .否则
        待转数据 ＝ 要转换的格林威治时间
    .如果结束
    返回 (增减时间 (time, #秒, 待转数据))

.子程序 FileTimeToUnixTime, 长整数型
    .参数 FlieTime, 长整数型
    .局部变量 ll, 长整数型

    ll ＝ FlieTime － 1.16444736e+017
    返回 (ll)

.子程序 UnixTimeToFileTime, 长整数型
    .参数 UnixTime, 长整数型
    .局部变量 ll, 长整数型
    .局部变量 位数, 整数型
    .局部变量 乘数, 整数型

    位数 ＝ 取整数位数 (UnixTime)
    乘数 ＝ 1
    .计次循环首 (17 － 位数, )
        乘数 ＝ 乘数 × 10
    .计次循环尾 ()
    ll ＝ UnixTime × 乘数 ＋ 1.16444736e+017
    返回 (ll)

.子程序 取整数位数, 整数型
    .参数 长整数, 长整数型
    .局部变量 n, 整数型
    .局部变量 l, 长整数型

    l ＝ 长整数
    n ＝ 1
    .循环判断首 ()
        l ＝ l ÷ 10
        .如果 (l ≥ 1)
            n ＝ n ＋ 1
        .否则
            跳出循环 ()
        .如果结束
        
    .循环判断尾 (l ≥ 1)
    返回 (n)

.子程序 时间_WMI时间转日期, 日期时间型, 公开, 失败返回  [100年1月1日]
    .参数 WMI数据时间, 文本型, , 如“20140926001639.000000+480”
    .局部变量 局_时间对象, 对象
    .局部变量 局_返回时间, 日期时间型

    CoInitialize (0)
    .如果 (局_时间对象.创建 (“WbemScripting.SWbemDateTime”, ))
        局_时间对象.写属性 (“Value”, WMI数据时间)
        局_返回时间 ＝ 局_时间对象.日期方法 (“GetVarDate”, )
        局_时间对象.清除 ()
    .否则
        局_返回时间 ＝ 到时间 (0)
    .如果结束
    CoUninitialize ()
    返回 (局_返回时间)

.子程序 时间_格式化, 文本型, 公开, 格式化指定日期与时间，失败返回空文本   GetTimeFormatA 该函数支持的最早时间是1600.1.1
    .参数 参_欲格式化时间, 日期时间型
    .参数 参_日期格式, 文本型, 可空, 可以为空，格式：yyyy [年]，M [月],d [日],dddd [星期]，如;yyyy/M/d dddd(年/月/日 星期几)
    .参数 参_时间格式, 文本型, 可空, 可以为空，格式：tt [上午或下午],h [小时],m [分钟], s [秒] 。如;hh:mm:ss(小时:分钟:秒),tt hh:mm:ss(上午或下午 小时:分钟:秒)
    .参数 参_是否为24小时制, 逻辑型, 可空
    .局部变量 局_系统时间, 精易_时间
    .局部变量 局_整数返回, 整数型
    .局部变量 局_日期, 文本型
    .局部变量 局_时间, 文本型
    .局部变量 局_Flags, 整数型

    局_整数返回 ＝ VariantTimeToSystemTime (参_欲格式化时间, 局_系统时间)
    .如果真 (1 ＝ 局_整数返回)
        .如果真 (取文本长度 (参_日期格式) ＞ 2)
            局_日期 ＝ 取空白文本 (50)
            GetDateFormatA (0, 0, 局_系统时间, 参_日期格式, 局_日期, 50)
        .如果真结束
        .如果真 (取文本长度 (参_时间格式) ＞ 2)
            局_时间 ＝ 取空白文本 (50)
            .如果真 (参_是否为24小时制 ＝ 真)
                局_Flags ＝ #TIME_FORCE24HOURFORMAT
            .如果真结束
            GetTimeFormatA (0, 局_Flags, 局_系统时间, 参_时间格式, 局_时间, 50)
        .如果真结束
        返回 (局_日期 ＋ 局_时间)
    .如果真结束
    返回 (“”)

.子程序 时间_格式化EX, 文本型, 公开, 将易语言中默认获取到的含有汉字的时间格式化为不含汉字的时间，源码由雪山凌狐提供
    .参数 未格式化时间, 日期时间型, , 使用易语言自带命令：取现行时间()等获取到的含汉字的时间
    .参数 日期间间隔, 文本型, 可空, 默认为“-”
    .参数 时间间间隔, 文本型, 可空, 默认为“:”
    .参数 返回模式, 整数型, 可空, 模式1为只返回时间（时分秒），模式为2为只返回日期（年月日），模式3为两者都返回，模式4为都返回，但日期与时间之间没有空格。如果为空，默认为1，如果不符合要求，返回为空
    .局部变量 格式化后时间, 文本型
    .局部变量 年份, 文本型
    .局部变量 月份, 文本型
    .局部变量 日, 文本型
    .局部变量 小时, 文本型
    .局部变量 分钟, 文本型
    .局部变量 秒, 文本型

    .如果真 (是否为空 (日期间间隔))
        日期间间隔 ＝ “-”
    .如果真结束
    .如果真 (是否为空 (时间间间隔))
        时间间间隔 ＝ “:”
    .如果真结束
    .如果真 (是否为空 (返回模式))
        返回模式 ＝ 1
    .如果真结束
    
    
    年份 ＝ 到文本 (取年份 (未格式化时间))
    月份 ＝ 到文本 (取月份 (未格式化时间))
    .如果真 (取文本长度 (月份) ＝ 1)
        月份 ＝ “0” ＋ 月份
    .如果真结束
    日 ＝ 到文本 (取日 (未格式化时间))
    .如果真 (取文本长度 (日) ＝ 1)
        日 ＝ “0” ＋ 日
    .如果真结束
    小时 ＝ 到文本 (取小时 (未格式化时间))
    .如果真 (取文本长度 (小时) ＝ 1)
        小时 ＝ “0” ＋ 小时
    .如果真结束
    分钟 ＝ 到文本 (取分钟 (未格式化时间))
    .如果真 (取文本长度 (分钟) ＝ 1)
        分钟 ＝ “0” ＋ 分钟
    .如果真结束
    秒 ＝ 到文本 (取秒 (未格式化时间))
    .如果真 (取文本长度 (秒) ＝ 1)
        秒 ＝ “0” ＋ 秒
    .如果真结束
    
    .判断开始 (返回模式 ＝ 1)
        格式化后时间 ＝ 小时 ＋ 时间间间隔 ＋ 分钟 ＋ 时间间间隔 ＋ 秒
    .判断 (返回模式 ＝ 2)
        格式化后时间 ＝ 年份 ＋ 日期间间隔 ＋ 月份 ＋ 日期间间隔 ＋ 日
    .判断 (返回模式 ＝ 3)
        格式化后时间 ＝ 年份 ＋ 日期间间隔 ＋ 月份 ＋ 日期间间隔 ＋ 日 ＋ “ ” ＋ 小时 ＋ 时间间间隔 ＋ 分钟 ＋ 时间间间隔 ＋ 秒
    .判断 (返回模式 ＝ 4)
        格式化后时间 ＝ 年份 ＋ 日期间间隔 ＋ 月份 ＋ 日期间间隔 ＋ 日 ＋ 小时 ＋ 时间间间隔 ＋ 分钟 ＋ 时间间间隔 ＋ 秒
    .默认
        
    .判断结束
    
    
    返回 (格式化后时间)
    
    

.子程序 时间_取制式, 整数型, 公开, 取当前系统时间制式，返回值；0表示12小时制，1表示24小时制
    .局部变量 局_缓冲区, 文本型

    局_缓冲区 ＝ 取空白文本 (4)
    GetLocaleInfo (0, 35, 局_缓冲区, 4)
    返回 (到整数 (局_缓冲区))

.子程序 时间_同步校时, 日期时间型, 公开, 调用http://www.baidu.com网址接口,win8以上系统需要管理员权限
    .参数 参_是否同步, 逻辑型, 可空, 是否同步到北京时间，为空默认不同步
    .局部变量 返回文本, 文本型
    .局部变量 取出的时间, 文本型
    .局部变量 时间, 日期时间型

    网页_访问S (“https://www.baidu.com”, 2, , , , , 返回文本)
    取出的时间 ＝ 删首尾空 (文本_取出中间文本 (返回文本, “Date:”, #换行符))
    时间 ＝ 时间_GMT转为时间 (取出的时间)
    .如果真 (参_是否同步)
        置现行时间 (时间)
    .如果真结束
    返回 (时间)

.子程序 时间_取月初, 日期时间型, 公开, 源码由论坛用户【ds9660】雕哥提供。
    .参数 参_指定时间, 日期时间型, 可空

    .如果真 (是否为空 (参_指定时间))
        参_指定时间 ＝ 取现行时间 ()
    .如果真结束
    返回 (增减时间 (取日期 (参_指定时间), #日, 1 － 取日 (参_指定时间)))

.子程序 时间_取月末, 日期时间型, 公开, 源码由论坛用户【ds9660】雕哥提供。
    .参数 参_指定时间, 日期时间型, 可空
    .局部变量 局_下月, 日期时间型

    .如果真 (是否为空 (参_指定时间))
        参_指定时间 ＝ 取现行时间 ()
    .如果真结束
    局_下月 ＝ 取日期 (增减时间 (参_指定时间, #月份, 1))
    返回 (增减时间 (局_下月, #日, －取日 (局_下月)))

.子程序 时间_取某年天数, 整数型, 公开, 阳历。
    .参数 某时间, 日期时间型

    返回 (选择 (取年份 (某时间) ％ 4 ＝ 0, 366, 365))

.子程序 时间_取月总天数, 整数型, 公开, 阳历。
    .参数 输入_年
    .参数 输入_月
    .局部变量 datestr, 日期时间型
    .局部变量 sday
    .局部变量 eday, 日期时间型

    datestr ＝ 到时间 (到文本 (输入_年) ＋ “-” ＋ 到文本 (输入_月) ＋ “-” ＋ “1”)
    eday ＝ 增减时间 (datestr, #月份, 1)
    eday ＝ 增减时间 (eday, #日, -1)
    返回 (取日 (eday))

.子程序 时间_取第几周, , 公开, 查询指定日期是第几周
    .参数 参_指定日期, 日期时间型, 可空
    .参数 参_当月第几周, 整数型, 参考 可空
    .参数 参_当年第几周, 整数型, 参考 可空
    .局部变量 局_时间, 日期时间型

    局_时间 ＝ 选择 (是否为空 (参_指定日期), 取现行时间 (), 参_指定日期)
    参_当月第几周 ＝ 到整数 (编码_运行JS代码 (#时间取周数, “getMonthWeek”, 到文本 (取年份 (局_时间)), 到文本 (取月份 (局_时间)), 到文本 (取日 (局_时间)), ))
    参_当年第几周 ＝ 到整数 (编码_运行JS代码 (#时间取周数, “getYearWeek”, 到文本 (取年份 (局_时间)), 到文本 (取月份 (局_时间)), 到文本 (取日 (局_时间)), ))

.子程序 时间_取月份, 文本型, 公开, 返回一个月份,源码由论坛用户【shandmail 】提供
    .参数 目标时间, 日期时间型, 可空, 如果空或为零时取下面时间的返回月份
    .参数 是否英文, 逻辑型, 可空
    .局部变量 月数, 整数型

    ' 命令出自：https://bbs.125.la/forum.php?mod=viewthread&tid=13928859
    目标时间 ＝ 选择 (是否为空 (目标时间), 取现行时间 (), 目标时间)
    月数 ＝ 取月份 (目标时间)
    .如果 (是否英文)
        返回 (多项选择 (月数, “January”, “February”, “March”, “April”, “May”, “June”, “July”, “August”, “September”, “October”, “November”, “December”))
    .否则
        返回 (多项选择 (月数, “一月”, “二月”, “三月”, “四月”, “五月”, “六月”, “七月”, “八月”, “九月”, “十月”, “十一月”, “十二月”))
    .如果结束
    

.子程序 时间_JSON转为时间, 日期时间型, 公开, 把JSON数据格式的日期（YYYY-MM-DDTHH:mm:ss.sssZ）转北京时间
    .参数 JSON时间, 文本型, , JSON数据格式的日期（YYYY-MM-DDTHH:mm:ss.sssZ）
    .局部变量 局_UTC, 文本型
    .局部变量 局_时间, 日期时间型

    局_UTC ＝ JSON时间
    局_UTC ＝ 文本_取左边 (局_UTC, “.”, , ) ' 2019-08-19T01:41:38.000Z
    局_UTC ＝ 子文本替换 (局_UTC, “T”, 字符 (32), , , 真)
    局_时间 ＝ 到时间 (局_UTC)
    局_时间 ＝ 增减时间 (局_时间, #小时, 8) ' 东八区
    返回 (局_时间)

.子程序 时间_时间转为JSON, 文本型, 公开, 格式化为JSON数据格式（YYYY-MM-DDTHH:mm:ss.sssZ）
    .参数 时间, 日期时间型, 可空, 留空时间默认取系统UTC时间格式。不为空请先减8个小时（东8区）。
    .局部变量 局_系统时间, 精易_时间
    .局部变量 局_年, 文本型
    .局部变量 局_月, 文本型
    .局部变量 局_日, 文本型
    .局部变量 局_时, 文本型
    .局部变量 局_分, 文本型
    .局部变量 局_秒, 文本型
    .局部变量 局_毫, 文本型

    .如果 (是否为空 (时间))
        GetSystemTime (局_系统时间) ' 系统时间以协调世界时（UTC）表示
    .否则
        VariantTimeToSystemTime (时间, 局_系统时间)
    .如果结束
    局_年 ＝ 文本_自动补零 (到文本 (局_系统时间.年), 4)
    局_月 ＝ 文本_自动补零 (到文本 (局_系统时间.月), 2)
    局_日 ＝ 文本_自动补零 (到文本 (局_系统时间.日), 2)
    局_时 ＝ 文本_自动补零 (到文本 (局_系统时间.时), 2)
    局_分 ＝ 文本_自动补零 (到文本 (局_系统时间.分), 2)
    局_秒 ＝ 文本_自动补零 (到文本 (局_系统时间.秒), 2)
    局_毫 ＝ 文本_自动补零 (到文本 (局_系统时间.毫), 3)
    返回 (局_年 ＋ “-” ＋ 局_月 ＋ “-” ＋ 局_日 ＋ “T” ＋ 局_时 ＋ “:” ＋ 局_分 ＋ “:” ＋ 局_秒 ＋ “.” ＋ 局_毫 ＋ “Z”) ' toJSON

.子程序 时间_判断二十四节气, 文本型, 公开, 返回文本型：若节气日，返回节气名称；不是节气日返回空。注：判断日期范围为1901年1月1日—2099年12月31日
    .参数 日期, 日期时间型
    .局部变量 年, 整数型
    .局部变量 月, 整数型
    .局部变量 日, 整数型
    .局部变量 节气判断, 文本型

    ' https://bbs.125.la/thread-14459286-1-1.html
    节气判断 ＝ “”
    年 ＝ 取年份 (日期)
    月 ＝ 取月份 (日期)
    日 ＝ 取日 (日期)
    .如果真 (年 ≥ 2000 且 年 ＜ 2100) ' 21世纪判断
        年 ＝ 年 － 2000
        .如果真 (月 ＝ 2 且 日 ＝ 取整 (年 × 0.2422 ＋ 3.87) － 取整 (年 ÷ 4))
            节气判断 ＝ “立春”
        .如果真结束
        .如果真 (月 ＝ 2 且 日 ＝ 取整 (年 × 0.2422 ＋ 18.73) － 取整 (年 ÷ 4) 且 年 ≠ 26)
            节气判断 ＝ “雨水”
        .如果真结束
        .如果真 (月 ＝ 3 且 日 ＝ 取整 (年 × 0.2422 ＋ 5.63) － 取整 (年 ÷ 4))
            节气判断 ＝ “惊蛰”
        .如果真结束
        .如果真 (月 ＝ 3 且 日 ＝ 取整 (年 × 0.2422 ＋ 20.646) － 取整 (年 ÷ 4) 且 年 ≠ 84)
            节气判断 ＝ “春分”
        .如果真结束
        .如果真 (月 ＝ 4 且 日 ＝ 取整 (年 × 0.2422 ＋ 4.81) － 取整 (年 ÷ 4))
            节气判断 ＝ “清明”
        .如果真结束
        .如果真 (月 ＝ 4 且 日 ＝ 取整 (年 × 0.2422 ＋ 20.1) － 取整 (年 ÷ 4))
            节气判断 ＝ “谷雨”
        .如果真结束
        .如果真 (月 ＝ 5 且 日 ＝ 取整 (年 × 0.2422 ＋ 5.52) － 取整 (年 ÷ 4))
            节气判断 ＝ “立夏”
        .如果真结束
        .如果真 (月 ＝ 5 且 日 ＝ 取整 (年 × 0.2422 ＋ 21.04) － 取整 (年 ÷ 4) 且 年 ≠ 8)
            节气判断 ＝ “小满”
        .如果真结束
        .如果真 (月 ＝ 6 且 日 ＝ 取整 (年 × 0.2422 ＋ 5.678) － 取整 (年 ÷ 4))
            节气判断 ＝ “芒种”
        .如果真结束
        .如果真 (月 ＝ 6 且 日 ＝ 取整 (年 × 0.2422 ＋ 21.37) － 取整 (年 ÷ 4))
            节气判断 ＝ “夏至”
        .如果真结束
        .如果真 (月 ＝ 7 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.108) － 取整 (年 ÷ 4) 且 年 ≠ 16)
            节气判断 ＝ “小暑”
        .如果真结束
        .如果真 (月 ＝ 7 且 日 ＝ 取整 (年 × 0.2422 ＋ 22.83) － 取整 (年 ÷ 4))
            节气判断 ＝ “大暑”
        .如果真结束
        .如果真 (月 ＝ 8 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.5) － 取整 (年 ÷ 4) 且 年 ≠ 2)
            节气判断 ＝ “立秋”
        .如果真结束
        .如果真 (月 ＝ 8 且 日 ＝ 取整 (年 × 0.2422 ＋ 23.13) － 取整 (年 ÷ 4))
            节气判断 ＝ “处暑”
        .如果真结束
        .如果真 (月 ＝ 9 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.646) － 取整 (年 ÷ 4))
            节气判断 ＝ “白露”
        .如果真结束
        .如果真 (月 ＝ 9 且 日 ＝ 取整 (年 × 0.2422 ＋ 23.042) － 取整 (年 ÷ 4))
            节气判断 ＝ “秋分”
        .如果真结束
        .如果真 (月 ＝ 10 且 日 ＝ 取整 (年 × 0.2422 ＋ 8.318) － 取整 (年 ÷ 4))
            节气判断 ＝ “寒露”
        .如果真结束
        .如果真 (月 ＝ 10 且 日 ＝ 取整 (年 × 0.2422 ＋ 23.438) － 取整 (年 ÷ 4) 且 年 ≠ 89)
            节气判断 ＝ “霜降”
        .如果真结束
        .如果真 (月 ＝ 11 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.438) － 取整 (年 ÷ 4) 且 年 ≠ 89)
            节气判断 ＝ “立冬”
        .如果真结束
        .如果真 (月 ＝ 11 且 日 ＝ 取整 (年 × 0.2422 ＋ 22.36) － 取整 (年 ÷ 4))
            节气判断 ＝ “小雪”
        .如果真结束
        .如果真 (月 ＝ 12 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.18) － 取整 (年 ÷ 4))
            节气判断 ＝ “大雪”
        .如果真结束
        .如果真 (月 ＝ 12 且 日 ＝ 取整 (年 × 0.2422 ＋ 21.94) － 取整 (年 ÷ 4) 且 年 ≠ 21)
            节气判断 ＝ “冬至”
        .如果真结束
        .如果真 (月 ＝ 1 且 日 ＝ 取整 (年 × 0.2422 ＋ 5.4055) － 取整 (年 ÷ 4) 且 年 ≠ 19)
            节气判断 ＝ “小寒”
        .如果真结束
        .如果真 (月 ＝ 1 且 日 ＝ 取整 (年 × 0.2422 ＋ 20.12) － 取整 (年 ÷ 4) 且 年 ≠ 82)
            节气判断 ＝ “大寒”
        .如果真结束
        ' 以下为21世纪特殊年份判断
        .如果真 (年 ＝ 26 且 月 ＝ 2 且 日 ＋ 1 ＝ 取整 (年 × 0.2422 ＋ 18.73) － 取整 (年 ÷ 4))
            节气判断 ＝ “雨水”
        .如果真结束
        .如果真 (年 ＝ 84 且 月 ＝ 2 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 20.64) － 取整 (年 ÷ 4))
            节气判断 ＝ “春风”
        .如果真结束
        .如果真 (年 ＝ 8 且 月 ＝ 2 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 21.04) － 取整 (年 ÷ 4))
            节气判断 ＝ “小满”
        .如果真结束
        .如果真 (年 ＝ 16 且 月 ＝ 2 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 7.108) － 取整 (年 ÷ 4))
            节气判断 ＝ “小暑”
        .如果真结束
        .如果真 (年 ＝ 2 且 月 ＝ 2 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 7.5) － 取整 (年 ÷ 4))
            节气判断 ＝ “立秋”
        .如果真结束
        .如果真 (年 ＝ 89 且 月 ＝ 2 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 23.438) － 取整 (年 ÷ 4))
            节气判断 ＝ “霜降”
        .如果真结束
        .如果真 (年 ＝ 89 且 月 ＝ 2 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 7.438) － 取整 (年 ÷ 4))
            节气判断 ＝ “立冬”
        .如果真结束
        .如果真 (年 ＝ 21 且 月 ＝ 2 且 日 ＋ 1 ＝ 取整 (年 × 0.2422 ＋ 21.94) － 取整 (年 ÷ 4))
            节气判断 ＝ “冬至”
        .如果真结束
        .如果真 (年 ＝ 19 且 月 ＝ 2 且 日 ＋ 1 ＝ 取整 (年 × 0.2422 ＋ 5.4055) － 取整 (年 ÷ 4))
            节气判断 ＝ “小寒”
        .如果真结束
        .如果真 (年 ＝ 82 且 月 ＝ 2 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 20.12) － 取整 (年 ÷ 4))
            节气判断 ＝ “大寒”
        .如果真结束
        
    .如果真结束
    .如果真 (年 ＞ 1900 且 年 ＜ 2000) ' 20世纪判断
        年 ＝ 年 － 1900
        .如果真 (月 ＝ 2 且 日 ＝ 取整 (年 × 0.2422 ＋ 4.6295) － 取整 (年 ÷ 4))
            节气判断 ＝ “立春”
        .如果真结束
        .如果真 (月 ＝ 2 且 日 ＝ 取整 (年 × 0.2422 ＋ 19.4599) － 取整 (年 ÷ 4))
            节气判断 ＝ “雨水”
        .如果真结束
        .如果真 (月 ＝ 3 且 日 ＝ 取整 (年 × 0.2422 ＋ 6.3826) － 取整 (年 ÷ 4))
            节气判断 ＝ “惊蛰”
        .如果真结束
        .如果真 (月 ＝ 3 且 日 ＝ 取整 (年 × 0.2422 ＋ 21.4155) － 取整 (年 ÷ 4))
            节气判断 ＝ “春分”
        .如果真结束
        .如果真 (月 ＝ 4 且 日 ＝ 取整 (年 × 0.2422 ＋ 5.59) － 取整 (年 ÷ 4))
            节气判断 ＝ “清明”
        .如果真结束
        .如果真 (月 ＝ 4 且 日 ＝ 取整 (年 × 0.2422 ＋ 20.888) － 取整 (年 ÷ 4))
            节气判断 ＝ “谷雨”
        .如果真结束
        .如果真 (月 ＝ 5 且 日 ＝ 取整 (年 × 0.2422 ＋ 6.318) － 取整 (年 ÷ 4) 且 年 ≠ 11)
            节气判断 ＝ “立夏”
        .如果真结束
        .如果真 (月 ＝ 5 且 日 ＝ 取整 (年 × 0.2422 ＋ 21.86) － 取整 (年 ÷ 4))
            节气判断 ＝ “小满”
        .如果真结束
        .如果真 (月 ＝ 6 且 日 ＝ 取整 (年 × 0.2422 ＋ 6.5) － 取整 (年 ÷ 4) 且 年 ≠ 2)
            节气判断 ＝ “芒种”
        .如果真结束
        .如果真 (月 ＝ 6 且 日 ＝ 取整 (年 × 0.2422 ＋ 22.2) － 取整 (年 ÷ 4) 且 年 ≠ 28)
            节气判断 ＝ “夏至”
        .如果真结束
        .如果真 (月 ＝ 7 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.928) － 取整 (年 ÷ 4) 且 年 ≠ 25)
            节气判断 ＝ “小暑”
        .如果真结束
        .如果真 (月 ＝ 7 且 日 ＝ 取整 (年 × 0.2422 ＋ 23.65) － 取整 (年 ÷ 4) 且 年 ≠ 22)
            节气判断 ＝ “大暑”
        .如果真结束
        .如果真 (月 ＝ 8 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.5) － 取整 (年 ÷ 4))
            节气判断 ＝ “立秋”
        .如果真结束
        .如果真 (月 ＝ 8 且 日 ＝ 取整 (年 × 0.2422 ＋ 23.95) － 取整 (年 ÷ 4))
            节气判断 ＝ “处暑”
        .如果真结束
        .如果真 (月 ＝ 9 且 日 ＝ 取整 (年 × 0.2422 ＋ 8.44) － 取整 (年 ÷ 4) 且 年 ≠ 27)
            节气判断 ＝ “白露”
        .如果真结束
        .如果真 (月 ＝ 9 且 日 ＝ 取整 (年 × 0.2422 ＋ 23.822) － 取整 (年 ÷ 4) 且 年 ≠ 42)
            节气判断 ＝ “秋分”
        .如果真结束
        .如果真 (月 ＝ 10 且 日 ＝ 取整 (年 × 0.2422 ＋ 9.098) － 取整 (年 ÷ 4))
            节气判断 ＝ “寒露”
        .如果真结束
        .如果真 (月 ＝ 10 且 日 ＝ 取整 (年 × 0.2422 ＋ 24.218) － 取整 (年 ÷ 4))
            节气判断 ＝ “霜降”
        .如果真结束
        .如果真 (月 ＝ 11 且 日 ＝ 取整 (年 × 0.2422 ＋ 8.218) － 取整 (年 ÷ 4))
            节气判断 ＝ “立冬”
        .如果真结束
        .如果真 (月 ＝ 11 且 日 ＝ 取整 (年 × 0.2422 ＋ 23.08) － 取整 (年 ÷ 4) 且 年 ≠ 78)
            节气判断 ＝ “小雪”
        .如果真结束
        .如果真 (月 ＝ 12 且 日 ＝ 取整 (年 × 0.2422 ＋ 7.9) － 取整 (年 ÷ 4) 且 年 ≠ 54)
            节气判断 ＝ “大雪”
        .如果真结束
        .如果真 (月 ＝ 12 且 日 ＝ 取整 (年 × 0.2422 ＋ 22.6) － 取整 (年 ÷ 4) 且 年 ≠ 18)
            节气判断 ＝ “冬至”
        .如果真结束
        .如果真 (月 ＝ 1 且 日 ＝ 取整 (年 × 0.2422 ＋ 6.11) － 取整 (年 ÷ 4) 且 年 ≠ 82)
            节气判断 ＝ “小寒”
        .如果真结束
        .如果真 (月 ＝ 1 且 日 ＝ 取整 (年 × 0.2422 ＋ 20.84) － 取整 (年 ÷ 4))
            节气判断 ＝ “大寒”
        .如果真结束
        ' 以下为20世纪特殊年份判断
        .如果真 (月 ＝ 5 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 6.318) － 取整 (年 ÷ 4) 且 年 ≠ 11)
            节气判断 ＝ “立夏”
        .如果真结束
        .如果真 (月 ＝ 6 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 6.5) － 取整 (年 ÷ 4) 且 年 ≠ 2)
            节气判断 ＝ “芒种”
        .如果真结束
        .如果真 (月 ＝ 6 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 22.2) － 取整 (年 ÷ 4) 且 年 ≠ 28)
            节气判断 ＝ “夏至”
        .如果真结束
        .如果真 (月 ＝ 7 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 7.928) － 取整 (年 ÷ 4) 且 年 ≠ 25)
            节气判断 ＝ “小暑”
        .如果真结束
        .如果真 (月 ＝ 7 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 23.65) － 取整 (年 ÷ 4) 且 年 ≠ 22)
            节气判断 ＝ “大暑”
        .如果真结束
        .如果真 (月 ＝ 9 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 8.44) － 取整 (年 ÷ 4) 且 年 ≠ 27)
            节气判断 ＝ “白露”
        .如果真结束
        .如果真 (月 ＝ 9 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 23.822) － 取整 (年 ÷ 4) 且 年 ≠ 42)
            节气判断 ＝ “秋分”
        .如果真结束
        .如果真 (月 ＝ 11 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 23.08) － 取整 (年 ÷ 4) 且 年 ≠ 78)
            节气判断 ＝ “小雪”
        .如果真结束
        .如果真 (月 ＝ 12 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 7.9) － 取整 (年 ÷ 4) 且 年 ≠ 54)
            节气判断 ＝ “大雪”
        .如果真结束
        .如果真 (月 ＝ 12 且 日 ＋ 1 ＝ 取整 (年 × 0.2422 ＋ 22.6) － 取整 (年 ÷ 4) 且 年 ≠ 18)
            节气判断 ＝ “冬至”
        .如果真结束
        .如果真 (月 ＝ 1 且 日 － 1 ＝ 取整 (年 × 0.2422 ＋ 6.11) － 取整 (年 ÷ 4) 且 年 ≠ 82)
            节气判断 ＝ “小寒”
        .如果真结束
        
    .如果真结束
    返回 (节气判断)

.子程序 时间_星期几取日期, 日期时间型, 公开, 返回指定时间段里指定星期几的日期
    .参数 参_时间, 日期时间型, 可空, 留空默认取当前时间
    .参数 参_星期几, 整数型, , 1-7，分别代表星期一到星期日
    .局部变量 局_星期几, 整数型
    .局部变量 局_天差, 整数型
    .局部变量 局_返回日期, 日期时间型

    ' https://bbs.125.la/thread-14479245-1-1.html
    .如果真 (是否为空 (参_时间))
        参_时间 ＝ 取现行时间 ()
    .如果真结束
    局_星期几 ＝ 取星期几 (参_时间)
    .判断开始 (局_星期几 ＝ 1)
        局_星期几 ＝ 7
    .默认
        局_星期几 ＝ 局_星期几 － 1
    .判断结束
    局_天差 ＝ 局_星期几 － 参_星期几
    局_返回日期 ＝ 增减时间 (参_时间, #日, －局_天差)
    返回 (局_返回日期)

