.版本 2

.程序集 集_DLL
.子程序 投递消息整数, , 公开
    .参数 窗口句柄, 整数型
    .参数 消息号, 整数型
    .参数 消息参数1, 整数型
    .参数 消息参数2, 整数型

    PostMessageA (窗口句柄, 消息号, 消息参数1, 消息参数2)

.子程序 发送消息整数, 整数型, 公开, 发送特定的消息到一个或多个窗口。该命令不会立即返回，只有在消息被处理完毕后才返回结果。返回值的具体含义随消息的不同而不同
    .参数 窗口句柄, 整数型, , 本参数指定接收消息的窗口的句柄
    .参数 消息号, 整数型, , 本参数指定欲发送的消息
    .参数 消息参数1, 整数型, , 本参数指定消息的第一个参数。具体含义随消息的不同而不同
    .参数 消息参数2, 整数型, , 本参数指定消息的第二个参数。具体含义随消息的不同而不同

    返回 (SendMessageA (窗口句柄, 消息号, 消息参数1, 消息参数2))

.子程序 发送消息文本, 整数型, 公开, 发送特定的消息到一个或多个窗口。该命令不会立即返回，只有在消息被处理完毕后才返回结果。返回值的具体含义随消息的不同而不同
    .参数 窗口句柄, 整数型, , 本参数指定接收消息的窗口的句柄
    .参数 消息号, 整数型, , 本参数指定欲发送的消息
    .参数 消息参数1, 整数型, , 本参数指定消息的第一个参数。具体含义随消息的不同而不同
    .参数 消息参数2, 文本型, , 本参数指定消息的第二个参数。具体含义随消息的不同而不同

    返回 (SendMessageA (窗口句柄, 消息号, 消息参数1, 取指针文本_ (消息参数2)))

.子程序 发送消息逻辑, 整数型, 公开, 发送特定的消息到一个或多个窗口。该命令不会立即返回，只有在消息被处理完毕后才返回结果。返回值的具体含义随消息的不同而不同
    .参数 窗口句柄, 整数型, , 本参数指定接收消息的窗口的句柄
    .参数 消息号, 整数型, , 本参数指定欲发送的消息
    .参数 消息参数1, 逻辑型, , 本参数指定消息的第一个参数。具体含义随消息的不同而不同
    .参数 消息参数2, 整数型, , 本参数指定消息的第二个参数。具体含义随消息的不同而不同

    返回 (SendMessageA (窗口句柄, 消息号, 到整数 (消息参数1), 消息参数2))

.子程序 API_取空白文本, 文本型, 公开, 返回具有指定数目半角空格的文本。
    .参数 重复次数, 整数型
    .局部变量 文本指针, 整数型

    ' 注：以下的代码仿效了易核心库命令“取空白文本”，只是填充空白字符0x20部分使用了API函数来实现。
    .如果真 (重复次数 ≤ 0)
        ' 这里嵌入汇编代码取代  返回 (“”)
        置入代码 ({ 51, 192, 139, 229, 93, 194, 4, 0 })
        ' XOR EAX,EAX
        ' MOV ESP,EBP
        ' POP EBP
        ' RETN 04
    .如果真结束
    
    ' 分配可用内存空间，+1是为了填充文本终止符“0”。
    文本指针 ＝ HeapAlloc (GetProcessHeap (), 0, 重复次数 ＋ 1)
    
    .如果真 (文本指针 ＝ 0)
        ' 这里嵌入汇编代码取代  返回 (“”)
        置入代码 ({ 139, 229, 93, 194, 4, 0 })
        ' MOV ESP,EBP
        ' POP EBP
        ' RETN 04
    .如果真结束
    ' 以空白字符0x20填充分配的内存块字节数据，这里使用了API函数，当然也可以用嵌入汇编来实现。
    RtlFillMemory_字节 (文本指针, 重复次数, 32)
    ' 嵌入汇编代码以替换掉原来的返回代码，并添加文本终止符标志“0”。
    ' 如用“指针到文本”命令会使效率大大减低，不推荐使用。
    置入代码 ({ 139, 69, 8, 3, 69, 252, 198, 0, 0, 139, 69, 252, 139, 229, 93, 194, 4, 0 })
    ' 至于释放内存就交给易IDE了，使用HeapAlloc申请内存的目的就在这里，因为易IDE也是使用HeapAlloc
    ' 申请内存的，最终易IDE会帮我们使用HeapFree释放内存。哈哈！是不是有点欺骗易IDE的味道。
    返回 (“”)

.子程序 API_取空白字节集, 字节集, 公开, 返回具有特定数目 0 字节的字节集
    .参数 零字节数目, 整数型
    .局部变量 字节集指针, 整数型

    .如果真 (零字节数目 ≤ 0)
        ' 这里嵌入汇编代码取代  返回 ({ })
        置入代码 ({ 51, 192, 139, 229, 93, 194, 4, 0 })
        ' XOR EAX,EAX
        ' MOV ESP,EBP
        ' POP EBP
        ' RETN 04
    .如果真结束
    ' 分配可用内存空间，+8是为了填充字节集类型标志
    字节集指针 ＝ HeapAlloc (GetProcessHeap (), 8, 零字节数目 ＋ 8) ' #HEAP_ZERO_MEMORY=8
    .如果真 (字节集指针 ＝ 0)
        ' 这里嵌入汇编代码取代  返回 ({ })
        置入代码 ({ 139, 229, 93, 194, 4, 0 })
        ' MOV ESP,EBP
        ' POP EBP
        ' RETN 04
    .如果真结束
    
    ' 嵌入汇编代码以替换掉原来的返回代码，并添加字节集类型标志。
    置入代码 ({ 254, 0, 255, 117, 8, 143, 64, 4, 139, 229, 93, 194, 4, 0 })
    返回 ({ })

