.版本 2

.程序集 类_编辑框菜单, , 公开, 对自身程序编辑框有效
.程序集变量 New_call, 整数型
.程序集变量 程_菜单结构, 菜单结构, , "0"

.子程序 _初始化, , , 1
    

.子程序 _销毁, , , 2
    移除全部 ()

.子程序 NewWndProc, 整数型, , 3
    .参数 hWnd, 整数型
    .参数 uMsg, 整数型
    .参数 wParam, 整数型
    .参数 lParam, 整数型
    .局部变量 局_是否弹出, 逻辑型, 静态
    .局部变量 局_菜单句柄, 整数型, 静态
    .局部变量 局_总数, 整数型
    .局部变量 i, 整数型
    .局部变量 局_标识, 整数型
    .局部变量 局_二级菜单, 整数型
    .局部变量 索引, 整数型

    索引 ＝ 内_取索引 (hWnd)
    .如果真 (索引 ≠ 0)
        局_总数 ＝ 取数组成员数 (程_菜单结构 [索引].菜单数据)
    .如果真结束
    .判断开始 (uMsg ＝ #WM_ENTERIDLE 且 wParam ＝ #MSGF_MENU 且 局_菜单句柄 ＝ 0)
        .如果真 (局_是否弹出)
            局_菜单句柄 ＝ SendMessageA (lParam, #MN_GETHMENU, 0, 0) ' 16=下拉,0无下拉,2048=分割线，256=无位图菜单，64=横宽菜单
            局_二级菜单 ＝ CreatePopupMenu ()
            InsertMenuA (局_菜单句柄, 程_菜单结构 [索引].插入位置, #MF_BYPOSITION ＋ #MF_POPUP ＋ #MF_STRING, 局_二级菜单, 程_菜单结构 [索引].菜单标题)
            .计次循环首 (局_总数, i)
                局_标识 ＝ i ＋ 2018
                .如果 (程_菜单结构 [索引].菜单数据 [i].是否插入)
                    InsertMenuA (局_二级菜单, i － 1, #MF_STRING, 局_标识, 程_菜单结构 [索引].菜单数据 [i].名称)
                .否则
                    AppendMenuA (局_菜单句柄, #MF_STRING, 局_标识, 程_菜单结构 [索引].菜单数据 [i].名称)
                .如果结束
                
                
            .计次循环尾 ()
        .如果真结束
        
    .默认
        .计次循环首 (局_总数, i)
            .如果真 (uMsg ＝ i ＋ 2018) ' 2018
                .如果真 (到整数 (程_菜单结构 [索引].菜单数据 [i].事件) ＞ 0)
                    程序_执行整数子程序 (程_菜单结构 [索引].菜单数据 [i].事件, hWnd)
                .如果真结束
                
            .如果真结束
            
        .计次循环尾 ()
    .判断结束
    .如果真 (uMsg ＝ #WM_CONTEXTMENU)
        局_菜单句柄 ＝ 0
        局_是否弹出 ＝ SendMessageA (hWnd, #WM_NCHITTEST, 0, lParam) ≠ #HTVSCROLL
    .如果真结束
    返回 (CallWindowProcA (程_菜单结构 [索引].原回调地址, hWnd, uMsg, wParam, lParam))

.子程序 添加编辑框, , 公开, 插入一个主菜单
    .参数 参_编辑框句柄, 整数型
    .参数 参_插入位置, 整数型
    .参数 参_菜单标题, 文本型
    .局部变量 局_结构, 菜单结构

    .如果真 (内_取索引 (参_编辑框句柄) ≠ 0)
        返回 ()
    .如果真结束
    .如果真 (New_call ＝ 0)
        New_call ＝ 类_取内部方法地址 (3)
    .如果真结束
    局_结构.窗口句柄 ＝ 参_编辑框句柄
    局_结构.菜单标题 ＝ 参_菜单标题
    局_结构.插入位置 ＝ 参_插入位置
    清除数组 (局_结构.菜单数据)
    局_结构.原回调地址 ＝ SetWindowLongA (参_编辑框句柄, #GWL_WNDPROC, New_call)
    加入成员 (程_菜单结构, 局_结构)

.子程序 内_取索引, 整数型
    .参数 句柄, 整数型
    .局部变量 i, 整数型

    .计次循环首 (取数组成员数 (程_菜单结构), i)
        .如果真 (程_菜单结构 [i].窗口句柄 ＝ 句柄)
            返回 (i)
        .如果真结束
        
    .计次循环尾 ()
    返回 (0)

.子程序 添加子菜单, , 公开, 添加二级菜单
    .参数 参_编辑框句柄, 整数型
    .参数 参_菜单消息, 菜单消息
    .局部变量 索引, 整数型

    索引 ＝ 内_取索引 (参_编辑框句柄)
    .如果真 (索引 ≠ 0)
        加入成员 (程_菜单结构 [索引].菜单数据, 参_菜单消息)
    .如果真结束
    

.子程序 重置菜单, , 公开
    .参数 参_编辑框句柄, 整数型
    .局部变量 索引, 整数型

    索引 ＝ 内_取索引 (参_编辑框句柄)
    .如果真 (索引 ≠ 0)
        清除数组 (程_菜单结构 [索引].菜单数据)
    .如果真结束
    

.子程序 移除编辑框, , 公开
    .参数 参_编辑框句柄, 整数型
    .局部变量 索引, 整数型

    索引 ＝ 内_取索引 (参_编辑框句柄)
    .如果真 (索引 ≠ 0)
        SetWindowLongA (程_菜单结构 [索引].窗口句柄, #GWL_WNDPROC, 程_菜单结构 [索引].原回调地址)
        删除成员 (程_菜单结构, 索引, 1)
    .如果真结束
    

.子程序 移除全部
    .局部变量 i, 整数型
    .局部变量 数量, 整数型

    数量 ＝ 取数组成员数 (程_菜单结构)
    .计次循环首 (数量, i)
        移除编辑框 (数量 － i ＋ 1)
    .计次循环尾 ()

