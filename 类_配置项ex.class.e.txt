.版本 2

.程序集 类_配置项ex, , 公开, 本类由【果子】编写，简约读写配置项
.程序集变量 集_配置文件路径, 文本型
.程序集变量 集_加密密码, 文本型
.程序集变量 集_默认节名称, 文本型
.程序集变量 集_节名前缀, 文本型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    集_默认节名称 ＝ “System”

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 初始化, , 公开, 可重复初始化，重复初始化将直接覆盖前面的参数值
    .参数 配置文件路径, 文本型, 可空, 可空，默认为“程序根目录\Data\Config.ini”
    .参数 加密密码, 文本型, 可空, 可空，为空=不加密
    .参数 节名前缀, 文本型, 可空, 可空
    .局部变量 dir, 文本型

    .判断开始 (是否为空 (配置文件路径) 或 删首尾空 (配置文件路径) ＝ “”)
        目录_创建 (取运行目录 () ＋ “\Data”)
        集_配置文件路径 ＝ 取运行目录 () ＋ “\Data\Config.ini”
        
    .默认
        集_配置文件路径 ＝ 配置文件路径
        dir ＝ 目录_取父路径 (配置文件路径)
        .如果真 (目录_是否存在 (dir) ＝ 假)
            目录_创建 (dir)
        .如果真结束
        
    .判断结束
    集_加密密码 ＝ 删首尾空 (加密密码)
    集_节名前缀 ＝ 节名前缀

.子程序 写, 逻辑型, 公开
    .参数 节名称, 文本型, 可空, 可空，默认为System，包含欲写入配置项所处节的名称。
    .参数 配置项名称, 文本型, 可空, 可以被省略。参数值指定欲写入配置项在其节中的名称。如果参数值被省略，则删除指定节及其下的所有配置项。
    .参数 欲写入值, 文本型, 可空, 可以被省略。参数值指定欲写入到指定配置项中的文本。如果参数值被省略，则删除所指定配置项。
    .参数 加密, 逻辑型, 可空, 用系统自动的密码进行加密。
    .局部变量 局_文本, 文本型

    .如果真 (是否为空 (节名称) 或 删首尾空 (节名称) ＝ “”)
        节名称 ＝ 集_默认节名称
    .如果真结束
    
    .判断开始 (是否为空 (配置项名称) 或 删首尾空 (配置项名称) ＝ “”)
        返回 (写配置项 (集_配置文件路径, 集_节名前缀 ＋ 节名称, , ))
        
    .判断 (是否为空 (欲写入值))
        返回 (写配置项 (集_配置文件路径, 集_节名前缀 ＋ 节名称, 配置项名称, ))
    .默认
        
    .判断结束
    
    .如果真 (删首尾空 (配置项名称) ＝ “”)
        输出调试文本 (“配置项类：写失败，原因：【配置项名称】为空”)
        返回 (假)
    .如果真结束
    局_文本 ＝ 删首尾空 (子文本替换 (欲写入值, #换行符, “{#换行符}”, , , 真))
    
    
    .判断开始 (集_加密密码 ≠ “”)
        局_文本 ＝ 文本_加密 (局_文本, 集_加密密码)
        
    .判断 (加密)
        局_文本 ＝ 文本_解密 (局_文本, “Wiha4w” ＋ 到文本 (4842 ÷ 5 × 564))
        
    .判断 (配置项名称 ＝ “User” 或 配置项名称 ＝ “Pass”) ' 如果是用户名，自动加密
        局_文本 ＝ 文本_加密 (局_文本, “Wiha4w” ＋ 到文本 (4842 ÷ 5 × 564))
    .默认
        
    .判断结束
    返回 (写配置项 (集_配置文件路径, 集_节名前缀 ＋ 节名称, 配置项名称, 局_文本))

.子程序 读, 文本型, 公开
    .参数 节名称, 文本型, 可空, 可空，默认为System，包含欲写入配置项所处节的名称。
    .参数 配置项名称, 文本型, , 参数值指定欲读入配置项在其节中的名称。
    .参数 默认文本, 文本型, 可空, 可以被省略。如果指定配置项不存在，将返回此默认文本。如果指定配置项不存在且本参数被省略，将返回空文本。
    .参数 解密, 逻辑型, 可空, 如果你在写的时候加密了，这里同样也要解决，用系统自动的密码进行解密。
    .局部变量 局_文本, 文本型
    .局部变量 局_默认文本, 文本型
    .局部变量 局_对比结果, 逻辑型

    .如果真 (是否为空 (节名称) 或 删首尾空 (节名称) ＝ “”)
        节名称 ＝ 集_默认节名称
    .如果真结束
    .如果真 (删首尾空 (配置项名称) ＝ “”)
        输出调试文本 (“配置项类：读失败，原因：【配置项名称】为空”)
        返回 (“”)
    .如果真结束
    局_默认文本 ＝ 取重复文本 (10, 字符 (1))
    局_文本 ＝ 读配置项 (集_配置文件路径, 集_节名前缀 ＋ 节名称, 配置项名称, 局_默认文本)
    局_对比结果 ＝ 局_文本 ＝ 局_默认文本
    .如果真 (局_对比结果)
        局_文本 ＝ 默认文本
    .如果真结束
    局_文本 ＝ 子文本替换 (局_文本, “{#换行符}”, #换行符, , , 真)
    局_文本 ＝ 子文本替换 (局_文本, “><br><”, #换行符, , , 真)
    .判断开始 (集_加密密码 ≠ “” 且 局_对比结果 ＝ 假)
        局_文本 ＝ 文本_解密 (局_文本, 集_加密密码)
        
    .判断 (解密)
        局_文本 ＝ 文本_解密 (局_文本, “Wiha4w” ＋ 到文本 (4842 ÷ 5 × 564))
        
    .判断 (配置项名称 ＝ “User” 或 配置项名称 ＝ “Pass”) ' 如果是用户名，自动加密
        局_文本 ＝ 文本_解密 (局_文本, “Wiha4w” ＋ 到文本 (4842 ÷ 5 × 564))
    .默认
        
    .判断结束
    返回 (局_文本)

.子程序 置_默认节名称, , 公开, 默认节名称，当节名称留空时，以此节名称为主
    .参数 默认节名称, 文本型, , 当节名称留空时，以此节名称为主

    集_默认节名称 ＝ 删全部空 (默认节名称)

.子程序 置_节名前缀, , 公开
    .参数 节名前缀, 文本型

    集_节名前缀 ＝ 节名前缀

.子程序 批量保存, 逻辑型, 公开, 【编辑框，选择框，单选框，组合框】保存指定窗口上指定组件的内容，与"批量读取()"命令一起用的。【特别注意：组件的[标识]将作为保存配置的项名称，标识为空将不保存】
    .参数 父窗口, 窗口, , 直接填写顶级窗口就行了，如:_启动窗口。
    .参数 组件类型, 文本型, , 如：“编辑框、组合框、单选框”等等，多个组件请使用半角逗号分隔开。
    .局部变量 局_寻找句柄, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_编辑框, 编辑框
    .局部变量 局_单选框, 单选框
    .局部变量 局_选择框, 选择框
    .局部变量 局_组合框, 组合框
    .局部变量 局_选择夹, 选择夹
    .局部变量 局_文本, 文本型
    .局部变量 局_组件数量, 整数型
    .局部变量 局_配置项名称, 文本型
    .局部变量 Arr, 文本型, , "0"

    .如果真 (是否已创建 (父窗口) ＝ 假)
        输出调试文本 (“配置项，批量保存失败，原因：参数【父窗口】为载入！”)
        返回 (假)
    .如果真结束
    
    .如果真 (删首尾空 (组件类型) ＝ “”)
        输出调试文本 (“配置项，批量保存失败，原因：参数【组件类型】为空！”)
        返回 (假)
    .如果真结束
    
    .判断开始 (寻找文本 (组件类型, “,”, , 假) ≠ -1)
        Arr ＝ 分割文本 (组件类型, “,”, )
        .计次循环首 (取数组成员数 (Arr), 局_计次)
            批量保存 (父窗口, Arr [局_计次])
        .计次循环尾 ()
        
    .默认
        局_寻找句柄 ＝ 寻找组件 (父窗口, , 组件类型, , )
        局_组件数量 ＝ 取找到组件数目 (局_寻找句柄)
        .计次循环首 (局_组件数量, 局_计次)
            .判断开始 (组件类型 ＝ “选择框”)
                局_选择框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_选择框.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                写 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, 到文本 (局_选择框.选中))
                
            .判断 (组件类型 ＝ “单选框”)
                局_单选框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_单选框.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                写 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, 到文本 (局_单选框.选中))
                
            .判断 (组件类型 ＝ “组合框”)
                局_组合框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_组合框.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                写 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, 到文本 (局_组合框.现行选中项))
                .如果真 (局_组合框.现行选中项 ＝ -1 且 局_组合框.内容 ≠ “”)
                    写 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称 ＋ “-内容”, 局_组合框.内容, )
                .如果真结束
                
            .判断 (组件类型 ＝ “编辑框”)
                局_编辑框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_编辑框.标记)
                局_文本 ＝ 子文本替换 (局_编辑框.内容, #换行符, “><br><”, , , 真)
                局_文本 ＝ 子文本替换 (局_文本, #引号, “%22”, , , 真)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                写 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, 局_文本, )
                
            .判断 (组件类型 ＝ “选择夹”)
                局_选择夹 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_选择夹.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                写 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, 到文本 (局_选择夹.现行子夹), )
            .默认
                
            .判断结束
            
        .计次循环尾 ()
    .判断结束
    返回 (真)

.子程序 批量读取, 逻辑型, 公开, 读取指定窗口上指定组件的配置内容，与"批量保存()"命令一起用的。【特别注意：组件的[标识]将作为读取配置的项名称，标识为空将不读取】
    .参数 父窗口, 窗口, , 直接填写顶级窗口就行了，如:_启动窗口。
    .参数 组件类型, 文本型, , 如：“编辑框、组合框、单选框”等等，多个组件请使用半角逗号分隔开。
    .参数 例外组件, 文本型, 可空, 请输入不需要读取的组件标识名，如：User,Pass  多个请使用半角逗号分隔开
    .局部变量 局_寻找句柄, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_编辑框, 编辑框
    .局部变量 局_单选框, 单选框
    .局部变量 局_选择框, 选择框
    .局部变量 局_组合框, 组合框
    .局部变量 局_选择夹, 选择夹
    .局部变量 局_组件数量, 整数型
    .局部变量 局_文本, 文本型
    .局部变量 局_配置项名称, 文本型
    .局部变量 Arr, 文本型, , "0"

    .如果真 (是否已创建 (父窗口) ＝ 假)
        输出调试文本 (“配置项，批量保存失败，原因：参数【父窗口】为载入！”)
        返回 (假)
    .如果真结束
    .如果真 (删首尾空 (组件类型) ＝ “”)
        输出调试文本 (“配置项，批量保存失败，原因：参数【组件类型】为空！”)
        返回 (假)
    .如果真结束
    
    .如果真 (删首尾空 (例外组件) ≠ “” 且 取文本右边 (例外组件, 1) ≠ “,”)
        例外组件 ＝ 例外组件 ＋ “,”
    .如果真结束
    
    
    .判断开始 (寻找文本 (组件类型, “,”, , 假) ≠ -1)
        Arr ＝ 分割文本 (组件类型, “,”, )
        .计次循环首 (取数组成员数 (Arr), 局_计次)
            批量读取 (父窗口, Arr [局_计次], 例外组件)
        .计次循环尾 ()
        
    .默认
        局_寻找句柄 ＝ 寻找组件 (父窗口, , 组件类型, , )
        局_组件数量 ＝ 取找到组件数目 (局_寻找句柄)
        .计次循环首 (局_组件数量, 局_计次)
            .判断开始 (组件类型 ＝ “选择框”)
                局_选择框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_选择框.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                .如果真 (例外组件 ≠ “”)
                    .如果真 (寻找文本 (例外组件, 局_配置项名称 ＋ “,”, , 假) ≠ -1)
                        到循环尾 ()
                    .如果真结束
                    
                .如果真结束
                局_文本 ＝ 读 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, “”)
                .如果真 (局_文本 ＝ “”)
                    到循环尾 ()
                .如果真结束
                局_选择框.选中 ＝ 局_文本 ＝ “真”
                
            .判断 (组件类型 ＝ “单选框”)
                局_单选框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_单选框.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                .如果真 (例外组件 ≠ “”)
                    .如果真 (寻找文本 (例外组件, 局_配置项名称 ＋ “,”, , 假) ≠ -1)
                        到循环尾 ()
                    .如果真结束
                    
                .如果真结束
                局_文本 ＝ 读 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, “”)
                .如果真 (局_文本 ＝ “”)
                    到循环尾 ()
                .如果真结束
                局_单选框.选中 ＝ 局_文本 ＝ “真”
                
            .判断 (组件类型 ＝ “组合框”)
                局_组合框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_组合框.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                .如果真 (例外组件 ≠ “”)
                    .如果真 (寻找文本 (例外组件, 局_配置项名称 ＋ “,”, , 假) ≠ -1)
                        到循环尾 ()
                    .如果真结束
                    
                .如果真结束
                局_文本 ＝ 读 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, “”)
                .如果真 (局_文本 ＝ “”)
                    到循环尾 ()
                .如果真结束
                
                .如果 (局_文本 ＝ “-1”)
                    局_组合框.内容 ＝ 读 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称 ＋ “-内容”, “”, )
                    
                .否则
                    局_组合框.现行选中项 ＝ 到数值 (局_文本)
                .如果结束
                
                
            .判断 (组件类型 ＝ “编辑框”)
                局_编辑框 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_编辑框.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                .如果真 (例外组件 ≠ “”)
                    .如果真 (寻找文本 (例外组件, 局_配置项名称 ＋ “,”, , 假) ≠ -1)
                        到循环尾 ()
                    .如果真结束
                    
                .如果真结束
                局_文本 ＝ 读 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, “”)
                .如果真 (局_文本 ＝ “”)
                    到循环尾 ()
                .如果真结束
                局_文本 ＝ 子文本替换 (局_文本, “%22”, #引号, , , 真)
                局_编辑框.内容 ＝ 子文本替换 (局_文本, “><br><”, #换行符, , , 真)
                
            .判断 (组件类型 ＝ “选择夹”)
                局_选择夹 ＝ 取所找到组件 (局_寻找句柄, 局_计次 － 1)
                局_配置项名称 ＝ 删首尾空 (局_选择夹.标记)
                .如果真 (局_配置项名称 ＝ “”)
                    到循环尾 ()
                .如果真结束
                .如果真 (例外组件 ≠ “”)
                    .如果真 (寻找文本 (例外组件, 局_配置项名称 ＋ “,”, , 假) ≠ -1)
                        到循环尾 ()
                    .如果真结束
                    
                .如果真结束
                局_文本 ＝ 读 (集_默认节名称, 组件类型 ＋ “-” ＋ 局_配置项名称, “”)
                .如果真 (局_文本 ＝ “”)
                    到循环尾 ()
                .如果真结束
                局_选择夹.现行子夹 ＝ 到数值 (局_文本)
            .默认
                
            .判断结束
            
        .计次循环尾 ()
    .判断结束
    返回 (真)

.子程序 窗口位置_保存, , 公开
    .参数 窗口, 窗口
    .参数 是否保存窗口大小, 逻辑型, 可空, 可空，默认为假，不保存
    .局部变量 节点名, 文本型

    .如果真 (是否为调试版 ())
        输出调试文本 (“调试环境不会保存【窗口位置】”)
        返回 ()
    .如果真结束
    节点名 ＝ 取执行文件名 ()
    写 (节点名, “WinTop”, 到文本 (窗口.顶边), )
    写 (节点名, “WinLeft”, 到文本 (窗口.左边), )
    .如果真 (是否保存窗口大小)
        写 (节点名, “WinWidth”, 到文本 (窗口.宽度), )
        写 (节点名, “WinHeight”, 到文本 (窗口.高度), )
    .如果真结束
    
    写 (节点名, “Maximization”, 到文本 (窗口.位置), )

.子程序 窗口位置_读取, , 公开
    .参数 窗口, 窗口
    .参数 是否读取窗口大小, 逻辑型, 可空, 可空，默认为假，不保存
    .局部变量 精易_矩形, 精易_矩形J
    .局部变量 节点名, 文本型

    节点名 ＝ 取执行文件名 ()
    .如果真 (读 (节点名, “Maximization”, “0”, ) ＝ “3”) ' 最大化
        窗口.位置 ＝ 3
        返回 ()
    .如果真结束
    
    精易_矩形.顶边 ＝ 到数值 (读 (节点名, “WinTop”, “-1”, ))
    精易_矩形.左边 ＝ 到数值 (读 (节点名, “WinLeft”, “-1”, ))
    .判断开始 (是否读取窗口大小)
        精易_矩形.右边 ＝ 到数值 (读 (节点名, “WinWidth”, “-1”, ))
        精易_矩形.底边 ＝ 到数值 (读 (节点名, “WinHeight”, “-1”, ))
        
    .默认
        精易_矩形.右边 ＝ -1
        精易_矩形.底边 ＝ -1
    .判断结束
    
    .判断开始 (精易_矩形.左边 ＜ 0)
        精易_矩形.左边 ＝ 0
        
    .判断 (精易_矩形.左边 ＞ 取屏幕宽度 () － 300)
        精易_矩形.左边 ＝ 取屏幕宽度 () ÷ 2
    .默认
        
    .判断结束
    
    .判断开始 (精易_矩形.顶边 ＜ 0)
        精易_矩形.顶边 ＝ 0
        
    .判断 (精易_矩形.顶边 ＞ 取屏幕高度 () － 300)
        精易_矩形.顶边 ＝ 取屏幕高度 () ÷ 2
    .默认
        
    .判断结束
    
    .如果真 (精易_矩形.顶边 ≠ -1)
        .判断开始 (精易_矩形.右边 ＝ -1)
            窗口.移动 (精易_矩形.左边, 精易_矩形.顶边, , )
            
        .默认
            窗口.移动 (精易_矩形.左边, 精易_矩形.顶边, 精易_矩形.右边, 精易_矩形.底边)
        .判断结束
        
    .如果真结束
    

