.版本 2

.程序集 注册表操作, , 公开
.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 _打开项, 整数型
    .参数 项名称, 文本型
    .参数 是否创建, 逻辑型, 可空, 默认为假:打开 真:如果项不存在则创建项
    .局部变量 局_根句柄, 整数型
    .局部变量 局_项句柄, 整数型
    .局部变量 局_项文本, 文本型
    .局部变量 局_位置, 整数型
    .局部变量 b, 整数型

    局_位置 ＝ 寻找文本 (项名称, “\”, , 假)
    局_项文本 ＝ 取文本左边 (项名称, 局_位置 － 1)
    局_项文本 ＝ 到大写 (局_项文本)
    .判断开始 (局_项文本 ＝ “HKEY_CLASSES_ROOT”)
        局_根句柄 ＝ 2147483648
    .判断 (局_项文本 ＝ “HKEY_CURRENT_USER”)
        局_根句柄 ＝ 2147483649
    .判断 (局_项文本 ＝ “HKEY_LOCAL_MACHINE”)
        局_根句柄 ＝ 2147483650
    .判断 (局_项文本 ＝ “HKEY_USERS”)
        局_根句柄 ＝ 2147483651
    .判断 (文本比较 (局_项文本, “HKEY_CURRENT_CONFIG”, 假) ＝ 0)
        局_根句柄 ＝ 2147483653
    .默认
        返回 (0)
    .判断结束
    .如果 (是否创建)
        b ＝ RegCreateKeyA (局_根句柄, 取文本右边 (项名称, 取文本长度 (项名称) － 局_位置), 局_项句柄)
    .否则
        b ＝ RegOpenKeyA (局_根句柄, 取文本右边 (项名称, 取文本长度 (项名称) － 局_位置), 局_项句柄)
    .如果结束
    .如果真 (b ＝ 0)
        返回 (局_项句柄)
    .如果真结束
    返回 (0)

.子程序 枚举子项, 整数型, 公开, 枚举指定项名称下的子项(成功返回子项目数,失败返回-1)
    .参数 项名称, 文本型
    .参数 子项, 文本型, 可空 数组, 获取的子项数组
    .局部变量 局_a, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_项名, 文本型

    清除数组 (子项)
    局_a ＝ _打开项 (项名称)
    .如果真 (局_a ＝ 0)
        返回 (-1)
    .如果真结束
    局_项名 ＝ 取空白文本 (256)
    .判断循环首 (RegEnumKeyA (局_a, 局_计次, 局_项名, 256) ＝ 0)
        加入成员 (子项, 局_项名)
        局_计次 ＝ 局_计次 ＋ 1
        处理事件 ()
    .判断循环尾 ()
    RegCloseKey (局_a)
    返回 (取数组成员数 (子项))

.子程序 枚举键名, 整数型, 公开, 枚举指定项名称下的键名(成功返回键名数,失败返回-1)
    .参数 项名称, 文本型
    .参数 键名, 文本型, 可空 数组, 获取的键名数组
    .参数 键值, 字节集, 可空 数组, 对应键名的键值
    .参数 类型, 整数型, 可空 数组, 对应键值的类型
    .局部变量 局_项句柄, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 缓存键名, 文本型
    .局部变量 缓存键值, 字节集
    .局部变量 局_键值缓寸, 整数型
    .局部变量 局_类型, 整数型
    .局部变量 ftime, 文件时间
    .局部变量 局_子键数量, 整数型
    .局部变量 局_返回值

    清除数组 (键名)
    清除数组 (键值)
    清除数组 (类型)
    局_项句柄 ＝ _打开项 (项名称)
    .如果真 (局_项句柄 ＝ 0)
        返回 (-1)
    .如果真结束
    局_返回值 ＝ RegQueryInfoKey (局_项句柄, 字符 (0), 字符 (0), 0, 0, 0, 0, 局_子键数量, 0, 0, 0, ftime)
    .如果真 (局_返回值 ＝ 0)
        缓存键名 ＝ 取空白文本 (256)
        .变量循环首 (0, 局_子键数量 － 1, 1, 局_计次)
            RegEnumValueA (局_项句柄, 局_计次, 缓存键名, 256, 0, 局_类型, 缓存键值, 局_键值缓寸)
            缓存键值 ＝ 取空白字节集 (局_键值缓寸 ＋ 1)
            局_返回值 ＝ RegEnumValueA (局_项句柄, 局_计次, 缓存键名, 256, 0, 局_类型, 缓存键值, 局_键值缓寸)
            .如果真 (局_返回值 ＝ 0)
                加入成员 (键名, 缓存键名)
                加入成员 (键值, 缓存键值)
                加入成员 (类型, 局_类型)
            .如果真结束
            
        .变量循环尾 ()
    .如果真结束
    RegCloseKey (局_项句柄)
    返回 (取数组成员数 (键名))

.子程序 取键名类型, 文本型, 公开, 失败返回空文本
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, 可空, 如：3600，留空将取第一个，(默认)
    .局部变量 局_项句柄, 整数型
    .局部变量 局_类型, 整数型
    .局部变量 缓存键值, 字节集
    .局部变量 局_键值缓寸, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 缓存键名, 文本型

    局_项句柄 ＝ _打开项 (项名称)
    缓存键名 ＝ 取空白文本 (256)
    .判断循环首 (RegEnumValueA (局_项句柄, 局_计次, 缓存键名, 256, 0, 局_类型, 缓存键值, 局_键值缓寸) ＝ 0)
        .如果真 (缓存键名 ＝ 键名)
            跳出循环 ()
        .如果真结束
        局_计次 ＝ 局_计次 ＋ 1
        局_类型 ＝ -1
    .判断循环尾 ()
    .判断开始 (局_类型 ＝ 0)
        返回 (“REG_NONE - 字节集，值为：” ＋ 到文本 (局_类型))
    .判断 (局_类型 ＝ 1)
        返回 (“REG_SZ - 字符串值，值为：” ＋ 到文本 (局_类型))
    .判断 (局_类型 ＝ 2)
        返回 (“REG_REG_EXPAND_SZ - 可扩充字符串值，值为：” ＋ 到文本 (局_类型))
    .判断 (局_类型 ＝ 3)
        返回 (“REG_BINARY - 二进制，值为：” ＋ 到文本 (局_类型))
    .判断 (局_类型 ＝ 4)
        返回 (“REG_DWORD - DWORD，值为：” ＋ 到文本 (局_类型))
    .判断 (局_类型 ＝ 7)
        返回 (“REG_MULTI_SZ - 多字符串值，值为：” ＋ 到文本 (局_类型))
    .默认
        
    .判断结束
    RegCloseKey (局_项句柄)
    返回 (“”)

.子程序 是否存在, 逻辑型, 公开, 判断指定注册项名称(键名)是否存在(存在返回真,否则返回假)
    .参数 项名称, 文本型, , 欲判断的项名称
    .参数 键名, 文本型, 可空, 欲判断的键名.如果为空则只判断项名称是否存在
    .局部变量 a, 整数型
    .局部变量 b, 整数型
    .局部变量 值, 字节集

    a ＝ _打开项 (项名称)
    .如果真 (是否为空 (键名))
        RegCloseKey (a)
        返回 (a ≠ 0)
    .如果真结束
    值 ＝ 取空白字节集 (256)
    b ＝ RegQueryValueExA (a, 键名, 0, 0, 值, 256)
    RegCloseKey (a)
    返回 (b ＝ 0)

.子程序 刷新项, 逻辑型, 公开, 将对项和它的子项作出的改动实际写入磁盘
    .参数 项名称, 文本型
    .局部变量 a, 整数型
    .局部变量 b, 整数型
    .局部变量 值, 字节集

    a ＝ _打开项 (项名称)
    .如果真 (a ＝ 0)
        返回 (假)
    .如果真结束
    b ＝ RegFlushKey (a)
    RegCloseKey (a)
    返回 (b ＝ 0)

.子程序 写字节集, 逻辑型, 公开, [REG_NONE]
    .参数 项名称, 文本型
    .参数 键名, 文本型
    .参数 欲写入值, 字节集
    .参数 类型, 整数型, 可空, 可空：写入的类型
    .局部变量 项句柄, 整数型
    .局部变量 b, 整数型
    .局部变量 值, 字节集

    项句柄 ＝ _打开项 (项名称, 真)
    .如果真 (项句柄 ＝ 0)
        返回 (假)
    .如果真结束
    b ＝ RegSetValueExA (项句柄, 键名, 0, 类型, 欲写入值, 取字节集长度 (欲写入值))
    RegCloseKey (项句柄)
    返回 (b ＝ 0)

.子程序 取字节集, 字节集, 公开, [REG_NONE]
    .参数 项名称, 文本型
    .参数 键名, 文本型
    .参数 类型, 整数型, 参考 可空, 可空：取出数据的类型
    .局部变量 项句柄, 整数型
    .局部变量 项值, 整数型
    .局部变量 值, 字节集
    .局部变量 长, 整数型

    项句柄 ＝ _打开项 (项名称)
    .如果真 (项句柄 ＝ 0)
        返回 ({ })
    .如果真结束
    项值 ＝ RegQueryValueExA (项句柄, 键名, 0, 类型, 值, 长)
    值 ＝ 取空白字节集 (长)
    项值 ＝ RegQueryValueExA (项句柄, 键名, 0, 类型, 值, 长)
    RegCloseKey (项句柄)
    .如果真 (项值 ＝ 0)
        返回 (值)
    .如果真结束
    返回 ({ })

.子程序 写注册项Ex, 逻辑型, 公开, 用于写入带有斜杠的注册项
    .参数 根目录, 整数型
    .参数 注册表路径, 文本型
    .参数 注册表子项名, 文本型
    .参数 欲写入值, 字节集
    .参数 写入类型, 整数型, 可空, #REG_
    .局部变量 root, 整数型
    .局部变量 hkey, 整数型
    .局部变量 hsubkey, 整数型
    .局部变量 ret, 逻辑型

    .如果真 (是否为空 (写入类型))
        写入类型 ＝ 1
    .如果真结束
    root ＝ 多项选择 (根目录, -2147483648, -2147483643, -2147483647, -2147483646, -2147483645)
    .如果真 (RegOpenKeyExA (root, “”, 0, 位或 (131097, 131078), hkey) ＝ 0)
        .如果真 (RegCreateKeyExA (hkey, 注册表路径, 0, 0, 0, 位或 (131097, 131078), 0, hsubkey, 0) ＝ 0)
            .如果真 (RegSetValueExA (hsubkey, 注册表子项名, 0, 写入类型, 欲写入值, 取字节集长度 (欲写入值)) ＝ 0)
                ret ＝ 真
            .如果真结束
            RegCloseKey (hsubkey)
        .如果真结束
        RegCloseKey (hkey)
    .如果真结束
    返回 (ret)

.子程序 写DWORD值, 逻辑型, 公开, [REG_DWORD]成功返回真,失败返回假
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600
    .参数 欲写入值, 整数型, , 如：0

    返回 (写字节集 (项名称, 键名, 到字节集 (欲写入值), 4))

.子程序 取DWORD值, 整数型, 公开, [REG_DWORD]
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600

    返回 (取字节集数据 (取字节集 (项名称, 键名, 4), #整数型, ))

.子程序 写二进制值, 逻辑型, 公开, [REG_BINARY]成功返回真,失败返回假
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600
    .参数 欲写入值, 字节集

    返回 (写字节集 (项名称, 键名, 欲写入值, 3))

.子程序 取二进制值, 字节集, 公开, [REG_BINARY]
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600

    返回 (取字节集 (项名称, 键名, 3))

.子程序 写字符串值, 逻辑型, 公开, [REG_SZ]成功返回真,失败返回假
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600
    .参数 欲写入值, 文本型

    返回 (写字节集 (项名称, 键名, 到字节集 (欲写入值) ＋ { 0 }, 1))

.子程序 取字符串值, 文本型, 公开, [REG_SZ]
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600

    返回 (到文本 (取字节集 (项名称, 键名, 1)))

.子程序 写多字符串值, 逻辑型, 公开, [REG_MULTI_SZ]成功返回真,失败返回假
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600
    .参数 欲写入值, 文本型

    返回 (写字节集 (项名称, 键名, 子字节集替换 (到字节集 (欲写入值), { 13, 10 }, { 0 }, , ) ＋ { 0, 0 }, 7))

.子程序 取多字符串值, 文本型, 公开, [REG_MULTI_SZ]
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600

    返回 (到文本 (子字节集替换 (取字节集 (项名称, 键名, 7), { 0 }, { 13, 10 }, , )))

.子程序 写可扩充字符串值, 逻辑型, 公开, [REG_EXPAND_SZ]成功返回真,失败返回假
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 键名, 文本型, , 如：3600
    .参数 欲写入值, 文本型

    返回 (写字节集 (项名称, 键名, 到字节集 (欲写入值) ＋ { 0 }, 2))

.子程序 取可扩充字符串值, 文本型, 公开, [REG_REG_EXPAND_SZ]
    .参数 项名称, 文本型
    .参数 键名, 文本型

    返回 (到文本 (取字节集 (项名称, 键名, 2)))

.子程序 删除项, 逻辑型, 公开, 删除指定项名称以及它的所有子项与键值
    .参数 项名称, 文本型, , 欲删除的项名称,尾部不要加"\"
    .局部变量 a, 整数型
    .局部变量 b, 整数型
    .局部变量 c, 整数型
    .局部变量 子项, 文本型, , "0"
    .局部变量 局_计次, 整数型

    .如果真 (取文本右边 (项名称, 1) ＝ “\”)
        返回 (假)
    .如果真结束
    c ＝ 倒找文本 (项名称, “\”, , 假)
    a ＝ _打开项 (取文本左边 (项名称, c))
    .如果真 (a ＝ 0)
        返回 (假)
    .如果真结束
    .计次循环首 (枚举子项 (项名称, 子项), 局_计次)
        删除项 (项名称 ＋ “\” ＋ 子项 [局_计次])
    .计次循环尾 ()
    b ＝ RegDeleteKeyA (a, 取文本右边 (项名称, 取文本长度 (项名称) － c))
    RegCloseKey (a)
    返回 (b ＝ 0)

.子程序 删除键名, 逻辑型, 公开, 删除指定项下的某个键名
    .参数 项名称, 文本型, , 欲删除键名所在的项名称
    .参数 键名, 文本型, , 欲删除的键名
    .局部变量 a, 整数型
    .局部变量 b, 整数型

    a ＝ _打开项 (项名称)
    .如果真 (a ＝ 0)
        返回 (假)
    .如果真结束
    b ＝ RegDeleteValueA (a, 键名)
    RegCloseKey (a)
    返回 (b ＝ 0)

.子程序 取字符串值_CMD, 文本型, 公开, 用CMD读取方式取指定字符串值
    .参数 项名称, 文本型, , 如：HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\NetBT\Parameters
    .参数 类型, 整数型, 可空, 如：#REG_SZ
    .参数 键名, 文本型, , 如：3600
    .局部变量 命令行, 文本型
    .局部变量 Type, 文本型

    ' https://bbs.125.la/forum.php?mod=redirect&goto=findpost&ptid=14302857&pid=15062805
    命令行 ＝ 系统_取DOS执行结果 (“REG QUERY ” ＋ 项名称 ＋ “ /v ” ＋ 键名 ＋ “ /reg:” ＋ 选择 (系统_是否64位操作系统 (), “64”, “32”))
    .如果真 (类型 ≤ 0 且 类型 ＞ 7)
        类型 ＝ #REG_SZ
    .如果真结束
    Type ＝ 多项选择 (类型, “REG_SZ”, “REG_EXPAND_SZ”, “REG_BINARY”, “REG_DWORD”, “REG_DWORD_BIG_ENDIAN”, “REG_LINK”, “REG_MULTI_SZ”)
    命令行 ＝ 文本_取出中间文本 (命令行, 键名 ＋ “    ” ＋ Type ＋ “    ”, #换行符, , )
    返回 (命令行)

