.版本 2

.程序集 类_配置项, , 公开, 本类由萧阳天编写；MSDN资料:http://msdn.microsoft.com/en-us/library/windows/desktop/ms724875(v=vs.85).aspx
.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 写入节, 逻辑型, 公开
    .参数 配置文件, 文本型
    .参数 节名称, 文本型
    .参数 写入内容, 字节集

    返回 (WritePrivateProfileSection (节名称, 写入内容, 配置文件))

.子程序 读取节, 字节集, 公开
    .参数 配置文件, 文本型
    .参数 节名称, 文本型
    .局部变量 sbuffer, 字节集
    .局部变量 Length

    sbuffer ＝ 取空白字节集 (32767) ' The maximum profile section size is 32,767 characters.
    Length ＝ GetPrivateProfileSection (节名称, sbuffer, 32767, 配置文件)
    返回 (取字节集左边 (sbuffer, Length))

.子程序 枚举节, 整数型, 公开, 返回配置文件中所有已有节名的数量。
    .参数 配置文件, 文本型
    .参数 节数组, 文本型, 数组, 变量储存返回值。
    .局部变量 Length
    .局部变量 sbuffer, 字节集
    .局部变量 Array, 字节集, , "0"
    .局部变量 cnt, 整数型

    清除数组 (节数组)
    sbuffer ＝ 取空白字节集 (32767) ' The maximum profile section size is 32,767 characters.
    Length ＝ GetPrivateProfileSectionNames (sbuffer, 32767, 配置文件)
    sbuffer ＝ 取字节集左边 (sbuffer, Length)
    Array ＝ 分割字节集 (sbuffer, { 0 }, )
    .计次循环首 (取数组成员数 (Array), cnt)
        加入成员 (节数组, 到文本 (Array [cnt]))
    .计次循环尾 ()
    返回 (取数组成员数 (Array))

.子程序 取节数, 整数型, 公开
    .参数 配置文件, 文本型
    .局部变量 Length
    .局部变量 sbuffer, 字节集
    .局部变量 Array, 字节集, , "0"

    sbuffer ＝ 取空白字节集 (32767) ' The maximum profile section size is 32,767 characters.
    Length ＝ GetPrivateProfileSectionNames (sbuffer, 32767, 配置文件)
    sbuffer ＝ 取字节集左边 (sbuffer, Length)
    Array ＝ 分割字节集 (sbuffer, { 0 }, )
    返回 (取数组成员数 (Array))

.子程序 删除节, 逻辑型, 公开, 注意，此命令将会删除指定节上所有项目，一但删除将无法挽回。成功返回真，失败返回假。
    .参数 配置文件, 文本型
    .参数 节名称, 文本型

    返回 (WritePrivateProfileString (节名称, 字符 (0), 字符 (0), 配置文件))

.子程序 读项文本, 文本型, 公开, 读取指定配置文件中指定项目的文本内容。本命令为初级命令。
    .参数 配置文件名, 文本型, , 指定配置文件的名称，通常以.ini作为文件名后缀。
    .参数 节名称, 文本型, , 包含欲读入配置项所处节的名称。
    .参数 配置项名称, 文本型, , 参数值指定欲读入配置项在其节中的名称。
    .参数 默认文本, 文本型, 可空, 可以被省略。如果指定配置项不存在，将返回此默认文本。如果指定配置项不存在且本参数被省略，将返回空文本。
    .局部变量 sbuffer, 文本型
    .局部变量 Length

    sbuffer ＝ 取空白文本 (32767)
    Length ＝ GetPrivateProfileString (节名称, 配置项名称, 默认文本, sbuffer, 32767, 配置文件名)
    返回 (取文本左边 (sbuffer, Length))

.子程序 写项文本, 逻辑型, 公开, 将指定文本内容写入指定配置项中或者删除指定的配置项或节，如果指定配置文件不存在，将会自动创建。成功返回真，失败返回假。本命令为初级命令。
    .参数 配置文件名, 文本型, , 指定配置文件的名称，通常以.ini作为文件名后缀。
    .参数 节名称, 文本型, , 包含欲写入配置项所处节的名称。
    .参数 配置项名称, 文本型, 可空, 可以被省略。参数值指定欲写入配置项在其节中的名称。如果参数值被省略，则删除指定节及其下的所有配置项。
    .参数 欲写入值, 文本型, 可空, 可以被省略。参数值指定欲写入到指定配置项中的文本。如果参数值被省略，则删除所指定配置项。

    返回 (WritePrivateProfileString (节名称, 配置项名称, 欲写入值, 配置文件名))

.子程序 删除项, 逻辑型, 公开, 注意，此命令将会删除指定节上指定配置项，一但删除将无法挽回。成功返回真，失败返回假。
    .参数 配置文件, 文本型
    .参数 节名称, 文本型
    .参数 配置项名称, 文本型

    返回 (WritePrivateProfileString (节名称, 配置项名称, 字符 (0), 配置文件))

.子程序 枚举项, 整数型, 公开
    .参数 配置文件, 文本型
    .参数 节名称, 文本型
    .参数 名称, 文本型, 数组, 变量储存返回值。
    .参数 值, 文本型, 数组
    .局部变量 sbuffer, 字节集
    .局部变量 Length
    .局部变量 Array, 字节集, , "0"
    .局部变量 i, 整数型
    .局部变量 Text, 文本型
    .局部变量 Number

    数组清零 (名称)
    sbuffer ＝ 取空白字节集 (32767) ' The maximum profile section size is 32,767 characters.
    Length ＝ GetPrivateProfileSection (节名称, sbuffer, 32767, 配置文件)
    sbuffer ＝ 取字节集左边 (sbuffer, Length)
    Array ＝ 分割字节集 (sbuffer, { 0 }, )
    .计次循环首 (取数组成员数 (Array), i)
        Text ＝ 到文本 (Array [i])
        Number ＝ 寻找文本 (Text, “=”, , 假)
        加入成员 (名称, 取文本左边 (Text, Number － 1))
        加入成员 (值, 取文本右边 (Text, 取文本长度 (Text) － Number))
    .计次循环尾 ()
    返回 (取数组成员数 (Array))

.子程序 取项数, 整数型, 公开
    .参数 配置文件, 文本型
    .参数 节名称, 文本型
    .局部变量 sbuffer, 字节集
    .局部变量 Length
    .局部变量 Array, 字节集, , "0"
    .局部变量 i, 整数型
    .局部变量 Text, 文本型
    .局部变量 Number

    sbuffer ＝ 取空白字节集 (32767) ' The maximum profile section size is 32,767 characters.
    Length ＝ GetPrivateProfileSection (节名称, sbuffer, 32767, 配置文件)
    sbuffer ＝ 取字节集左边 (sbuffer, Length)
    Array ＝ 分割字节集 (sbuffer, { 0 }, )
    返回 (取数组成员数 (Array))

.子程序 读项数值, 整数型, 公开, 读取指定配置文件中指定项目的文本内容。本命令为初级命令。
    .参数 配置文件名, 文本型, , 指定配置文件的名称，通常以.ini作为文件名后缀。
    .参数 节名称, 文本型, , 包含欲读入配置项所处节的名称。
    .参数 配置项名称, 文本型, , 参数值指定欲读入配置项在其节中的名称。
    .参数 默认数值, 整数型, 可空, 可以被省略。如果指定配置项不存在，将返回此默认文本。如果指定配置项不存在且本参数被省略，将返回空文本。

    返回 (GetPrivateProfileInt (到字节集 (节名称), 配置项名称, 默认数值, 配置文件名))

.子程序 写项结构, 逻辑型, 公开, 把指定数据结构以字节集方式存入配置项，成功返回真，失败返回假。
    .参数 配置文件名, 文本型
    .参数 节名称, 文本型
    .参数 配置项名称, 文本型
    .参数 数据结构, 字节集

    返回 (WritePrivateProfileStruct (配置项名称, 节名称, 数据结构, 取字节集长度 (数据结构), 配置文件名))

.子程序 读项结构, 字节集, 公开, 取回｛写项结构()｝写入的结构数据，失败返回Null。
    .参数 配置文件名, 文本型
    .参数 节名称, 文本型
    .参数 配置项名称, 文本型
    .参数 数据结构大小
    .局部变量 sbuffer, 字节集

    sbuffer ＝ 取空白字节集 (数据结构大小)
    GetPrivateProfileStruct (配置项名称, 节名称, sbuffer, 数据结构大小, 配置文件名)
    返回 (sbuffer)

.子程序 系统配置项注册表目录, , , HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\IniFileMapping\win.ini
    

.子程序 写系统节, 逻辑型, 公开, 注意：设置系统配置项可能造成系统不稳定，请谨慎。向系统Win.ini中写入配置项，成功返回真，失败返回假。
    .参数 节名称, 文本型
    .参数 内容, 字节集

    返回 (WriteProfileSection (节名称, 内容))

.子程序 读系统节, 字节集, 公开
    .参数 节名称, 文本型
    .局部变量 sbuffer, 字节集
    .局部变量 Length

    sbuffer ＝ 取空白字节集 (32768)
    Length ＝ GetProfileSection (节名称, sbuffer, 32767)
    sbuffer ＝ 取字节集左边 (sbuffer, Length)
    返回 (sbuffer)

.子程序 枚举系统项, 整数型, 公开, 枚举系统win.ini中的配置项及值，失败返回Null。
    .参数 节名称, 文本型
    .参数 项名称, 文本型, 数组, 变量储存返回值。
    .参数 值, 文本型, 可空 数组, 变量储存返回值。
    .局部变量 sbuffer, 字节集
    .局部变量 Length
    .局部变量 Array, 字节集, , "0"
    .局部变量 cnt, 整数型
    .局部变量 Text, 文本型
    .局部变量 Number, 整数型

    sbuffer ＝ 取空白字节集 (32768)
    Length ＝ GetProfileSection (节名称, sbuffer, 32767)
    sbuffer ＝ 取字节集左边 (sbuffer, Length)
    Array ＝ 分割字节集 (sbuffer, { 0 }, )
    .计次循环首 (取数组成员数 (Array), cnt)
        Text ＝ 到文本 (Array [cnt])
        Number ＝ 寻找文本 (Text, “=”, , 假)
        加入成员 (项名称, 取文本左边 (Text, Number － 1))
        加入成员 (值, 取文本右边 (Text, 取文本长度 (Text) － Number))
    .计次循环尾 ()
    返回 (取数组成员数 (Array))
    

.子程序 取系统项数, 整数型, 公开
    .参数 节名称, 文本型
    .局部变量 sbuffer, 字节集
    .局部变量 Length
    .局部变量 Array, 字节集, , "0"
    .局部变量 cnt, 整数型
    .局部变量 Text, 文本型
    .局部变量 Number, 整数型

    sbuffer ＝ 取空白字节集 (32768)
    Length ＝ GetProfileSection (节名称, sbuffer, 32767)
    sbuffer ＝ 取字节集左边 (sbuffer, Length)
    Array ＝ 分割字节集 (sbuffer, { 0 }, )
    返回 (取数组成员数 (Array))

.子程序 读系统项文本, 文本型, 公开, 从系统Win.ini中读取指定配置项文本，失败失败Null。本命令为初级命令。
    .参数 节名称, 文本型, , 包含欲读入配置项所处节的名称。
    .参数 配置项名称, 文本型, , 参数值指定欲读入配置项在其节中的名称。
    .参数 默认文本, 文本型, 可空, 可以被省略。如果指定配置项不存在，将返回此默认文本。如果指定配置项不存在且本参数被省略，将返回空文本。
    .局部变量 sbuffer, 文本型
    .局部变量 Length

    sbuffer ＝ 取空白文本 (256)
    Length ＝ GetProfileString (节名称, 配置项名称, 默认文本, sbuffer, 256)
    返回 (取文本左边 (sbuffer, Length))

.子程序 写系统项文本, 逻辑型, 公开, 注意：设置系统配置项可能造成系统不稳定，请谨慎。向系统Win.ini配置项中写入文本值，成功返回真，失败返回假。
    .参数 节名称, 文本型, , 包含欲读入配置项所处节的名称。
    .参数 配置项名称, 文本型, , 参数值指定欲读入配置项在其节中的名称。
    .参数 欲写入值, 文本型

    返回 (WriteProfileString (节名称, 配置项名称, 欲写入值))

.子程序 读系统项数值, 整数型, 公开, 从系统Win.ini中读取指定配置项数值，失败失败Null。本命令为初级命令。
    .参数 节名称, 文本型, , 包含欲读入配置项所处节的名称。
    .参数 配置项名称, 文本型, , 参数值指定欲读入配置项在其节中的名称。
    .参数 默认数值, 整数型, 可空, 可以被省略。如果指定配置项不存在，将返回此默认文本。如果指定配置项不存在且本参数被省略，将返回空文本。

    返回 (GetProfileInt (节名称, 配置项名称, 默认数值)) ' 对应注册表；HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\IniFileMapping\win.ini

