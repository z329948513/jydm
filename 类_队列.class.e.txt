.版本 2

.程序集 类_队列, , 公开, 【先进先出】
.程序集变量 临界资源, 临界许可, , , 临界许可 用来做线程兼容的
.程序集变量 Heap, 整数型, , , 堆句柄
.程序集变量 Ptr, 整数型, , , 内存快句柄
.程序集变量 Ptrlen, 整数型, , , 内存快当前长度
.程序集变量 Tmplen, 整数型, , , 缓冲区大小
.程序集变量 Pushi, 整数型, , , 压入位置
.程序集变量 Popi, 整数型, , , 弹出位置
.程序集变量 Count, 整数型, , , 队列数量

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    InitializeCriticalSection_临界许可 (临界资源) ' 初始化临界许可 （许可证）
    Tmplen ＝ 1048576 ' 1mb‘默认缓冲区大小1MB
    Heap ＝ HeapCreate (1, Tmplen, 0) ' 申请一个堆
    Ptr ＝ HeapAlloc (Heap, 8, Tmplen) ' 申请一个内存快 大小为1M
    Ptrlen ＝ Tmplen
    Pushi ＝ 0
    Popi ＝ 0
    Count ＝ 0

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    HeapDestroy (Heap) ' 销毁堆
    Count ＝ 0
    Ptr ＝ 0
    Heap ＝ 0
    DeleteCriticalSection_临界许可 (临界资源) ' 删除临界资源

.子程序 增量缓冲区, 逻辑型, , 增量缓冲区
    .参数 len, 整数型
    .局部变量 tmp_len, 整数型
    .局部变量 tmp_ptr, 整数型

    tmp_len ＝ Ptrlen － Popi ' 算出有效数据长度
    Ptrlen ＝ tmp_len ＋ Tmplen ＋ len ' 算出移动后的大小 （其实应该先判断下减去演出位置后是否够本次压入 够的话可以直接返回的）
    tmp_ptr ＝ HeapAlloc (Heap, 8, Ptrlen) ' 新申请一块内存 （其实可以 HeapReAlloc ）
    .如果真 (tmp_ptr ＝ 0) ' 申请不到 那只能返回啦
        Ptrlen ＝ tmp_len ＋ Popi ' 还原内存快长度
        返回 (假)
    .如果真结束
    RtlMoveMemory_整数型 (tmp_ptr, Ptr ＋ Popi, tmp_len) ' 把有效数据移动到新内存快
    HeapFree (Heap, 1, Ptr) ' 销毁旧内存快
    Ptr ＝ tmp_ptr ' 赋值新内存快地址
    Pushi ＝ Pushi － Popi ' 算出移动后的压入位置
    Popi ＝ 0
    返回 (真)

.子程序 收缩缓冲区, , , 收缩缓冲区
    .局部变量 tmp_len, 整数型
    .局部变量 tmp_ptr, 整数型

    ' //收缩和增量原理一样  （其实这里可以优化的很多 下面的写法内存收回并不及时）
    tmp_len ＝ Ptrlen － Popi
    tmp_ptr ＝ HeapAlloc (Heap, 8, tmp_len)
    .如果真 (tmp_ptr ＝ 0)
        返回 ()
    .如果真结束
    RtlMoveMemory_整数型 (tmp_ptr, Ptr ＋ Popi, tmp_len)
    HeapFree (Heap, 1, Ptr)
    Ptr ＝ tmp_ptr
    Ptrlen ＝ tmp_len
    Pushi ＝ Pushi － Popi
    Popi ＝ 0
    返回 ()

.子程序 置缓冲区大小, , 公开, 置缓冲区大小 缓冲区大小建议是压入数据长度的倍数。（缓冲区大小不影响队列的容量）
    .参数 缓冲区大小, 整数型, 可空, 单位：KB，缓冲区大小 默认1M
    .局部变量 temp, 整数型

    EnterCriticalSection_临界许可 (临界资源) ' 进入临界
    .如果 (缓冲区大小 ＝ 0)
        Tmplen ＝ 1024 × 1024 ' 默认1MB
    .否则
        Tmplen ＝ 缓冲区大小 × 1024
    .如果结束
    LeaveCriticalSection_临界许可 (临界资源) ' 退出临界

.子程序 压入字节集, 逻辑型, 公开, 压入字节集 。成功返回真，失败返回假。
    .参数 bin, 字节集, , 要压入的数据
    .局部变量 len, 整数型

    len ＝ 取字节集长度 (bin)
    .如果真 (len ＜ 1)
        返回 (假)
    .如果真结束
    EnterCriticalSection_临界许可 (临界资源) ' 进入临界
    .如果真 (Pushi ＋ 4 ＋ len ＞ Ptrlen) ' 如果 压入位置+本次长度+占位符4 超过了内存快长度
        .如果真 (增量缓冲区 (len ＋ 4) ＝ 假) ' 执行增量操作
            LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
            返回 (假)
        .如果真结束
        
    .如果真结束
    RtlMoveMemory_Eint_int (Ptr ＋ Pushi, len, 4) ' 写入bin的长度
    Pushi ＝ Pushi ＋ 4 ' 压入位置 + 4
    RtlMoveMemory_Ebin_int (Ptr ＋ Pushi, bin, len) ' 写入bin
    Pushi ＝ Pushi ＋ len ' 压入位置 + bin的长度
    Count ＝ Count ＋ 1 ' 队列数 + 1
    LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
    返回 (真)

.子程序 弹出字节集, 逻辑型, 公开, 弹出字节集 。成功返回真，失败返回假。
    .参数 bin, 字节集, 参考, 用来接收弹出数据的变量
    .局部变量 len, 整数型

    EnterCriticalSection_临界许可 (临界资源) ' 进入临界
    .如果真 (Count ＜ 1) ' 队列数量少于 1
        LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
        返回 (假)
    .如果真结束
    .如果真 (Popi ＞ Tmplen) ' 弹出位置超过缓冲区长度 表示内存快前端有很多空闲内存可以收回
        收缩缓冲区 ()
    .如果真结束
    len ＝ 0
    RtlMoveMemory_整数传址1 (len, Ptr ＋ Popi, 4) ' 取出本次bin数据的长度信息
    Popi ＝ Popi ＋ 4
    bin ＝ 取空白字节集 (len) ' 初始化E的变量用来接收bin
    RtlMoveMemory_字节集传址1 (bin, Ptr ＋ Popi, len) ' 复制bin
    Popi ＝ Popi ＋ len ' 弹出位置后移
    Count ＝ Count － 1 ' 队列数量减去 1
    LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
    返回 (真)

.子程序 压入文本, 逻辑型, 公开, 压入文本 。成功返回真，失败返回假。
    .参数 str, 文本型, , 要压入的数据
    .局部变量 len, 整数型

    len ＝ 取文本长度 (str)
    .如果真 (len ＜ 1)
        返回 (假)
    .如果真结束
    EnterCriticalSection_临界许可 (临界资源) ' 进入临界
    .如果真 (Pushi ＋ 4 ＋ len ＞ Ptrlen)
        .如果真 (增量缓冲区 (len ＋ 4) ＝ 假)
            LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
            返回 (假)
        .如果真结束
        
    .如果真结束
    RtlMoveMemory_Eint_int (Ptr ＋ Pushi, len, 4)
    Pushi ＝ Pushi ＋ 4
    RtlMoveMemory_Estr_int (Ptr ＋ Pushi, str, len)
    Pushi ＝ Pushi ＋ len
    Count ＝ Count ＋ 1
    LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
    返回 (真)

.子程序 弹出文本, 逻辑型, 公开, 弹出文本 。成功返回真，失败返回假。
    .参数 str, 文本型, 参考, 用来接收弹出数据的变量
    .局部变量 len, 整数型

    EnterCriticalSection_临界许可 (临界资源) ' 进入临界
    .如果真 (Count ＜ 1)
        LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
        返回 (假)
    .如果真结束
    .如果真 (Popi ＞ Tmplen)
        收缩缓冲区 ()
    .如果真结束
    len ＝ 0
    RtlMoveMemory_整数传址1 (len, Ptr ＋ Popi, 4)
    Popi ＝ Popi ＋ 4
    str ＝ 取空白文本 (len)
    RtlMoveMemory_整数型 (取指针_文本型 (str), Ptr ＋ Popi, len)
    Popi ＝ Popi ＋ len
    Count ＝ Count － 1
    LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
    返回 (真)

.子程序 压入整数, 逻辑型, 公开, 压入整数 。成功返回真，失败返回假。
    .参数 int, 整数型, , 要压入的数据

    EnterCriticalSection_临界许可 (临界资源) ' 进入临界
    .如果真 (Pushi ＋ 8 ＞ Ptrlen)
        .如果真 (增量缓冲区 (8) ＝ 假)
            LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
            返回 (假)
        .如果真结束
        
    .如果真结束
    RtlMoveMemory_Eint_int (Ptr ＋ Pushi, 4, 4)
    Pushi ＝ Pushi ＋ 4
    RtlMoveMemory_Eint_int (Ptr ＋ Pushi, int, 4)
    Pushi ＝ Pushi ＋ 4
    Count ＝ Count ＋ 1
    LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
    返回 (真)

.子程序 弹出整数, 逻辑型, 公开, 弹出整数 。成功返回真，失败返回假。
    .参数 int, 整数型, 参考, 用来接收弹出数据的变量

    EnterCriticalSection_临界许可 (临界资源) ' 进入临界
    .如果真 (Count ＜ 1)
        LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
        返回 (假)
    .如果真结束
    .如果真 (Popi ＞ Tmplen)
        收缩缓冲区 ()
    .如果真结束
    Popi ＝ Popi ＋ 4
    RtlMoveMemory_整数传址1 (int, Ptr ＋ Popi, 4)
    Popi ＝ Popi ＋ 4
    Count ＝ Count － 1
    LeaveCriticalSection_临界许可 (临界资源) ' 退出临界
    返回 (真)

.子程序 取数量, 整数型, 公开
    返回 (Count)

