.版本 2

.程序集 集_算术运算
.子程序 运算_求阶乘, 长整数型, 公开, 返回参数中给定数值的计算阶乘结果。
    .参数 阶乘数, 整数型, , 要求阶乘的数值
    .局部变量 计算结果, 长整数型
    .局部变量 计数, 整数型

    计算结果 ＝ 1
    .计次循环首 (阶乘数, 计数)
        计算结果 ＝ 计算结果 × 计数
    .计次循环尾 ()
    返回 (计算结果) ' 将计算结果返回给调用本程序的命令行
    

.子程序 运算_二分查找, 整数型, 公开
    .参数 数组, 文本型, 数组
    .参数 待查, 文本型
    .参数 类型, 整数型
    .参数 区分大小写, 逻辑型
    .局部变量 位置, 整数型
    .局部变量 数组总数, 整数型
    .局部变量 对分, 整数型
    .局部变量 数值A, 双精度小数型
    .局部变量 数值B, 双精度小数型
    .局部变量 文本, 文本型

    位置 ＝ 1
    数组总数 ＝ 取数组成员数 (数组)
    .如果真 (数组总数 ＝ 0)
        返回 (1)
    .如果真结束
    .判断开始 (类型 ＝ 0)
        数值A ＝ 到数值 (待查)
        .如果真 (数值A ≥ 到数值 (数组 [数组总数]))
            返回 (数组总数 ＋ 1)
        .如果真结束
        .如果真 (数值A ≤ 到数值 (数组 [1]))
            返回 (1)
        .如果真结束
        
        .判断循环首 (位置 ＜ 数组总数 － 1)
            对分 ＝ (位置 ＋ 数组总数) ＼ 2
            数值B ＝ 到数值 (数组 [对分])
            .如果真 (数值A ＝ 数值B)
                位置 ＝ 对分
                跳出循环 ()
            .如果真结束
            
            .如果 (数值A ＜ 数值B)
                数组总数 ＝ 对分
            .否则
                位置 ＝ 对分
            .如果结束
            
        .判断循环尾 ()
    .判断 (类型 ＝ 1)
        .如果真 (文本比较 (待查, 数组 [数组总数], 区分大小写) ≥ 0)
            返回 (数组总数 ＋ 1)
        .如果真结束
        .如果真 (文本比较 (待查, 数组 [1], 区分大小写) ≤ 0)
            返回 (1)
        .如果真结束
        .判断循环首 (位置 ＜ 数组总数 － 1)
            对分 ＝ (位置 ＋ 数组总数) ＼ 2
            .如果真 (文本比较 (待查, 数组 [对分], 区分大小写) ＝ 0)
                位置 ＝ 对分
                跳出循环 ()
            .如果真结束
            .如果 (文本比较 (待查, 数组 [对分], 区分大小写) ＜ 0)
                数组总数 ＝ 对分
            .否则
                位置 ＝ 对分
            .如果结束
            
        .判断循环尾 ()
        
    .判断 (类型 ＝ 2)
        数值A ＝ 取字节集数据 (到字节集 (到时间 (待查)), #双精度小数型)
        .如果真 (数值A ≥ 取字节集数据 (到字节集 (到时间 (数组 [数组总数])), #双精度小数型))
            返回 (数组总数 ＋ 1)
        .如果真结束
        .如果真 (数值A ≤ 取字节集数据 (到字节集 (到时间 (数组 [1])), #双精度小数型))
            返回 (1)
        .如果真结束
        .判断循环首 (位置 ＜ 数组总数 － 1)
            对分 ＝ (位置 ＋ 数组总数) ＼ 2
            数值B ＝ 取字节集数据 (到字节集 (到时间 (数组 [对分])), #双精度小数型)
            .如果真 (数值A ＝ 数值B)
                位置 ＝ 对分
                跳出循环 ()
            .如果真结束
            .如果 (数值A ＜ 数值B)
                数组总数 ＝ 对分
            .否则
                位置 ＝ 对分
            .如果结束
            
        .判断循环尾 ()
    .默认
        
    .判断结束
    返回 (位置 ＋ 1)

.子程序 运算_函数计算, 文本型, 公开, 求正弦、余弦、正切、余切
    .参数 Text, 文本型
    .参数 单位, 逻辑型, , 真为弧度制，假为角度制
    .局部变量 TempText, 文本型
    .局部变量 FText, 文本型, , , 寻找值
    .局部变量 FNum, 整数型, , , 寻找值的长度
    .局部变量 ZText, 文本型, , , 函数括号内文本
    .局部变量 RetText, 文本型, , , 函数括号内计算结果
    .局部变量 i, 整数型
    .局部变量 Find, 整数型, , , 寻找到的位置
    .局部变量 YFind, 整数型, , , 寻找到右括号的位置

    TempText ＝ Text
    .计次循环首 (4, i)
        FText ＝ 多项选择 (i, “sin(”, “cos(”, “tg(”, “ctg(”)
        FNum ＝ 多项选择 (i, 4, 4, 3, 4)
        Find ＝ 寻找文本 (TempText, FText, , 假)
        .判断循环首 (Find ≠ -1)
            YFind ＝ 寻找文本 (TempText, “)”, Find, 假) ' 寻找右括号
            .如果真 (YFind ≠ -1)
                ZText ＝ 取文本中间 (TempText, Find ＋ FNum, YFind － Find － FNum)
                .判断开始 (ZText ＝ “”) ' 括号内无文本
                    TempText ＝ 文本替换 (TempText, Find, FNum ＋ 1, )
                .默认
                    RetText ＝ 运算_括号计算 (ZText)
                    .如果真 (RetText ＝ “”) ' 失败返回空文本
                        返回 (“”)
                    .如果真结束
                    .判断开始 (i ＝ 1) ' 正弦
                        RetText ＝ 到文本 (求正弦 (选择 (单位, 到数值 (RetText), 到数值 (RetText) × #pi ÷ 180)))
                    .判断 (i ＝ 2) ' 余弦
                        RetText ＝ 到文本 (求余弦 (选择 (单位, 到数值 (RetText), 到数值 (RetText) × #pi ÷ 180)))
                    .判断 (i ＝ 3) ' 正切
                        RetText ＝ 到文本 (求正切 (选择 (单位, 到数值 (RetText), 到数值 (RetText) × #pi ÷ 180)))
                    .默认
                        RetText ＝ 到文本 (1 ÷ 求正切 (选择 (单位, 到数值 (RetText), 到数值 (RetText) × #pi ÷ 180))) ' 余切
                    .判断结束
                    .如果真 (是否运算正确 (到数值 (RetText)) ＝ 假)
                        返回 (“”)
                    .如果真结束
                    TempText ＝ 文本替换 (TempText, Find, 取文本长度 (ZText) ＋ FNum ＋ 1, RetText)
                .判断结束
                
            .如果真结束
            Find ＝ 寻找文本 (TempText, FText, , 假)
        .判断循环尾 ()
    .计次循环尾 ()
    返回 (运算_括号计算 (TempText))

.子程序 运算_括号计算, 文本型, 公开
    .参数 Text, 文本型
    .局部变量 i, 整数型
    .局部变量 Zk, 整数型, , "0", 左括号位置数组
    .局部变量 ZkNum, 整数型, , , Zk成员数量
    .局部变量 SigText, 文本型, , , 单个文本
    .局部变量 Asc, 整数型, , , 单个文本的Asc码
    .局部变量 SwText, 文本型, , , 括号内文本
    .局部变量 SwReText, 文本型, , , 括号内计算后文本
    .局部变量 TempText, 文本型, , , 替换后的结果

    TempText ＝ Text
    i ＝ 1
    .判断循环首 (i ≤ 取文本长度 (TempText))
        SigText ＝ 取文本中间 (TempText, i, 1)
        .判断开始 (SigText ＝ “(”)
            加入成员 (Zk, i)
            i ＝ i ＋ 1
        .判断 (SigText ＝ “)”)
            ZkNum ＝ 取数组成员数 (Zk)
            .如果 (ZkNum ＞ 0) ' 是否存在左括号
                SwText ＝ 取文本中间 (TempText, Zk [ZkNum] ＋ 1, i － Zk [ZkNum] － 1) ' 取出括号内文本
                SwReText ＝ 运算_内部计算 (SwText) ' 计算括号内文本
                .如果真 (SwReText ＝ “”) ' 失败返回空文本
                    返回 (“”)
                .如果真结束
                TempText ＝ 文本替换 (TempText, Zk [ZkNum], 取文本长度 (“(” ＋ SwText ＋ “)”), SwReText)
                i ＝ Zk [ZkNum] ＋ 1
                删除成员 (Zk, ZkNum, )
            .否则
                TempText ＝ 文本替换 (TempText, i, 1, ) ' 去掉多余右括号
            .如果结束
            
        .判断 (Asc ＝ 92)
            TempText ＝ 文本替换 (TempText, i, 1, “\”)
            i ＝ i ＋ 1
        .默认
            Asc ＝ 取代码 (SigText, )
            .判断开始 (Asc ＜ 46 或 Asc ＞ 57 且 Asc ≠ 42 且 Asc ≠ 43 且 Asc ≠ 45 且 Asc ≠ 92 且 Asc ≠ 94) ' 去掉非法字符
                TempText ＝ 文本替换 (TempText, i, 1, )
            .默认
                i ＝ i ＋ 1
            .判断结束
            
        .判断结束
        
    .判断循环尾 ()
    ZkNum ＝ 取数组成员数 (Zk)
    .如果真 (ZkNum ＞ 0) ' 去掉多余左括号
        .计次循环首 (ZkNum, i)
            TempText ＝ 文本替换 (TempText, Zk [ZkNum － i ＋ 1], 1, ) ' 从最后一个开始替换
        .计次循环尾 ()
    .如果真结束
    返回 (运算_内部计算 (TempText))

.子程序 运算_内部计算, 文本型, 公开, 加减乘除及乘方运算，失败返回空文本
    .参数 Text, 文本型
    .局部变量 i, 整数型
    .局部变量 Sign, 整数型, , "0", 操作符号位置
    .局部变量 SiNum, 整数型, , , Sign的数组成员数量
    .局部变量 SigText, 文本型, , , 单个文本
    .局部变量 TempText, 文本型
    .局部变量 Js, 文本型, , "2", 计算的左边和右边
    .局部变量 Result, 文本型, , , 单符号计算结果

    TempText ＝ Text
    .如果真 (寻找文本 (TempText, “^”, , 假) ≠ -1)
        .判断循环首 (i ≤ 取文本长度 (TempText)) ' 计算乘方
            SigText ＝ 取文本中间 (TempText, i, 1)
            .判断开始 (SigText ＝ “+” 或 (SigText ＝ “-” 且 i ＞ 1) 或 SigText ＝ “*” 或 SigText ＝ “/”)
                加入成员 (Sign, i)
                i ＝ i ＋ 1
            .判断 (SigText ＝ “^”)
                SiNum ＝ 取数组成员数 (Sign)
                .判断开始 (SiNum ＞ 0)
                    Js [1] ＝ 取文本中间 (TempText, Sign [SiNum] ＋ 1, i － Sign [SiNum] － 1)
                    Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
                    Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
                    Result ＝ 到文本 (求次方 (到数值 (Js [1]), 到数值 (Js [2])))
                    TempText ＝ 文本替换 (TempText, Sign [SiNum] ＋ 1, 取文本长度 (Js [1] ＋ “^” ＋ Js [2]), Result)
                    i ＝ Sign [SiNum] ＋ 1
                .默认
                    Js [1] ＝ 取文本左边 (TempText, i － 1)
                    Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
                    Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
                    Result ＝ 到文本 (求次方 (到数值 (Js [1]), 到数值 (Js [2])))
                    TempText ＝ 文本替换 (TempText, 1, 取文本长度 (Js [1] ＋ “^” ＋ Js [2]), Result)
                    i ＝ 1
                .判断结束
                
            .默认
                i ＝ i ＋ 1
            .判断结束
            
        .判断循环尾 ()
    .如果真结束
    i ＝ 1
    清除数组 (Sign)
    .如果真 (寻找文本 (TempText, “*”, , 假) ≠ -1 或 寻找文本 (TempText, “/”, , 假) ≠ -1)
        .判断循环首 (i ≤ 取文本长度 (TempText)) ' 计算乘除
            SigText ＝ 取文本中间 (TempText, i, 1)
            .判断开始 (SigText ＝ “+” 或 (SigText ＝ “-” 且 i ＞ 1))
                加入成员 (Sign, i)
                i ＝ i ＋ 1
            .判断 (SigText ＝ “*”)
                SiNum ＝ 取数组成员数 (Sign)
                .判断开始 (SiNum ＞ 0)
                    Js [1] ＝ 取文本中间 (TempText, Sign [SiNum] ＋ 1, i － Sign [SiNum] － 1)
                    Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
                    Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
                    Result ＝ 到文本 (到数值 (Js [1]) × 到数值 (Js [2]))
                    TempText ＝ 文本替换 (TempText, Sign [SiNum] ＋ 1, 取文本长度 (Js [1] ＋ “*” ＋ Js [2]), Result)
                    i ＝ Sign [SiNum] ＋ 1
                .默认
                    Js [1] ＝ 取文本左边 (TempText, i － 1)
                    Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
                    Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
                    Result ＝ 到文本 (到数值 (Js [1]) × 到数值 (Js [2]))
                    TempText ＝ 文本替换 (TempText, 1, 取文本长度 (Js [1] ＋ “*” ＋ Js [2]), Result)
                    i ＝ 1
                .判断结束
                
            .判断 (SigText ＝ “/”)
                SiNum ＝ 取数组成员数 (Sign)
                .判断开始 (SiNum ＞ 0)
                    Js [1] ＝ 取文本中间 (TempText, Sign [SiNum] ＋ 1, i － Sign [SiNum] － 1)
                    Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
                    Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
                    .如果真 (到数值 (Js [2]) ＝ 0) ' 除数不能为0
                        返回 (“”)
                    .如果真结束
                    .如果 (到数值 (Js [1]) ＝ 0) ' 避免出现被除数不能为0
                        Result ＝ “0”
                    .否则
                        Result ＝ 到文本 (到数值 (Js [1]) ÷ 到数值 (Js [2]))
                    .如果结束
                    TempText ＝ 文本替换 (TempText, Sign [SiNum] ＋ 1, 取文本长度 (Js [1] ＋ “/” ＋ Js [2]), Result)
                    i ＝ Sign [SiNum] ＋ 1
                .默认
                    Js [1] ＝ 取文本左边 (TempText, i － 1)
                    Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
                    Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
                    .如果真 (到数值 (Js [2]) ＝ 0) ' 被除数不能为0
                        返回 (“”)
                    .如果真结束
                    .如果 (到数值 (Js [1]) ＝ 0) ' 避免出现被除数不能为0
                        Result ＝ “0”
                    .否则
                        Result ＝ 到文本 (到数值 (Js [1]) ÷ 到数值 (Js [2]))
                    .如果结束
                    TempText ＝ 文本替换 (TempText, 1, 取文本长度 (Js [1] ＋ “/” ＋ Js [2]), Result)
                    i ＝ 1
                .判断结束
                
            .默认
                i ＝ i ＋ 1
            .判断结束
            
        .判断循环尾 ()
    .如果真结束
    i ＝ 1
    .判断循环首 (i ≤ 取文本长度 (TempText)) ' 计算加减
        SigText ＝ 取文本中间 (TempText, i, 1)
        .判断开始 (SigText ＝ “+”)
            Js [1] ＝ 取文本左边 (TempText, i － 1)
            Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
            Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
            Result ＝ 到文本 (到数值 (Js [1]) ＋ 到数值 (Js [2]))
            TempText ＝ 文本替换 (TempText, 1, 取文本长度 (Js [1] ＋ “+” ＋ Js [2]), Result)
            i ＝ 1
        .判断 (SigText ＝ “-” 且 i ＞ 1)
            Js [1] ＝ 取文本左边 (TempText, i － 1)
            Js [2] ＝ 取文本右边 (TempText, 取文本长度 (TempText) － i)
            Js [2] ＝ 到文本 (到数值 (Js [2])) ' 去掉后面的操作符
            Result ＝ 到文本 (到数值 (Js [1]) － 到数值 (Js [2]))
            TempText ＝ 文本替换 (TempText, 1, 取文本长度 (Js [1] ＋ “-” ＋ Js [2]), Result)
            i ＝ 1
        .默认
            i ＝ i ＋ 1
        .判断结束
        
    .判断循环尾 ()
    返回 (TempText)

.子程序 运算_取圆周率, 文本型, 公开, 返回小数最大1000位圆周率,实际上有10位计算就已很精确了
    .参数 位数, 整数型, , 返回圆周率小数点后的位数
    .局部变量 行1, 文本型
    .局部变量 行2, 文本型
    .局部变量 行3, 文本型
    .局部变量 行4, 文本型
    .局部变量 行5, 文本型
    .局部变量 行6, 文本型
    .局部变量 行7, 文本型
    .局部变量 行8, 文本型
    .局部变量 行9, 文本型
    .局部变量 行10, 文本型

    行1 ＝ “1415926535897932384626433832795028841971693993751058209749445923078164062862089986280348253421170679”
    行2 ＝ “8214808651328230664709384460955058223172535940812848111745028410270193852110555964462294895493038196”
    行3 ＝ “4428810975665933446128475648233786783165271201909145648566923460348610454326648213393607260249141273”
    行4 ＝ “7245870066063155881748815209209628292540917153643678925903600113305305488204665213841469519415116094”
    行5 ＝ “3305727036575959195309218611738193261179310511854807446237996274956735188575272489122793818301194912”
    行6 ＝ “9833673362440656643086021394946395224737190702179860943702770539217176293176752384674818467669405132”
    行7 ＝ “0005681271452635608277857713427577896091736371787214684409012249534301465495853710507922796892589235”
    行8 ＝ “4201995611212902196086403441815981362977477130996051870721134999999837297804995105973173281609631859”
    行9 ＝ “5024459455346908302642522308253344685035261931188171010003137838752886587533208381420617177669147303”
    行10 ＝ “5982534904287554687311595628638823537875937519577818577805321712268066130019278766111959092164201989”
    返回 (取文本左边 (“3.” ＋ 行1 ＋ 行2 ＋ 行3 ＋ 行4 ＋ 行5 ＋ 行6 ＋ 行7 ＋ 行8 ＋ 行9 ＋ 行10, 位数 ＋ 2))

.子程序 运算_求反正弦, 双精度小数型, 公开, 返回弧度值
    .参数 正弦值, 双精度小数型, , -1到1，否则返回0

    .如果真 (正弦值 ＜ -1 或 正弦值 ＞ 1)
        返回 (0)
    .如果真结束
    返回 (求反正切 (正弦值 ÷ 求平方根 (1 － 正弦值 × 正弦值)))

.子程序 运算_求反余弦, 双精度小数型, 公开, 返回弧度值
    .参数 余弦值, 双精度小数型, , -1到1，否则返回0

    .如果真 (余弦值 ＞ 1 或 余弦值 ＜ -1)
        返回 (0)
    .如果真结束
    返回 (#pi ÷ 2 － 运算_求反正弦 (余弦值))

.子程序 运算_颜色转换, 文本型, 公开, 将10进制的颜色值转到16进制的颜色值；   还原调用 网页_取十进制颜色值
    .参数 待转换颜色值, 整数型
    .局部变量 字节集, 字节集
    .局部变量 十六进制颜色, 文本型

    字节集 ＝ 到字节集 (待转换颜色值)
    十六进制颜色 ＝ 取文本右边 (“00” ＋ 取十六进制文本 (字节集 [1]), 2)
    十六进制颜色 ＝ 十六进制颜色 ＋ 取文本右边 (“00” ＋ 取十六进制文本 (字节集 [2]), 2)
    十六进制颜色 ＝ 十六进制颜色 ＋ 取文本右边 (“00” ＋ 取十六进制文本 (字节集 [3]), 2)
    返回 (十六进制颜色)

.子程序 运算_表达式计算, 文本型, 公开, 成功返回计算后的文本结果，失败返回空文本
    .参数 Text, 文本型
    .参数 单位, 逻辑型, , 函数计算时使用，真为弧度制，假为角度制
    .局部变量 TempText, 文本型

    TempText ＝ 运算_函数计算 (Text, 单位)
    返回 (运算_括号计算 (TempText))

.子程序 运算_象素到厘米, 双精度小数型, 公开
    .参数 象素值
    .局部变量 厘米值, 双精度小数型

    厘米值 ＝ 象素值 ÷ 28.346
    返回 (厘米值)

.子程序 运算_厘米到象素, 双精度小数型, 公开
    .参数 厘米值, 双精度小数型
    .局部变量 象素值, 双精度小数型

    象素值 ＝ 厘米值 × 28.346
    返回 (象素值)

.子程序 运算_求直线距离, 双精度小数型, 公开, 返回两个点之间的直线距离,可以返回象素,也可以返回厘米
    .参数 水平象素1, 整数型
    .参数 垂直象素1, 整数型
    .参数 水平象素2, 整数型
    .参数 垂直象素2, 整数型
    .参数 类型, 整数型, 可空, 1为返回象素,2为返回厘米
    .局部变量 距离象素, 整数型
    .局部变量 距离厘米, 小数型

    距离象素 ＝ 求平方根 (求次方 (水平象素2 － 水平象素1, 2) ＋ 求次方 (垂直象素2 － 垂直象素1, 2))
    .如果 (是否为空 (类型) ＝ 真 或 类型 ＝ 1)
        返回 (距离象素)
    .否则
        距离厘米 ＝ 距离象素 ÷ 28
        返回 (距离厘米)
    .如果结束
    返回 (-1)

.子程序 运算_四舍六入五留双, 双精度小数型, 公开
    .参数 欲修约值, 双精度小数型
    .参数 保留位数, 整数型

    .如果真 (欲修约值 × 求次方 (10, 保留位数) － 取整 (欲修约值 × 求次方 (10, 保留位数)) ≠ 0.5)
        返回 (四舍五入 (欲修约值, 保留位数))
    .如果真结束
    .如果真 (欲修约值 × 求次方 (10, 保留位数) － 取整 (欲修约值 × 求次方 (10, 保留位数)) ＝ 0.5)
        .如果真 (取整 (欲修约值 × 求次方 (10, 保留位数)) ％ 2 ＝ 0)
            返回 (四舍五入 (欲修约值 － 求次方 (10, 保留位数 × -1), 保留位数))
        .如果真结束
        .如果真 (取整 (欲修约值 × 求次方 (10, 保留位数)) ％ 2 ＝ 1)
            返回 (四舍五入 (欲修约值, 保留位数))
        .如果真结束
        
    .如果真结束
    返回 (0)

