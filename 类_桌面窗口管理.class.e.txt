.版本 2

.程序集 类_桌面窗口管理, , 公开, 桌面窗口管理器（DWM）
.程序集变量 集_缩略图ID, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    ' 本类原始文档：https://docs.microsoft.com/zh-cn/windows/desktop/api/_dwm/

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 缩略图_创建, 整数型, 公开, 创建一个窗口略缩图，成功返回0，失败返回错误HRESULT代码。非零为失败
    .参数 参_目标窗口句柄, 整数型, , 呈现缩略图的窗口，必须为顶层窗口，否则参数无效
    .参数 参_源窗口句柄, 整数型, , 被缩略图的窗口，必须为顶层窗口，否则参数无效
    .局部变量 局_返回值, 整数型

    局_返回值 ＝ DwmRegisterThumbnail (参_目标窗口句柄, 参_源窗口句柄, 集_缩略图ID)
    返回 (局_返回值)
    ' 错误代码 ＝ 取十六进制文本 (局_返回值)
    ' .判断开始 (错误代码 ＝ “80263001”)
        ' 调试输出 (“略缩图”, 源窗口句柄, “由于桌面组合被禁用，操作无法完成。”)
    ' .判断 (错误代码 ＝ “80263002”)
        ' 调试输出 (“略缩图”, 源窗口句柄, “在远程会话中运行时不支持该操作。”)
    ' .判断 (错误代码 ＝ “80263003”)
        ' 调试输出 (“略缩图”, 源窗口句柄, “桌面窗口管理器(DWM)无法提供重定向面以完成DirectX当前。”)
    ' .判断 (错误代码 ＝ “80263004”)
        ' 调试输出 (“略缩图”, 源窗口句柄, “队列错误！”)
    ' .判断 (错误代码 ＝ “80280003”)
        ' 调试输出 (“略缩图”, 源窗口句柄, “参数错误！”)
    ' .判断 (错误代码 ＝ “80070057”)
        ' 调试输出 (“略缩图”, 源窗口句柄, “参数无效！”)
    ' .默认
        ' 调试输出 (“略缩图”, 源窗口句柄, “未知错误！HRESULT代码：” ＋ 错误代码)
        ' ' 更多错误查询页面：https://msdn.microsoft.com/en-us/library/cc704587.aspx
    ' .判断结束
    

.子程序 缩略图_更新, 整数型, 公开, 更新桌面窗口管理器（DWM）缩略图的属性。 非零为失败。目标窗口最小化时，缩略图不会更新
    .参数 参_绘制左, 整数型, 可空, 可空 默认0
    .参数 参_绘制顶, 整数型, 可空, 可空 默认0
    .参数 参_绘制宽, 整数型, 可空, 可空 默认50
    .参数 参_绘制高, 整数型, 可空, 可空 默认50
    .参数 参_透明度, 整数型, 可空, 可空  默认255
    .参数 参_是否可视, 逻辑型, 可空, 可空  默认真
    .局部变量 局_属性, 缩略图属性
    .局部变量 局_矩形, 精易_位置和大小J
    .局部变量 局_返回值, 整数型

    .如果真 (集_缩略图ID ＜ 0)
        返回 (-1)
    .如果真结束
    
    .如果真 (是否为空 (参_绘制左))
        参_绘制左 ＝ 0
    .如果真结束
    .如果真 (是否为空 (参_绘制顶))
        参_绘制顶 ＝ 0
    .如果真结束
    .如果真 (是否为空 (参_绘制宽))
        参_绘制宽 ＝ 50
    .如果真结束
    .如果真 (是否为空 (参_绘制高))
        参_绘制高 ＝ 50
    .如果真结束
    .如果真 (是否为空 (参_透明度))
        参_透明度 ＝ 255
    .如果真结束
    .如果真 (是否为空 (参_是否可视))
        参_是否可视 ＝ 真
    .如果真结束
    局_矩形.左边 ＝ 参_绘制左
    局_矩形.顶边 ＝ 参_绘制顶
    局_矩形.宽度 ＝ 参_绘制宽
    局_矩形.高度 ＝ 参_绘制高
    
    局_属性.目标矩形 ＝ 局_矩形
    局_属性.透明度 ＝ 参_透明度
    局_属性.可视 ＝ 参_是否可视
    局_属性.dwFlags ＝ 位或 (1, 4, 8)
    局_返回值 ＝ DwmUpdateThumbnailProperties (集_缩略图ID, 局_属性)
    返回 (局_返回值)
    

.子程序 缩略图_销毁, 整数型, 公开, 删除由DwmRegisterThumbnail函数创建的桌面窗口管理器（DWM）缩略图关系。 非零为失败
    .局部变量 局_返回值, 整数型

    .如果真 (集_缩略图ID ＜ 0)
        返回 (-1)
    .如果真结束
    局_返回值 ＝ DwmUnregisterThumbnail (集_缩略图ID)
    返回 (局_返回值)
    

.子程序 Aero_是否开启, 逻辑型, 公开, 是否启用了桌面窗口管理器（DWM）组合
    .局部变量 局_结果, 逻辑型

    ' 获取一个值，该值指示是否启用了桌面窗口管理器（DWM）组合。运行Windows 7或更早版本的计算机上的应用程序可以通过处理WM_DWMCOMPOSITIONCHANGED通知来侦听组合状态更改。
    DwmIsCompositionEnabled (局_结果)
    返回 (局_结果)

.子程序 Aero_禁用, 整数型, 公开, 非零为失败   注意：从win8开始此函数无效
    .局部变量 局_返回值, 整数型

    ' 注意   从Windows 8开始，使用DWM_EC_DISABLECOMPOSITION调用此函数无效。但是，该函数仍将返回成功代码。
    局_返回值 ＝ DwmEnableComposition (#DWM_EC_DISABLECOMPOSITION)
    返回 (局_返回值)

.子程序 Aero_启用, 整数型, 公开, 非零为失败   注意：从win8开始此函数无效
    .局部变量 局_返回值, 整数型

    局_返回值 ＝ DwmEnableComposition (#DWM_EC_ENABLECOMPOSITION)
    返回 (局_返回值)

.子程序 Aero_边框透明, 整数型, 公开, 非零为失败 注意：从win8开始此函数无效
    .参数 参_目标窗口句柄, 整数型
    .局部变量 sRT, 精易_位置和大小J
    .局部变量 局_返回值, 整数型

    sRT.宽度 ＝ -1
    局_返回值 ＝ DwmExtendFrameIntoClientArea (参_目标窗口句柄, sRT)
    返回 (局_返回值)

.子程序 Aero_客户区透明, 整数型, 公开, 非零为失败 注意：从win8开始此函数无效
    .参数 参_窗口句柄, 整数型
    .局部变量 sBlur, 模糊属性
    .局部变量 sRT, 精易_位置和大小J
    .局部变量 局_返回值, 整数型

    sRT.左边 ＝ 0
    sRT.顶边 ＝ 0
    sRT.宽度 ＝ 0
    sRT.高度 ＝ 0
    局_返回值 ＝ DwmExtendFrameIntoClientArea (参_窗口句柄, sRT)
    .如果真 (局_返回值 ≠ 0)
        返回 (局_返回值)
    .如果真结束
    sBlur.dwFlags ＝ #DWMWA_TRANSITIONS_FORCEDISABLED
    sBlur.fEnable ＝ 真
    局_返回值 ＝ DwmEnableBlurBehindWindow (参_窗口句柄, sBlur)
    返回 (局_返回值)

.子程序 Aero_非客户区特效范围, 整数型, 公开, 非零为失败 注意：从win8开始此函数无效
    .参数 参_窗口句柄, 整数型
    .参数 参_绘制左, 整数型, 可空, 可空 默认0
    .参数 参_绘制顶, 整数型, 可空, 可空 默认0
    .参数 参_绘制宽, 整数型, 可空, 可空 默认50
    .参数 参_绘制高, 整数型, 可空, 可空 默认50
    .局部变量 局_位置和大小, 精易_位置和大小J
    .局部变量 局_返回值, 整数型

    局_位置和大小.左边 ＝ 参_绘制左
    局_位置和大小.顶边 ＝ 参_绘制顶
    局_位置和大小.宽度 ＝ 参_绘制宽
    局_位置和大小.高度 ＝ 参_绘制高
    局_返回值 ＝ DwmExtendFrameIntoClientArea (参_窗口句柄, 局_位置和大小)
    返回 (局_返回值)
    

