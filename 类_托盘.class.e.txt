.版本 2

.程序集 类_托盘, , 公开
.程序集变量 集_重建句柄, 整数型
.程序集变量 集_通知结构, NOTIFYICONDATA
.程序集变量 集_回调指针, 整数型
.程序集变量 集_窗口句柄, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    集_重建句柄 ＝ RegisterWindowMessageA (“TaskbarCreated”) ' 注册一个消息，任务栏重建时发送此消息，拦截此消息重新添加托盘图标。

.子程序 FindTrayWnd, 整数型
    .局部变量 hWnd, 整数型

    hWnd ＝ FindWindowA (“Shell_TrayWnd”, 字符 (0))
    hWnd ＝ FindWindowExA (hWnd, 0, 取指针文本_ (“TrayNotifyWnd”), 0)
    hWnd ＝ FindWindowExA (hWnd, 0, 取指针文本_ (“SysPager”), 0)
    hWnd ＝ FindWindowExA (hWnd, 0, 取指针文本_ (“ToolbarWindow32”), 0)
    返回 (hWnd)

.子程序 FindNotifyIconOverflowWindow, 整数型, , win7特有
    .局部变量 hWnd, 整数型

    hWnd ＝ FindWindowA (“NotifyIconOverflowWindow”, 字符 (0))
    hWnd ＝ FindWindowExA (hWnd, 0, 取指针文本_ (“ToolbarWindow32”), 0)
    返回 (hWnd)

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    销毁 ()

.子程序 创建, 逻辑型, 公开, 创建一个托盘图标，成功返回真，失败返回假。
    .参数 窗口句柄, , , 不能设为第三方窗口
    .参数 图标数据, 字节集, 可空, 可为空，默认为自身图标。
    .参数 提示信息, 文本型, 可空, 本参数指定当鼠标移动到图标上后显示的提示信息。如果省略本参数，默认为空文本。

    .如果真 (IsWindow (窗口句柄) ＝ 1)
        集_窗口句柄 ＝ 窗口句柄
        .如果 (是否为空 (图标数据))
            集_通知结构.hIcon ＝ SendMessageA (窗口句柄, 127, 0, 0)
        .否则
            集_通知结构.hIcon ＝ 取图标句柄 (图标数据)
        .如果结束
        集_通知结构.cbSize ＝ 488
        集_通知结构.hWnd ＝ 窗口句柄
        集_通知结构.uID ＝ 1
        集_通知结构.uFlags ＝ 位或 (#NIF_ICON, #NIF_TIP, #NIF_INFO, #NIF_MESSAGE)
        集_通知结构.uCallbackMessage ＝ 11685
        集_通知结构.uTimeoutAndVersion ＝ 10000
        lstrcpy_bytes (集_通知结构.szTip, 提示信息)
        返回 (Shell_NotifyIcon (0, 集_通知结构))
    .如果真结束
    返回 (假)

.子程序 销毁, 逻辑型, 公开
    .如果真 (集_回调指针 ≠ 0)
        SetWindowLongA (集_窗口句柄, -4, 集_回调指针)
    .如果真结束
    连续赋值 (0, 集_窗口句柄, 集_回调指针)
    集_通知结构.szInfo ＝ { 0 }
    集_通知结构.szInfoTitle ＝ { 0 }
    返回 (Shell_NotifyIcon (2, 集_通知结构))

.子程序 置提示信息, 逻辑型, 公开
    .参数 提示信息, 文本型, , 限定128字符以内，超出默认被截断
    .局部变量 m_szTip, 文本型

    m_szTip ＝ 选择 (取文本长度 (提示信息) ＜ 128, 提示信息, 取文本左边 (提示信息, 128))
    ' 清除数组 (集_通知结构.szInfoTitle)
    ' 清除数组 (集_通知结构.szInfo)
    lstrcpy_bytes (集_通知结构.szTip, m_szTip)
    返回 (Shell_NotifyIcon (1, 集_通知结构)) ' #NIM_MODIFY

.子程序 气泡提示, 逻辑型, 公开
    .参数 提示标题, 文本型, , 限定64字符以内，超出默认被截断
    .参数 提示内容, 文本型, , 限定256字符以内，超出默认被截断
    .参数 提示图标, 整数型, 可空, 0.托盘图标_无图标,1.托盘图标_信息图标,2.托盘图标_警告图标,3.托盘图标_错误图标
    .参数 显示时间, 整数型, 可空, 设置气泡提示的时间，单位：毫秒 可空 默认3秒
    .局部变量 m_szInfo, 文本型
    .局部变量 m_szInfoTitle, 文本型

    集_通知结构.dwInfoFlags ＝ 提示图标
    m_szInfoTitle ＝ 选择 (取文本长度 (提示标题) ≤ 64, 提示标题, 取文本左边 (提示标题, 64))
    m_szInfo ＝ 选择 (取文本长度 (提示内容) ＜ 256, 提示内容, 取文本左边 (提示内容, 256))
    lstrcpy_bytes (集_通知结构.szInfoTitle, m_szInfoTitle)
    lstrcpy_bytes (集_通知结构.szInfo, m_szInfo)
    .判断开始 (显示时间 ＝ 0)
        集_通知结构.uTimeoutAndVersion ＝ 3000
    .默认
        集_通知结构.uTimeoutAndVersion ＝ 显示时间
    .判断结束
    返回 (Shell_NotifyIcon (1, 集_通知结构))

.子程序 置图标数据, 逻辑型, 公开
    .参数 图标数据, 字节集
    .局部变量 hIcon, 整数型

    hIcon ＝ 取图标句柄 (图标数据, 0)
    .如果真 (hIcon ＝ 0)
        返回 (假)
    .如果真结束
    集_通知结构.uFlags ＝ 位或 (#NIF_ICON, #NIF_TIP, #NIF_INFO, #NIF_MESSAGE)
    集_通知结构.hIcon ＝ hIcon
    返回 (Shell_NotifyIcon (1, 集_通知结构))

.子程序 取图标句柄, 整数型, , 凌晨孤星提供
    .参数 图标数据, 字节集, , 图标文件信息
    .参数 图标索引, 整数型, 可空, 从0开始
    .参数 图标宽度, 整数型, 参考 可空
    .参数 图标高度, 整数型, 参考 可空
    .局部变量 IconDirEntry, IconDirEntry
    .局部变量 图标句柄, 整数型

    CopyMemory_IconDirEntry (IconDirEntry, 取字节集中间 (图标数据, 6 ＋ 图标索引 × 16 ＋ 1, 16), 16)
    图标宽度 ＝ IconDirEntry.bWidth
    图标高度 ＝ IconDirEntry.bHeight
    图标句柄 ＝ CreateIconFromResource (取字节集中间 (图标数据, IconDirEntry.dwImageOffset ＋ 1, IconDirEntry.dwBytesInRes), IconDirEntry.dwBytesInRes, 真, 196608)
    返回 (图标句柄)

.子程序 挂接事件, , 公开, 当托盘图标创建成功后可建立挂接事件。
    .参数 执行事件, 子程序指针, 可空, 当用户用鼠标单击或双击本“托盘图标”后调用的子程序，该子程序应该有一个参数，传递事件类型，请参考“托盘事件_”开头的常量

    .如果真 (IsWindow (集_窗口句柄) ＝ 1)
        集_回调指针 ＝ SetWindowLongA (集_窗口句柄, -4, 到整数 (&托盘消息回调))
        SetPropA (集_窗口句柄, “WinProc”, 集_回调指针)
        SetPropA (集_窗口句柄, “CallbackMessage”, 11685)
        SetPropA (集_窗口句柄, “TaskbarCreatedMessage”, 集_重建句柄)
        .如果真 (是否为空 (执行事件) ＝ 假)
            SetPropA (集_窗口句柄, “Exec Event”, 到整数 (执行事件))
        .如果真结束
        
    .如果真结束
    返回 ()

.子程序 刷新托盘, , 公开, 刷新系统托盘(清除死掉的图标)
    .参数 隐藏区域, 逻辑型, 可空, 针对Win7以上系统有托盘隐藏区域，为真则刷新隐藏区域
    .局部变量 Rect, 精易_矩形J
    .局部变量 i, 整数型
    .局部变量 n, 整数型
    .局部变量 TrayWnd, 整数型

    TrayWnd ＝ 选择 (隐藏区域, FindNotifyIconOverflowWindow (), FindTrayWnd ())
    GetClientRect (TrayWnd, Rect)
    .计次循环首 (Rect.底边 ÷ 2, n)
        .计次循环首 (Rect.右边 ÷ 2, i)
            SendMessageA (TrayWnd, 512, n × 2, i × 2)
        .计次循环尾 ()
    .计次循环尾 ()
    

