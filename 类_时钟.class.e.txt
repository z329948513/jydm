.版本 2

.程序集 类_时钟, , 公开, http://msdn.microsoft.com/en-us/library/windows/desktop/ms682485(v=vs.85).aspx
.程序集变量 hTimerQueue, 整数型
.程序集变量 hTimer, 整数型
.程序集变量 集_程序指针, 子程序指针
.程序集变量 集_时钟周期, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 创建, 逻辑型, 公开
    .参数 子程序, 子程序指针, , 周期执行事件。周期事件格式：无返回值，参数1：整数型(参数)，参数2逻辑型
    .参数 时钟周期, 整数型, 可空, 默认为1000毫秒，即1秒。
    .参数 时钟参数, 整数型, 可空, 传递给时钟周期事件的参数，该参数是周期事件的第一个参数

    .如果真 (hTimerQueue ≠ 0 或 hTimer ≠ 0)
        销毁 ()
    .如果真结束
    .如果真 (集_程序指针 ≠ 子程序)
        集_程序指针 ＝ 子程序
    .如果真结束
    时钟周期 ＝ 选择 (是否为空 (时钟周期), 1000, 时钟周期)
    集_时钟周期 ＝ 时钟周期
    hTimerQueue ＝ CreateTimerQueue ()
    返回 (CreateTimerQueueTimer (hTimer, hTimerQueue, 到整数 (集_程序指针), 时钟参数, 0, 时钟周期, 0)) ' #WT_EXECUTEDEFAULT

.子程序 销毁, 逻辑型, 公开
    .局部变量 bool, 逻辑型

    .如果真 (hTimerQueue ＝ 0 或 hTimer ＝ 0)
        返回 (真)
    .如果真结束
    bool ＝ DeleteTimerQueueTimer (hTimerQueue, hTimer, 0)
    DeleteTimerQueue (hTimerQueue)
    连续赋值 (0, hTimer, 集_时钟周期)
    返回 (bool)

.子程序 置周期, 逻辑型, 公开
    .参数 时钟周期, 整数型

    .判断开始 (集_时钟周期 ＝ 0 且 时钟周期 ＞ 0 且 到整数 (集_程序指针) ≠ 0)
        返回 (创建 (集_程序指针, 时钟周期))
    .判断 (集_时钟周期 ＝ 时钟周期)
        返回 (真)
    .判断 (时钟周期 ＝ 0)
        返回 (销毁 ())
    .默认
        
    .判断结束
    集_时钟周期 ＝ 时钟周期
    返回 (ChangeTimerQueueTimer (hTimerQueue, hTimer, 0, 时钟周期))

.子程序 取周期, 整数型, 公开
    返回 (集_时钟周期)

