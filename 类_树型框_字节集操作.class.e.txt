.版本 2

.程序集 类_树型框_字节集操作, , 公开, 仅作为树型框项目数据的操作，并非对外部或自身组件操作
.程序集变量 类_ID, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    类_ID ＝ 打开内存文件 ()

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    清空 ()
    关闭文件 (类_ID)

.子程序 生成项目, 字节集, 公开, 创建一条项目。
    .参数 项目文本, 文本型
    .参数 图片索引, 整数型
    .参数 选中后图片索引, 整数型
    .参数 项目数值, 整数型
    .参数 是否加粗, 逻辑型, 可空, 空 不加粗
    .参数 是否扩展, 逻辑型, 可空, 空 展开
    .参数 缩进层次, 整数型
    .参数 检查框状态索引, 整数型, 可空, 空 = -1 表示无检查框，索引从零开始
    .局部变量 局_长度, 整数型
    .局部变量 局_项目数据, 字节集

    局_长度 ＝ 取文本长度 (项目文本)
    局_项目数据 ＝ 取空白字节集 (局_长度 ＋ 21)
    局_项目数据 ＝ 字节集替换 (局_项目数据, 1, 局_长度, 到字节集 (项目文本)) ' 项目文本
    局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 2, 4, 到字节集 (图片索引))
    局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 6, 4, 到字节集 (选中后图片索引))
    局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 10, 4, 到字节集 (项目数值))
    
    .如果真 (是否为空 (是否加粗))
        是否加粗 ＝ 假
    .如果真结束
    .如果真 (是否为空 (是否扩展))
        是否扩展 ＝ 真
    .如果真结束
    
    .判断开始 (是否加粗 ＝ 假 且 是否扩展 ＝ 假)
        局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 14, 4, { 0, 0, 0, 0 })
    .判断 (是否加粗 且 是否扩展 ＝ 假)
        局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 14, 4, { 1, 0, 0, 0 })
    .判断 (是否加粗 ＝ 假 且 是否扩展)
        局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 14, 4, { 2, 0, 0, 0 })
    .默认
        局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 14, 4, { 3, 0, 0, 0 })
    .判断结束
    
    .如果 (是否为空 (检查框状态索引) 或 检查框状态索引 ＝ -1)
        
    .否则
        局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 17, 1, 到字节集 (到字节 ((检查框状态索引 ＋ 1) × 16)))
    .如果结束
    局_项目数据 ＝ 字节集替换 (局_项目数据, 局_长度 ＋ 18, 4, 到字节集 (缩进层次))
    返回 (局_项目数据)

.子程序 初始化自字节集, 逻辑型, 公开, 从字节集创建树项目数据。
    .参数 树项目, 字节集

    .如果真 (类_ID ＝ 0)
        返回 (假)
    .如果真结束
    清空 ()
    
    写出字节集 (类_ID, 树项目)
    移到文件首 (类_ID)
    返回 (真)

.子程序 初始化自文件, 逻辑型, 公开, 从文件创建树项目数据。
    .参数 文件, 文本型

    .如果真 (类_ID ＝ 0)
        返回 (假)
    .如果真结束
    清空 ()
    
    .如果真 (文件是否存在 (文件))
        写出字节集 (类_ID, 读入文件 (文件))
        移到文件首 (类_ID)
        返回 (真)
    .如果真结束
    返回 (假)

.子程序 初始化, 逻辑型, 公开, 创建一个空的树项目数据。
    .如果真 (类_ID ＝ 0)
        返回 (假)
    .如果真结束
    清空 ()
    返回 (真)

.子程序 取项目, 字节集, 公开, 失败返回一个空字节集
    .局部变量 Z, 字节集

    移到文件首 (类_ID)
    Z ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    返回 (Z)

.子程序 取项目数, 整数型, 公开, 返回树型框中所有项目的数目。
    .局部变量 项目, 字节集
    .局部变量 临位, 整数型
    .局部变量 索引, 整数型

    移到文件首 (类_ID)
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 临位 ＞ 21)
        临位 ＝ 寻找字节集 (项目, { 0 }, 临位)
        索引 ＝ 索引 ＋ 1
        临位 ＝ 临位 ＋ 21
    .判断循环尾 ()
    返回 (索引)

.子程序 取缩进层次, 整数型, 公开, 返回指定项目所处缩进层次，层次值从1开始。如果未找到指定项目，返回 0 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            返回 (到数值 (取字节集数据 (项目, #字节型, 起位置 ＋ 17)))
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (0)

.子程序 取项目文本, 文本型, 公开, 返回指定项目的文本。如果该项目不存在，将返回空文本。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 临位, 整数型
    .局部变量 索引, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 临位 ＞ 21)
        临位 ＝ 寻找字节集 (项目, { 0 }, 临位)
        索引 ＝ 索引 ＋ 1
        临位 ＝ 临位 ＋ 21
        .如果真 (索引 ＝ 项目索引) ' 与上一项目索引一样时，刚好对应的是目标项目的位置
            返回 (到文本 (取字节集中间 (项目, 临位, 寻找字节集 (项目, { 0 }, 临位) － 临位)))
        .如果真结束
        
    .判断循环尾 ()
    返回 (“”)

.子程序 取图片, 整数型, 公开, 返回指定项目的图片索引。如果该项目不存在，将返回 -1 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            返回 (取字节集数据 (项目, #整数型, 起位置 ＋ 1))
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (0)

.子程序 取选中图片, 整数型, 公开, 返回指定项目被选中后的图片索引。如果该项目不存在，将返回 -1 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            返回 (取字节集数据 (项目, #整数型, 起位置 ＋ 5))
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (0)

.子程序 取项目数值, 整数型, 公开, 返回与指定项目相关联的数值。如果该项目不存在，将返回 0 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            返回 (取字节集数据 (项目, #整数型, 起位置 ＋ 9))
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (0)

.子程序 置项目文本, 逻辑型, 公开, 设置指定项目的文本。成功返回真，失败返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 欲置入的项目文本, 文本型
    .局部变量 项目, 字节集
    .局部变量 临位, 整数型
    .局部变量 索引, 整数型
    .局部变量 起始位置, 整数型
    .局部变量 结束位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 临位 ＞ 21)
        临位 ＝ 寻找字节集 (项目, { 0 }, 临位)
        索引 ＝ 索引 ＋ 1
        临位 ＝ 临位 ＋ 21
        .如果真 (索引 ＝ 项目索引) ' 与上一项目索引一样时，刚好对应的是目标项目的位置
            起始位置 ＝ 临位
            结束位置 ＝ 寻找字节集 (项目, { 0 }, 临位)
            跳出循环 ()
        .如果真结束
        
    .判断循环尾 ()
    清空 ()
    写出字节集 (类_ID, 字节集替换 (项目, 起始位置, 结束位置 － 起始位置, 到字节集 (欲置入的项目文本)))
    返回 (移到文件首 (类_ID))

.子程序 置图片, 逻辑型, 公开, 设置在指定项目首部所显示图片的索引。成功返回真，失败返回假。
    .参数 项目索引, , , 0 为项目一，1 为项目二，如此类推。
    .参数 图片索引, 整数型, , 图片索引用于指定图片组属性中的某张图片，从 0 开始。
    .局部变量 索引, 整数型
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            清空 ()
            写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 1, #整数型, 到字节集 (图片索引)))
            跳出循环 ()
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (移到文件首 (类_ID))

.子程序 置选中图片, 逻辑型, 公开, 设置指定项目被选中后所显示图片的索引。成功返回真，失败返回假。
    .参数 项目索引, , , 0 为项目一，1 为项目二，如此类推。
    .参数 图片索引, 整数型, , 图片索引用于指定图片组属性中的某张图片，从 0 开始。
    .局部变量 索引, 整数型
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            清空 ()
            写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 5, #整数型, 到字节集 (图片索引)))
            跳出循环 ()
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (移到文件首 (类_ID))

.子程序 置项目数值, 逻辑型, 公开, 设置与指定项目相关联的数值。成功返回真，失败返回假。
    .参数 项目索引, , , 0 为项目一，1 为项目二，如此类推。
    .参数 欲置入的项目数值, 整数型, 可空, 可以被省略。该数值与指定项目相关联。如果本参数被省略，默认值为 0 。
    .局部变量 索引, 整数型
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            清空 ()
            写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 9, #整数型, 到字节集 (欲置入的项目数值)))
            跳出循环 ()
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (移到文件首 (类_ID))

.子程序 加粗, , 公开
    .局部变量 项目索引, 整数型, , , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型
    .局部变量 粗展状态, 字节型
    .局部变量 起位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            清空 ()
            .判断开始 (粗展状态 ＝ 0)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 1 }))
            .判断 (粗展状态 ＝ 2)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 3 }))
            .默认
                
            .判断结束
            跳出循环 ()
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    移到文件首 (类_ID)

.子程序 取消加粗, , 公开, 将指定项目的标题取消加粗显示。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型
    .局部变量 粗展状态, 字节型
    .局部变量 起位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            清空 ()
            .判断开始 (粗展状态 ＝ 1)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 0 }))
            .判断 (粗展状态 ＝ 3)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 2 }))
            .默认
                
            .判断结束
            跳出循环 ()
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    移到文件首 (类_ID)

.子程序 是否已加粗, 逻辑型, 公开, 如果树型框中的指定项目的标题被加粗显示，返回真，否则返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 粗展状态, 字节型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            .判断开始 (粗展状态 ＝ 1 或 粗展状态 ＝ 3)
                返回 (真)
            .判断 (粗展状态 ＝ 0 或 粗展状态 ＝ 2)
                返回 (假)
            .默认
                
            .判断结束
            
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (假)

.子程序 扩展, , 公开, 打开指定项目的下属分枝。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型
    .局部变量 粗展状态, 字节型
    .局部变量 起位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            清空 ()
            .判断开始 (粗展状态 ＝ 0)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 2 }))
            .判断 (粗展状态 ＝ 1)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 3 }))
            .默认
                
            .判断结束
            跳出循环 ()
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    移到文件首 (类_ID)

.子程序 是否已扩展, 逻辑型, 公开, 指定项目的下属分枝是否已经展开。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 粗展状态, 字节型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            .判断开始 (粗展状态 ＝ 0 或 粗展状态 ＝ 1)
                返回 (假)
            .判断 (粗展状态 ＝ 2 或 粗展状态 ＝ 3)
                返回 (真)
            .默认
                
            .判断结束
            
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (假)

.子程序 收缩, , 公开, 缩回指定项目的下属分枝。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型
    .局部变量 粗展状态, 字节型
    .局部变量 起位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            清空 ()
            .判断开始 (粗展状态 ＝ 2)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 0 }))
            .判断 (粗展状态 ＝ 3)
                写出字节集 (类_ID, 字节集替换 (项目, 起位置 ＋ 13, 1, { 1 }))
            .默认
                
            .判断结束
            跳出循环 ()
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    移到文件首 (类_ID)

.子程序 是否已收缩, 逻辑型, 公开, 指定项目的下属分枝是否已经缩回。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 粗展状态, 字节型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            .判断开始 (粗展状态 ＝ 0 或 粗展状态 ＝ 1)
                返回 (真)
            .判断 (粗展状态 ＝ 2 或 粗展状态 ＝ 3)
                返回 (假)
            .默认
                
            .判断结束
            
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (假)

.子程序 加入项目, 整数型, 公开, 将单个项目加入到树型框中，成功返回加入后该项目所处的位置，失败返回 -1 。
    .参数 父项目索引, 整数型, 可空, 可以被省略。指定欲加入项目所处的父项目，0 为项目一，1 为项目二，如此类推。如果没有父项目（即欲加入项目为顶层项目），请提供值 -1 。如果本参数被省略，默认值为 -1 。
    .参数 项目文本, 文本型
    .参数 图片索引, 整数型, 可空, 可以被省略，如果本参数被省略，所加入项目的图片索引默认为 0 。
    .参数 选中图片索引, 整数型, 可空, 选中图片索引用于指定项目被选中后所显示的图片，-1 表示与图片索引一致。如果本参数被省略，所加入项目的选中图片索引默认为 -1 。
    .参数 项目数值, 整数型, 可空, 指定与本项目相关联的数值。如果本参数被省略，所加入项目的项目数值默认为 0 。
    .参数 是否加粗, 逻辑型, 可空, 可以被省略，如果本参数被省略，所加入项目的项目数值默认为假。
    .参数 检查框状态, 整数型, 可空, 可以被省略，如果本参数被省略，所加入项目的项目数值默认为 0 。默认情况下，0：未选中，1：选中，2：半选中。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 父层次, 整数型
    .局部变量 层次, 整数型
    .局部变量 插入位置, 整数型
    .局部变量 粗展状态, 字节型
    .局部变量 检查状态, 字节型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    
    .如果真 (是否为空 (图片索引) 或 图片索引 ＜ 0)
        图片索引 ＝ 0
    .如果真结束
    
    .如果真 (是否为空 (选中图片索引) 或 选中图片索引 ＜ 0)
        选中图片索引 ＝ 图片索引
    .如果真结束
    
    .如果真 (是否为空 (项目数值) 或 项目数值 ＜ 0)
        项目数值 ＝ 0
    .如果真结束
    
    .如果真 (是否为空 (是否加粗))
        是否加粗 ＝ 假
    .如果真结束
    
    .如果真 (是否为空 (检查框状态) 或 检查框状态 ＜ 0)
        检查框状态 ＝ 0
    .如果真结束
    
    
    
    .如果真 (是否为空 (父项目索引) 或 父项目索引 ＜ 0)
        移到文件尾 (类_ID)
        写出字节集 (类_ID, 生成项目 (项目文本, 图片索引, 选中图片索引, 项目数值, 是否加粗, , 1, 检查框状态))
        移到文件首 (类_ID)
        返回 (取项目数 () － 1)
    .如果真结束
    
    
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 父项目索引)
            父层次 ＝ 取字节集数据 (项目, #整数型, 起位置 ＋ 17)
            粗展状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 13)
            检查状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 16) ÷ 16 － 1
            .判断开始 (粗展状态 ＝ 0 或 粗展状态 ＝ 2)
                项目 ＝ 字节集替换 (项目, 起位置 ＋ 13, 1, { 2 })
            .判断 (粗展状态 ＝ 1 或 粗展状态 ＝ 3)
                项目 ＝ 字节集替换 (项目, 起位置 ＋ 13, 1, { 3 })
            .默认
                
            .判断结束
            
            .如果真 (检查状态 ＝ 1)
                .如果真 (选中图片索引 ＝ 0)
                    检查状态 ＝ 2
                .如果真结束
                
            .如果真结束
            项目 ＝ 字节集替换 (项目, 起位置 ＋ 16, 1, 到字节集 (检查状态))
            
            插入位置 ＝ 起位置 ＋ 21
        .如果真结束
        
        .如果真 (索引 ＞ 父项目索引)
            层次 ＝ 取字节集数据 (项目, #整数型, 起位置 ＋ 17)
            .如果 (层次 ≤ 父层次)
                跳出循环 ()
            .否则
                插入位置 ＝ 起位置 ＋ 21
            .如果结束
            
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    清空 ()
    写出字节集 (类_ID, 字节集替换 (项目, 插入位置, 0, 生成项目 (项目文本, 图片索引, 选中图片索引, 项目数值, 是否加粗, , 父层次 ＋ 1, 检查框状态)))
    移到文件首 (类_ID)
    返回 (索引)

.子程序 删除项目, , 公开, 删除树型框中的指定项目。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 层次, 整数型
    .局部变量 目标层次, 整数型
    .局部变量 目标索引, 整数型
    .局部变量 开始位置, 整数型
    .局部变量 结束位置, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            目标索引 ＝ 索引
            目标层次 ＝ 取字节集数据 (项目, #整数型, 起位置 ＋ 17)
        .如果真结束
        
        .如果真 (索引 ＞ 项目索引)
            层次 ＝ 取字节集数据 (项目, #整数型, 起位置 ＋ 17)
            .判断开始 (层次 ＞ 目标层次)
                结束位置 ＝ 起位置 ＋ 21
            .判断 (层次 ≤ 目标层次)
                跳出循环 ()
            .默认
                
            .判断结束
            
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    
    索引 ＝ 0
    起位置 ＝ 0
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
        .如果真 (索引 ＝ 项目索引)
            开始位置 ＝ 起位置
            跳出循环 ()
        .如果真结束
        
    .判断循环尾 ()
    清空 ()
    写出字节集 (类_ID, 字节集替换 (项目, 开始位置, 结束位置 － 开始位置, ))
    移到文件首 (类_ID)

.子程序 清空, , 公开, 删除树型框中的所有项目。
    移到文件首 (类_ID)
    删除数据 (类_ID, 取文件长度 (类_ID))

.子程序 取父项目, 整数型, 公开, 返回指定项目的父项目索引。如果该项目为顶层项目，将返回 -1 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 层次, 整数型
    .局部变量 上层次, 整数型, , "0"
    .局部变量 i, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .判断开始 (索引 ＜ 项目索引)
            加入成员 (上层次, 取字节集数据 (项目, #整数型, 起位置 ＋ 17))
        .判断 (索引 ＝ 项目索引)
            层次 ＝ 取字节集数据 (项目, #整数型, 起位置 ＋ 17)
            .变量循环首 (取数组成员数 (上层次), 1, -1, i)
                .如果真 (上层次 [i] ＜ 层次)
                    返回 (i － 1)
                .如果真结束
                
            .变量循环尾 ()
            跳出循环 ()
        .默认
            跳出循环 ()
        .判断结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (-1)

.子程序 是否有子项目, 逻辑型, 公开, 如果指定项目存在子项目，返回真，否则返回假。如果本命令返回真，意味着指定项目的下一项即为此项目的第一个子项目。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 层次, 整数型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            层次 ＝ 取字节集数据 (项目, #整数型, 起位置 ＋ 17)
        .如果真结束
        
        .如果真 (索引 ＞ 项目索引)
            .如果真 (取字节集数据 (项目, #整数型, 起位置 ＋ 17) ＞ 层次)
                返回 (真)
            .如果真结束
            
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (假)

.子程序 取检查框状态, 整数型, 公开, 返回值表示检查框中选中状态。默认情况下，0：未选中，1：选中，2：半选中。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 索引, 整数型
    .局部变量 状态, 字节型

    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (索引 ＝ 项目索引)
            状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 16)
            返回 (状态 ÷ 16 － 1)
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    返回 (0)

.子程序 置检查框状态, , 公开, 设置项目的检查框状态。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。在参数“连同子项目”为真的情况下，此参数可为-1，表示设置所有项目的检查框状态。
    .参数 检查框状态, 整数型, , 默认情况下，0表示未选中，1表示已选中，2表示半选中。
    .参数 连同子项目, 逻辑型, 可空, 可以被省略。如果此参数为真，则参数“项目索引”所指定项目的所有子项目也被一并设置其检查框状态。默认为假。
    .局部变量 项目, 字节集
    .局部变量 临位, 整数型
    .局部变量 索引, 整数型
    .局部变量 层次, 整数型

    .如果真 (项目索引 ＜ 0)
        返回 ()
    .如果真结束
    
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 临位 ＞ 21)
        临位 ＝ 寻找字节集 (项目, { 0 }, 临位)
        .如果真 (索引 ＝ 项目索引)
            项目 ＝ 字节集替换 (项目, 临位 ＋ 16, 1, 到字节集 (到字节 ((检查框状态 ＋ 1) × 16)))
            层次 ＝ 取字节集数据 (项目, #整数型, 临位 ＋ 17)
        .如果真结束
        
        .如果真 (连同子项目 且 索引 ＞ 项目索引)
            .如果真 (取字节集数据 (项目, #整数型, 临位 ＋ 17) ＞ 层次)
                项目 ＝ 字节集替换 (项目, 临位 ＋ 16, 1, 到字节集 (到字节 ((检查框状态 ＋ 1) × 16)))
            .如果真结束
            
        .如果真结束
        
        索引 ＝ 索引 ＋ 1
        临位 ＝ 临位 ＋ 21
    .判断循环尾 ()
    清空 ()
    写出字节集 (类_ID, 项目)
    移到文件首 (类_ID)

.子程序 查找项目_缩进层次, 整数型, 公开, 找到返回首个被找到项目的索引，失败返回-1。
    .参数 项目缩进层次, 整数型, , 默认情况下，0表示未选中，1表示已选中，2表示半选中。
    .参数 查找结果, 整数型, 参考 可空 数组, 提供变量容器，存放找到的项目索引。
    .局部变量 起位置, 整数型
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型

    重定义数组 (查找结果, 假, 0)
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (取字节集数据 (项目, #整数型, 起位置 ＋ 17) ＝ 项目缩进层次)
            加入成员 (查找结果, 索引)
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    .如果 (取数组成员数 (查找结果) ≠ 0)
        返回 (查找结果 [1])
    .否则
        返回 (-1)
    .如果结束
    

.子程序 查找项目_检查框状态, 整数型, 公开, 找到返回首个被找到项目的索引，失败返回-1。
    .参数 项目检查框状态, 整数型, , 默认情况下，0表示未选中，1表示已选中，2表示半选中。
    .参数 查找结果, 整数型, 参考 可空 数组, 提供变量容器，存放找到的项目索引。
    .局部变量 起位置, 整数型
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型
    .局部变量 状态, 字节型

    重定义数组 (查找结果, 假, 0)
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        状态 ＝ 取字节集数据 (项目, #字节型, 起位置 ＋ 16)
        .如果真 (状态 ÷ 16 － 1 ＝ 项目检查框状态)
            加入成员 (查找结果, 索引)
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    .如果 (取数组成员数 (查找结果) ≠ 0)
        返回 (查找结果 [1])
    .否则
        返回 (-1)
    .如果结束
    

.子程序 查找项目_选中图片, 整数型, 公开, 找到返回首个被找到项目的索引，失败返回-1。
    .参数 项目选中图片索引, 整数型
    .参数 查找结果, 整数型, 参考 可空 数组, 提供变量容器，存放找到的项目索引。
    .局部变量 起位置, 整数型
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型

    重定义数组 (查找结果, 假, 0)
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (取字节集数据 (项目, #整数型, 起位置 ＋ 5) ＝ 项目选中图片索引)
            加入成员 (查找结果, 索引)
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    .如果 (取数组成员数 (查找结果) ≠ 0)
        返回 (查找结果 [1])
    .否则
        返回 (-1)
    .如果结束
    

.子程序 查找项目_图片, 整数型, 公开, 找到返回首个被找到项目的索引，失败返回-1。
    .参数 项目图片索引, 整数型
    .参数 查找结果, 整数型, 参考 可空 数组, 提供变量容器，存放找到的项目索引。
    .局部变量 起位置, 整数型
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型

    重定义数组 (查找结果, 假, 0)
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (取字节集数据 (项目, #整数型, 起位置 ＋ 1) ＝ 项目图片索引)
            加入成员 (查找结果, 索引)
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    .如果 (取数组成员数 (查找结果) ≠ 0)
        返回 (查找结果 [1])
    .否则
        返回 (-1)
    .如果结束
    

.子程序 查找项目_数值, 整数型, 公开, 找到返回首个被找到项目的索引，失败返回-1。
    .参数 项目数值, 整数型
    .参数 查找结果, 整数型, 参考 可空 数组, 提供变量容器，存放找到的项目索引。
    .局部变量 起位置, 整数型
    .局部变量 项目, 字节集
    .局部变量 索引, 整数型

    重定义数组 (查找结果, 假, 0)
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
        起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
        .如果真 (取字节集数据 (项目, #整数型, 起位置 ＋ 9) ＝ 项目数值)
            加入成员 (查找结果, 索引)
        .如果真结束
        索引 ＝ 索引 ＋ 1
        起位置 ＝ 起位置 ＋ 21
    .判断循环尾 ()
    .如果 (取数组成员数 (查找结果) ≠ 0)
        返回 (查找结果 [1])
    .否则
        返回 (-1)
    .如果结束
    

.子程序 查找项目_文本, 整数型, , 找到返回首个被找到项目的索引，失败返回-1。
    .参数 项目文本, 文本型
    .参数 是否精确匹配, 逻辑型, 可空, 默认为真，为假时所有包含“参数一”的项目。
    .参数 区分大小写, 逻辑型, 可空, 默认不区分，“参数二”为真时将忽略本参数。
    .参数 查找结果, 整数型, 参考 可空 数组, 提供变量容器，存放找到的项目索引。
    .局部变量 目标长度, 整数型
    .局部变量 项目, 字节集
    .局部变量 起位置, 整数型
    .局部变量 下位置, 整数型
    .局部变量 索引, 整数型

    重定义数组 (查找结果, 假, 0)
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    目标长度 ＝ 取文本长度 (项目文本)
    
    .判断开始 (是否精确匹配 或 是否为空 (是否精确匹配))
        .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
            起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
            .如果真 (文本比较 (到文本 (取字节集中间 (项目, 起位置 － 目标长度, 目标长度)), 项目文本, 真) ＝ 0)
                加入成员 (查找结果, 索引)
            .如果真结束
            索引 ＝ 索引 ＋ 1
            起位置 ＝ 起位置 ＋ 21
        .判断循环尾 ()
        
    .默认
        
        
        .如果 (取反 (区分大小写) 或 是否为空 (区分大小写))
            .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
                起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
                .如果真 (寻找文本 (到文本 (取字节集中间 (项目, 起位置 － 目标长度, 目标长度)), 项目文本, , 真) ≠ -1)
                    加入成员 (查找结果, 索引)
                .如果真结束
                索引 ＝ 索引 ＋ 1
                起位置 ＝ 起位置 ＋ 21
            .判断循环尾 ()
            
        .否则
            
            .判断循环首 (取字节集长度 (项目) － 起位置 ＞ 21)
                起位置 ＝ 寻找字节集 (项目, { 0 }, 起位置)
                .如果真 (寻找文本 (到文本 (取字节集中间 (项目, 起位置 － 目标长度, 目标长度)), 项目文本, , 假) ≠ -1)
                    加入成员 (查找结果, 索引)
                .如果真结束
                索引 ＝ 索引 ＋ 1
                起位置 ＝ 起位置 ＋ 21
            .判断循环尾 ()
        .如果结束
        
    .判断结束
    
    .如果 (取数组成员数 (查找结果) ≠ 0)
        返回 (查找结果 [1])
    .否则
        返回 (-1)
    .如果结束
    

.子程序 取项目开始位置, 整数型, 公开, 查找指定索引的项目在字节集中的位置，失败返回-1。
    .参数 项目索引, 整数型
    .局部变量 项目, 字节集
    .局部变量 临位, 整数型
    .局部变量 索引, 整数型

    .如果真 (项目索引 ＜ 0)
        返回 (-1)
    .如果真结束
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 临位 ＞ 21)
        临位 ＝ 寻找字节集 (项目, { 0 }, 临位)
        索引 ＝ 索引 ＋ 1
        临位 ＝ 临位 ＋ 21
        .如果真 (索引 ＝ 项目索引) ' 与上一项目索引一样时，刚好对应的是目标项目的位置
            返回 (临位 － 1)
        .如果真结束
        
    .判断循环尾 ()
    返回 (-1)

.子程序 取项目长度, 整数型, 公开, 取指定项目的数据长度，失败返回-1。
    .参数 项目索引, 整数型
    .局部变量 项目, 字节集
    .局部变量 临位, 整数型
    .局部变量 索引, 整数型

    .如果真 (项目索引 ＜ 0)
        返回 (-1)
    .如果真结束
    项目 ＝ 读入字节集 (类_ID, 取文件长度 (类_ID))
    移到文件首 (类_ID)
    .判断循环首 (取字节集长度 (项目) － 临位 ＞ 21)
        临位 ＝ 寻找字节集 (项目, { 0 }, 临位)
        索引 ＝ 索引 ＋ 1
        临位 ＝ 临位 ＋ 21
        .如果真 (索引 ＝ 项目索引)
            返回 (寻找字节集 (项目, { 0 }, 临位) － 临位 ＋ 21)
        .如果真结束
        
    .判断循环尾 ()
    返回 (-1)

