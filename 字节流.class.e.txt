.版本 2

.程序集 字节流, , 公开
.程序集变量 内存指针, 整数型
.程序集变量 字节流对象, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    释放字节流 ()

.子程序 创建字节流, 整数型, 公开, 创建一个字节集流,并返回字节流对象指针
    .参数 字节集, 字节集, 可空
    .局部变量 局_指针, 整数型
    .局部变量 局_字节集长度, 整数型

    释放字节流 ()
    .如果 (是否为空 (字节集))
        内存指针 ＝ GlobalAlloc (2, 0) ' GMEM_MOVEABLE=2
        CreateStreamOnHGlobal (内存指针, 真, 字节流对象)
    .否则
        局_字节集长度 ＝ 取字节集长度 (字节集)
        内存指针 ＝ GlobalAlloc (0, 局_字节集长度) ' GMEM_FIXED=0
        局_指针 ＝ GlobalLock (内存指针)
        写到内存 (字节集, 局_指针, 局_字节集长度)
        GlobalUnlock (内存指针)
        CreateStreamOnHGlobal (内存指针, 假, 字节流对象)
    .如果结束
    返回 (字节流对象)

.子程序 释放字节流, , 公开, 释放字节集流,使用后释放掉内存空间
    .如果真 (字节流对象 ≠ 0)
        COM_Release (字节流对象)
        字节流对象 ＝ 0
    .如果真结束
    
    .如果真 (内存指针 ≠ 0)
        GlobalFree (内存指针)
        内存指针 ＝ 0
    .如果真结束
    

.子程序 取字节流指针, 整数型, 公开, 返回字节流对象指针
    返回 (字节流对象)

.子程序 取字节流尺寸, 整数型, 公开, 返回字节流的字节集尺寸
    返回 (GlobalSize (内存指针))

.子程序 取出字节集, 字节集, 公开, 取出字节流中的字节集数据
    .局部变量 局_字节集, 字节集
    .局部变量 局_指针, 整数型
    .局部变量 局_字节流尺寸, 整数型

    局_字节流尺寸 ＝ 取字节流尺寸 ()
    局_指针 ＝ GlobalLock (内存指针)
    局_字节集 ＝ 指针到字节集 (局_指针, 局_字节流尺寸)
    GlobalUnlock (内存指针)
    返回 (局_字节集)

