.版本 2

.程序集 内存共享, , 公开
.程序集变量 集_内存数组, 整数型, , "0"

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    全部释放 ()

.子程序 申请, 整数型, 公开, 申请指定大小的内存空间,返回内存地址
    .参数 内存大小, 整数型, , 要申请的内存空间大小
    .局部变量 lpdata, 整数型

    lpdata ＝ LocalAlloc (0, 内存大小)
    增加地址 (lpdata)
    返回 (lpdata)

.子程序 写入, 整数型, 公开, 如果指定的内存地址不存在,将自动申请并返回新内存地址,否则将返回原来的内存地址
    .参数 地址, 整数型, 可空, 指定写入地址,如果为空将自动创建
    .参数 数据, 字节集, 参考, 欲写入的字节集数据
    .局部变量 length, 整数型, , , 数据长度

    length ＝ 取字节集长度 (数据)
    .如果真 (length ＝ 0)
        返回 (0)
    .如果真结束
    .如果真 (地址 ＝ 0)
        地址 ＝ 申请 (length)
    .如果真结束
    RtlMoveMemory_字节集 (地址, 数据, length)
    返回 (地址)

.子程序 读取, 字节集, 公开, 读取地址数据！！注意，也可以调用核心库方法 指针到字节集(地址,内存共享.取内存长度(地址)) 来同样取得数据
    .参数 地址, 整数型, , 由内存映射返回的地址
    .参数 读取位置, 整数型, 可空, 从1开始到结束,如果留空则从首地址开始,超出作用范围将指定0
    .参数 长度, 整数型, 可空, 若读取位置+长度超过总数据长度,将自动指定0
    .局部变量 数据, 字节集, , , 指定缓冲区,如果缓冲区长度不够或超出将自动匹配,如果包含数据将自动清空
    .局部变量 length, 整数型
    .局部变量 offset, 整数型, , , 偏移,位置

    length ＝ LocalSize (地址)
    .如果真 (length ＝ 0) ' 无效地址或空数据
        返回 ({ })
    .如果真结束
    长度 ＝ 选择 (长度 ＝ 0, LocalSize (地址) － 读取位置, 长度)
    数据 ＝ 选择 (长度 ≤ 0, 选择 (length ≠ 取字节集长度 (数据), 取空白字节集 (length), { }), 取空白字节集 (长度))
    offset ＝ 选择 (读取位置 ＜ 0 或 读取位置 ＋ 长度 ＞ length, 0, 读取位置)
    RtlMoveMemory_int2Bytes (数据, 地址 ＋ offset, 长度)
    返回 (数据)

.子程序 释放, 逻辑型, 公开, 释放指定的内存
    .参数 内存地址, 整数型, , 由内存映射返回的地址

    删除地址 (内存地址)
    返回 (LocalFree (内存地址) ＝ 0)

.子程序 取内存长度, 整数型, 公开, 取指定内存地址的长度
    .参数 地址, 整数型, , 内存地址

    返回 (LocalSize (地址))

.子程序 全部释放, , 公开, 释放所有申请的内存
    .局部变量 局_计次, 整数型

    .计次循环首 (取数组成员数 (集_内存数组), 局_计次)
        LocalFree (集_内存数组 [局_计次])
    .计次循环尾 ()
    清除数组 (集_内存数组)

.子程序 增加地址
    .参数 地址, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_记录, 整数型

    .计次循环首 (取数组成员数 (集_内存数组), 局_计次)
        .如果真 (集_内存数组 [局_计次] ≠ 地址)
            局_记录 ＝ 局_记录 ＋ 1
        .如果真结束
        
    .计次循环尾 ()
    .如果真 (局_记录 ＝ 取数组成员数 (集_内存数组))
        加入成员 (集_内存数组, 地址)
    .如果真结束
    

.子程序 删除地址
    .参数 内存地址, 整数型
    .局部变量 局_计次, 整数型

    .计次循环首 (取数组成员数 (集_内存数组), 局_计次)
        .如果真 (集_内存数组 [局_计次] ＝ 内存地址)
            删除成员 (集_内存数组, 局_计次, 1)
            跳出循环 ()
        .如果真结束
        
    .计次循环尾 ()

