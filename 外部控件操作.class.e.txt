.版本 2

.程序集 外部控件操作, , 公开
.程序集变量 系统类别, 整数型
.程序集变量 映射文件, 内存映射数据9X, , "0"

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 结束操作, , 公开
    .参数 进程句柄, 整数型

    .如果真 (进程句柄 ≠ 0)
        CloseHandle (进程句柄) ' 关闭一个内核对象。
        清除数组 (映射文件)
        进程句柄 ＝ 0
    .如果真结束
    

.子程序 打开进程, 整数型, 公开, 成功就返回进程句柄
    .参数 对象句柄, 整数型
    .局部变量 进程ID, 整数型

    GetWindowThreadProcessId (对象句柄, 进程ID)
    返回 (OpenProcess (2035711, 0, 进程ID)) ' 2035711

.子程序 写进程内存, 逻辑型, 公开
    .参数 进程句柄, 整数型
    .参数 数据指针, 整数型
    .参数 数据地址, 整数型
    .参数 数据长度, 整数型

    返回 (WriteProcessMemory_整数型 (进程句柄, 数据指针, 数据地址, 数据长度, 0) ≠ 0) ' 写进程内存

.子程序 分配外部指针, 整数型, 公开
    .参数 进程句柄, 整数型
    .参数 数据长度, 整数型, , 分配的数据长度
    .局部变量 TEMP, 内存映射数据9X

    系统类别 ＝ 取操作系统类别 ()
    .判断开始 (系统类别 ＝ 3)
        返回 (VirtualAllocEx (进程句柄, 0, 数据长度, 4096, 4))
    .默认
        TEMP.映射文件号 ＝ CreateFileMappingA (-1, 0, 4, 0, 数据长度, “”) ' 创建一个新的文件映射对象
        TEMP.指针 ＝ MapViewOfFile (TEMP.映射文件号, 983071, 0, 0, 0) ' 将一个文件映射对象映射到当前应用程序的地址空间
        重定义数组 (映射文件, 真, 取数组成员数 (映射文件) ＋ 1)
        映射文件 [取数组成员数 (映射文件)] ＝ TEMP
        返回 (TEMP.指针)
    .判断结束
    

.子程序 销毁外部指针, , 公开
    .参数 进程句柄, 整数型
    .参数 外部指针, 整数型
    .局部变量 成员数, 整数型
    .局部变量 I, 整数型

    .判断开始 (系统类别 ＝ 3)
        VirtualFreeEx (进程句柄, 外部指针, 0, 32768) ' 在目标进程中释放内存空间。
    .默认
        成员数 ＝ 取数组成员数 (映射文件)
        .变量循环首 (1, 成员数, 1, I)
            .如果真 (映射文件 [I].指针 ＝ 外部指针)
                UnmapViewOfFile (外部指针) ' 在当前应用程序的内存地址空间解除对一个文件映射对象的映射
                CloseHandle (映射文件 [I].映射文件号)
                删除成员 (映射文件, I, )
                跳出循环 ()
            .如果真结束
            
        .变量循环尾 ()
    .判断结束
    

.子程序 读外部指针数据, 字节集, 公开
    .参数 进程句柄, 整数型
    .参数 外部程序指针, 整数型
    .参数 读取数据大小, 整数型
    .局部变量 内存数据, 字节集

    内存数据 ＝ 取空白字节集 (读取数据大小)
    ReadProcessMemory_字节集 (进程句柄, 外部程序指针, 内存数据, 读取数据大小, 0)
    返回 (内存数据)
    

