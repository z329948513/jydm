.版本 2
.支持库 dp1
.支持库 spec
.支持库 shell

.程序集 集_蓝奏网盘
.程序集变量 集_id, 文本型
.程序集变量 集_sn, 文本型
.程序集变量 集_串码, 文本型


.子程序 更新_软件自动更新蓝奏, , 公开
    .参数 软件名和版本号, 文本型
    .参数 下载地址_原网址, 文本型
    .参数 保存目录, 文本型
    .参数 下载完是否运行程序, 文本型, 可空, 可以填“假”
    .局部变量 下载地址_真实, 文本型
    .局部变量 更新程序路径, 文本型
    .局部变量 命令行文本, 文本型
    .局部变量 原网址_加密, 文本型
    .局部变量 保存目录_加密, 文本型
    .局部变量 下载地址_真实加密, 文本型
    .局部变量 软件名和版本号_加密, 文本型

    更新程序路径 ＝ 取运行目录 () ＋ “\更新.exe”
    
    .如果真 (文件是否存在 (更新程序路径) ＝ 假 或 取数据摘要 (读入文件 (更新程序路径)) ≠ “4d0033cdabb86032cd2458a2d83a0c21”)
        删除文件 (更新程序路径)
        写到文件 (更新程序路径, #更新)
        
    .如果真结束
    
    
    ' 集_下载地址 ＝ 文本_解密 (命令行 [1], “a1”)
    ' 集_真实下载地址 ＝ 文本_解密 (命令行 [2], “a1”)
    ' 集_保存目录 ＝ 文本_解密 (命令行 [3], “a1”)
    ' 集_软件名或版本号 ＝ 文本_解密 (命令行 [4], “a1”)
    ' 集_下载完是否运行程序 ＝ 命令行 [5]
    
    
    软件名和版本号_加密 ＝ 文本_加密 (软件名和版本号, “a1”)
    
    原网址_加密 ＝ 文本_加密 (下载地址_原网址, “a1”)
    
    保存目录_加密 ＝ 文本_加密 (保存目录, “a1”)
    
    .如果真 (下载完是否运行程序 ＝ “”)
        下载完是否运行程序 ＝ “真”
    .如果真结束
    
    线程_初始化COM库 ()
    蓝奏网盘_取直链 (下载地址_原网址, , 下载地址_真实)
    线程_取消COM库 ()
    
    调试输出 (“下载地址_原网址”, 下载地址_原网址)
    调试输出 (“下载地址_真实”, 下载地址_真实)
    
    
    下载地址_真实加密 ＝ 文本_加密 (下载地址_真实, “a1”)
    
    
    调试输出 (下载地址_真实加密)
    
    命令行文本 ＝ 原网址_加密 ＋ “ ” ＋ 下载地址_真实加密 ＋ “ ” ＋ 保存目录_加密 ＋ “ ” ＋ 软件名和版本号_加密 ＋ “ ” ＋ 下载完是否运行程序 ＋ “ ”
    
    执行 (4, 更新程序路径, 命令行文本, , 2)
    程序_延时 (800)
    结束 ()

.子程序 蓝奏网盘_取直链, 逻辑型
    .参数 targetUrl, 文本型, , 解析的目标地址如'https://wwi.lanzoux.com/xxxxxxxx,https://xiaodao.lanzout.com/xxxxxxxx
    .参数 pwd, 文本型, 可空, 如果有密码请传入密码，自动判断页面是否为需要输入密码 参数有密码要填 没密码可填可不填
    .参数 返回直连网址, 文本型, 参考, 参数方式接受结果
    .局部变量 tagUrl, 文本型, , , 文件标识的地址
    .局部变量 webUrl, 文本型, , , 文件域名
    .局部变量 headerPost, 类_POST数据类, , , 普通的浏览器协议
    .局部变量 dataPost, 类_POST数据类, , , 需要获取真实入口连接提交所需要的专属协议
    .局部变量 srcPost, 类_POST数据类, , , 需要获取真实入口连接提交所需要的专属协议
    .局部变量 time, 文本型, , , time为了兼容不同下载页面的取值范围
    .局部变量 encryptionText, 文本型, , , 接收标识地址里面的加密文本 如sign
    .局部变量 realUrlEntranceJson, 文本型, , , 返回真实地址的入口Json串
    .局部变量 Json, 类_json2
    .局部变量 resultUrl, 文本型, , , 最终的URL地址
    .局部变量 dataText, 文本型, , , 需要提交的加密参数post组合文本
    .局部变量 regular, 正则表达式类
    .局部变量 i, 整数型
    .局部变量 sign, 文本型
    .局部变量 signTemp, 文本型
    .局部变量 fileIds, 文本型, , "0"
    .局部变量 fileApiText, 文本型

    .如果真 (targetUrl ＝ “” 或 寻找文本 (targetUrl, “https://”, , 假) ＝ -1 或 寻找文本 (targetUrl, “lan”, , 假) ＝ -1)
        返回直连网址 ＝ “链接错误，不是一个正常的蓝奏地址”
        返回 (假)
        
    .如果真结束
    
    ' 获取主域名
    webUrl ＝ 文本_取左边 (targetUrl, “/”, -1)
    ' 浏览器识别头
    headerPost.添加 (“user-agent”, “Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/100.0.4896.75 Safari/537.36”, )
    srcPost.添加 (“user-agent”, headerPost.获取Post数据 ())
    ' 获取文件标识地址
    
    
    tagUrl ＝ 编码_utf8到gb2312 (到文本 (网页_访问J (targetUrl, 0, , , , headerPost.获取协议头数据 (), , , , , , , )))
    .如果真 (tagUrl ＝ “”)
        返回直连网址 ＝ “获取文件标识地址失败”
        返回 (假)
    .如果真结束
    
    .如果 (寻找文本 (tagUrl, “输入密码”, , 假) ＞ -1)
        ' 有密码
        .如果真 (pwd ＝ “”)
            返回直连网址 ＝ “请传入文件获取的密码”
            返回 (假)
        .如果真结束
        
        ' 2025-06-10
        tagUrl ＝ 文本_替换 (tagUrl, , , , 文本_取出中间文本 (tagUrl, “/*”, “*/”, , ), “”)
        regular.创建 (“'([^'\\]*(?:\\.[^'\\]*)*)'”, tagUrl)
        .计次循环首 (regular.取匹配数量 (), i)
            signTemp ＝ 子文本替换 (regular.取匹配文本 (i), “'”, “”, , , 真)
            .如果真 (取文本长度 (signTemp) ＞ 取文本长度 (sign))
                sign ＝ signTemp
            .如果真结束
            
        .计次循环尾 ()
        .如果真 (sign ＝ “”)
            返回直连网址 ＝ “没有找到sign的值,解析失败”
            返回 (假)
        .如果真结束
        dataText ＝ “action=” ＋ 文本_取出中间文本 (tagUrl, “'action':'”, “'”) ＋ “&sign=” ＋ sign ＋ “&kd=” ＋ 文本_取出中间文本 (tagUrl, “kdns =”, “;”) ＋ “&p=” ＋ pwd
        ' 2025-06-10
        文本_取中间_批量 (tagUrl, “url : '”, “',”, fileIds)
        .计次循环首 (取数组成员数 (fileIds), i)
            .如果真 (取文本长度 (fileApiText) ＜ 取文本长度 (fileIds [i]))
                fileApiText ＝ fileIds [i]
            .如果真结束
            
        .计次循环尾 ()
        srcPost.添加 (“Referer”, targetUrl)
    .否则
        ' 无密码
        ' time为了兼容页面不同的范围取值
        time ＝ 取文本左边 (时间_取北京时间戳 (真), 3)
        tagUrl ＝ 文本_取出中间文本 (tagUrl, “name=” ＋ #引号 ＋ “” ＋ time, “</iframe>”, , )
        tagUrl ＝ 文本_取出中间文本 (tagUrl, “src=” ＋ #引号, #引号, , )
        .如果真 (tagUrl ＝ “”)
            返回直连网址 ＝ “获取文件标识解析取值失败”
            返回 (假)
        .如果真结束
        tagUrl ＝ webUrl ＋ tagUrl
        ' 请求真实入口串获取真实地址的时候需要用到的协议
        srcPost.添加 (“Referer”, tagUrl)
        ' 访问标识地址获取 需要访问获取真实链接的加密参数
        tagUrl ＝ 编码_utf8到gb2312 (到文本 (网页_访问J (tagUrl, 0, , , , headerPost.获取协议头数据 (), , , , , , , )))
        .如果真 (tagUrl ＝ “”)
            返回直连网址 ＝ “获取需要访问获取真实链接的加密参数失败”
            返回 (假)
        .如果真结束
        ' 2022-07-05修复了识别sign错误
        encryptionText ＝ 文本_取出中间文本 (子文本替换 (tagUrl, “//data”, “”, , , 假), “data :”, “},”) ' 2022-07-05
        .如果真 (encryptionText ＝ “”)
            返回直连网址 ＝ “数据中无sign加密信息或解析失败”
            返回 (假)
        .如果真结束
        
        
        regular.创建 (“[0-9a-zA-Z\_]{70,}”, tagUrl, , , , )
        dataPost.添加 (“action”, 文本_取出中间文本 (encryptionText, “'action':'”, “',”))
        dataPost.添加 (“signs”, 文本_取出中间文本 (tagUrl, “ajaxdata = '”, “';”))
        .如果 (regular.取匹配文本 (1, ) ＝ “”)
            dataPost.添加 (“sign”, regular.取匹配文本 (2, ))
        .否则
            dataPost.添加 (“sign”, regular.取匹配文本 (1, ))
        .如果结束
        dataPost.添加 (“ves”, “1”)
        dataPost.添加 (“websign”, 文本_取出中间文本 (tagUrl, “ciucjdsdc = '”, “';”))
        dataPost.添加 (“websignkey”, 文本_取出中间文本 (tagUrl, “aihidcms = '”, “';”))
        ' 2025-06-10
        dataPost.添加 (“kd”, 文本_取出中间文本 (tagUrl, “kdns =”, “;”))
        ' 2025-06-10
        文本_取中间_批量 (tagUrl, “url : '”, “',”, fileIds)
        .计次循环首 (取数组成员数 (fileIds), i)
            .如果真 (取文本长度 (fileApiText) ＜ 取文本长度 (fileIds [i]))
                fileApiText ＝ fileIds [i]
            .如果真结束
            
        .计次循环尾 ()
        dataText ＝ dataPost.获取Post数据 ()
    .如果结束
    
    ' 2025-06-10
    .如果真 (fileApiText ＝ “”)
        返回直连网址 ＝ “获取文件提交api的接口失败”
        返回 (假)
    .如果真结束
    
    realUrlEntranceJson ＝ 编码_utf8到gb2312 (到文本 (网页_访问J (webUrl ＋ fileApiText, 1, dataText, , , srcPost.获取协议头数据 (), , , , , , , )))
    .如果真 (realUrlEntranceJson ＝ “” 或 Json.解析 (realUrlEntranceJson) ＝ 假)
        返回直连网址 ＝ “获取真实地址入口地址失败或解析失败”
        返回 (假)
    .如果真结束
    
    .如果真 (Json.取通用属性 (“zt”) ＝ “0”)
        返回直连网址 ＝ Json.取通用属性 (“inf”)
        返回 (假)
    .如果真结束
    
    ' 访求真实加密的下载地址来获取最终协议返回的真实地址
    网页_访问_对象 (Json.取通用属性 (“dom”) ＋ “/file/” ＋ Json.取通用属性 (“url”), 0, , , , , resultUrl, , 真, , , , )
    ' 协议自带真实地址 Location
    resultUrl ＝ 网页_协议头_取信息 (resultUrl, “Location”)
    .如果真 (resultUrl ＝ “”)
        返回直连网址 ＝ “获取真实下载地址失败”
        返回 (假)
    .如果真结束
    
    返回直连网址 ＝ resultUrl
    返回 (真)

