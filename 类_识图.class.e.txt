.版本 2

.程序集 类_识图
.程序集变量 LF内容, 文本型
.程序集变量 LF颜色, 整数型
.程序集变量 LF大小, 整数型
.程序集变量 LF加粗, 整数型
.程序集变量 LF斜体, 逻辑型
.程序集变量 LF下划线, 逻辑型
.程序集变量 LF删除线, 逻辑型
.程序集变量 LF字体名称, 文本型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 制作范围, 精易_矩形J, 公开, 生成一个矩形的范围,并返回
    .参数 左边, 整数型, 可空, 查找范围的左边
    .参数 顶边, 整数型, 可空, 查找范围的顶边
    .参数 宽度, 整数型, 可空, 查找范围的宽度
    .参数 高度, 整数型, 可空, 查找范围的高度
    .局部变量 矩形, 精易_矩形J

    矩形.左边 ＝ 左边
    矩形.顶边 ＝ 顶边
    矩形.右边 ＝ 矩形.左边 ＋ 宽度
    矩形.底边 ＝ 矩形.顶边 ＋ 高度
    返回 (矩形)

.子程序 找图_从句柄, 精易_坐标J, 公开, 在屏幕或指定窗口上找图，成功返回找到的位置坐标，失败坐标返回-1。，BUG命令谨慎使用
    .参数 窗口句柄, 整数型, 可空, 可空。默认在屏幕中查找。
    .参数 目标图片, 字节集, , 要查找的图片数据   bmp图片
    .参数 寻找范围, 精易_矩形J, 可空, 可空。可用 制作范围() 制作返回。留空为查找整个屏幕或窗口。
    .参数 是否在后台, 逻辑型, 可空, 可空。默认为假，在当前屏幕中查找，真为后台窗口中查找。
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 位图, 类_位图操作
    .局部变量 保存位图1, 字节集
    .局部变量 宽1, 整数型
    .局部变量 高1, 整数型
    .局部变量 保存位图2, 字节集
    .局部变量 保存位图集, 字节集, , "0"
    .局部变量 宽2, 整数型
    .局部变量 高2, 整数型
    .局部变量 补零2, 整数型
    .局部变量 i, 整数型
    .局部变量 找到位置, 整数型
    .局部变量 i2, 整数型
    .局部变量 窗口坐标, 精易_坐标J
    .局部变量 矩形范围, 精易_矩形J

    找到坐标.x ＝ -1
    找到坐标.y ＝ -1
    .判断开始 (是否在后台)
        位图.从窗口创建 (窗口句柄, 寻找范围.左边, 寻找范围.顶边, 寻找范围.右边, 寻找范围.底边, 8)
    .默认
        .如果真 (窗口句柄 ≠ 0)
            ClientToScreen (窗口句柄, 窗口坐标)
            GetClientRect (窗口句柄, 矩形范围)
            .如果真 (寻找范围.右边 ＝ 0 或 寻找范围.右边 ＞ 矩形范围.右边 － 寻找范围.左边)
                寻找范围.右边 ＝ 矩形范围.右边 － 寻找范围.左边
            .如果真结束
            .如果真 (寻找范围.底边 ＝ 0 或 寻找范围.底边 ＞ 矩形范围.底边 － 寻找范围.顶边)
                寻找范围.底边 ＝ 矩形范围.底边 － 寻找范围.顶边
            .如果真结束
            
        .如果真结束
        位图.从屏幕创建 (窗口坐标.x ＋ 寻找范围.左边, 窗口坐标.y ＋ 寻找范围.顶边, 寻找范围.右边, 寻找范围.底边, 8)
    .判断结束
    保存位图1 ＝ 位图.取出颜色数据 ()
    宽1 ＝ 位图.取宽度2 ()
    高1 ＝ 位图.取高度2 ()
    宽1 ＝ 宽1 ＋ (4 － 宽1 ％ 4) ％ 4 ' 将宽补足4位
    
    .如果真 (位图.从数据创建 (目标图片) ＝ 假) ' 创建失败,直接返回
        返回 (找到坐标)
    .如果真结束
    位图.转换位深度 (8)
    保存位图2 ＝ 位图.取出颜色数据 ()
    宽2 ＝ 位图.取宽度2 ()
    高2 ＝ 位图.取高度2 ()
    补零2 ＝ (4 － 宽2 ％ 4) ％ 4
    清除数组 (保存位图集)
    .计次循环首 (高2, i)
        加入成员 (保存位图集, 取字节集中间 (保存位图2, (宽2 ＋ 补零2) × (i － 1) ＋ 1, 宽2))
    .计次循环尾 ()
    
    .循环判断首 ()
        找到位置 ＝ 寻找字节集 (保存位图1, 保存位图集 [1], 找到位置 ＋ 1)
        ' 输出调试文本 (找到位置)
        .如果真 (找到位置 ＝ -1)
            返回 (找到坐标)
        .如果真结束
        .计次循环首 (高2 － 1, i2)
            .如果 (取字节集中间 (保存位图1, 找到位置 ＋ 宽1 × i2, 宽2) ＝ 保存位图集 [i2 ＋ 1])
                .如果真 (i2 ＝ 高2 － 1)
                    找到坐标.x ＝ (找到位置 － 1) ％ 宽1 ' 得到初始点坐标
                    找到坐标.y ＝ 高1 － (找到位置 － 1) ＼ 宽1 － 1
                    找到坐标.x ＝ 找到坐标.x ＋ 宽2 ＼ 2 ＋ 寻找范围.左边 ＋ 窗口坐标.x ' 得到中心坐标
                    找到坐标.y ＝ 找到坐标.y － 高2 ＼ 2 ＋ 寻找范围.顶边 ＋ 窗口坐标.y
                    返回 (找到坐标)
                .如果真结束
                
            .否则
                跳出循环 ()
            .如果结束
            
        .计次循环尾 ()
    .循环判断尾 (找到位置 ＞ 0)
    返回 (找到坐标)

.子程序 找图_从字节集, 精易_坐标J, 公开, 在位图数据中找图片(返回找到的图片中心坐标,找不到返回-1坐标)，BUG命令谨慎使用
    .参数 范围图片, 字节集, , 默认为上次范围图片
    .参数 目标图片, 字节集, , 默认为上次目标图片
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 位图, 类_位图操作
    .局部变量 保存位图1, 字节集
    .局部变量 宽1, 整数型
    .局部变量 高1, 整数型
    .局部变量 保存位图2, 字节集
    .局部变量 保存位图集, 字节集, , "0"
    .局部变量 宽2, 整数型
    .局部变量 高2, 整数型
    .局部变量 补零2, 整数型
    .局部变量 i, 整数型
    .局部变量 找到位置, 整数型
    .局部变量 i2, 整数型

    找到坐标.x ＝ -1
    找到坐标.y ＝ -1
    .如果真 (位图.从数据创建 (范围图片) ＝ 假)
        返回 (找到坐标)
    .如果真结束
    位图.转换位深度 (8)
    保存位图1 ＝ 位图.取出颜色数据 ()
    宽1 ＝ 位图.取宽度2 ()
    高1 ＝ 位图.取高度2 ()
    宽1 ＝ 宽1 ＋ (4 － 宽1 ％ 4) ％ 4 ' 将宽补足4位
    
    .如果真 (位图.从数据创建 (目标图片) ＝ 假)
        返回 (找到坐标)
    .如果真结束
    位图.转换位深度 (8)
    保存位图2 ＝ 位图.取出颜色数据 ()
    宽2 ＝ 位图.取宽度2 ()
    高2 ＝ 位图.取高度2 ()
    补零2 ＝ (4 － 宽2 ％ 4) ％ 4
    .计次循环首 (高2, i)
        加入成员 (保存位图集, 取字节集中间 (保存位图2, (宽2 ＋ 补零2) × (i － 1) ＋ 1, 宽2))
    .计次循环尾 ()
    .循环判断首 ()
        找到位置 ＝ 寻找字节集 (保存位图1, 保存位图集 [1], 找到位置 ＋ 1)
        .如果真 (找到位置 ＝ -1)
            返回 (找到坐标)
        .如果真结束
        .计次循环首 (高2 － 1, i2)
            .如果 (取字节集中间 (保存位图1, 找到位置 ＋ 宽1 × i2, 宽2) ＝ 保存位图集 [i2 ＋ 1])
                .如果真 (i2 ＝ 高2 － 1)
                    找到坐标.x ＝ (找到位置 － 1) ％ 宽1 ' 得到初始点坐标
                    找到坐标.y ＝ 高1 － (找到位置 － 1) ＼ 宽1 － 1
                    找到坐标.x ＝ 找到坐标.x ＋ 宽2 ＼ 2 ' 得到中心坐标
                    找到坐标.y ＝ 找到坐标.y － 高2 ＼ 2
                    返回 (找到坐标)
                .如果真结束
                
            .否则
                跳出循环 ()
            .如果结束
            
        .计次循环尾 ()
    .循环判断尾 (找到位置 ＞ 0)
    返回 (找到坐标)

.子程序 找字_初始化, , 公开, 在使用 找字() 命令前,必须先初始化本命令
    .参数 要查找的文字, 文本型
    .参数 文字的颜色, 整数型
    .参数 文字的字体, 字体, 可空, 可空。默认：9号宋体

    LF内容 ＝ 要查找的文字
    LF颜色 ＝ 文字的颜色
    LF大小 ＝ 文字的字体.字体大小
    .如果 (文字的字体.加粗)
        LF加粗 ＝ 700
    .否则
        LF加粗 ＝ 400
    .如果结束
    LF斜体 ＝ 文字的字体.倾斜
    LF下划线 ＝ 文字的字体.下划线
    LF删除线 ＝ 文字的字体.删除线
    LF字体名称 ＝ 文字的字体.字体名称

.子程序 找字, 精易_坐标J, 公开, 在屏幕或指定窗口上找字。使用此命令前，必须先使用 找字_初始化() 命令。成功返回找到的位置坐标，失败坐标返回-1。
    .参数 窗口句柄, 整数型, 可空, 可空。默认在屏幕中查找。
    .参数 寻找范围, 精易_矩形J, 可空, 可空。可用 制作范围() 制作返回。留空为查找整个屏幕或窗口。
    .参数 误差值, 整数型, 可空, 可空。默认为0， 值：0-255。值越大，速度越慢。
    .参数 吻合值, 整数型, 可空, 可空。默认为完全吻合，值：0-100。建议设置在90以上。
    .参数 是否在后台, 逻辑型, 可空, 可空。默认为假，在当前屏幕中查找，真为后台窗口中查找。
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 位图, 类_位图操作
    .局部变量 相同点阵, 精易_坐标J, , "0"
    .局部变量 不同点阵, 精易_坐标J, , "0"
    .局部变量 窗口坐标, 精易_坐标J
    .局部变量 矩形范围, 精易_矩形J

    .如果 (是否在后台)
        位图.从窗口创建 (窗口句柄, 寻找范围.左边, 寻找范围.顶边, 寻找范围.右边, 寻找范围.底边, 8)
    .否则
        .如果真 (窗口句柄 ≠ 0)
            ClientToScreen (窗口句柄, 窗口坐标)
            GetClientRect (窗口句柄, 矩形范围)
            .如果真 (寻找范围.右边 ＝ 0 或 寻找范围.右边 ＞ 矩形范围.右边 － 寻找范围.左边)
                寻找范围.右边 ＝ 矩形范围.右边 － 寻找范围.左边
            .如果真结束
            .如果真 (寻找范围.底边 ＝ 0 或 寻找范围.底边 ＞ 矩形范围.底边 － 寻找范围.顶边)
                寻找范围.底边 ＝ 矩形范围.底边 － 寻找范围.顶边
            .如果真结束
            
        .如果真结束
        位图.从屏幕创建 (窗口坐标.x ＋ 寻找范围.左边, 窗口坐标.y ＋ 寻找范围.顶边, 寻找范围.右边, 寻找范围.底边, 8)
    .如果结束
    取文字点阵 (相同点阵, 不同点阵)
    找到坐标 ＝ 找点阵 (位图.取出颜色数据 (), 位图.取宽度2 (), 位图.取高度2 (), 相同点阵, 不同点阵, 误差值, 吻合值)
    .如果真 (找到坐标.x ＝ -1)
        返回 (找到坐标)
    .如果真结束
    找到坐标.x ＝ 找到坐标.x ＋ 寻找范围.左边 ＋ 窗口坐标.x
    找到坐标.y ＝ 找到坐标.y ＋ 寻找范围.顶边 ＋ 窗口坐标.y
    返回 (找到坐标)

.子程序 找字_从字节集, 精易_坐标J, 公开, 在指定的图片位图上寻找指定的文字。使用此命令前，必须先使用 找字_初始化() 命令。成功返回找到的位置坐标，失败坐标返回-1。
    .参数 位图数据, 字节集, , 欲被查找的图片
    .参数 误差值, 整数型, 可空, 可空。默认为0， 值：0-255。值越大，速度越慢。
    .参数 吻合值, 整数型, 可空, 可空。默认为完全吻合，值：0-100。建议设置在90以上。
    .局部变量 位图, 类_位图操作
    .局部变量 相同点阵, 精易_坐标J, , "0"
    .局部变量 不同点阵, 精易_坐标J, , "0"

    位图.从数据创建 (位图数据)
    .如果真 (位图.取位深度 () ≠ 8)
        位图.转换位深度 (8)
    .如果真结束
    取文字点阵 (相同点阵, 不同点阵)
    返回 (找点阵 (位图.取出颜色数据 (), 位图.取宽度2 (), 位图.取高度2 (), 相同点阵, 不同点阵, 误差值, 吻合值))

.子程序 找字_批量, 整数型, 公开, 在指定的图片位图上找出所有指定文字的坐标。使用此命令前，必须先使用 找字_初始化() 命令。成功返回坐标数组。
    .参数 坐标数组, 精易_坐标J, 可空 数组, 请提供一个"精易_坐标"类型的变量数组，用于接收找到的坐标数组。
    .参数 位图数据, 字节集, , 欲被查找的图片
    .参数 误差值, 整数型, 可空, 可空。默认为0， 值：0-255。值越大，速度越慢。
    .参数 前景吻合度, 整数型, 可空, 可空。默认为完全吻合，值：0-100。建议设置在90以上。
    .参数 背景吻合度, 整数型, 可空, 可空。默认为完全吻合，值：0-100。建议设置在90以上。
    .局部变量 位图, 类_位图操作
    .局部变量 相同点阵, 精易_坐标J, , "0"
    .局部变量 不同点阵, 精易_坐标J, , "0"

    位图.从数据创建 (位图数据)
    .如果真 (位图.取位深度 () ≠ 8)
        位图.转换位深度 (8)
    .如果真结束
    取文字点阵 (相同点阵, 不同点阵)
    返回 (找点阵2 (坐标数组, 位图.取出颜色数据 (), 位图.取宽度2 (), 位图.取高度2 (), 相同点阵, 不同点阵, 误差值, 前景吻合度, 背景吻合度))

.子程序 取色, 整数型, 公开, 在屏幕或指定窗口上取出某点的颜色值，成功返回10进制颜色值。
    .参数 窗口句柄, 整数型, 可空, 可空。默认在屏幕中查找。
    .参数 水平位置, 整数型, , 目标横坐标
    .参数 垂直位置, 整数型, , 目标纵坐标
    .参数 是否在后台, 逻辑型, 可空, 可空。默认为假，在当前屏幕中查找，真为后台窗口中查找。
    .局部变量 位图, 类_位图操作
    .局部变量 坐标, 精易_坐标J

    .如果 (是否在后台)
        位图.从窗口创建 (窗口句柄, 水平位置, 垂直位置, 1, 1, 24)
    .否则
        .如果真 (窗口句柄 ≠ 0)
            ClientToScreen (窗口句柄, 坐标)
        .如果真结束
        位图.从屏幕创建 (坐标.x ＋ 水平位置, 坐标.y ＋ 垂直位置, 1, 1, 24)
    .如果结束
    返回 (位图.取某点颜色 (0, 0))

.子程序 找色, 精易_坐标J, 公开, 在屏幕或指定窗口上找指定的颜色，成功返回此颜色的坐标，失败坐标返回为-1
    .参数 窗口句柄, 整数型, 可空, 可空。默认在屏幕中查找。
    .参数 要查找的颜色值, 整数型, , 要查找的颜色值
    .参数 寻找范围, 精易_矩形J, 可空, 可空。可用 制作范围() 制作返回。留空为查找整个屏幕或窗口。
    .参数 误差值, 整数型, 可空, 可空。默认为0， 值：0-255。值越大，速度越慢。
    .参数 是否在后台, 逻辑型, 可空, 可空。默认为假，在当前屏幕中查找，真为后台窗口中查找。
    .局部变量 保存颜色值, 字节集
    .局部变量 找到位置, 整数型
    .局部变量 位图, 类_位图操作
    .局部变量 坐标, 精易_坐标J
    .局部变量 实际宽, 整数型
    .局部变量 矩形范围, 精易_矩形J

    保存颜色值 ＝ 到可用颜色2 (要查找的颜色值)
    .如果 (是否在后台)
        位图.从窗口创建 (窗口句柄, 寻找范围.左边, 寻找范围.顶边, 寻找范围.右边, 寻找范围.底边, 8)
    .否则
        .如果真 (窗口句柄 ≠ 0)
            ClientToScreen (窗口句柄, 坐标)
            GetClientRect (窗口句柄, 矩形范围)
            .如果真 (寻找范围.右边 ＝ 0 或 寻找范围.右边 ＞ 矩形范围.右边 － 寻找范围.左边)
                寻找范围.右边 ＝ 矩形范围.右边 － 寻找范围.左边
            .如果真结束
            .如果真 (寻找范围.底边 ＝ 0 或 寻找范围.底边 ＞ 矩形范围.底边 － 寻找范围.顶边)
                寻找范围.底边 ＝ 矩形范围.底边 － 寻找范围.顶边
            .如果真结束
            
        .如果真结束
        位图.从屏幕创建 (坐标.x ＋ 寻找范围.左边, 坐标.y ＋ 寻找范围.顶边, 寻找范围.右边, 寻找范围.底边, 8)
    .如果结束
    坐标.x ＝ -1
    坐标.y ＝ -1
    实际宽 ＝ 位图.取宽度2 ()
    实际宽 ＝ 实际宽 ＋ (4 － 实际宽 ％ 4) ％ 4 ' 将宽补足4位
    .如果 (误差值 ＝ 0)
        找到位置 ＝ 寻找字节集 (位图.取出颜色数据 (), 保存颜色值, 找到位置 ＋ 1)
    .否则
        找到位置 ＝ 寻找字节集_字节 (位图.取出颜色数据 (), 取字节集数据 (保存颜色值, #字节型, ), 误差值, 找到位置 ＋ 1)
    .如果结束
    .如果真 (找到位置 ≠ -1)
        坐标.x ＝ (找到位置 － 1) ％ 实际宽
        坐标.y ＝ 位图.取高度2 () － (找到位置 － 1) ＼ 实际宽 － 1
    .如果真结束
    返回 (坐标)

.子程序 取出数值, 整数型, , 从标准数值的截图中取出它的数值(成功返回数值,失败返回-1)
    .参数 位图数据, 字节集, , 标准数值的截图(建议小图,不宜掺杂多余数字)
    .参数 颜色, 整数型, 可空, 默认为 #白色
    .参数 字体, 字体, 可空, 默认为9号宋体字
    .局部变量 位图, 类_位图操作
    .局部变量 字符表, 文本型, , "0"
    .局部变量 相同点阵, 精易_坐标J, , "0"
    .局部变量 不同点阵, 精易_坐标J, , "0"
    .局部变量 找到坐标, 精易_坐标J, , "0"
    .局部变量 i, 整数型
    .局部变量 i2, 整数型
    .局部变量 位置, 整数型
    .局部变量 结果, 文本型, , "0"
    .局部变量 返回内容, 文本型

    位图.从数据创建 (位图数据)
    .如果真 (位图.取位深度 () ≠ 8)
        位图.转换位深度 (8)
    .如果真结束
    找字_初始化 (“”, 颜色, 字体)
    重定义数组 (结果, 假, 位图.取宽度2 ())
    字符表 ＝ { “0”, “1”, “2”, “3”, “4”, “5”, “6”, “7”, “8”, “9” }
    .计次循环首 (取数组成员数 (字符表), i)
        LF内容 ＝ 字符表 [i]
        取文字点阵 (相同点阵, 不同点阵)
        .计次循环首 (找点阵5 (找到坐标, 位图.取出颜色数据 (), 位图.取宽度2 (), 位图.取高度2 (), 相同点阵, 不同点阵), i2)
            位置 ＝ 找到坐标 [i2].x
            结果 [位置] ＝ 字符表 [i]
        .计次循环尾 ()
    .计次循环尾 ()
    .计次循环首 (取数组成员数 (结果), i)
        .如果真 (结果 [i] ≠ “”)
            返回内容 ＝ 返回内容 ＋ 结果 [i]
        .如果真结束
        
    .计次循环尾 ()
    .如果真 (返回内容 ＝ “”)
        返回 (-1)
    .如果真结束
    返回 (到数值 (返回内容))

.子程序 高级找色, 精易_坐标J, , 在位图数据中找颜色(返回找到的颜色所在位图中的坐标,找不到返回-1坐标)
    .参数 位图数据, 字节集, 可空, 非首次查找可将本参数留空,表示采用上次的位图数据
    .参数 颜色值, 整数型, 可空, 非首次查找可将本参数留空,可接着上次找到的点往下找
    .参数 误差, 整数型, 可空, 默认为0 表示最低误差值 最大值为255,建议0-10之间
    .局部变量 保存位图, 字节集, 静态
    .局部变量 保存颜色值, 字节集, 静态
    .局部变量 找到位置, 整数型, 静态
    .局部变量 宽, 整数型, 静态
    .局部变量 高, 整数型, 静态
    .局部变量 位图, 类_位图操作
    .局部变量 坐标, 精易_坐标J

    .如果真 (是否为空 (颜色值) ＝ 假)
        保存颜色值 ＝ 到可用颜色2 (颜色值)
        找到位置 ＝ 0
    .如果真结束
    .如果真 (位图数据 ≠ { })
        位图.从数据创建 (位图数据)
        位图.转换位深度 (8)
        保存位图 ＝ 位图.取出颜色数据 ()
        宽 ＝ 位图.取宽度2 ()
        高 ＝ 位图.取高度2 ()
        宽 ＝ 宽 ＋ (4 － 宽 ％ 4) ％ 4 ' 将宽补足4位
    .如果真结束
    坐标.x ＝ -1
    坐标.y ＝ -1
    .如果 (误差 ＝ 0)
        找到位置 ＝ 寻找字节集 (保存位图, 保存颜色值, 找到位置 ＋ 1)
    .否则
        找到位置 ＝ 寻找字节集_字节 (保存位图, 取字节集数据 (保存颜色值, #字节型, ), 误差, 找到位置 ＋ 1)
    .如果结束
    .如果真 (找到位置 ≠ -1)
        坐标.x ＝ (找到位置 － 1) ％ 宽
        坐标.y ＝ 高 － (找到位置 － 1) ＼ 宽 － 1
    .如果真结束
    返回 (坐标)

.子程序 高级找图, 精易_坐标J, , 在位图数据中找图片(返回找到的图片左下角第一个有效点的坐标,找不到返回-1坐标)
    .参数 范围图片, 字节集, 可空, 默认为上次范围图片
    .参数 目标图片, 字节集, 可空, 默认为上次目标图片 (建议使用10*10以内位图)
    .参数 透明色, 整数型, 可空, 目标图片中欲被忽略的颜色值  默认为-1 如果目标图片为空,为上次透明色
    .参数 吻合度, 整数型, 可空, 默认为100完全吻合 相吻合的百分比(0-100之间)建议90以上
    .参数 误差值, 整数型, 可空, 默认为0 表示最低误差值 最大值为255,建议0-10之间
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 位图, 类_位图操作
    .局部变量 保存位图1, 字节集, 静态
    .局部变量 宽1, 整数型, 静态
    .局部变量 高1, 整数型, 静态
    .局部变量 基色, 整数型, 静态
    .局部变量 颜色点阵, 点阵型, 静态, "0"

    找到坐标.x ＝ -1
    找到坐标.y ＝ -1
    .如果 (范围图片 ≠ { })
        保存位图1 ＝ { }
        .如果真 (位图.从数据创建 (范围图片) ＝ 假)
            返回 (找到坐标)
        .如果真结束
        位图.转换位深度 (8)
        保存位图1 ＝ 位图.取出颜色数据 ()
        宽1 ＝ 位图.取宽度2 ()
        高1 ＝ 位图.取高度2 ()
    .否则
        .如果真 (保存位图1 ＝ { })
            返回 (找到坐标)
        .如果真结束
        
    .如果结束
    .如果真 (是否为空 (透明色))
        透明色 ＝ -1
    .如果真结束
    .如果真 (是否为空 (目标图片) ＝ 假)
        基色 ＝ 取图片点阵 (目标图片, 透明色, 颜色点阵)
    .如果真结束
    .如果真 (基色 ＝ -1)
        返回 (找到坐标)
    .如果真结束
    返回 (找点阵3 (保存位图1, 宽1, 高1, 颜色点阵, 基色, 误差值, 吻合度))

.子程序 高级找图2, 整数型, , 在位图数据中找出图片所有坐标点(返回找到的图片数目,找不到返回0)
    .参数 找到坐标, 精易_坐标J, 可空 数组, 返回所有找到的(图片左下角第一个有效点的坐标)坐标数组
    .参数 范围图片, 字节集, 可空, 默认为上次范围图片
    .参数 目标图片, 字节集, 可空, 默认为上次目标图片 (建议使用10*10以内位图)
    .参数 透明色, 整数型, 可空, 目标图片中欲被忽略的颜色值  默认为-1 如果目标图片为空,为上次透明色
    .参数 吻合度, 整数型, 可空, 默认为100完全吻合 相吻合的百分比(0-100之间)建议90以上
    .参数 误差值, 整数型, 可空, 默认为0 表示最低误差值 最大值为255,建议0-10之间
    .局部变量 位图, 类_位图操作
    .局部变量 保存位图1, 字节集, 静态
    .局部变量 宽1, 整数型, 静态
    .局部变量 高1, 整数型, 静态
    .局部变量 基色, 整数型, 静态
    .局部变量 颜色点阵, 点阵型, 静态, "0"

    .如果 (范围图片 ≠ { })
        保存位图1 ＝ { }
        .如果真 (位图.从数据创建 (范围图片) ＝ 假)
            返回 (0)
        .如果真结束
        位图.转换位深度 (8)
        保存位图1 ＝ 位图.取出颜色数据 ()
        宽1 ＝ 位图.取宽度2 ()
        高1 ＝ 位图.取高度2 ()
    .否则
        .如果真 (保存位图1 ＝ { })
            返回 (0)
        .如果真结束
        
    .如果结束
    .如果真 (是否为空 (透明色))
        透明色 ＝ -1
    .如果真结束
    .如果真 (是否为空 (目标图片) ＝ 假)
        基色 ＝ 取图片点阵 (目标图片, 透明色, 颜色点阵)
    .如果真结束
    .如果真 (基色 ＝ -1)
        返回 (0)
    .如果真结束
    返回 (找点阵4 (找到坐标, 保存位图1, 宽1, 高1, 颜色点阵, 基色, 误差值, 吻合度))

.子程序 高级精确找图2, 整数型, , 在位图数据中找出图片所有坐标点(返回找到的图片数目,找不到返回0)
    .参数 找到坐标, 精易_坐标J, 可空 数组, 返回所有找到的(图片中心)坐标数组
    .参数 范围图片, 字节集, 可空, 默认为上次范围图片
    .参数 目标图片, 字节集, 可空, 默认为上次目标图片
    .局部变量 临时坐标, 精易_坐标J
    .局部变量 位图, 类_位图操作
    .局部变量 保存位图1, 字节集, 静态
    .局部变量 宽1, 整数型, 静态
    .局部变量 高1, 整数型, 静态
    .局部变量 保存位图2, 字节集, 静态
    .局部变量 保存位图集, 字节集, 静态, "0"
    .局部变量 宽2, 整数型, 静态
    .局部变量 高2, 整数型, 静态
    .局部变量 补零2, 整数型, 静态
    .局部变量 保存忽略色, 整数型, 静态
    .局部变量 i, 整数型
    .局部变量 找到位置, 整数型
    .局部变量 i2, 整数型

    .如果 (范围图片 ≠ { })
        保存位图1 ＝ { }
        .如果真 (位图.从数据创建 (范围图片) ＝ 假)
            返回 (0)
        .如果真结束
        位图.转换位深度 (8)
        保存位图1 ＝ 位图.取出颜色数据 ()
        宽1 ＝ 位图.取宽度2 ()
        高1 ＝ 位图.取高度2 ()
        宽1 ＝ 宽1 ＋ (4 － 宽1 ％ 4) ％ 4 ' 将宽补足4位
    .否则
        .如果真 (保存位图1 ＝ { })
            返回 (0)
        .如果真结束
        
    .如果结束
    .如果 (目标图片 ≠ { })
        保存位图2 ＝ { }
        .如果真 (位图.从数据创建 (目标图片) ＝ 假)
            返回 (0)
        .如果真结束
        位图.转换位深度 (8)
        保存位图2 ＝ 位图.取出颜色数据 ()
        宽2 ＝ 位图.取宽度2 ()
        高2 ＝ 位图.取高度2 ()
        补零2 ＝ (4 － 宽2 ％ 4) ％ 4
        清除数组 (保存位图集)
        .计次循环首 (高2, i)
            加入成员 (保存位图集, 取字节集中间 (保存位图2, (宽2 ＋ 补零2) × (i － 1) ＋ 1, 宽2))
        .计次循环尾 ()
    .否则
        .如果真 (保存位图2 ＝ { })
            返回 (0)
        .如果真结束
        
    .如果结束
    
    .循环判断首 ()
        找到位置 ＝ 寻找字节集 (保存位图1, 保存位图集 [1], 找到位置 ＋ 1)
        ' 输出调试文本 (找到位置)
        .如果真 (找到位置 ＝ -1)
            跳出循环 ()
        .如果真结束
        .计次循环首 (高2 － 1, i2)
            .如果 (取字节集中间 (保存位图1, 找到位置 ＋ 宽1 × i2, 宽2) ＝ 保存位图集 [i2 ＋ 1])
                .如果真 (i2 ＝ 高2 － 1)
                    临时坐标.x ＝ (找到位置 － 1) ％ 宽1 ' 得到初始点坐标
                    临时坐标.y ＝ 高1 － (找到位置 － 1) ＼ 宽1 － 1
                    临时坐标.x ＝ 临时坐标.x ＋ 宽2 ＼ 2 ' 得到中心坐标
                    临时坐标.y ＝ 临时坐标.y － 高2 ＼ 2
                    加入成员 (找到坐标, 临时坐标)
                .如果真结束
                
            .否则
                跳出循环 ()
            .如果结束
            
        .计次循环尾 ()
    .循环判断尾 (找到位置 ＞ 0)
    返回 (取数组成员数 (找到坐标))

.子程序 到可用颜色, 整数型
    .参数 颜色值, 整数型
    .局部变量 位图, 类_位图操作

    ' 0.299R 0.587G 0.114B
    位图.创建空白位图 (1, 1, 32)
    位图.置某点颜色 (0, 0, 颜色值)
    位图.转换位深度 (8)
    返回 (取字节集数据 (位图.取出颜色数据 (), #字节型, 1))

.子程序 到可用颜色2, 字节集
    .参数 颜色值, 整数型
    .局部变量 位图, 类_位图操作

    位图.创建空白位图 (1, 1, 32)
    位图.置某点颜色 (0, 0, 颜色值)
    位图.转换位深度 (8)
    返回 (取字节集左边 (位图.取出颜色数据 (), 1))

.子程序 到可用图片, 字节集, , <用不到,仅作参考>
    .参数 图片数据, 字节集
    .局部变量 位图, 类_位图操作

    位图.从数据创建 (图片数据)
    位图.转换位深度 (8)
    返回 (位图.取出颜色数据 ())

.子程序 寻找字节集_字节, 整数型
    .参数 字节集, 字节集
    .参数 字节, 整数型
    .参数 误差, 整数型
    .参数 起始位置, 整数型, 可空
    .局部变量 i, 整数型

    .计次循环首 (取字节集长度 (字节集), i)
        .如果真 (取绝对值 (字节集 [i] － 字节) ≤ 误差 且 i ≥ 起始位置)
            返回 (i)
        .如果真结束
        
    .计次循环尾 ()
    返回 (-1)

.子程序 寻找字节集3, 整数型, , 返回一字节集在另一字节集中最先出现的位置，位置值从 1 开始。如果未找到，返回 -1
    .参数 被搜寻的字节集, 字节集
    .参数 欲寻找的字节集, 字节集
    .参数 起始寻找位置, 整数型, 可空, 可空为1
    .参数 忽略值, 整数型, 可空, 欲寻找的字节集中,被忽略的字节值
    .参数 误差值, 整数型, 可空, 寻找字节集的误差值 每个字节(0-255)所允许的最大差值
    .局部变量 计次1, 整数型
    .局部变量 长度, 整数型
    .局部变量 计次2, 整数型
    .局部变量 总长度, 整数型

    .如果真 (起始寻找位置 ＜ 1)
        起始寻找位置 ＝ 1
    .如果真结束
    长度 ＝ 取字节集长度 (欲寻找的字节集)
    总长度 ＝ 取字节集长度 (被搜寻的字节集)
    .如果 (忽略值 ＝ -1)
        .如果 (误差值 ＝ 0)
            返回 (寻找字节集 (被搜寻的字节集, 欲寻找的字节集, 起始寻找位置))
        .否则
            .计次循环首 (总长度 － 长度 ＋ 1, 计次1)
                .如果真 (计次1 ≥ 起始寻找位置 且 取绝对值 (欲寻找的字节集 [1] － 被搜寻的字节集 [计次1]) ≤ 误差值)
                    计次2 ＝ 1
                    .循环判断首 () ' 循环判断比计次循环效率要高得多
                        .如果真 (计次2 ＝ 长度)
                            返回 (计次1)
                        .如果真结束
                        计次2 ＝ 计次2 ＋ 1
                    .循环判断尾 (取绝对值 (欲寻找的字节集 [计次2] － 被搜寻的字节集 [计次1 ＋ 计次2 － 1]) ≤ 误差值)
                .如果真结束
                
            .计次循环尾 ()
        .如果结束
        
    .否则
        .如果 (误差值 ＝ 0)
            .计次循环首 (总长度 － 长度 ＋ 1, 计次1)
                .如果真 (计次1 ≥ 起始寻找位置 且 欲寻找的字节集 [1] ＝ 被搜寻的字节集 [计次1])
                    计次2 ＝ 1
                    .循环判断首 () ' 循环判断比计次循环效率要高得多
                        .如果真 (计次2 ＝ 长度)
                            返回 (计次1)
                        .如果真结束
                        计次2 ＝ 计次2 ＋ 1
                    .循环判断尾 (忽略值 ＝ 欲寻找的字节集 [计次2] 或 欲寻找的字节集 [计次2] ＝ 被搜寻的字节集 [计次1 ＋ 计次2 － 1])
                .如果真结束
                
            .计次循环尾 ()
        .否则
            .计次循环首 (总长度 － 长度 ＋ 1, 计次1)
                .如果真 (计次1 ≥ 起始寻找位置)
                    计次2 ＝ 1
                    .判断循环首 (忽略值 ＝ 欲寻找的字节集 [计次2] 或 取绝对值 (欲寻找的字节集 [计次2] － 被搜寻的字节集 [计次1 ＋ 计次2 － 1]) ≤ 误差值)
                        .如果真 (计次2 ＝ 长度)
                            返回 (计次1)
                        .如果真结束
                        计次2 ＝ 计次2 ＋ 1
                    .判断循环尾 ()
                .如果真结束
                
            .计次循环尾 ()
        .如果结束
        
    .如果结束
    返回 (-1)

.子程序 是否吻合, 逻辑型, , 判断两段同长度字节集是否相似
    .参数 字节集1, 字节集
    .参数 字节集2, 字节集, , 忽略值
    .参数 忽略值, 整数型, , -1
    .参数 误差值, 整数型, , 0-255之间,0为必须完全吻合
    .局部变量 长, 整数型
    .局部变量 I, 整数型

    长 ＝ 取字节集长度 (字节集1)
    .如果真 (取字节集长度 (字节集2) ≠ 长)
        返回 (假)
    .如果真结束
    
    .如果 (忽略值 ＝ -1)
        .如果 (误差值 ＝ 0)
            .计次循环首 (长, I)
                .如果真 (字节集1 [I] ≠ 字节集2 [I])
                    返回 (假)
                .如果真结束
                
            .计次循环尾 ()
        .否则
            .计次循环首 (长, I)
                .如果真 (取绝对值 (字节集1 [I] － 字节集2 [I]) ＞ 误差值)
                    返回 (假)
                .如果真结束
                
            .计次循环尾 ()
        .如果结束
        
    .否则
        .如果 (误差值 ＝ 0)
            .计次循环首 (长, I)
                .如果真 (忽略值 ≠ 字节集2 [I] 且 字节集1 [I] ≠ 字节集2 [I])
                    返回 (假)
                .如果真结束
                
            .计次循环尾 ()
        .否则
            .计次循环首 (长, I)
                .如果真 (忽略值 ≠ 字节集2 [I] 且 取绝对值 (字节集1 [I] － 字节集2 [I]) ＞ 误差值)
                    返回 (假)
                .如果真结束
                
            .计次循环尾 ()
        .如果结束
        
    .如果结束
    返回 (真)

.子程序 取文字点阵
    .参数 前景偏移点, 精易_坐标J, 参考 可空 数组
    .参数 背景偏移点, 精易_坐标J, 参考 可空 数组
    .局部变量 点, 精易_坐标J
    .局部变量 基点, 精易_坐标J
    .局部变量 hdc, 整数型
    .局部变量 场景句柄, 整数型
    .局部变量 高, 整数型
    .局部变量 hf, 整数型
    .局部变量 h, 整数型
    .局部变量 hbk, 整数型
    .局部变量 htext, 整数型
    .局部变量 x, 整数型
    .局部变量 y, 整数型
    .局部变量 位图句柄, 整数型

    清除数组 (前景偏移点)
    清除数组 (背景偏移点)
    hdc ＝ GetDC (0)
    高 ＝ LF大小 × GetDeviceCaps (hdc, 90) ÷ 72
    场景句柄 ＝ CreateCompatibleDC (hdc)
    位图句柄 ＝ CreateCompatibleBitmap (hdc, 高 × 取文本长度 (LF内容) ＼ 2, 高)
    SelectObject (场景句柄, 位图句柄)
    hf ＝ CreateFontA (高, 0, 0, 0, LF加粗, LF斜体, LF下划线, LF删除线, 134, 0, 0, 0, 0, LF字体名称)
    h ＝ SelectObject (场景句柄, hf)
    hbk ＝ SetBkMode (场景句柄, 1)
    htext ＝ SetTextColor (场景句柄, 255)
    TextOutA (场景句柄, 0, 0, LF内容, 取文本长度 (LF内容))
    SetBkMode (场景句柄, hbk)
    SetTextColor (场景句柄, htext)
    SelectObject (场景句柄, h)
    DeleteObject (hf)
    基点.x ＝ -1
    .计次循环首 (高, y)
        .计次循环首 (高 × 取文本长度 (LF内容) ＼ 2, x)
            .如果 (基点.x ＝ -1)
                .如果真 (GetPixel (场景句柄, x － 1, 高 － y) ＞ 0)
                    基点.x ＝ x － 1
                    基点.y ＝ 高 － y
                .如果真结束
                
            .否则
                点.x ＝ x － 1 － 基点.x
                点.y ＝ 基点.y － (高 － y)
                .如果 (GetPixel (场景句柄, x － 1, 高 － y) ＞ 0)
                    加入成员 (前景偏移点, 点)
                .否则
                    加入成员 (背景偏移点, 点)
                .如果结束
                
            .如果结束
            
        .计次循环尾 ()
    .计次循环尾 ()
    .计次循环首 (高, y)
        .计次循环首 (高 × 取文本长度 (LF内容) ＼ 2, x)
            .如果真 (GetPixel (场景句柄, x － 1, 高 － y) ＞ 0)
                跳出循环 ()
            .如果真结束
            点.x ＝ x － 1 － 基点.x
            点.y ＝ 基点.y － (高 － y)
            加入成员 (背景偏移点, 点)
        .计次循环尾 ()
        .如果真 (基点.y ＝ 高 － y)
            .如果真 (基点.x ＝ x － 1)
                跳出循环 ()
            .如果真结束
            
        .如果真结束
        
    .计次循环尾 ()
    DeleteDC (场景句柄)
    ReleaseDC (0, hdc)

.子程序 取图片点阵, 整数型, , 成功返回255基点颜色,失败返回-1
    .参数 目标图片, 字节集
    .参数 透明色, 整数型, 参考, 返回255颜色值,无透明色-1
    .参数 前景偏移点, 点阵型, 参考 数组
    .局部变量 位图, 类_位图操作
    .局部变量 颜色数据, 字节集
    .局部变量 宽, 整数型
    .局部变量 高, 整数型
    .局部变量 补零, 整数型
    .局部变量 临时字节集, 字节集
    .局部变量 y, 整数型
    .局部变量 x, 整数型
    .局部变量 临时偏移点, 点阵型
    .局部变量 基点, 点阵型

    .如果真 (位图.从数据创建 (目标图片) ＝ 假)
        返回 (-1)
    .如果真结束
    位图.转换位深度 (8)
    颜色数据 ＝ 位图.取出颜色数据 ()
    宽 ＝ 位图.取宽度2 ()
    高 ＝ 位图.取高度2 ()
    补零 ＝ (4 － 宽 ％ 4) ％ 4
    .如果真 (透明色 ≠ -1)
        透明色 ＝ 到可用颜色 (透明色)
    .如果真结束
    基点.z ＝ -1
    .计次循环首 (高, y)
        临时字节集 ＝ 取字节集中间 (颜色数据, (宽 ＋ 补零) × (y － 1) ＋ 1, 宽)
        .计次循环首 (宽, x)
            .如果真 (临时字节集 [x] ≠ 透明色)
                .如果 (基点.z ＝ -1)
                    基点.x ＝ x － 1
                    基点.y ＝ 高 － y
                    基点.z ＝ 临时字节集 [x]
                .否则
                    临时偏移点.x ＝ x － 1 － 基点.x
                    临时偏移点.y ＝ 基点.y － (高 － y)
                    临时偏移点.z ＝ 临时字节集 [x]
                    加入成员 (前景偏移点, 临时偏移点)
                    ' 输出调试文本 (临时偏移点.z)
                .如果结束
                
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (基点.z)

.子程序 找点阵, 精易_坐标J, , 找字
    .参数 数据, 字节集
    .参数 宽, 整数型
    .参数 高, 整数型
    .参数 点阵, 精易_坐标J, 数组
    .参数 不同点阵, 精易_坐标J, 数组
    .参数 误差值, 整数型, , 默认为精确找色,可指定误差0-255,建议在20点误差以内
    .参数 符合程度, 整数型, , 默认为90  100 完全相吻合  80 百分之80像素点相吻合 
    .局部变量 颜色值, 整数型
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 总长, 整数型
    .局部变量 i, 整数型
    .局部变量 i2, 整数型
    .局部变量 i3, 整数型
    .局部变量 i4, 整数型
    .局部变量 行字节集, 字节集
    .局部变量 位置, 整数型
    .局部变量 吻合值, 整数型
    .局部变量 已吻合值, 整数型
    .局部变量 不吻合值, 整数型
    .局部变量 临时颜色值, 整数型
    .局部变量 补零, 字节型

    找到坐标.x ＝ -1
    找到坐标.y ＝ -1
    总长 ＝ 取字节集长度 (数据)
    颜色值 ＝ 到可用颜色 (LF颜色)
    .如果 (符合程度 ＝ 0)
        吻合值 ＝ 取数组成员数 (点阵) × 100 ＼ 100
        不吻合值 ＝ 取数组成员数 (不同点阵) × 100 ＼ 100
    .否则
        吻合值 ＝ 取数组成员数 (点阵) × 符合程度 ＼ 100
        不吻合值 ＝ 取数组成员数 (不同点阵) × 符合程度 ＼ 100
    .如果结束
    补零 ＝ (4 － 宽 ％ 4) ％ 4
    .计次循环首 (高, i)
        行字节集 ＝ 取字节集中间 (数据, 1 ＋ (宽 ＋ 补零) × (i － 1), 宽)
        .计次循环首 (宽, i2)
            .如果真 (取绝对值 (行字节集 [i2] － 颜色值) ≤ 误差值)
                已吻合值 ＝ 0
                .计次循环首 (取数组成员数 (点阵), i3)
                    位置 ＝ i2 ＋ 点阵 [i3].x － 1 ' 这是X位置
                    .如果真 (位置 ＞ 宽) ' 偏移点超出范围
                        ' 输出调试文本 (“超出”)
                        跳出循环 ()
                    .如果真结束
                    位置 ＝ (宽 ＋ 补零) × (点阵 [i3].y ＋ i － 1) ＋ 位置 ＋ 1
                    .如果真 (总长 ＜ 位置)
                        ' 输出调试文本 (“超出2”)
                        跳出循环 ()
                    .如果真结束
                    .如果真 (取绝对值 (数据 [位置] － 行字节集 [i2]) ≤ 误差值)
                        已吻合值 ＝ 已吻合值 ＋ 1
                        临时颜色值 ＝ 行字节集 [i2]
                        .如果真 (已吻合值 ≥ 吻合值)
                            .如果真 (取数组成员数 (不同点阵) ＝ 0)
                                ' 输出调试文本 (当前吻合值)
                                找到坐标.x ＝ i2 － 1
                                找到坐标.y ＝ 高 － i
                                返回 (找到坐标)
                            .如果真结束
                            已吻合值 ＝ 0
                            .计次循环首 (取数组成员数 (不同点阵), i4) ' 判断文字以外点与文字颜色不同,防止找到的是色块不是文字!!
                                位置 ＝ i2 ＋ 不同点阵 [i4].x － 1
                                .如果真 (位置 ＞ 宽)
                                    跳出循环 ()
                                .如果真结束
                                位置 ＝ 1 ＋ (宽 ＋ 补零) × (不同点阵 [i4].y ＋ i － 1) ＋ 位置
                                .如果真 (总长 ＜ 位置 ＋ 2)
                                    跳出循环 ()
                                .如果真结束
                                .如果真 (数据 [位置] ＝ 临时颜色值)
                                    到循环尾 ()
                                .如果真结束
                                已吻合值 ＝ 已吻合值 ＋ 1
                                .如果真 (已吻合值 ≥ 不吻合值)
                                    找到坐标.x ＝ i2 － 1
                                    找到坐标.y ＝ 高 － i
                                    返回 (找到坐标)
                                .如果真结束
                                
                            .计次循环尾 ()
                            跳出循环 ()
                        .如果真结束
                        
                    .如果真结束
                    
                .计次循环尾 ()
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (找到坐标)

.子程序 找点阵2, 整数型, , 找所有字
    .参数 坐标组, 精易_坐标J, 可空 数组
    .参数 数据, 字节集
    .参数 宽, 整数型
    .参数 高, 整数型
    .参数 点阵, 精易_坐标J, 数组
    .参数 不同点阵, 精易_坐标J, 数组
    .参数 误差值, 整数型, , 默认为精确找色,可指定误差0-255,建议在20点误差以内
    .参数 符合程度, 整数型, , 默认为90  100 完全相吻合  80 百分之80像素点相吻合 
    .参数 符合程度2, 整数型
    .局部变量 颜色值, 整数型
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 总长, 整数型
    .局部变量 i, 整数型
    .局部变量 i2, 整数型
    .局部变量 i3, 整数型
    .局部变量 i4, 整数型
    .局部变量 行字节集, 字节集
    .局部变量 位置, 整数型
    .局部变量 吻合值, 整数型
    .局部变量 已吻合值, 整数型
    .局部变量 不吻合值, 整数型
    .局部变量 临时颜色值, 整数型
    .局部变量 补零, 字节型

    清除数组 (坐标组)
    找到坐标.x ＝ -1
    找到坐标.y ＝ -1
    总长 ＝ 取字节集长度 (数据)
    颜色值 ＝ 到可用颜色 (LF颜色)
    .如果 (符合程度 ＝ 0)
        吻合值 ＝ 取数组成员数 (点阵) × 100 ＼ 100
    .否则
        吻合值 ＝ 取数组成员数 (点阵) × 符合程度 ＼ 100
    .如果结束
    .如果 (符合程度2 ＝ 0)
        不吻合值 ＝ 取数组成员数 (不同点阵) × 100 ＼ 100
    .否则
        不吻合值 ＝ 取数组成员数 (不同点阵) × 符合程度2 ＼ 100
    .如果结束
    补零 ＝ (4 － 宽 ％ 4) ％ 4
    .计次循环首 (高, i)
        行字节集 ＝ 取字节集中间 (数据, 1 ＋ (宽 ＋ 补零) × (i － 1), 宽)
        .计次循环首 (宽, i2)
            .如果真 (取绝对值 (行字节集 [i2] － 颜色值) ≤ 误差值)
                已吻合值 ＝ 0
                .计次循环首 (取数组成员数 (点阵), i3)
                    位置 ＝ i2 ＋ 点阵 [i3].x － 1 ' 这是X位置
                    .如果真 (位置 ＞ 宽) ' 偏移点超出范围
                        跳出循环 ()
                    .如果真结束
                    位置 ＝ (宽 ＋ 补零) × (点阵 [i3].y ＋ i － 1) ＋ 位置 ＋ 1
                    .如果真 (总长 ＜ 位置)
                        跳出循环 ()
                    .如果真结束
                    .如果真 (取绝对值 (数据 [位置] － 行字节集 [i2]) ≤ 误差值)
                        已吻合值 ＝ 已吻合值 ＋ 1
                        临时颜色值 ＝ 行字节集 [i2]
                        .如果真 (已吻合值 ≥ 吻合值)
                            .如果真 (取数组成员数 (不同点阵) ＝ 0)
                                找到坐标.x ＝ i2 － 1
                                找到坐标.y ＝ 高 － i
                                加入成员 (坐标组, 找到坐标)
                                跳出循环 ()
                            .如果真结束
                            已吻合值 ＝ 0
                            .计次循环首 (取数组成员数 (不同点阵), i4) ' 判断文字以外点与文字颜色不同,防止找到的是色块不是文字!!
                                位置 ＝ i2 ＋ 不同点阵 [i4].x － 1
                                .如果真 (位置 ＞ 宽)
                                    跳出循环 ()
                                .如果真结束
                                位置 ＝ 1 ＋ (宽 ＋ 补零) × (不同点阵 [i4].y ＋ i － 1) ＋ 位置
                                .如果真 (总长 ＜ 位置 ＋ 2)
                                    跳出循环 ()
                                .如果真结束
                                .如果真 (数据 [位置] ＝ 临时颜色值)
                                    到循环尾 ()
                                .如果真结束
                                已吻合值 ＝ 已吻合值 ＋ 1
                                .如果真 (已吻合值 ≥ 不吻合值)
                                    找到坐标.x ＝ i2 － 1
                                    找到坐标.y ＝ 高 － i
                                    加入成员 (坐标组, 找到坐标)
                                    跳出循环 ()
                                .如果真结束
                                
                            .计次循环尾 ()
                            跳出循环 ()
                        .如果真结束
                        
                    .如果真结束
                    
                .计次循环尾 ()
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (取数组成员数 (坐标组))

.子程序 找点阵3, 精易_坐标J, , 找图
    .参数 数据, 字节集
    .参数 宽, 整数型
    .参数 高, 整数型
    .参数 点阵, 点阵型, 数组
    .参数 基色, 整数型
    .参数 误差值, 整数型, , 默认为精确找色,可指定误差0-255,建议在20点误差以内
    .参数 符合程度, 整数型, , 默认为90  100 完全相吻合  80 百分之80像素点相吻合 
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 总长, 整数型
    .局部变量 i, 整数型
    .局部变量 i2, 整数型
    .局部变量 i3, 整数型
    .局部变量 i4, 整数型
    .局部变量 行字节集, 字节集
    .局部变量 位置, 整数型
    .局部变量 吻合值, 整数型
    .局部变量 已吻合值, 整数型
    .局部变量 临时颜色值, 整数型
    .局部变量 补零, 字节型

    找到坐标.x ＝ -1
    找到坐标.y ＝ -1
    总长 ＝ 取字节集长度 (数据)
    .如果 (符合程度 ＝ 0)
        吻合值 ＝ 取数组成员数 (点阵) × 100 ＼ 100
    .否则
        吻合值 ＝ 取数组成员数 (点阵) × 符合程度 ＼ 100
    .如果结束
    补零 ＝ (4 － 宽 ％ 4) ％ 4
    .计次循环首 (高, i)
        行字节集 ＝ 取字节集中间 (数据, 1 ＋ (宽 ＋ 补零) × (i － 1), 宽)
        .计次循环首 (宽, i2)
            .如果真 (取绝对值 (行字节集 [i2] － 基色) ≤ 误差值)
                已吻合值 ＝ 0
                .计次循环首 (取数组成员数 (点阵), i3)
                    位置 ＝ i2 ＋ 点阵 [i3].x － 1 ' 这是X位置
                    .如果真 (位置 ＞ 宽) ' 偏移点超出范围
                        跳出循环 ()
                    .如果真结束
                    位置 ＝ (宽 ＋ 补零) × (点阵 [i3].y ＋ i － 1) ＋ 位置 ＋ 1
                    .如果真 (总长 ＜ 位置)
                        跳出循环 ()
                    .如果真结束
                    .如果真 (取绝对值 (数据 [位置] － 点阵 [i3].z) ≤ 误差值)
                        已吻合值 ＝ 已吻合值 ＋ 1
                        临时颜色值 ＝ 行字节集 [i2]
                        .如果真 (已吻合值 ≥ 吻合值)
                            找到坐标.x ＝ i2 － 1
                            找到坐标.y ＝ 高 － i
                            返回 (找到坐标)
                        .如果真结束
                        
                    .如果真结束
                    处理事件 ()
                .计次循环尾 ()
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (找到坐标)

.子程序 找点阵4, 整数型, , 找所有图
    .参数 所有坐标, 精易_坐标J, 参考 数组
    .参数 数据, 字节集
    .参数 宽, 整数型
    .参数 高, 整数型
    .参数 点阵, 点阵型, 数组
    .参数 基色, 整数型
    .参数 误差值, 整数型, , 默认为精确找色,可指定误差0-255,建议在20点误差以内
    .参数 符合程度, 整数型, , 默认为90  100 完全相吻合  80 百分之80像素点相吻合 
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 总长, 整数型
    .局部变量 i, 整数型
    .局部变量 i2, 整数型
    .局部变量 i3, 整数型
    .局部变量 i4, 整数型
    .局部变量 行字节集, 字节集
    .局部变量 位置, 整数型
    .局部变量 吻合值, 整数型
    .局部变量 已吻合值, 整数型
    .局部变量 临时颜色值, 整数型
    .局部变量 补零, 字节型

    清除数组 (所有坐标)
    总长 ＝ 取字节集长度 (数据)
    .如果 (符合程度 ＝ 0)
        吻合值 ＝ 取数组成员数 (点阵) × 100 ＼ 100
    .否则
        吻合值 ＝ 取数组成员数 (点阵) × 符合程度 ＼ 100
    .如果结束
    补零 ＝ (4 － 宽 ％ 4) ％ 4
    .计次循环首 (高, i)
        行字节集 ＝ 取字节集中间 (数据, 1 ＋ (宽 ＋ 补零) × (i － 1), 宽)
        .计次循环首 (宽, i2)
            .如果真 (取绝对值 (行字节集 [i2] － 基色) ≤ 误差值)
                已吻合值 ＝ 0
                .计次循环首 (取数组成员数 (点阵), i3)
                    位置 ＝ i2 ＋ 点阵 [i3].x － 1 ' 这是X位置
                    .如果真 (位置 ＞ 宽) ' 偏移点超出范围
                        跳出循环 ()
                    .如果真结束
                    位置 ＝ (宽 ＋ 补零) × (点阵 [i3].y ＋ i － 1) ＋ 位置 ＋ 1
                    .如果真 (总长 ＜ 位置)
                        跳出循环 ()
                    .如果真结束
                    .如果真 (取绝对值 (数据 [位置] － 点阵 [i3].z) ≤ 误差值)
                        已吻合值 ＝ 已吻合值 ＋ 1
                        临时颜色值 ＝ 行字节集 [i2]
                        .如果真 (已吻合值 ≥ 吻合值)
                            ' 输出调试文本 (当前吻合值)
                            找到坐标.x ＝ i2 － 1
                            找到坐标.y ＝ 高 － i
                            加入成员 (所有坐标, 找到坐标)
                            跳出循环 ()
                        .如果真结束
                        
                    .如果真结束
                    处理事件 ()
                .计次循环尾 ()
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (取数组成员数 (所有坐标))

.子程序 找点阵5, 整数型, , 快速找所有字
    .参数 坐标组, 精易_坐标J, 可空 数组
    .参数 数据, 字节集
    .参数 宽, 整数型
    .参数 高, 整数型
    .参数 点阵, 精易_坐标J, 数组
    .参数 不同点阵, 精易_坐标J, 数组
    .局部变量 颜色值, 整数型
    .局部变量 找到坐标, 精易_坐标J
    .局部变量 总长, 整数型
    .局部变量 i, 整数型
    .局部变量 i2, 整数型
    .局部变量 i3, 整数型
    .局部变量 i4, 整数型
    .局部变量 行字节集, 字节集
    .局部变量 位置, 整数型
    .局部变量 吻合值, 整数型
    .局部变量 已吻合值, 整数型
    .局部变量 不吻合值, 整数型
    .局部变量 临时颜色值, 整数型
    .局部变量 补零, 字节型

    清除数组 (坐标组)
    总长 ＝ 取字节集长度 (数据)
    颜色值 ＝ 到可用颜色 (LF颜色)
    吻合值 ＝ 取数组成员数 (点阵)
    不吻合值 ＝ 取数组成员数 (不同点阵)
    补零 ＝ (4 － 宽 ％ 4) ％ 4
    .计次循环首 (高, i)
        行字节集 ＝ 取字节集中间 (数据, 1 ＋ (宽 ＋ 补零) × (i － 1), 宽)
        .计次循环首 (宽, i2)
            .如果真 (行字节集 [i2] ＝ 颜色值)
                已吻合值 ＝ 0
                .计次循环首 (取数组成员数 (点阵), i3)
                    位置 ＝ i2 ＋ 点阵 [i3].x － 1 ' 这是X位置
                    .如果真 (位置 ＞ 宽) ' 偏移点超出范围
                        跳出循环 ()
                    .如果真结束
                    位置 ＝ (宽 ＋ 补零) × (点阵 [i3].y ＋ i － 1) ＋ 位置 ＋ 1
                    .如果真 (总长 ＜ 位置)
                        跳出循环 ()
                    .如果真结束
                    .如果真 (数据 [位置] ＝ 行字节集 [i2])
                        已吻合值 ＝ 已吻合值 ＋ 1
                        临时颜色值 ＝ 行字节集 [i2]
                        .如果真 (已吻合值 ≥ 吻合值)
                            已吻合值 ＝ 0
                            .计次循环首 (取数组成员数 (不同点阵), i4) ' 判断文字以外点与文字颜色不同,防止找到的是色块不是文字!!
                                位置 ＝ i2 ＋ 不同点阵 [i4].x － 1
                                .如果真 (位置 ＞ 宽)
                                    跳出循环 ()
                                .如果真结束
                                位置 ＝ 1 ＋ (宽 ＋ 补零) × (不同点阵 [i4].y ＋ i － 1) ＋ 位置
                                .如果真 (总长 ＜ 位置 ＋ 2)
                                    跳出循环 ()
                                .如果真结束
                                .如果真 (数据 [位置] ＝ 临时颜色值)
                                    到循环尾 ()
                                .如果真结束
                                已吻合值 ＝ 已吻合值 ＋ 1
                                .如果真 (已吻合值 ≥ 不吻合值)
                                    找到坐标.x ＝ i2 － 1
                                    找到坐标.y ＝ 高 － i
                                    加入成员 (坐标组, 找到坐标)
                                    跳出循环 ()
                                .如果真结束
                                
                            .计次循环尾 ()
                            跳出循环 ()
                        .如果真结束
                        
                    .如果真结束
                    
                .计次循环尾 ()
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (取数组成员数 (坐标组))

