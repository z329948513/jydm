.版本 2

.程序集 类_内存配置项, , 公开
.程序集变量 集_配置节, 配置节数据, , "0"
.程序集变量 集_保存文件名, 文本型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    写到文件 ()
    清除数组 (集_配置节)

.子程序 写配置项J, 逻辑型, 公开, 将配置项写入内存
    .参数 节名称, 文本型
    .参数 配置项名称, 文本型
    .参数 欲写入值, 文本型
    .局部变量 项目是否存在, 逻辑型
    .局部变量 节名序号, 整数型
    .局部变量 临时_配置节, 配置节数据
    .局部变量 临时_配置项, 配置项数据
    .局部变量 i, 整数型
    .局部变量 o, 整数型

    .如果真 (节名称 ＝ “” 或 配置项名称 ＝ “”)
        返回 (假)
    .如果真结束
    欲写入值 ＝ 内部方法_换行符转换行符标识符 (欲写入值)
    .计次循环首 (取数组成员数 (集_配置节), i)
        .如果真 (集_配置节 [i].节名称 ＝ 节名称)
            节名序号 ＝ i
            .计次循环首 (取数组成员数 (集_配置节 [i].项), o)
                .如果真 (集_配置节 [i].项 [o].配置项名称 ＝ 配置项名称) ' 如果存在'节名称'且'配置项名称'也存在  ,则直接写入值
                    集_配置节 [i].项 [o].值 ＝ 欲写入值
                    项目是否存在 ＝ 真
                    跳出循环 ()
                .如果真结束
                
            .计次循环尾 ()
        .如果真结束
        
    .计次循环尾 ()
    .如果真 (节名序号 ＝ 0) ' 无此配置节 添加配置节 赋值配置节的序号
        临时_配置节.节名称 ＝ 节名称
        清除数组 (临时_配置节.项)
        加入成员 (集_配置节, 临时_配置节)
        节名序号 ＝ 取数组成员数 (集_配置节)
    .如果真结束
    .如果真 (取反 (项目是否存在)) ' 如果不存在就添加 节名称,配置项名称,值
        ' 临时_配置项.节名称 ＝ 节名称
        临时_配置项.配置项名称 ＝ 配置项名称
        临时_配置项.值 ＝ 欲写入值
        加入成员 (集_配置节 [节名序号].项, 临时_配置项)
        
        
    .如果真结束
    
    返回 (真)

.子程序 读配置项J, 文本型, 公开, 返回 节名称->配置项名称->值
    .参数 节名称, 文本型
    .参数 配置项名称, 文本型
    .参数 默认文本, 文本型, 可空, 注明：如果指定配置项不存在，将返回此默认文本。如果指定配置项不存在且本参数被省略，将返回空文本。
    .局部变量 i, 整数型
    .局部变量 o, 整数型

    .如果真 (配置项名称 ＝ “” 或 节名称 ＝ “”)
        返回 (默认文本)
    .如果真结束
    .计次循环首 (取数组成员数 (集_配置节), i)
        .如果真 (集_配置节 [i].节名称 ＝ 节名称) ' 存在此配置节
            .计次循环首 (取数组成员数 (集_配置节 [i].项), o)
                .如果真 (集_配置节 [i].项 [o].配置项名称 ＝ 配置项名称)
                    返回 (内部方法_换行符标识符转换行符 (集_配置节 [i].项 [o].值))
                .如果真结束
                
            .计次循环尾 ()
            返回 (默认文本) ' 无对应配置项
        .如果真结束
        
    .计次循环尾 ()
    返回 (默认文本) ' 无对应配置节

.子程序 读配置项_整数, 整数型, 公开, 返回 节名称->配置项名称->值
    .参数 节名称, 文本型
    .参数 配置项名称, 文本型
    .参数 默认数值, , 可空, 注明：如果指定配置项不存在，将返回此默认数值。如果指定配置项不存在且本参数被省略，将返回0。
    .局部变量 i, 整数型
    .局部变量 o, 整数型

    .如果真 (配置项名称 ＝ “” 或 节名称 ＝ “”)
        返回 (默认数值)
    .如果真结束
    .计次循环首 (取数组成员数 (集_配置节), i)
        .如果真 (集_配置节 [i].节名称 ＝ 节名称) ' 存在此配置节
            .计次循环首 (取数组成员数 (集_配置节 [i].项), o)
                .如果真 (集_配置节 [i].项 [o].配置项名称 ＝ 配置项名称)
                    返回 (到数值 (内部方法_换行符标识符转换行符 (集_配置节 [i].项 [o].值)))
                .如果真结束
                
            .计次循环尾 ()
            返回 (默认数值) ' 无对应配置项
        .如果真结束
        
    .计次循环尾 ()
    返回 (默认数值) ' 无对应配置节

.子程序 删除配置项, 逻辑型, 公开, 返回删除是否i成功
    .参数 节名称, 文本型
    .参数 配置项名称, 文本型, 可空, 留空则删除'节名称'的所有'配置项'
    .局部变量 配置项_长度, 整数型
    .局部变量 i, 整数型
    .局部变量 o, 整数型

    .如果真 (节名称 ＝ “”)
        返回 (假)
    .如果真结束
    
    配置项_长度 ＝ 取数组成员数 (集_配置节)
    
    .如果 (是否为空 (配置项名称))
        .变量循环首 (配置项_长度, 1, -1, i)
            .如果真 (集_配置节 [i].节名称 ＝ 节名称) ' 删除节列表名称
                删除成员 (集_配置节, i, 1)
                跳出循环 ()
            .如果真结束
            
        .变量循环尾 ()
        
    .否则
        
        .变量循环首 (配置项_长度, 1, -1, i)
            .如果真 (集_配置节 [i].节名称 ＝ 节名称)
                .计次循环首 (取数组成员数 (集_配置节 [i].项), o)
                    .如果真 (集_配置节 [i].项 [o].配置项名称 ＝ 配置项名称)
                        删除成员 (集_配置节 [i].项, o, 1)
                        返回 (真)
                    .如果真结束
                    
                .计次循环尾 ()
            .如果真结束
            
        .变量循环尾 ()
    .如果结束
    返回 (真)

.子程序 清空, , 公开, 所有数据将丢失
    重定义数组 (集_配置节, 假, 0)

.子程序 取所有配置项, 整数型, 公开, 返回'配置项名'的数量
    .参数 节名称, 文本型, 可空, 留空取所有的配置项数目
    .参数 配置项_参考_数组, 配置项数据, 参考 可空 数组, 留空为不取
    .局部变量 配置项_长度, 整数型
    .局部变量 k, 整数型
    .局部变量 i, 整数型

    配置项_长度 ＝ 取数组成员数 (集_配置节)
    .如果真 (是否为空 (节名称))
        返回 (配置项_长度)
    .如果真结束
    
    .如果 (取反 (是否为空 (配置项_参考_数组)))
        重定义数组 (配置项_参考_数组, 假, 0)
        .计次循环首 (配置项_长度, i)
            .如果真 (集_配置节 [i].节名称 ＝ 节名称)
                配置项_参考_数组 ＝ 集_配置节 [i].项
                跳出循环 ()
            .如果真结束
            
        .计次循环尾 ()
        返回 (取数组成员数 (配置项_参考_数组))
        
    .否则
        
        .计次循环首 (配置项_长度, i)
            .如果真 (集_配置节 [i].节名称 ＝ 节名称)
                k ＝ k ＋ 1
            .如果真结束
            
        .计次循环尾 ()
        返回 (k)
    .如果结束
    
    

.子程序 取配置项名, 整数型, 公开, 返回'配置项名'的数量
    .参数 节名称, 文本型, , 留空取所有的配置项数目
    .参数 配置项名称_参考_数组, 文本型, 参考 数组
    .局部变量 k, 整数型
    .局部变量 i, 整数型

    .如果真 (节名称 ＝ “”)
        返回 (0)
    .如果真结束
    重定义数组 (配置项名称_参考_数组, 假, 0)
    .计次循环首 (取数组成员数 (集_配置节), i)
        .如果真 (集_配置节 [i].节名称 ＝ 节名称)
            .计次循环首 (取数组成员数 (集_配置节 [i].项), k)
                加入成员 (配置项名称_参考_数组, 集_配置节 [i].项 [k].配置项名称)
            .计次循环尾 ()
            跳出循环 ()
        .如果真结束
        
    .计次循环尾 ()
    返回 (取数组成员数 (配置项名称_参考_数组))
    
    

.子程序 取配置节名, 整数型, 公开, 返回'节名称'的数量
    .参数 节名称_参考_数组, 文本型, 参考 可空 数组, 接收节名称数组
    .局部变量 i, 整数型
    .局部变量 局_配置节数, 整数型

    局_配置节数 ＝ 取数组成员数 (集_配置节)
    .如果真 (取反 (是否为空 (节名称_参考_数组)))
        清除数组 (节名称_参考_数组)
        .计次循环首 (局_配置节数, i)
            加入成员 (节名称_参考_数组, 集_配置节 [i].节名称)
        .计次循环尾 ()
    .如果真结束
    返回 (局_配置节数)

.子程序 加载配置项_从文件, 逻辑型, 公开, 将一配置文件内容加载到内存配置中，但不管内存配置项如何变动不会对该配置文件进行任何修改。
    .参数 配置文件名路径, 文本型, , 例 c:\config.ini
    .参数 自动保存, 逻辑型, 可空, 类销毁时,是否自动保存配置到文件(配置文件名)
    .局部变量 执行结果, 逻辑型
    .局部变量 局_配置数据, 字节集

    集_保存文件名 ＝ 选择 (自动保存, 配置文件名路径, “”)
    .如果真 (文件是否存在 (配置文件名路径))
        局_配置数据 ＝ 读入文件 (配置文件名路径)
        执行结果 ＝ 内部方法_加载配置项 (局_配置数据)
    .如果真结束
    返回 (执行结果)

.子程序 内部方法_加载配置项, 逻辑型
    .参数 文本数据, 字节集
    .局部变量 局部_文本, 文本型
    .局部变量 当前节名, 文本型
    .局部变量 位置, 整数型
    .局部变量 数组_长度
    .局部变量 局_字集数组, 字节集, , "0"
    .局部变量 i, 整数型

    局_字集数组 ＝ 分割字节集 (文本数据, { 13, 10 }, )
    数组_长度 ＝ 取数组成员数 (局_字集数组)
    .如果真 (数组_长度 ＞ 0)
        重定义数组 (集_配置节, 假, 0)
        .计次循环首 (数组_长度, i)
            局部_文本 ＝ 删首尾空 (到文本 (局_字集数组 [i]))
            .如果真 (局部_文本 ≠ “”)
                .如果 (局部_文本 ≈ “[”)
                    当前节名 ＝ 取文本中间 (局部_文本, 2, 取文本长度 (局部_文本) － 2)
                .否则
                    位置 ＝ 寻找文本 (局部_文本, “=”, 0, 假)
                    .如果真 (位置 ≠ -1)
                        写配置项J (当前节名, 删首尾空 (取文本左边 (局部_文本, 位置 － 1)), 取文本右边 (局部_文本, 取文本长度 (局部_文本) － 位置))
                    .如果真结束
                    
                .如果结束
                
            .如果真结束
            处理事件 ()
        .计次循环尾 ()
        返回 (真)
    .如果真结束
    返回 (假)

.子程序 内部方法_换行符转换行符标识符, 文本型
    .参数 待转文本, 文本型

    返回 (子文本替换 (待转文本, #换行符, “{\r\n}”, , , 真)) ' 处理换行符

.子程序 内部方法_换行符标识符转换行符, 文本型
    .参数 待转文本, 文本型

    返回 (子文本替换 (待转文本, “{\r\n}”, #换行符, , , 真)) ' 处理换行符

.子程序 加载配置项_从内存, 逻辑型, 公开, 将一符合配置文件格式的内存数据加载到内存配置中。
    .参数 文本数据, 字节集

    返回 (内部方法_加载配置项 (文本数据))

.子程序 取配置数据, 字节集, 公开, 配置项的文件信息,用于下次加载
    .局部变量 配置项名数组, 文本型, , "0"
    .局部变量 节名称数组, 文本型, , "0"
    .局部变量 i, 整数型
    .局部变量 j, 整数型
    .局部变量 文件号, 整数型
    .局部变量 文件数据, 字节集

    文件号 ＝ 打开内存文件 ()
    .计次循环首 (取数组成员数 (集_配置节), i)
        写出文本 (文件号, “[”, 集_配置节 [i].节名称, “]”, #换行符)
        .计次循环首 (取数组成员数 (集_配置节 [i].项), j)
            写出文本 (文件号, 集_配置节 [i].项 [j].配置项名称, “=”, 内部方法_换行符转换行符标识符 (集_配置节 [i].项 [j].值), #换行符)
        .计次循环尾 ()
    .计次循环尾 ()
    移到文件首 (文件号)
    文件数据 ＝ 读入字节集 (文件号, 取文件长度 (文件号))
    关闭文件 (文件号)
    返回 (文件数据)

.子程序 写到文件, 逻辑型, 公开, 将当前内存配置内容写出到文件
    .参数 配置文件名, 文本型, 可空, 例： c:\config.ini,留空,则使用加载配置项_从文件()时提供的 '配置文件名路径'

    .如果真 (是否为空 (配置文件名))
        配置文件名 ＝ 集_保存文件名
    .如果真结束
    .如果真 (配置文件名 ＝ “”)
        返回 (假)
    .如果真结束
    返回 (写到文件 (配置文件名, 取配置数据 ()))
    

