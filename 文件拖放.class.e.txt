.版本 2

.程序集 文件拖放, , 公开, 文件拖放
.程序集变量 集_窗口句柄, 整数型
.程序集变量 集_返回指针, 整数型
.程序集变量 集_挂接指针, 子程序指针

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁
    拖放对象_撤销 ()

.子程序 拖放对象_注册, , 公开, 将类命令声明到程序集，局部变量会失败
    .参数 窗口句柄, 整数型
    .参数 挂接拖放事件, 子程序指针, , 创建一个取回文件名子程序.需要带参数;   参数1:文本型
    .局部变量 局_系统, 文本型

    集_窗口句柄 ＝ 窗口句柄
    集_挂接指针 ＝ 挂接拖放事件
    
    DragAcceptFiles (集_窗口句柄, 1)
    集_返回指针 ＝ SetWindowLongA (集_窗口句柄, #GWL_WNDPROC, 类_取内部方法地址 (5))
    
    局_系统 ＝ 系统_取系统版本 (, , , , , )
    .如果真 (取文本左边 (局_系统, 10) ≠ “Windows XP”)
        ChangeWindowMessageFilter (#WM_DROPFILES, #MSGFLT_ADD)
        ChangeWindowMessageFilter (#WM_COPYDATA, #MSGFLT_ADD)
        ChangeWindowMessageFilter (73, #MSGFLT_ADD)
    .如果真结束
    

.子程序 拖放对象_撤销, , 公开
    DragAcceptFiles (集_窗口句柄, 0)
    SetWindowLongA (集_窗口句柄, -4, 集_返回指针)

.子程序 拖放对象回调, 整数型
    .参数 hWnd, 整数型
    .参数 uMsg, 整数型
    .参数 wParam, 整数型
    .参数 lParam, 整数型
    .局部变量 局_文件标题, 文本型
    .局部变量 局_标识, 整数型
    .局部变量 局_句柄, 整数型
    .局部变量 i, 整数型

    .如果真 (uMsg ＝ 563)
        局_文件标题 ＝ 取空白文本 (1000)
        局_句柄 ＝ DragQueryFileA (wParam, -1, 局_文件标题, 1000)
        .变量循环首 (0, 局_句柄 － 1, 1, i)
            DragQueryFileA (wParam, i, 局_文件标题, 1000)
            局_标识 ＝ 到整数 (集_挂接指针)
            置入代码 ({ 139, 117, 252, 139, 69, 240, 80, 86, 255, 208, 88 })
        .变量循环尾 ()
        DragFinish (wParam)
        返回 (0)
    .如果真结束
    返回 (CallWindowProcA (集_返回指针, hWnd, uMsg, wParam, lParam))
    

