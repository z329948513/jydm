.版本 2

.程序集 类_外部选择夹, , 公开
.程序集变量 集_选择夹句柄, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 初始化选择夹, 逻辑型, 公开
    .参数 参_窗口句柄, 整数型

    .判断开始 (集_选择夹句柄 ＝ 0)
        集_选择夹句柄 ＝ 参_窗口句柄
        返回 (真)
    .默认
        返回 (假)
    .判断结束
    

.子程序 取类名, 文本型
    .参数 窗口句柄, 整数型

    返回 (窗口_取类名J (窗口句柄))

.子程序 删除子夹, 逻辑型, 公开, 成功返回真
    .参数 欲删除的子夹索引, 整数型, , 0 代表子夹一，1 代表子夹二，如此类推。-1为最末端子夹。(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    .局部变量 结果, 整数型

    .如果真 (欲删除的子夹索引 ＝ -1)
        欲删除的子夹索引 ＝ 取子夹数目 () － 1
    .如果真结束
    结果 ＝ SendMessageA (集_选择夹句柄, #TCM_DELETEITEM, 欲删除的子夹索引, 0)
    返回 (选择 (结果 ≠ 0, 真, 假))
    

.子程序 插入子夹, 整数型, 公开, 成功返回位置索引，失败返回-1
    .参数 插入位置, 整数型, 可空, 默认为-1，加到子夹末尾。0 代表子夹一，1 代表子夹二，如此类推(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    .参数 子夹标题, 文本型
    .参数 子夹项目数值, 整数型, 可空, 默认为0
    .参数 图片索引, 整数型, 可空, 默认为-1(无图片)
    .局部变量 子夹结构, TCITEM
    .局部变量 结果, 整数型
    .局部变量 子夹数, 整数型

    .如果真 (是否为空 (图片索引) 或 取图片组句柄 () ＝ 0)
        图片索引 ＝ -1
    .如果真结束
    子夹数 ＝ 取子夹数目 ()
    .如果真 (是否为空 (插入位置) 或 插入位置 ＞ 子夹数 － 1 或 插入位置 ＝ -1)
        插入位置 ＝ 子夹数
    .如果真结束
    子夹结构.mask ＝ #TCIF_TEXT ＋ #TCIF_IMAGE ＋ #TCIF_PARAM ' 1 +  2 + 8
    子夹结构.dwState ＝ 0
    子夹结构.dwStateMask ＝ 0
    子夹结构.pszText ＝ 子夹标题
    子夹结构.cchTextMax ＝ 0
    子夹结构.iImage ＝ 图片索引
    子夹结构.lParam ＝ 子夹项目数值
    结果 ＝ SendMessageA (集_选择夹句柄, #TCM_INSERTITEMA, 插入位置, 取数据地址 (子夹结构, 子夹结构, 0))
    .如果真 (子夹数 ＝ 0)
        UpdateWindow (GetParent (集_选择夹句柄))
    .如果真结束
    返回 (结果)
    

.子程序 取子夹数目, 整数型, 公开
    返回 (SendMessageA (集_选择夹句柄, #TCM_GETITEMCOUNT, 0, 0))

.子程序 取子夹标题, 文本型, 公开
    .参数 子夹索引, 整数型, , 0 代表子夹一，1 代表子夹二，如此类推。-1为最末端子夹。(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    .局部变量 子夹结构, TCITEM
    .局部变量 结果, 整数型

    .如果真 (子夹索引 ＝ -1)
        子夹索引 ＝ 取子夹数目 () － 1
    .如果真结束
    子夹结构.mask ＝ #TCIF_TEXT
    子夹结构.dwState ＝ 0
    子夹结构.dwStateMask ＝ 0
    子夹结构.pszText ＝ 取空白文本 (256)
    子夹结构.cchTextMax ＝ 256
    子夹结构.iImage ＝ -1
    子夹结构.lParam ＝ 1
    SendMessageA (集_选择夹句柄, #TCM_GETITEMA, 子夹索引, 取数据地址 (子夹结构, 子夹结构, 0))
    返回 (子夹结构.pszText)

.子程序 取子夹数值, 整数型, 公开
    .参数 子夹索引, 整数型, , 0 代表子夹一，1 代表子夹二，如此类推。-1为最末端子夹。(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    .局部变量 子夹结构, TCITEM
    .局部变量 结果, 整数型

    .如果真 (子夹索引 ＝ -1)
        子夹索引 ＝ 取子夹数目 () － 1
    .如果真结束
    子夹结构.mask ＝ #TCIF_PARAM
    SendMessageA (集_选择夹句柄, #TCM_GETITEMA, 子夹索引, 取数据地址 (子夹结构, 子夹结构, 0))
    返回 (子夹结构.lParam)
    

.子程序 置子夹标题, 逻辑型, 公开
    .参数 子夹索引, 整数型, , 0 代表子夹一，1 代表子夹二，如此类推。-1为最末端子夹。(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    .参数 欲置入的子夹标题, 文本型
    .局部变量 子夹结构, TCITEM
    .局部变量 结果, 整数型

    .如果真 (子夹索引 ＝ -1)
        子夹索引 ＝ 取子夹数目 () － 1
    .如果真结束
    子夹结构.mask ＝ #TCIF_TEXT
    子夹结构.dwState ＝ 0
    子夹结构.dwStateMask ＝ 0
    子夹结构.pszText ＝ 欲置入的子夹标题
    子夹结构.cchTextMax ＝ 0
    子夹结构.iImage ＝ -1
    子夹结构.lParam ＝ 1
    结果 ＝ SendMessageA (集_选择夹句柄, #TCM_SETITEMA, 子夹索引, 取数据地址 (子夹结构, 子夹结构, 0))
    返回 (选择 (结果 ≠ 0, 真, 假))

.子程序 置子夹数值, 逻辑型, 公开
    .参数 子夹索引, 整数型, , 0 代表子夹一，1 代表子夹二，如此类推。-1为最末端子夹。(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    .参数 欲置入的子夹数值, 整数型
    .局部变量 子夹结构, TCITEM
    .局部变量 结果, 整数型

    .如果真 (子夹索引 ＝ -1)
        子夹索引 ＝ 取子夹数目 () － 1
    .如果真结束
    子夹结构.mask ＝ #TCIF_PARAM
    子夹结构.lParam ＝ 欲置入的子夹数值
    结果 ＝ SendMessageA (集_选择夹句柄, #TCM_SETITEMA, 子夹索引, 取数据地址 (子夹结构, 子夹结构, 0))
    返回 (选择 (结果 ≠ 0, 真, 假))

.子程序 置现行子夹, , 公开
    .参数 欲选择的子夹索引, 整数型, , 0 代表子夹一，1 代表子夹二，如此类推。-1为最末端子夹。(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。

    .如果真 (欲选择的子夹索引 ＝ -1)
        欲选择的子夹索引 ＝ 取子夹数目 () － 1
    .如果真结束
    SendMessageA (集_选择夹句柄, #TCM_SETCURFOCUS, 欲选择的子夹索引, 0)
    

.子程序 取现行子夹, 整数型, 公开, 0 代表子夹一，1 代表子夹二，如此类推(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    返回 (SendMessageA (集_选择夹句柄, #TCM_GETCURFOCUS, 0, 0))

.子程序 删除全部子夹, , 公开
    SendMessageA (集_选择夹句柄, #TCM_DELETEALLITEMS, 0, 0)
    

.子程序 取图片组句柄, 整数型, 公开
    返回 (SendMessageA (集_选择夹句柄, #TCM_GETIMAGELIST, 0, 0))
    

.子程序 取行数, 整数型, 公开
    返回 (SendMessageA (集_选择夹句柄, #TCM_GETROWCOUNT, 0, 0))
    

.子程序 置入图片组, , 公开
    .参数 图片组句柄, 整数型

    SendMessageA (集_选择夹句柄, #TCM_SETIMAGELIST, 0, 图片组句柄)
    

.子程序 置子夹图片, 逻辑型, 公开
    .参数 子夹索引, 整数型, , 0 代表子夹一，1 代表子夹二，如此类推。-1为最末端子夹。(注意：易语言的子夹索引是从1计起，这个类是按照微软的标准从0计起)。
    .参数 图片索引, 整数型
    .局部变量 子夹结构, TCITEM
    .局部变量 结果, 整数型

    .如果真 (子夹索引 ＝ -1)
        子夹索引 ＝ 取子夹数目 () － 1
    .如果真结束
    子夹结构.mask ＝ #TCIF_IMAGE
    子夹结构.iImage ＝ 图片索引
    结果 ＝ SendMessageA (集_选择夹句柄, #TCM_SETITEMA, 子夹索引, 取数据地址 (子夹结构, 子夹结构, 0))
    返回 (选择 (结果 ≠ 0, 真, 假))
    

.子程序 置子夹栏尺寸, , 公开
    .参数 子夹栏宽度
    .参数 子夹栏高度

    SendMessageA (集_选择夹句柄, #TCM_SETITEMSIZE, 0, 合并整数 (子夹栏宽度, 子夹栏高度))
    

.子程序 置子夹栏宽度, , 公开
    .参数 子夹栏宽度

    SendMessageA (集_选择夹句柄, #TCM_SETMINTABWIDTH, 0, 子夹栏宽度)

