.版本 2

.程序集 集_字节集
.子程序 字节集_字节集转文本, 文本型, 公开, 以文本表示字节集，适合于字节集的调试用,查看字节集可以将一串字节集用文本形式表示出来，极大地方便了调试字节集变量。感谢易友[缘诚]提供优化查看速度方案
    .参数 原始字节集, 字节集, , 要查看的字节集
    .局部变量 局_文件句柄, 整数型
    .局部变量 局_字节集长度, 整数型
    .局部变量 局_i, 整数型
    .局部变量 局_文本, 文本型

    局_文件句柄 ＝ 打开内存文件 ()
    局_字节集长度 ＝ 取字节集长度 (原始字节集)
    .计次循环首 (局_字节集长度, 局_i)
        插入文本 (局_文件句柄, 到文本 (原始字节集 [局_i]) ＋ “,”)
        处理事件 ()
    .计次循环尾 ()
    移到文件首 (局_文件句柄)
    局_文本 ＝ “{” ＋ 读入文本 (局_文件句柄, )
    关闭文件 (局_文件句柄)
    .如果 (取文本右边 (局_文本, 1) ＝ “,”)
        局_文本 ＝ 取文本左边 (局_文本, 取文本长度 (局_文本) － 1) ＋ “}”
    .否则
        局_文本 ＝ 局_文本 ＋ “}”
    .如果结束
    
    返回 (局_文本)
    
    

.子程序 字节集_字节集转文本ex, 文本型, 公开, 感谢【@贫民窟 】提供源码
    .参数 字节集, 字节集
    .局部变量 长度, 整数型
    .局部变量 缓冲区, 字节集
    .局部变量 缓冲区索引, 整数型
    .局部变量 i, 整数型
    .局部变量 字节, 字节型
    .局部变量 字节1, 字节型
    .局部变量 字节2, 字节型
    .局部变量 字节3, 字节型

    长度 ＝ 取字节集长度 (字节集)
    .如果真 (长度 ＝ 0)
        返回 (“”)
    .如果真结束
    
    缓冲区 ＝ 取空白字节集 (长度 × 4 ＋ 2)
    缓冲区 [1] ＝ 123
    缓冲区索引 ＝ 1
    
    .计次循环首 (长度, i)
        字节 ＝ 字节集 [i]
        缓冲区索引 ＝ 缓冲区索引 ＋ 1
        
        字节1 ＝ 字节 ＼ 100
        字节 ＝ 字节 － 字节1 × 100
        
        字节2 ＝ 字节 ＼ 10
        
        字节 ＝ 字节 － 字节2 × 10
        
        .判断开始 (字节1 ＞ 0)
            缓冲区 [缓冲区索引] ＝ 字节1 ＋ 48
            缓冲区索引 ＝ 缓冲区索引 ＋ 1
            缓冲区 [缓冲区索引] ＝ 字节2 ＋ 48
            缓冲区索引 ＝ 缓冲区索引 ＋ 1
        .默认
            .如果真 (字节2 ＞ 0)
                缓冲区 [缓冲区索引] ＝ 字节2 ＋ 48
                缓冲区索引 ＝ 缓冲区索引 ＋ 1
            .如果真结束
            
        .判断结束
        
        缓冲区 [缓冲区索引] ＝ 字节 ＋ 48
        .判断开始 (长度 ＝ i)
            缓冲区 [缓冲区索引 ＋ 1] ＝ 125
            
        .默认
            缓冲区索引 ＝ 缓冲区索引 ＋ 1
            缓冲区 [缓冲区索引] ＝ 44
        .判断结束
        
    .计次循环尾 ()
    返回 (到文本 (缓冲区))

.子程序 字节集_文本转字节集ex, 字节集, 公开, 感谢【@贫民窟 】提供源码
    .参数 参_文本, 文本型
    .局部变量 局_缓冲区, 字节集
    .局部变量 局_缓冲区索引, 整数型
    .局部变量 局_数字缓冲区, 字节集
    .局部变量 局_数字缓冲索引, 整数型
    .局部变量 局_字节集, 字节集
    .局部变量 局_长度, 整数型
    .局部变量 i, 整数型
    .局部变量 局_字节, 字节型
    .局部变量 局_插入逻辑, 逻辑型
    .局部变量 局_位数, 整数型
    .局部变量 局_文本, 文本型

    局_文本 ＝ 参_文本
    
    .如果真 (取文本左边 (局_文本, 1) ≠ “{”)
        局_文本 ＝ “{” ＋ 局_文本
    .如果真结束
    
    .如果真 (取文本右边 (局_文本, 1) ≠ “}”)
        局_文本 ＝ 局_文本 ＋ “}”
    .如果真结束
    
    局_字节集 ＝ 到字节集 (删全部空 (局_文本))
    局_长度 ＝ 取字节集长度 (局_字节集)
    .如果真 (局_长度 ＜ 1)
        返回 ({ })
    .如果真结束
    
    局_缓冲区 ＝ 取空白字节集 (局_长度)
    局_缓冲区索引 ＝ 1
    局_数字缓冲区 ＝ { 0, 0, 0 }
    局_数字缓冲索引 ＝ 1
    .计次循环首 (局_长度, i)
        局_字节 ＝ 局_字节集 [i]
        .判断开始 (局_字节 ＞ 47 且 局_字节 ＜ 58) ' 数字。
            .如果真 (局_数字缓冲索引 ＞ 3)
                信息框 (“error”, 0, , )
                返回 ({ })
            .如果真结束
            局_数字缓冲区 [局_数字缓冲索引] ＝ 局_字节 － 48
            局_数字缓冲索引 ＝ 局_数字缓冲索引 ＋ 1
            局_插入逻辑 ＝ 真
            局_位数 ＝ 局_位数 ＋ 1
            
        .默认
            
            .如果真 (局_插入逻辑)
                局_插入逻辑 ＝ 假
                .判断开始 (局_位数 ＝ 1)
                    局_字节 ＝ 局_数字缓冲区 [1]
                .判断 (局_位数 ＝ 2)
                    局_字节 ＝ 局_数字缓冲区 [1] × 10 ＋ 局_数字缓冲区 [2]
                .默认
                    局_字节 ＝ 局_数字缓冲区 [1] × 100 ＋ 局_数字缓冲区 [2] × 10 ＋ 局_数字缓冲区 [3]
                .判断结束
                局_缓冲区 [局_缓冲区索引] ＝ 局_字节
                局_缓冲区索引 ＝ 局_缓冲区索引 ＋ 1
                局_数字缓冲区 [1] ＝ 0
                局_数字缓冲区 [2] ＝ 0
                局_数字缓冲区 [3] ＝ 0
                局_数字缓冲索引 ＝ 1
                局_位数 ＝ 0
            .如果真结束
            
        .判断结束
        
    .计次循环尾 ()
    返回 (取字节集左边 (局_缓冲区, 局_缓冲区索引 － 1))

.子程序 字节集_文本转字节集, 字节集, 公开, 将“字节集_字节集转文本”得到的文本字节集重新转换为字节集。感谢【御风软件】提供代码
    .参数 文本字节集, 文本型
    .局部变量 N, 整数型
    .局部变量 文本数组, 文本型, , "0"
    .局部变量 字节长度, 整数型
    .局部变量 新字节集, 字节集

    文本数组 ＝ 分割文本 (取文本中间 (文本字节集, 2, 取文本长度 (文本字节集) － 1), “,”, )
    字节长度 ＝ 取数组成员数 (文本数组)
    新字节集 ＝ 取空白字节集 (字节长度)
    .计次循环首 (字节长度, N)
        新字节集 [N] ＝ 到字节 (文本数组 [N])
    .计次循环尾 ()
    返回 (新字节集)

.子程序 字节集_十六进制到字节集, 字节集, 公开, 将一串十六进制文本还原为字节集。还原为字节集可以将默认的16进制文本转换成字节集。如“00BFAD00”可还原为{0,191,173,0}
    .参数 原始16进制文本, 文本型, , 如“00BFAD00AFD4FD4F9E054F9FF91FC5FF2076FBAFDB071044”
    .局部变量 字节集, 字节集
    .局部变量 次数, 整数型
    .局部变量 临时变量, 长整数型

    .变量循环首 (1, 取文本长度 (原始16进制文本), 2, 次数)
        临时变量 ＝ 进制_十六到十 (取文本中间 (原始16进制文本, 次数, 2))
        字节集 ＝ 字节集 ＋ 到字节集 (到字节 (临时变量))
        处理事件 ()
    .变量循环尾 ()
    返回 (字节集)

.子程序 字节集_字节集到十六进制, 文本型, 公开, 将字节集用16进制表示.可以将字节集用系统标准16进制表示
    .参数 原始字节集, 字节集, , 用于转换的字节集
    .局部变量 局_文件句柄, 整数型
    .局部变量 局_字节集长度, 整数型
    .局部变量 局_i, 整数型
    .局部变量 局_文本, 文本型

    局_文件句柄 ＝ 打开内存文件 ()
    局_字节集长度 ＝ 取字节集长度 (原始字节集)
    .计次循环首 (局_字节集长度, 局_i)
        插入文本 (局_文件句柄, 到十六进制 (原始字节集 [局_i]))
        处理事件 ()
    .计次循环尾 ()
    移到文件首 (局_文件句柄)
    局_文本 ＝ 读入文本 (局_文件句柄, )
    关闭文件 (局_文件句柄)
    返回 (局_文本)

.子程序 到十六进制, 文本型, , 返回转换后的十六进制文本
    .参数 十进制数据, 整数型, , 传入十进制整型数据
    .局部变量 局部_结果, 文本型

    局部_结果 ＝ 取十六进制文本 (十进制数据)
    .如果真 (取文本长度 (局部_结果) ％ 2 ＝ 1)
        返回 (“0” ＋ 局部_结果)
    .如果真结束
    返回 (局部_结果)

.子程序 字节集_寻找取左, 字节集, 公开, 从字节集的左边按 欲寻找的字节集 开始寻找 如 字节集_取左边({105,108,121,13,10,83,101},{13,10}) 将返回 {105,108,121}
    .参数 被查找的字节集, 字节集, , 被查找的字节集
    .参数 欲寻找的字节集, 字节集, , 欲寻找的字节集
    .参数 起始寻找位置, 整数型, 可空, 可为空，默认为1，从左往右寻找，如果为负数，则从右往左寻找。
    .局部变量 局_长度, 整数型
    .局部变量 局_起始搜寻位置, 整数型
    .局部变量 局_方向, 整数型
    .局部变量 局_找到的位置, 整数型
    .局部变量 局_返回结果, 字节集

    局_长度 ＝ 取字节集长度 (被查找的字节集)
    
    局_起始搜寻位置 ＝ 起始寻找位置
    .如果真 (局_起始搜寻位置 ＜ 0)
        局_起始搜寻位置 ＝ 局_起始搜寻位置 × -1
        .如果真 (局_起始搜寻位置 ＝ 1)
            局_起始搜寻位置 ＝ 0
        .如果真结束
        局_方向 ＝ 1
    .如果真结束
    
    .如果 (局_方向 ＝ 1)
        局_找到的位置 ＝ 倒找字节集 (被查找的字节集, 欲寻找的字节集, 局_长度 － 局_起始搜寻位置)
    .否则
        .如果真 (局_起始搜寻位置 ＝ 0)
            局_起始搜寻位置 ＝ 1
        .如果真结束
        局_找到的位置 ＝ 寻找字节集 (被查找的字节集, 欲寻找的字节集, 局_起始搜寻位置)
    .如果结束
    
    .如果真 (局_找到的位置 ＝ -1)
        返回 ({ })
    .如果真结束
    
    局_返回结果 ＝ 取字节集左边 (被查找的字节集, 局_找到的位置 － 1)
    返回 (局_返回结果)

.子程序 字节集_寻找取右, 字节集, 公开, 从字节集的右边按 欲寻找的字节集 开始寻找  如 字节集_取右边({105,108,121,13,10,83,101},{13,10}) 将返回 {83,101}
    .参数 被查找的字节集, 字节集, , 被查找的字节集
    .参数 欲寻找的字节集, 字节集, , 欲寻找的字节集
    .参数 起始寻找位置, 整数型, 可空, 可为空，默认为1，从左往右寻找，如果为负数，则从右往左寻找。
    .局部变量 局_长度, 整数型
    .局部变量 局_起始搜寻位置, 整数型
    .局部变量 局_方向, 整数型
    .局部变量 局_找到的位置, 整数型
    .局部变量 局_返回结果, 字节集

    局_长度 ＝ 取字节集长度 (被查找的字节集)
    
    局_起始搜寻位置 ＝ 起始寻找位置
    .如果真 (局_起始搜寻位置 ＜ 0)
        局_起始搜寻位置 ＝ 局_起始搜寻位置 × -1
        .如果真 (局_起始搜寻位置 ＝ 1)
            局_起始搜寻位置 ＝ 0
        .如果真结束
        局_方向 ＝ 1
    .如果真结束
    
    .如果 (局_方向 ＝ 1)
        局_找到的位置 ＝ 倒找字节集 (被查找的字节集, 欲寻找的字节集, 局_长度 － 局_起始搜寻位置)
    .否则
        .如果真 (局_起始搜寻位置 ＝ 0)
            局_起始搜寻位置 ＝ 1
        .如果真结束
        局_找到的位置 ＝ 寻找字节集 (被查找的字节集, 欲寻找的字节集, 局_起始搜寻位置)
    .如果结束
    
    .如果真 (局_找到的位置 ＝ -1)
        返回 ({ })
    .如果真结束
    
    局_返回结果 ＝ 取字节集右边 (被查找的字节集, 局_长度 － 取字节集长度 (欲寻找的字节集) － 局_找到的位置 ＋ 1)
    返回 (局_返回结果)

.子程序 字节集_寻找取中, 字节集, 公开
    .参数 要操作的字节集, 字节集
    .参数 前面字节集, 字节集
    .参数 后面字节集, 字节集
    .参数 起始搜寻位置, 整数型, 可空, 可空。1为首位置，2为第2个位置，如此类推，如果被省略，则“寻找字节集”命令默认从首部开始，“倒找字节集”命令默认从尾部开始。
    .局部变量 局_字节集, 字节集
    .局部变量 局_位置, 整数型

    局_位置 ＝ 寻找字节集 (要操作的字节集, 前面字节集, 起始搜寻位置)
    .如果真 (局_位置 ＝ -1)
        返回 ({ })
    .如果真结束
    局_字节集 ＝ 取字节集右边 (要操作的字节集, 取字节集长度 (要操作的字节集) － 局_位置 － 取字节集长度 (前面字节集) ＋ 1)
    局_位置 ＝ 寻找字节集 (局_字节集, 后面字节集, )
    .如果真 (局_位置 ＝ -1)
        返回 ({ })
    .如果真结束
    局_字节集 ＝ 取字节集左边 (局_字节集, 局_位置 － 1)
    返回 (局_字节集)

.子程序 字节集_到文本, 文本型, 公开
    .参数 字节集, 字节集
    .局部变量 返回值, 文本型
    .局部变量 Temp, 字节集

    Temp ＝ 字节集 ＋ { 0 }
    返回 (指针到文本 (字节集_取指针 (Temp)))

.子程序 字节集_到整数, 整数型, 公开
    .参数 字节集, 字节集
    .参数 位置, 整数型, 可空, 默认为1
    .局部变量 返回值, 整数型
    .局部变量 Address, 整数型
    .局部变量 Len, 整数型

    Address ＝ 字节集_取指针 (字节集)
    Len ＝ 字节集_取长度 (字节集)
    .如果真 (是否为空 (位置) ＝ 真)
        位置 ＝ 1
    .如果真结束
    .如果真 (位置 ＞ Len 或 位置 ＜ 1)
        返回 (0)
    .如果真结束
    .如果 (Len － 位置 ＜ 4)
        RtlMoveMemory_整数传址1 (返回值, Address ＋ 位置 － 1, Len － 位置 ＋ 1)
    .否则
        RtlMoveMemory_整数传址1 (返回值, Address ＋ 位置 － 1, 4)
    .如果结束
    返回 (返回值)

.子程序 整数型_到文本, 文本型
    .参数 整数, 整数型
    .局部变量 返回值, 文本型
    .局部变量 参数, 整数型, , "1"

    返回值 ＝ 文本型_取空白 (4)
    参数 [1] ＝ 整数
    wsprintfA (返回值, “%d”, 参数)
    返回 (返回值)

.子程序 字节集_取左边, 字节集, 公开
    .参数 字节集, 字节集
    .参数 欲取出字节的数目, 整数型
    .局部变量 Len, 整数型
    .局部变量 Address, 整数型

    Len ＝ 字节集_取长度 (字节集)
    .如果真 (Len ＜ 1)
        返回 ({ })
    .如果真结束
    .判断开始 (欲取出字节的数目 ＜ 1)
        返回 ({ })
    .判断 (欲取出字节的数目 ＞ Len)
        欲取出字节的数目 ＝ Len
    .默认
        
    .判断结束
    Address ＝ 字节集_取指针 (字节集)
    返回 (指针到字节集 (Address, 欲取出字节的数目))

.子程序 字节集_取右边, 字节集, 公开
    .参数 字节集, 字节集
    .参数 欲取出字节的数目, 整数型
    .局部变量 Len, 整数型
    .局部变量 Address, 整数型

    Len ＝ 字节集_取长度 (字节集)
    .如果真 (Len ＜ 1)
        返回 ({ })
    .如果真结束
    .判断开始 (欲取出字节的数目 ＜ 1)
        返回 ({ })
    .判断 (欲取出字节的数目 ＞ Len)
        欲取出字节的数目 ＝ Len
    .默认
        
    .判断结束
    Address ＝ 字节集_取指针 (字节集) ＋ Len － 欲取出字节的数目
    返回 (指针到字节集 (Address, 欲取出字节的数目))

.子程序 字节集_取空白, 字节集, 公开
    .参数 零字节数目, 整数型
    .局部变量 Address, 整数型
    .局部变量 返回值, 字节集

    .如果真 (零字节数目 ＜ 1)
        返回 ({ })
    .如果真结束
    Address ＝ LocalAlloc (64, 零字节数目)
    .如果真 (Address ＝ 0)
        返回 ({ })
    .如果真结束
    返回值 ＝ 指针到字节集 (Address, 零字节数目)
    LocalFree (Address)
    返回 (返回值)

.子程序 字节集_取长度, 整数型, 公开
    .参数 字节集, 字节集
    .局部变量 返回值, 整数型
    .局部变量 Address, 整数型

    .如果真 (字节集 ＝ { })
        返回 (0)
    .如果真结束
    
    置入代码 ({ 139, 93, 8, 139, 27, 139, 91, 4, 137, 93, 252 }) ' 取字节集长度
    ' Address ＝ 字节集_取指针 (字节集)
    ' RtlMoveMemory_Int (返回值, Address － 4, 4)
    返回 (返回值)

.子程序 字节集_取指定位置字节集, 字节集, 公开
    .参数 字节集, 字节集
    .参数 起始取出位置, 整数型
    .参数 欲取出字节的数目, 整数型
    .局部变量 Len, 整数型
    .局部变量 Address, 整数型

    Len ＝ 字节集_取长度 (字节集)
    .如果真 (Len ＜ 1)
        返回 ({ })
    .如果真结束
    .判断开始 (起始取出位置 ＜ 1)
        起始取出位置 ＝ 1
    .判断 (起始取出位置 ＞ Len)
        返回 ({ })
    .默认
        
    .判断结束
    .如果真 (欲取出字节的数目 ＜ 1)
        返回 ({ })
    .如果真结束
    .如果真 (欲取出字节的数目 ＋ 起始取出位置 － 1 ＞ Len)
        欲取出字节的数目 ＝ Len － 起始取出位置 ＋ 1
    .如果真结束
    Address ＝ 字节集_取指针 (字节集) ＋ 起始取出位置 － 1
    返回 (指针到字节集 (Address, 欲取出字节的数目))

.子程序 字节集_取中间, 字节集, 公开, 取指定字节集中 指定前后字节集中间的数据，用法与”文本_取出中间文本“类似
    .参数 要操作的字节集, 字节集
    .参数 前面字节集, 字节集
    .参数 后面字节集, 字节集
    .参数 起始搜寻位置, 整数型, 可空, 可空。1为首位置，2为第2个位置，如此类推，如果被省略，则“寻找字节集”命令默认从首部开始，“倒找字节集”命令默认从尾部开始。
    .局部变量 局_字节集, 字节集
    .局部变量 局_位置, 整数型

    局_位置 ＝ 寻找字节集 (要操作的字节集, 前面字节集, 起始搜寻位置)
    .如果真 (局_位置 ＝ -1)
        返回 ({ })
    .如果真结束
    局_字节集 ＝ 取字节集右边 (要操作的字节集, 取字节集长度 (要操作的字节集) － 局_位置 － 取字节集长度 (前面字节集) ＋ 1)
    局_位置 ＝ 寻找字节集 (局_字节集, 后面字节集, 起始搜寻位置)
    .如果真 (局_位置 ＝ -1)
        返回 ({ })
    .如果真结束
    局_字节集 ＝ 取字节集左边 (局_字节集, 局_位置 － 1)
    返回 (局_字节集)
    
    ' 文本_取出中间文本 ()
    ' 子程序名：文本_取出中间文本
    ' 比如：欲取全文本为“12345”,现在要取出“3”，<3>的前面为“2”，<3>的后面为“4”，
    ' 返回值类型：文本型
    ' 参数<1>的名称为“欲取全文本”，类型为“文本型”。注明：比如：欲取全文本为 12345。
    ' 参数<2>的名称为“前面文本”，类型为“文本型”。注明：3的前面为“2”，引号直接用 #引号，如："<font color=#引号red#引号>"。
    ' 参数<3>的名称为“后面文本”，类型为“文本型”。注明：3的后面为“4”，引号直接用 #引号，如："<font color=#引号red#引号>"。
    

.子程序 字节集_替换, 字节集, 公开, 将指定字节集的某一部分用其它的字节集替换,然后返回替换后的结果.
    .参数 欲替换其部分的字节集, 字节集
    .参数 起始替换位置, 整数型, , 替换的起始位置,1为首位置,2为第2个位置,如此类推.
    .参数 替换长度, 整数型
    .参数 用作替换的字节集, 字节集, 可空, 可以被省略.如果本参数被省略,则删除字节集中的指定部分.
    .局部变量 Len_X, 整数型
    .局部变量 Temp1, 字节集
    .局部变量 Temp2, 字节集

    .如果真 (欲替换其部分的字节集 ＝ { })
        返回 ({ })
    .如果真结束
    .如果真 (替换长度 ＜ 1)
        返回 (欲替换其部分的字节集)
    .如果真结束
    Len_X ＝ 字节集_取长度 (欲替换其部分的字节集)
    .如果真 (起始替换位置 ＜ 1)
        起始替换位置 ＝ 1
    .如果真结束
    .如果真 (起始替换位置 ＞ Len_X)
        返回 (欲替换其部分的字节集)
    .如果真结束
    Temp1 ＝ 字节集_取左边 (欲替换其部分的字节集, 起始替换位置 － 1)
    Temp2 ＝ 字节集_取右边 (欲替换其部分的字节集, Len_X － (起始替换位置 ＋ 替换长度) ＋ 1)
    返回 (Temp1 ＋ 用作替换的字节集 ＋ Temp2)

.子程序 字节集_寻找, 整数型, 公开, 返回一字节集在另一字节集中最先出现的位置,位置值从 1 开始.如果未找到,返回 -1.
    .参数 被搜寻的字节集, 字节集
    .参数 欲寻找的字节集, 字节集
    .参数 起始搜寻位置, 整数型, 可空, 1为首位置,2为第2个位置,如此类推,如果被省略,则“寻找字节集”命令默认从首部开始,“倒找字节集”命令默认从尾部开始.

    置入代码 ({ 83, 82, 86, 87, 139, 125, 8, 139, 63, 133, 255, 116, 119, 139, 95, 4, 131, 251, 0, 126, 111, 141, 87, 8, 139, 117, 12, 139, 54, 133, 246, 116, 99, 139, 78, 4, 131, 249, 0, 126, 91, 141, 70, 8, 137, 69, 244, 139, 69, 16, 72, 131, 248, 0, 125, 2, 49, 192, 137, 69, 16, 41, 195, 57, 217, 127, 65, 73, 137, 77, 248, 41, 203, 137, 93, 252, 1, 194, 139, 117, 244, 137, 215, 137, 217, 138, 38, 70, 227, 42, 138, 7, 71, 73, 48, 224, 117, 246, 137, 250, 137, 203, 139, 77, 248, 227, 15, 138, 38, 138, 7, 70, 71, 48, 224, 117, 217, 73, 227, 2, 235, 241, 139, 69, 16, 3, 69, 252, 41, 216, 235, 3, 131, 200, 255, 95, 94, 90, 91, 131, 196, 12, 137, 236, 93, 194, 16, 0 })
    返回 (0)

.子程序 字节集_取指针, 整数型, 公开
    .参数 字节集, 字节集

    返回 (lstrcpyn_字节集 (字节集, 字节集, 0))

.子程序 字节集_子替换, 字节集, 公开, 将所有的预被替换的字节集用 用作替换的子字节集 替换
    .参数 欲被替换的字节集, 字节集
    .参数 欲被替换的子字节集, 字节集
    .参数 用作替换的子字节集, 字节集, 可空, 可以被省略。如果省略本参数，则默认为空字节集。
    .参数 进行替换的起始位置, 整数型, 可空, 可以被省略。被替换子字节集的起始搜索位置。如果省略，默认从1开始。
    .参数 替换进行的次数, 整数型, 可空, 可以被省略。对子字节集进行替换的次数。如果省略，默认进行所有可能的替换。
    .局部变量 offset, 整数型, , "0"
    .局部变量 count, 整数型
    .局部变量 i, 整数型
    .局部变量 ret, 字节集
    .局部变量 len, 整数型
    .局部变量 偏移, 整数型

    .如果真 (欲被替换的字节集 ＝ { } 或 欲被替换的子字节集 ＝ { })
        返回 ({ })
    .如果真结束
    .如果真 (是否为空 (进行替换的起始位置))
        进行替换的起始位置 ＝ 1
    .如果真结束
    count ＝ 字节集_寻找子 (欲被替换的字节集, 欲被替换的子字节集, offset, 进行替换的起始位置)
    .如果真 (count ＝ 0)
        返回 (欲被替换的字节集)
    .如果真结束
    .如果真 (是否为空 (替换进行的次数) 或 替换进行的次数 ＞ count)
        替换进行的次数 ＝ count
    .如果真结束
    ret ＝ 欲被替换的字节集
    len ＝ 字节集_取长度 (欲被替换的子字节集)
    
    偏移 ＝ 字节集_取长度 (用作替换的子字节集) － len
    
    .计次循环首 (替换进行的次数, i)
        .如果 (i ＝ 1)
            ret ＝ 字节集_替换 (ret, offset [i], len, 用作替换的子字节集)
        .否则
            ret ＝ 字节集_替换 (ret, offset [i] ＋ 偏移 × (i － 1), len, 用作替换的子字节集)
        .如果结束
        
    .计次循环尾 ()
    返回 (ret)

.子程序 字节集_寻找子, 整数型, 公开, 查找所有欲寻找的字节集 ，将每个位置放入 结果数组
    .参数 被搜寻的字节集, 字节集
    .参数 欲寻找的字节集, 字节集
    .参数 结果数组, 整数型, 数组
    .参数 开始偏移, 整数型, 可空
    .局部变量 i
    .局部变量 j
    .局部变量 len_y

    清除数组 (结果数组)
    i ＝ 1
    .如果 (是否为空 (开始偏移))
        j ＝ 1
    .否则
        j ＝ 开始偏移
    .如果结束
    len_y ＝ 字节集_取长度 (欲寻找的字节集)
    .判断循环首 (i ≠ -1)
        i ＝ 字节集_寻找 (被搜寻的字节集, 欲寻找的字节集, j)
        .如果真 (i ≠ -1)
            加入成员 (结果数组, i)
            j ＝ i ＋ len_y
        .如果真结束
        
    .判断循环尾 ()
    返回 (取数组成员数 (结果数组))

