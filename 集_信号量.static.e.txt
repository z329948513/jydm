.版本 2

.程序集 集_信号量
.子程序 信号量_创建, 整数型, 公开, 创建一个信号量。成功返回信号量句柄，失败返回0 。
    .参数 初始数值, 整数型, 可空
    .参数 最大数值, 整数型, 可空, 如果留空 默认为1
    .参数 标识, 文本型, 可空

    返回 (CreateSemaphore (0, 初始数值, 选择 (是否为空 (最大数值) ＝ 真, 1, 最大数值), 标识))

.子程序 信号量_打开, 整数型, 公开, 打开一个信号量。成功返回信号量句柄，失败返回0 。
    .参数 标识, 文本型, 可空

    返回 (OpenSemaphore (2031619, 真, 标识))

.子程序 信号量_销毁, 逻辑型, 公开, 销毁指定信号量。
    .参数 句柄, 整数型

    .如果真 (句柄 ＝ 0)
        返回 (假)
    .如果真结束
    返回 (CloseHandle (句柄))

.子程序 信号量_增加, 逻辑型, 公开, 成功返回真，失败返回假。
    .参数 句柄, 整数型
    .参数 递增数值, 整数型, 可空, 留空为1 要增加的值，这个值必须大于0。
    .参数 返回递增前的值, 整数型, 参考 可空, 返回信号量上次的值（被增加前的值）。

    .如果真 (句柄 ＝ 0)
        返回 (假)
    .如果真结束
    .如果真 (是否为空 (递增数值) ＝ 真)
        递增数值 ＝ 1
    .如果真结束
    返回 (ReleaseSemaphore (句柄, 递增数值, 返回递增前的值))

.子程序 信号量_递减, 逻辑型, 公开, 成功返回真，失败返回假。
    .参数 句柄, 整数型
    .参数 超时值, 整数型, 可空
    .局部变量 ret, 整数型

    .如果真 (句柄 ＝ 0)
        返回 (假)
    .如果真结束
    ret ＝ WaitForSingleObject (句柄, 选择 (是否为空 (超时值) ＝ 真, -1, 超时值))
    返回 (ret ＝ 0)
    

