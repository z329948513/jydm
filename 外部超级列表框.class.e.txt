.版本 2

.程序集 外部超级列表框, , 公开
.程序集变量 集_目标句柄, 整数型
.程序集变量 集_表头句柄, 整数型
.程序集变量 集_外部控件, 逻辑型
.程序集变量 集_外部控件操作, 外部控件操作
.程序集变量 集_进程句柄, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    
    ' 连续赋值 (250, 临时_项目.项目文本长度, 临时_列.缓冲区长度, 集_外部列.缓冲区长度)

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    集_外部控件操作.结束操作 (集_进程句柄)

.子程序 初始化, 逻辑型, 公开, 调用此类必须初始化此命令，否则无法调用后面的命令
    .参数 超级列表框句柄, 整数型
    .参数 是否为外部进程的超级列表框, 逻辑型, 可空, 可以被省略。当为真时，可对这个外部进程一超级列表框进行操作。默认为假。
    .参数 超级列表框类名, 文本型, 可空, 默认为空  SYSLISTVIEW32 

    .如果真 (是否为空 (超级列表框类名))
        超级列表框类名 ＝ “SysListView32”
    .如果真结束
    .判断开始 (窗口_取类名J (超级列表框句柄) ＝ 超级列表框类名) ' SysListView32
        集_外部控件 ＝ 是否为外部进程的超级列表框
        集_目标句柄 ＝ 超级列表框句柄
        集_进程句柄 ＝ 集_外部控件操作.打开进程 (集_目标句柄) ' 全部子程序共用
        返回 (真)
    .默认
        返回 (假)
    .判断结束
    

.子程序 取标题, 文本型, 公开, 取指定表项或子项的标题。
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .参数 列索引, 整数型, 可空, 可以被省略。用作指定报表型列表框中项目所处的列，索引值从 0 开始，如果当前列表框类型不是报表型列表框，提供 0 值即可。如果本参数被省略，默认值为 0 。
    .参数 缓冲尺寸, 整数型, 可空, 可以被省略
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 局_缓冲区, 文本型
    .局部变量 hMem, 整数型
    .局部变量 text, 文本型

    缓冲尺寸 ＝ 选择 (是否为空 (缓冲尺寸), 260, 缓冲尺寸)
    .判断开始 (集_外部控件)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 缓冲尺寸)
        局部_外部项目.项目文本 ＝ 临时_数据地址2
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.子项目 ＝ 列索引
        局部_外部项目.掩码 ＝ 1
        局部_外部项目.项目文本长度 ＝ 缓冲尺寸
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        .判断开始 (SendMessageA (集_目标句柄, 4101, 0, 临时_数据地址1) ≠ 0)
            局_缓冲区 ＝ 到文本 (集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址2, 缓冲尺寸))
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            
            返回 (局_缓冲区)
        .默认
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            
            返回 (“”)
        .判断结束
        
    .默认
        hMem ＝ HeapAlloc (GetProcessHeap (), 8, 缓冲尺寸)
        局部_外部项目.子项目 ＝ 列索引
        局部_外部项目.项目文本长度 ＝ 缓冲尺寸
        局部_外部项目.项目文本 ＝ hMem
        SendMessageA (集_目标句柄, 4141, 表项索引, 取数据_通用型 (局部_外部项目)) ' #LVM_GETITEMTEXT=4141
        text ＝ 指针到文本 (hMem)
        HeapFree (GetProcessHeap (), 8, hMem)
        返回 (text)
    .判断结束
    

.子程序 取图片, 整数型, 公开
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .参数 列索引, 整数型, 可空, 可以被省略。用作指定报表型列表框中项目所处的列，索引值从 0 开始，如果当前列表框类型不是报表型列表框，提供 0 值即可。如果本参数被省略，默认值为 0 。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局_缓冲区, 字节集
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.子项目 ＝ 列索引
        局部_外部项目.掩码 ＝ 2
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4101, 0, 临时_数据地址1)
        局_缓冲区 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 40)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        写到内存 (局_缓冲区, 临时_项目地址, 40)
        返回 (局部_外部项目.图片索引)
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.子项目 ＝ 列索引
    临时_项目.掩码 ＝ 2
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4101, 0, 临时_项目地址)
    返回 (临时_项目.图片索引)

.子程序 取当前状态图片, 整数型, 公开, 取回指定表项的当前状态图片索引，图片索引指向状态图片组属性中的某一图片，从 0 开始，-1 表示无图片。如果“是否有检查框”属性为真，返回值是当前检查框的选中状态，1表示被选中，0表示未选中。
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。

    返回 (SendMessageA (集_目标句柄, #LVM_GETITEMSTATE, 表项索引, -4096) ÷ 4096 － 1)

.子程序 取缩进数目, 整数型, 公开
    .参数 表项索引, 整数型, , 。用于指定列表框中的某一项目，索引值从 0 开始。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局_缓冲区, 字节集
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.掩码 ＝ 16
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4101, 0, 临时_数据地址1)
        局_缓冲区 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 40)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
        写到内存 (局_缓冲区, 临时_项目地址, 40)
        返回 (局部_外部项目.缩进)
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.掩码 ＝ 16
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4101, 0, 临时_项目地址)
    返回 (临时_项目.缩进)

.子程序 取表项数, 整数型, 公开, 返回列表框中所有表项的数目
    返回 (SendMessageA (集_目标句柄, #LVM_GETITEMCOUNT, 0, 0))

.子程序 取表项数值, 整数型, 公开
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 局_缓冲区, 字节集
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.掩码 ＝ 4
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4101, 0, 临时_数据地址1)
        局_缓冲区 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 40)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        写到内存 (局_缓冲区, 临时_项目地址, 40)
        返回 (局部_外部项目.项目数值)
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.掩码 ＝ 4
    ' 集_单一项目.pszText ＝ 取空白文本 (260)
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4101, 0, 临时_项目地址)
    返回 (临时_项目.项目数值)

.子程序 置标题, , 公开, 设置指定表项或子项的标题。
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .参数 列索引, 整数型, 可空, 可以被省略。用作指定报表型列表框中项目所处的列，索引值从 0 开始，如果当前列表框类型不是报表型列表框，提供 0 值即可。如果本参数被省略，默认值为 0 。
    .参数 标题, 文本型, 可空, 可以被省略。本参数指定表项或子项的标题文本。如果被省略，默认值为空文本。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 局_项目文本长度, 整数型
    .局部变量 局_缓冲区, 整数型
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        局_项目文本长度 ＝ 取字节集长度 (到字节集 (标题)) ＋ 1
        
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 局_项目文本长度)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.子项目 ＝ 列索引
        局_缓冲区 ＝ lstrcpyn_文本型 (标题, 标题, 0)
        局部_外部项目.项目文本 ＝ 临时_数据地址2
        局部_外部项目.项目文本长度 ＝ 局_项目文本长度
        局部_外部项目.掩码 ＝ 1
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_缓冲区, 局_项目文本长度)
        SendMessageA (集_目标句柄, 4102, 0, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
        
        返回 ()
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.子项目 ＝ 列索引
    临时_项目.掩码 ＝ 1
    临时_项目.项目文本 ＝ 标题
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4102, 0, 临时_项目地址)

.子程序 置图片, , 公开
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .参数 列索引, 整数型, 可空, 可以被省略。用作指定报表型列表框中项目所处的列，索引值从 0 开始，如果当前列表框类型不是报表型列表框，提供 0 值即可。如果本参数被省略，默认值为 0 。
    .参数 图片索引, 整数型, 可空, 可以被省略。图片索引用于指定“图片组”属性中的某张图片。索引值从 0 开始，-1 表示无图片。如果本参数被省略，默认值为 -1 。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.子项目 ＝ 列索引
        局部_外部项目.掩码 ＝ 2
        局部_外部项目.图片索引 ＝ 图片索引
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4102, 0, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 ()
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.子项目 ＝ 列索引
    临时_项目.掩码 ＝ 2
    临时_项目.图片索引 ＝ 图片索引
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4102, 0, 临时_项目地址)

.子程序 置状态图片, , 公开
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .参数 状态图片索引, 整数型, 可空, 可以被省略。状态图片索引用于指定“状态图片组”属性中的某张图片。索引值从 0 开始，-1 表示无图片。如果本参数被省略，默认值为 -1 。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (是否为空 (状态图片索引))
        状态图片索引 ＝ -1
    .如果真结束
    .如果真 (集_外部控件)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.掩码 ＝ 8
        局部_外部项目.状态 ＝ (状态图片索引 ＋ 1) × 4096
        局部_外部项目.状态掩码 ＝ -4096
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4102, 0, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 ()
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.掩码 ＝ 8
    临时_项目.状态 ＝ (状态图片索引 ＋ 1) × 4096
    临时_项目.状态掩码 ＝ -4096
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4139, 表项索引, 临时_项目地址)

.子程序 置缩进数目, , 公开, 设置指定表项的缩进数目。
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .参数 缩进数目, 整数型, 可空, 可以被省略。“缩进数目”仅在报表型列表框才有效，用作指定显示时在表项首部增加的空白数。如果本参数被省略，默认值为 0 。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.掩码 ＝ 16
        局部_外部项目.缩进 ＝ 缩进数目
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4102, 0, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 ()
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.掩码 ＝ 16
    临时_项目.缩进 ＝ 缩进数目
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4102, 0, 临时_项目地址)

.子程序 置表项数值, , 公开, 设置与指定表项相关联的数值
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .参数 表项数值, 整数型, 可空, 可以被省略。该数值与指定表项相关联。如果本参数被省略，默认值为 0 。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.掩码 ＝ 4
        局部_外部项目.项目数值 ＝ 表项数值
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4102, 0, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 ()
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.掩码 ＝ 4
    临时_项目.项目数值 ＝ 表项数值
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, 4102, 0, 临时_项目地址)

.子程序 取被选择表项, 变体型, 公开, 返回一个整数数组，內含所有当前被选择表项的位置索引。如果当前没有被选择表项，返回空数组。
    .局部变量 局_选中项索引, 变体型
    .局部变量 局_成员数, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_选中项索引A, 整数型, , "0"

    局_选中项索引.置类型 (#变体类型.数值型数组)
    局_成员数 ＝ 取表项数 ()
    .变量循环首 (1, 局_成员数, 1, 局_计次)
        .如果真 (是否被选择 (局_计次 － 1))
            加入成员 (局_选中项索引A, 局_计次 － 1)
        .如果真结束
        
    .变量循环尾 ()
    局_选中项索引.赋值 (局_选中项索引A, )
    返回 (局_选中项索引)

.子程序 取被选中表项, 变体型, 公开, 如果“是否有检查框”属性为真，返回当前所有检查框被选中的表项位置索引
    .局部变量 局_选中项索引, 变体型
    .局部变量 局_成员数, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_选中项索引A, 整数型, , "0"

    局_选中项索引.置类型 (#变体类型.数值型数组)
    局_成员数 ＝ 取表项数 ()
    .变量循环首 (1, 局_成员数, 1, 局_计次)
        .如果真 (取当前状态图片 (局_计次 － 1) ≠ 0)
            加入成员 (局_选中项索引A, 局_计次 － 1)
        .如果真结束
        
    .变量循环尾 ()
    局_选中项索引.赋值 (局_选中项索引A, )
    返回 (局_选中项索引)

.子程序 取已选择表项数, 整数型, 公开, 返回当前已被选择表项的数目
    返回 (SendMessageA (集_目标句柄, #LVM_GETSELECTEDCOUNT, 0, 0))

.子程序 是否被选择, 逻辑型, 公开, 判断指定表项是否已经被选择，如已被选择，返回真，否则返回假。
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。

    返回 (SendMessageA (集_目标句柄, #LVM_GETITEMSTATE, 表项索引, 2) ＝ 2)

.子程序 选择表项, , 公开
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        ' '集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.掩码 ＝ 8
        局部_外部项目.状态 ＝ 2
        局部_外部项目.状态掩码 ＝ 2
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4139, 表项索引, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 ()
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.掩码 ＝ 8
    临时_项目.状态 ＝ 2
    临时_项目.状态掩码 ＝ 2
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, #LVM_SETITEMSTATE, 表项索引, 临时_项目地址)

.子程序 取消表项选择, , 公开, 取消对指定表项的选择。
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (集_外部控件)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        局部_外部项目.表项索引 ＝ 表项索引
        局部_外部项目.掩码 ＝ 8
        局部_外部项目.状态 ＝ 位取反 (2)
        局部_外部项目.状态掩码 ＝ 2
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        SendMessageA (集_目标句柄, 4102, 表项索引, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 ()
    .如果真结束
    临时_项目.项目 ＝ 表项索引
    临时_项目.掩码 ＝ 8
    临时_项目.状态 ＝ 位取反 (2)
    临时_项目.状态掩码 ＝ 2
    临时_项目地址 ＝ 取数据_通用型 (临时_项目)
    SendMessageA (集_目标句柄, #LVM_SETITEMSTATE, 表项索引, 临时_项目地址)

.子程序 插入表项, 整数型, 公开
    .参数 插入位置, 整数型, 可空, 可以被省略。指定新表项插入时的位置索引，索引值从 0 开始。如果提供 -1 ，则插入到列表框的尾部。如果本参数被省略，默认值为 -1 。
    .参数 标题, 文本型, 可空, 可以被省略。本参数指定表项的标题文本。如果被省略，默认值为空文本。
    .参数 图片索引, 整数型, 可空, 可以被省略。图片索引用于指定“图片组”属性中的某张图片。索引值从 0 开始，-1 表示无图片。如果本参数被省略，默认值为 -1 。
    .参数 状态图片索引, 整数型, 可空, 可以被省略。状态图片索引用于指定“状态图片组”属性中的某张图片。索引值从 0 开始，-1 表示无图片。如果本参数被省略，默认值为 -1 。
    .参数 缩进数目, 整数型, 可空, 可以被省略。“缩进数目”仅在报表型列表框才有效，用作指定显示时在表项首部增加的空白数。如果本参数被省略，默认值为 0 。
    .参数 表项数值, 整数型, 可空, 可以被省略。该数值与指定表项相关联。如果本参数被省略，默认值为 0 。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局_数值, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    .如果真 (是否为空 (图片索引))
        图片索引 ＝ -1
    .如果真结束
    .如果真 (是否为空 (状态图片索引))
        状态图片索引 ＝ -1
    .如果真结束
    .判断开始 (集_外部控件)
        .判断开始 (是否为空 (插入位置) 或 插入位置 ＝ -1)
            局部_外部项目.表项索引 ＝ 取表项数 ()
        .默认
            局部_外部项目.表项索引 ＝ 插入位置
        .判断结束
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 取字节集长度 (到字节集 (标题)) ＋ 1)
        局部_外部项目.掩码 ＝ 4103 ＋ 8
        局部_外部项目.状态 ＝ 位取反 (2)
        局部_外部项目.项目文本 ＝ 临时_数据地址2
        局部_外部项目.项目文本长度 ＝ 取字节集长度 (到字节集 (标题)) ＋ 1
        
        局部_外部项目.图片索引 ＝ 图片索引
        局部_外部项目.状态 ＝ (状态图片索引 ＋ 1) × 4096
        局部_外部项目.状态掩码 ＝ -4096
        局部_外部项目.缩进 ＝ 缩进数目
        局部_外部项目.项目数值 ＝ 表项数值
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        局_数值 ＝ lstrcpyn_文本型 (标题, 标题, 0)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_数值, 取字节集长度 (到字节集 (标题)) ＋ 1)
        局_数值 ＝ SendMessageA (集_目标句柄, 4103, 0, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 (局_数值)
    .默认
        .判断开始 (是否为空 (插入位置) 或 插入位置 ＝ -1)
            临时_项目.项目 ＝ 取表项数 ()
        .默认
            临时_项目.项目 ＝ 插入位置
        .判断结束
        临时_项目.掩码 ＝ 4103 ＋ 8
        临时_项目.项目文本 ＝ 标题
        临时_项目.项目文本长度 ＝ 取字节集长度 (到字节集 (标题)) ＋ 1
        临时_项目.图片索引 ＝ 图片索引
        临时_项目.状态 ＝ (状态图片索引 ＋ 1) × 4096
        临时_项目.状态掩码 ＝ -4096
        临时_项目.缩进 ＝ 缩进数目
        临时_项目.项目数值 ＝ 表项数值
        临时_项目地址 ＝ 取数据_通用型 (临时_项目)
        返回 (SendMessageA (集_目标句柄, 4103, 0, 临时_项目地址))
    .判断结束
    

.子程序 高速插入表项, , 公开
    .参数 插入位置, 整数型, 可空, 可以被省略。指定新表项插入时的位置索引，索引值从 0 开始。如果提供 -1 ，则插入到列表框的尾部。如果本参数被省略，默认值为 -1 。
    .参数 列表框信息, 列表框信息, 数组
    .参数 排序方式, 整数型, 可空, 可以被省略。指定新新插入数据的排序方式。默认为“#排序方式_不排序”。如要逆向排序，请将常量位取反。如要对指定列排序，请用#排序方式_表项列标题排序+要排序的列索引
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型

    .局部变量 局_信息1, 列表框信息
    .局部变量 局_信息2, 列表框信息, , "0"
    .局部变量 局_信息3, 整数型
    .局部变量 局_计次1, 整数型
    .局部变量 局_成员数, 整数型
    .局部变量 局_数据列, 文本型, , "0"
    .局部变量 局_当前索引, 整数型
    .局部变量 局_计次2, 整数型
    .局部变量 局_列图片成员数, 整数型
    .局部变量 局_列标题成员数, 整数型
    .局部变量 局_列成员数, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_列, 超级列表框7
    .局部变量 临时_项目, 超级列表框0

    局_信息2 ＝ 列表框信息
    快速排序 (局_信息2, , , 排序方式)
    局_成员数 ＝ 取数组成员数 (局_信息2)
    SendMessageA (集_目标句柄, 11, 0, 0)
    .判断开始 (集_外部控件)
        .判断开始 (是否为空 (插入位置) 或 插入位置 ＝ -1)
            局部_外部项目.表项索引 ＝ 取表项数 ()
        .默认
            局部_外部项目.表项索引 ＝ 插入位置
        .判断结束
        局部_外部项目.掩码 ＝ 4103 ＋ 8
        局部_外部项目.状态掩码 ＝ -4096
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        .判断循环首 (局_计次1 ＜ 局_成员数)
            局部_外部项目.表项索引 ＝ 插入位置 ＋ 局_计次1
            局_计次1 ＝ 局_计次1 ＋ 1
            局部_外部项目.子项目 ＝ 0
            临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 取文本长度 (局_信息2 [局_计次1].标题) ＋ 1)
            局部_外部项目.项目文本 ＝ 临时_数据地址2
            局部_外部项目.状态 ＝ (局_信息2 [局_计次1].状态图片索引 ＋ 1) × 4096
            局部_外部项目.图片索引 ＝ 局_信息2 [局_计次1].图片索引
            局部_外部项目.缩进 ＝ 局_信息2 [局_计次1].缩进数目
            局部_外部项目.项目数值 ＝ 局_信息2 [局_计次1].表项数值
            局部_外部项目.项目文本长度 ＝ 取字节集长度 (到字节集 (局_信息2 [局_计次1].标题)) ＋ 1
            局_信息3 ＝ lstrcpyn_文本型 (局_信息2 [局_计次1].标题, 局_信息2 [局_计次1].标题, 0)
            集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
            集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_信息3, 取文本长度 (局_信息2 [局_计次1].标题) ＋ 1)
            局_当前索引 ＝ SendMessageA (集_目标句柄, 4103, 0, 临时_数据地址1)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
            .如果真 (局_当前索引 ≠ -1)
                局_列图片成员数 ＝ 取数组成员数 (局_信息2 [局_计次1].列图片索引)
                局_列标题成员数 ＝ 取数组成员数 (局_信息2 [局_计次1].列标题)
                局_列成员数 ＝ 选择 (局_列标题成员数 ＞ 局_列图片成员数, 局_列标题成员数, 局_列图片成员数)
                .判断循环首 (局_计次2 ＜ 局_列成员数)
                    局_计次2 ＝ 局_计次2 ＋ 1
                    局部_外部项目.子项目 ＝ 局_计次2
                    .判断开始 (局_计次2 ≤ 局_列标题成员数)
                        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 取文本长度 (局_信息2 [局_计次1].列标题 [局_计次2]) ＋ 1)
                        局_信息3 ＝ lstrcpyn_文本型 (局_信息2 [局_计次1].列标题 [局_计次2], 局_信息2 [局_计次1].列标题 [局_计次2], 0)
                        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_信息3, 取文本长度 (局_信息2 [局_计次1].列标题 [局_计次2]) ＋ 1)
                        局部_外部项目.项目文本 ＝ 临时_数据地址2 ' TEMP2 [I].列标题 [I2]
                        局部_外部项目.项目文本长度 ＝ 取文本长度 (局_信息2 [局_计次1].列标题 [局_计次2]) ＋ 1
                        
                        局部_外部项目.掩码 ＝ 1
                    .默认
                        局部_外部项目.项目文本 ＝ 0
                        局部_外部项目.掩码 ＝ 0
                    .判断结束
                    .判断开始 (局_计次2 ≤ 局_列图片成员数)
                        局部_外部项目.图片索引 ＝ 局_信息2 [局_计次1].列图片索引 [局_计次2]
                        局部_外部项目.掩码 ＝ 位或 (局部_外部项目.掩码, 2)
                    .默认
                        局部_外部项目.图片索引 ＝ 0
                    .判断结束
                    集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
                    SendMessageA (集_目标句柄, 4102, 0, 临时_数据地址1)
                    .如果真 (局部_外部项目.项目文本 ≠ 0)
                        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
                    .如果真结束
                    
                .判断循环尾 ()
                局_计次2 ＝ 0
                局部_外部项目.掩码 ＝ 4103 ＋ 8
                局部_外部项目.状态掩码 ＝ -4096
            .如果真结束
            
        .判断循环尾 ()
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
    .默认
        
        .判断开始 (是否为空 (插入位置) 或 插入位置 ＝ -1)
            临时_项目.项目 ＝ 取表项数 ()
        .默认
            临时_项目.项目 ＝ 插入位置
        .判断结束
        临时_项目.掩码 ＝ 4103 ＋ 8
        临时_项目.状态掩码 ＝ -4096
        临时_项目地址 ＝ 取数据_通用型 (临时_项目)
        .判断循环首 (局_计次1 ＜ 局_成员数)
            临时_项目.项目 ＝ 插入位置 ＋ 局_计次1
            局_计次1 ＝ 局_计次1 ＋ 1
            临时_项目.子项目 ＝ 0
            临时_项目.项目文本 ＝ 局_信息2 [局_计次1].标题
            临时_项目.状态 ＝ (局_信息2 [局_计次1].状态图片索引 ＋ 1) × 4096
            临时_项目.图片索引 ＝ 局_信息2 [局_计次1].图片索引
            临时_项目.缩进 ＝ 局_信息2 [局_计次1].缩进数目
            临时_项目.项目数值 ＝ 局_信息2 [局_计次1].表项数值
            局_当前索引 ＝ SendMessageA (集_目标句柄, 4103, 0, 临时_项目地址)
            .如果真 (局_当前索引 ≠ -1)
                局_列图片成员数 ＝ 取数组成员数 (局_信息2 [局_计次1].列图片索引)
                局_列标题成员数 ＝ 取数组成员数 (局_信息2 [局_计次1].列标题)
                局_列成员数 ＝ 选择 (局_列标题成员数 ＞ 局_列图片成员数, 局_列标题成员数, 局_列图片成员数)
                .判断循环首 (局_计次2 ＜ 局_列成员数)
                    局_计次2 ＝ 局_计次2 ＋ 1
                    临时_项目.子项目 ＝ 局_计次2
                    .判断开始 (局_计次2 ≤ 局_列标题成员数)
                        临时_项目.项目文本 ＝ 局_信息2 [局_计次1].列标题 [局_计次2]
                        临时_项目.掩码 ＝ 1
                    .默认
                        临时_项目.项目文本 ＝ “”
                        临时_项目.掩码 ＝ 0
                    .判断结束
                    .判断开始 (局_计次2 ≤ 局_列图片成员数)
                        临时_项目.图片索引 ＝ 局_信息2 [局_计次1].列图片索引 [局_计次2]
                        临时_项目.掩码 ＝ 位或 (临时_项目.掩码, 2)
                    .默认
                        临时_项目.图片索引 ＝ 0
                    .判断结束
                    SendMessageA (集_目标句柄, 4102, 0, 临时_项目地址)
                .判断循环尾 ()
                局_计次2 ＝ 0
                临时_项目.掩码 ＝ 4103 ＋ 8
                临时_项目.状态掩码 ＝ -4096
            .如果真结束
            
        .判断循环尾 ()
    .判断结束
    SendMessageA (集_目标句柄, 11, 1, 0)

.子程序 快速排序
    .参数 列表框信息, 列表框信息, 数组
    .参数 左边, 整数型, 可空
    .参数 右边, 整数型, 可空
    .参数 排序方式, 整数型
    .局部变量 局_基准点, 整数型
    .局部变量 局_基准值, 列表框信息
    .局部变量 局_计次1, 整数型
    .局部变量 局_计次2, 整数型
    .局部变量 局_列排序, 整数型

    .如果真 (排序方式 ＝ 0 或 排序方式 ＝ -1)
        返回 ()
    .如果真结束
    .如果真 (是否为空 (左边))
        左边 ＝ 1
    .如果真结束
    .如果真 (是否为空 (右边))
        右边 ＝ 取数组成员数 (列表框信息)
    .如果真结束
    .如果真 (左边 ≥ 右边)
        返回 ()
    .如果真结束
    局_计次1 ＝ 左边
    局_计次2 ＝ 右边
    局_基准值 ＝ 列表框信息 [左边]
    .判断开始 (排序方式 ＞ 0)
        .判断开始 (位与 (排序方式, 2048) ≠ 2048 或 位异或 (排序方式, 2048) ＝ 0)
            ' 项目排序
            .判断开始 (排序方式 ＝ 1 或 位异或 (排序方式, 2048) ＝ 0)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].标题 ≥ 局_基准值.标题) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].标题 ≤ 局_基准值.标题) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .判断 (排序方式 ＝ 2)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].图片索引 ≥ 局_基准值.图片索引) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].图片索引 ≤ 局_基准值.图片索引) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .判断 (排序方式 ＝ 3)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].状态图片索引 ≥ 局_基准值.状态图片索引) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].状态图片索引 ≤ 局_基准值.状态图片索引) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .判断 (排序方式 ＝ 4)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].表项数值 ≥ 局_基准值.表项数值) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].表项数值 ≤ 局_基准值.表项数值) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .默认
                
            .判断结束
            
        .默认
            ' 列排序
            局_列排序 ＝ 位异或 (排序方式, 2048)
            ' 取得要排序的列索引数
            .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].列标题 [局_列排序] ≥ 局_基准值.列标题 [局_列排序]) ' 向左侧寻找小于基准值的元素
                    局_计次2 ＝ 局_计次2 － 1
                .判断循环尾 ()
                .如果真 (局_计次1 ＜ 局_计次2)
                    列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                    局_计次1 ＝ 局_计次1 ＋ 1
                .如果真结束
                .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].列标题 [局_列排序] ≤ 局_基准值.列标题 [局_列排序]) ' 向右侧寻找大于基准值的元素
                    局_计次1 ＝ 局_计次1 ＋ 1
                .判断循环尾 ()
                .如果真 (局_计次1 ＜ 局_计次2)
                    列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                    局_计次2 ＝ 局_计次2 － 1
                .如果真结束
                
            .判断循环尾 ()
            
        .判断结束
        
    .默认
        ' 排序方式 ＝ 位取反(排序方式)
        ' 逆排方式
        .判断开始 (位与 (位取反 (排序方式), 2048) ≠ 2048 或 位异或 (位取反 (排序方式), 2048) ＝ 0)
            ' 项目排序
            .判断开始 (位取反 (排序方式) ＝ 1 或 位异或 (位取反 (排序方式), 2048) ＝ 0)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].标题 ≤ 局_基准值.标题) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].标题 ≥ 局_基准值.标题) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .判断 (位取反 (排序方式) ＝ 2)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].图片索引 ≤ 局_基准值.图片索引) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].图片索引 ≥ 局_基准值.图片索引) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .判断 (位取反 (排序方式) ＝ 3)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].状态图片索引 ≤ 局_基准值.状态图片索引) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].状态图片索引 ≥ 局_基准值.状态图片索引) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .判断 (位取反 (排序方式) ＝ 4)
                .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].表项数值 ≤ 局_基准值.表项数值) ' 向左侧寻找小于基准值的元素
                        局_计次2 ＝ 局_计次2 － 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .如果真结束
                    .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].表项数值 ≥ 局_基准值.表项数值) ' 向右侧寻找大于基准值的元素
                        局_计次1 ＝ 局_计次1 ＋ 1
                    .判断循环尾 ()
                    .如果真 (局_计次1 ＜ 局_计次2)
                        列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                        局_计次2 ＝ 局_计次2 － 1
                    .如果真结束
                    
                .判断循环尾 ()
            .默认
                
            .判断结束
            
        .默认
            ' 列排序
            局_列排序 ＝ 位异或 (位取反 (排序方式), 2048)
            ' 取得要排序的列索引数
            .如果真 (局_列排序 ＞ 取数组成员数 (列表框信息 [局_计次2].列标题))
                返回 ()
            .如果真结束
            .判断循环首 (局_计次1 ＜ 局_计次2) ' 循环直至左右指针相遇
                .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次2].列标题 [局_列排序] ≤ 局_基准值.列标题 [局_列排序]) ' 向左侧寻找小于基准值的元素
                    局_计次2 ＝ 局_计次2 － 1
                .判断循环尾 ()
                .如果真 (局_计次1 ＜ 局_计次2)
                    列表框信息 [局_计次1] ＝ 列表框信息 [局_计次2] ' 交换i和j的值
                    局_计次1 ＝ 局_计次1 ＋ 1
                .如果真结束
                .判断循环首 (局_计次1 ＜ 局_计次2 且 列表框信息 [局_计次1].列标题 [局_列排序] ≥ 局_基准值.列标题 [局_列排序]) ' 向右侧寻找大于基准值的元素
                    局_计次1 ＝ 局_计次1 ＋ 1
                .判断循环尾 ()
                .如果真 (局_计次1 ＜ 局_计次2)
                    列表框信息 [局_计次2] ＝ 列表框信息 [局_计次1] ' 交换i和j的值
                    局_计次2 ＝ 局_计次2 － 1
                .如果真结束
                
            .判断循环尾 ()
            
        .判断结束
        
    .判断结束
    列表框信息 [局_计次1] ＝ 局_基准值 ' 前面把基准值弄丢了，这里补回来。
    快速排序 (列表框信息, 左边, 局_计次1 － 1, 排序方式)
    快速排序 (列表框信息, 局_计次1 ＋ 1, 右边, 排序方式)

.子程序 删除表项, , 公开
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。

    SendMessageA (集_目标句柄, #LVM_DELETEITEM, 表项索引, 0)

.子程序 全部删除, , 公开
    SendMessageA (集_目标句柄, #LVM_DELETEALLITEMS, 0, 0)

.子程序 查找表项, 整数型, 公开
    .参数 标题, 文本型, , 本参数指定欲查找表项的标题文本。
    .参数 起始查找位置, 整数型, 可空, 可以被省略。本参数指定查找表项的起始位置。如果本参数被省略，默认值为 0  ，即从首项开始查找。
    .参数 是否精确匹配, 逻辑型, 可空, 可以被省略。本参数指定匹配标题文本时是否为精确匹配，如果本参数为假，则只要表项标题首部包含指定文本即认为匹配成功。如果本参数被省略，默认值为真。
    .参数 如果查找不到是否从头开始, 逻辑型, 可空, 可以被省略。本参数指定在查找标题文本时，如果从“起始查找位置”查找不到，是否从头开始查找。默认值为假。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 局_结构1, 超级列表框3
    .局部变量 局_结构2, 超级列表框4
    .局部变量 局_数值, 整数型

    .如果真 (集_外部控件)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 20)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 取文本长度 (标题) ＋ 1)
        局_结构2.flags ＝ 位或 (选择 (是否精确匹配, 2, 8), 选择 (如果查找不到是否从头开始 ＝ 真, 32, 0), 1)
        局_结构2.LPCTSTR ＝ 临时_数据地址2
        临时_项目地址 ＝ 取数据_通用型 (局_结构2)
        局_数值 ＝ lstrcpyn_文本型 (标题, 标题, 0)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 20)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_数值, 取文本长度 (标题) ＋ 1)
        局_数值 ＝ SendMessageA (集_目标句柄, 4109, 起始查找位置, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
        返回 (局_数值)
    .如果真结束
    
    局_结构1.flags ＝ 位或 (选择 (是否精确匹配, 2, 8), 选择 (如果查找不到是否从头开始 ＝ 真, 32, 0))
    局_结构1.LPCTSTR ＝ 标题
    临时_项目地址 ＝ 取数据_通用型 (局_结构1)
    返回 (SendMessageA (集_目标句柄, 4109, 起始查找位置, 临时_项目地址))

.子程序 查找表项数值, 整数型, 公开
    .参数 起始查找位置, 整数型, 可空, 可以被省略。本参数指定查找表项的起始位置。如果本参数被省略，默认值为 0  ，即从首项开始查找。
    .参数 表项数值, 整数型, 可空, 可以被省略。该数值与指定表项相关联。如果本参数被省略，默认值为 0 。
    .参数 如果查找不到是否从头开始, 逻辑型, 可空, 可以被省略。本参数指定在查找标题文本时，如果从“起始查找位置”查找不到，是否从头开始查找。默认值为假。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 局_结构1, 超级列表框3
    .局部变量 局_结构2, 超级列表框4
    .局部变量 局_数值, 整数型

    .如果真 (集_外部控件)
        
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 20)
        局_结构2.flags ＝ 位或 (1, 选择 (如果查找不到是否从头开始 ＝ 真, 32, 0))
        局_结构2.lParam ＝ 表项数值
        临时_项目地址 ＝ 取数据_通用型 (局_结构2)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 20)
        局_数值 ＝ SendMessageA (集_目标句柄, 4109, 起始查找位置, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        
        返回 (局_数值)
        
    .如果真结束
    局_结构1.flags ＝ 位或 (1, 选择 (如果查找不到是否从头开始 ＝ 真, 32, 0))
    局_结构1.lParam ＝ 表项数值
    临时_项目地址 ＝ 取数据_通用型 (局_结构1)
    返回 (SendMessageA (集_目标句柄, 4109, 起始查找位置, 临时_项目地址))

.子程序 保证显示, , 公开
    .参数 表项索引, 整数型

    SendMessageA (集_目标句柄, #LVM_ENSUREVISIBLE, 表项索引, 1)

.子程序 排列图标, , 公开, 自动排列大图标或小图标列表框中的图标。
    SendMessageA (集_目标句柄, #LVM_ARRANGE, 0, 0)

.子程序 取列标题, 文本型, 公开, 返回报表型列表框中指定列的标题。
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 临时_列, 超级列表框7
    .局部变量 局_数据, 字节集


    .如果真 (集_外部控件)
        临时_外部列.mask ＝ 4 ' LVCF_TEXT    表示获取列的信息。
        
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 260)
        临时_外部列.列标题 ＝ 临时_数据地址2
        临时_外部列.缓冲区长度 ＝ 260
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        ' LVM_GETCOLUMN=4121    ’表示获取列信息
        .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_数据地址1) ≠ 0)
            局_数据 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址2, 260)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
            
            返回 (到文本 (局_数据))
        .默认
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
            返回 (“”)
        .判断结束
        
    .如果真结束
    临时_列.mask ＝ 4
    临时_列.列标题 ＝ 取空白文本 (260)
    临时_列.缓冲区长度 ＝ 260
    临时_项目地址 ＝ 取数据_通用型 (临时_列)
    .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_项目地址) ≠ 0)
        返回 (临时_列.列标题)
    .默认
        返回 (“”)
    .判断结束
    

.子程序 取列对齐方式, 整数型, 公开, 取回报表型列表框中指定列的对齐方式。返回值为以下常量之一： 0：0； 1：1； 2：2；失败返回-1。
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 局_数据, 字节集
    .局部变量 临时_列, 超级列表框7

    .如果真 (集_外部控件)
        临时_外部列.mask ＝ 1
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_数据地址1) ≠ 0)
            局_数据 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 32)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            ' 外部控件操作.结束操作 (集_进程句柄)
            写到内存 (局_数据, 临时_项目地址, 32)
            .判断开始 (位与 (临时_外部列.对齐方式, 2) ＝ 2)
                返回 (1)
            .判断 (位与 (临时_外部列.对齐方式, 1) ＝ 1)
                返回 (2)
            .默认
                返回 (0)
            .判断结束
            
        .默认
            返回 (-1)
        .判断结束
        
    .如果真结束
    临时_列.mask ＝ 1
    临时_项目地址 ＝ 取数据_通用型 (临时_列)
    .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_项目地址) ≠ 0)
        .判断开始 (位与 (临时_列.对齐方式, 2) ＝ 2)
            返回 (1)
        .判断 (位与 (临时_列.对齐方式, 1) ＝ 1)
            返回 (2)
        .默认
            返回 (0)
        .判断结束
        
    .默认
        返回 (-1)
    .判断结束
    

.子程序 取列宽, 整数型, 公开, 返回报表型列表框中指定列的宽度，如指定列不存在，返回 0 。
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 局_数据, 字节集
    .局部变量 临时_列, 超级列表框7

    .如果真 (集_外部控件)
        临时_外部列.mask ＝ 2
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_数据地址1) ≠ 0)
            局_数据 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 32)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            ' 外部控件操作.结束操作 (集_进程句柄)
            写到内存 (局_数据, 临时_项目地址, 32)
            返回 (临时_外部列.列宽度)
        .默认
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            ' 外部控件操作.结束操作 (集_进程句柄)
            返回 (0)
        .判断结束
        
    .如果真结束
    临时_列.mask ＝ 2
    临时_项目地址 ＝ 取数据_通用型 (临时_列)
    .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_项目地址) ≠ 0)
        返回 (临时_列.列宽度)
    .默认
        返回 (0)
    .判断结束
    

.子程序 取列图片, 整数型, 公开, 返回报表型列表框中指定列的图片索引
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 局_数据, 字节集
    .局部变量 临时_列, 超级列表框7

    .如果真 (集_外部控件)
        临时_外部列.mask ＝ 16 ＋ 1
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_数据地址1) ≠ 0)
            局_数据 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 32)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            ' 外部控件操作.结束操作 (集_进程句柄)
            写到内存 (局_数据, 临时_项目地址, 32)
            返回 (临时_外部列.列图标)
        .默认
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            ' 外部控件操作.结束操作 (集_进程句柄)
            返回 (-1)
        .判断结束
        
    .如果真结束
    临时_列.mask ＝ 16 ＋ 1
    临时_项目地址 ＝ 取数据_通用型 (临时_列)
    .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_项目地址) ≠ 0)
        返回 (临时_列.列图标)
    .默认
        返回 (-1)
    .判断结束
    

.子程序 图片是否居右, 逻辑型, 公开, 如果报表型列表框中指定列的图片居右显示，返回真，否则返回假
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 局_结构, 字节集
    .局部变量 临时_列, 超级列表框7

    .如果真 (集_外部控件)
        临时_外部列.mask ＝ 16 ＋ 1
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_数据地址1) ≠ 0)
            局_结构 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 32)
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            ' 外部控件操作.结束操作 (集_进程句柄)
            写到内存 (局_结构, 临时_项目地址, 32)
            返回 (位与 (临时_外部列.对齐方式, 4096) ＝ 4096)
        .默认
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
            ' 外部控件操作.结束操作 (集_进程句柄)
            返回 (假)
        .判断结束
        
    .如果真结束
    临时_列.mask ＝ 16 ＋ 1
    临时_项目地址 ＝ 取数据_通用型 (临时_列)
    .判断开始 (SendMessageA (集_目标句柄, 4121, 列索引, 临时_项目地址) ≠ 0)
        返回 (位与 (临时_列.对齐方式, 4096) ＝ 4096)
    .默认
        返回 (假)
    .判断结束
    

.子程序 置列标题, 逻辑型, 公开, 设置报表型列表框中指定列的标题
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .参数 标题, 文本型, 可空, 可以被省略。本参数指定报表列的标题文本。如果被省略，默认值为空文本。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 局_数值, 整数型
    .局部变量 临时_列, 超级列表框7
    .局部变量 局部_返回值, 整数型

    .判断开始 (集_外部控件)
        临时_外部列.mask ＝ 4
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 取文本长度 (标题) ＋ 1)
        临时_外部列.列标题 ＝ 临时_数据地址2
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        局_数值 ＝ lstrcpyn_文本型 (标题, 标题, 0)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_数值, 取文本长度 (标题) ＋ 1)
        局部_返回值 ＝ SendMessageA (集_目标句柄, 4122, 列索引, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
        ' 外部控件操作.结束操作 (集_进程句柄)
    .默认
        临时_列.mask ＝ 4
        临时_列.列标题 ＝ 标题
        临时_项目地址 ＝ 取数据_通用型 (临时_列)
        局部_返回值 ＝ SendMessageA (集_目标句柄, 4122, 列索引, 临时_项目地址)
    .判断结束
    返回 (局部_返回值 ≠ 0)

.子程序 置列宽, , 公开, 设置报表型列表框中指定列的宽度
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .参数 列宽, 整数型, 可空, 可以被省略。本参数指定报表列的宽度，单位为像素。如果参数值提供为 -1 ，则根据列数据最大宽度自动调整列尺寸，如果参数值提供为 -2 ，则根据列标题宽度自动调整列尺寸。如果本参数被省略，默认值为 -1 。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 临时_列, 超级列表框7

    .判断开始 (集_外部控件)
        临时_外部列.mask ＝ 2
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_外部列.列宽度 ＝ 列宽
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        SendMessageA (集_目标句柄, 4122, 列索引, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
    .默认
        临时_列.mask ＝ 2
        临时_列.列宽度 ＝ 列宽
        临时_项目地址 ＝ 取数据_通用型 (临时_列)
        SendMessageA (集_目标句柄, 4122, 列索引, 临时_项目地址)
    .判断结束
    

.子程序 置列图片, , 公开, 设置报表型列表框中指定列的标志图片。
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。
    .参数 图片索引, 整数型, 可空, 可以被省略。图片索引用于指定“表头图片组”属性中的某张图片。索引值从 0 开始，-1 表示无图片。如果本参数被省略，默认值为 -1 。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 临时_列, 超级列表框7

    .判断开始 (集_外部控件)
        临时_外部列.mask ＝ 16
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_外部列.列图标 ＝ 图片索引
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        SendMessageA (集_目标句柄, 4122, 列索引, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
    .默认
        临时_列.mask ＝ 16
        临时_列.列图标 ＝ 图片索引
        临时_项目地址 ＝ 取数据_通用型 (临时_列)
        SendMessageA (集_目标句柄, 4122, 列索引, 临时_项目地址)
    .判断结束
    

.子程序 取列数, 整数型, 公开, 返回报表型列表框中列的总数
    集_表头句柄 ＝ SendMessageA (集_目标句柄, #LVM_GETHEADER, 0, 0)
    返回 (SendMessageA (集_表头句柄, 4608, 0, 0))

.子程序 插入列, 整数型, 公开, 在报表型列表框中指定位置插入新的列，成功返回新列的位置索引，失败返回 -1 。
    .参数 插入位置, 整数型, 可空, 可以被省略。指定新列插入时的位置索引，索引值从 0 开始。如果提供 -1 ，则插入到尾部。如果本参数被省略，默认值为 -1 。
    .参数 标题, 文本型, 可空, 可以被省略。本参数指定报表列的标题文本。如果被省略，默认值为空文本。
    .参数 列宽, 整数型, 可空, 可以被省略。本参数指定报表列的宽度，单位为像素。如果被省略，默认值为 50 。
    .参数 对齐方式, 整数型, 可空, 可以被省略。参数可以为以下常量之一： 0：0； 1：1； 2：2。如果本参数被省略，默认值为“0”。
    .参数 图片索引, 整数型, 可空, 可以被省略。图片索引用于指定“表头图片组”属性中的某张图片。索引值从 0 开始，-1 表示无图片。如果本参数被省略，默认值为 -1 。
    .参数 图片是否居右, 逻辑型, 可空, 可以被省略。指定图片是否显示在右边，如果本参数被省略，默认值为假。
    .局部变量 临时_项目地址, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 临时_外部列, 超级列表框8
    .局部变量 局_数值, 整数型
    .局部变量 临时_列, 超级列表框7

    .如果真 (是否为空 (插入位置) 或 插入位置 ＝ -1)
        插入位置 ＝ 取列数 ()
    .如果真结束
    .判断开始 (集_外部控件)
        临时_外部列.mask ＝ 16 ＋ 1 ＋ 2 ＋ 4
        ' 集_单一列.pszText ＝ 取空白文本 (256)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 32)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 取文本长度 (标题) ＋ 1)
        临时_外部列.列标题 ＝ 临时_数据地址2
        .判断开始 (是否为空 (列宽))
            临时_外部列.列宽度 ＝ 50
        .默认
            临时_外部列.列宽度 ＝ 列宽
        .判断结束
        .判断开始 (是否为空 (对齐方式) 或 对齐方式 ＝ 0)
            临时_外部列.对齐方式 ＝ 0
        .判断 (对齐方式 ＝ 2)
            临时_外部列.对齐方式 ＝ 1
        .判断 (对齐方式 ＝ 1)
            临时_外部列.对齐方式 ＝ 2
        .默认
            
        .判断结束
        .判断开始 (是否为空 (图片索引))
            临时_外部列.列图标 ＝ -1
        .默认
            临时_外部列.列图标 ＝ 图片索引
        .判断结束
        .如果真 (图片是否居右)
            临时_外部列.对齐方式 ＝ 位或 (临时_外部列.对齐方式, 4096)
        .如果真结束
        临时_项目地址 ＝ 取数据_通用型 (临时_外部列)
        局_数值 ＝ lstrcpyn_文本型 (标题, 标题, 0)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 32)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_数值, 取文本长度 (标题) ＋ 1)
        局_数值 ＝ SendMessageA (集_目标句柄, 4123, 插入位置, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 (局_数值)
    .默认
        临时_列.mask ＝ 16 ＋ 1 ＋ 2 ＋ 4
        临时_列.列标题 ＝ 标题
        .判断开始 (是否为空 (列宽))
            临时_列.列宽度 ＝ 50
        .默认
            临时_列.列宽度 ＝ 列宽
        .判断结束
        .判断开始 (是否为空 (对齐方式) 或 对齐方式 ＝ 0)
            临时_列.对齐方式 ＝ 0
        .判断 (对齐方式 ＝ 2)
            临时_列.对齐方式 ＝ 1
        .判断 (对齐方式 ＝ 1)
            临时_列.对齐方式 ＝ 2
        .默认
            
        .判断结束
        .判断开始 (是否为空 (图片索引))
            临时_列.列图标 ＝ -1
        .默认
            临时_列.列图标 ＝ 图片索引
        .判断结束
        .如果真 (图片是否居右)
            临时_列.对齐方式 ＝ 位或 (临时_列.对齐方式, 4096)
        .如果真结束
        
        临时_项目地址 ＝ 取数据_通用型 (临时_列)
        返回 (SendMessageA (集_目标句柄, 4123, 插入位置, 临时_项目地址))
        
    .判断结束
    
    

.子程序 删除列, , 公开, 删除报表型列表框中指定位置处的列。
    .参数 列索引, 整数型, , 用作指定报表型列表框中项目所处的列，索引值从 0 开始。

    SendMessageA (集_目标句柄, #LVM_DELETECOLUMN, 列索引, 0)

.子程序 进入编辑, , 公开, 让指定表项进入编辑状态，执行本命令成功的前提是“允许编辑”属性为真。
    .参数 表项索引, 整数型, , 用于指定列表框中的某一项目，索引值从 0 开始。

    .判断开始 (集_外部控件)
        SetActiveWindow (GetWindow (集_目标句柄, 4))
        SetFocus (集_目标句柄)
    .默认
        SetFocus (集_目标句柄)
    .判断结束
    SendMessageA (集_目标句柄, 4119, 表项索引, 0)

.子程序 读超级列表框项目数据, , 公开, 取出全部的项目数据
    .参数 列表框信息, 列表框信息, 参考 可空 数组
    .局部变量 局_项目数, 整数型
    .局部变量 局_计次, 整数型

    局_项目数 ＝ 取表项数 ()
    重定义数组 (列表框信息, 假, 局_项目数)
    局_项目数 ＝ 局_项目数 － 1
    .变量循环首 (0, 局_项目数, 1, 局_计次)
        列表框信息 [局_计次 ＋ 1] ＝ 取项目数据 (局_计次)
    .变量循环尾 ()

.子程序 取项目数据, 列表框信息, , 取指定项目的数据
    .参数 项目索引, 整数型
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局_结构, 列表框信息
    .局部变量 局_列数, 整数型
    .局部变量 局_数据, 字节集
    .局部变量 局_计次, 整数型
    .局部变量 局部_外部项目, 超级列表框1
    .局部变量 临时_项目, 超级列表框0

    局_列数 ＝ 取列数 () － 1
    重定义数组 (局_结构.列标题, 假, 局_列数)
    重定义数组 (局_结构.列图片索引, 假, 局_列数)
    .判断开始 (集_外部控件)
        局部_外部项目.表项索引 ＝ 项目索引
        局部_外部项目.掩码 ＝ 1 ＋ 2 ＋ 4 ＋ 16 ＋ 8
        局部_外部项目.状态掩码 ＝ -4096
        临时_项目地址 ＝ 取数据_通用型 (局部_外部项目)
        局部_外部项目.子项目 ＝ 0
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 40)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 260)
        局部_外部项目.项目文本 ＝ 临时_数据地址2
        局部_外部项目.项目文本长度 ＝ 255
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
        .如果真 (SendMessageA (集_目标句柄, 4101, 0, 临时_数据地址1) ≠ 0)
            局_数据 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 40)
            写到内存 (局_数据, 临时_项目地址, 40)
            局_结构.标题 ＝ 到文本 (集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址2, 260))
            集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
            局_结构.图片索引 ＝ 局部_外部项目.图片索引
            局_结构.表项数值 ＝ 局部_外部项目.项目数值
            局_结构.缩进数目 ＝ 局部_外部项目.缩进
            局_结构.状态图片索引 ＝ 局部_外部项目.状态 ÷ 4096 － 1
            局部_外部项目.掩码 ＝ 1 ＋ 2
            .变量循环首 (1, 局_列数, 1, 局_计次)
                局部_外部项目.子项目 ＝ 局_计次
                临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 260)
                局部_外部项目.项目文本 ＝ 临时_数据地址2
                局部_外部项目.项目文本长度 ＝ 255
                
                集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 40)
                .如果真 (SendMessageA (集_目标句柄, 4101, 0, 临时_数据地址1) ≠ 0)
                    局_数据 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址1, 40)
                    写到内存 (局_数据, 临时_项目地址, 40)
                    局_结构.列标题 [局_计次] ＝ 到文本 (集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址2, 260))
                    局_结构.列图片索引 [局_计次] ＝ 局部_外部项目.图片索引
                .如果真结束
                集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
            .变量循环尾 ()
        .如果真结束
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        ' 外部控件操作.结束操作 (集_进程句柄)
    .默认
        临时_项目.项目 ＝ 项目索引
        临时_项目.掩码 ＝ 1 ＋ 2 ＋ 4 ＋ 16 ＋ 8
        临时_项目.状态掩码 ＝ -4096
        临时_项目地址 ＝ 取数据_通用型 (临时_项目)
        临时_项目.子项目 ＝ 0
        临时_项目.项目文本 ＝ 取空白文本 (260)
        .如果真 (SendMessageA (集_目标句柄, 4101, 0, 临时_项目地址) ≠ 0)
            局_结构.标题 ＝ 临时_项目.项目文本
            局_结构.图片索引 ＝ 临时_项目.图片索引
            局_结构.表项数值 ＝ 临时_项目.项目数值
            局_结构.缩进数目 ＝ 临时_项目.缩进
            局_结构.状态图片索引 ＝ 临时_项目.状态 ÷ 4096 － 1
            临时_项目.掩码 ＝ 1 ＋ 2
            .变量循环首 (1, 局_列数, 1, 局_计次)
                临时_项目.子项目 ＝ 局_计次
                临时_项目.项目文本 ＝ 取空白文本 (260)
                .如果真 (SendMessageA (集_目标句柄, 4101, 0, 临时_项目地址) ≠ 0)
                    局_结构.列标题 [局_计次] ＝ 临时_项目.项目文本
                    局_结构.列图片索引 [局_计次] ＝ 临时_项目.图片索引
                .如果真结束
                
            .变量循环尾 ()
        .如果真结束
        
    .判断结束
    返回 (局_结构)

.子程序 取文本颜色, 整数型, 公开
    返回 (SendMessageA (集_目标句柄, #LVM_GETTEXTCOLOR, 0, 0))

.子程序 置文本颜色, , 公开
    .参数 文本颜色, 整数型

    SendMessageA (集_目标句柄, #LVM_SETTEXTCOLOR, 0, 文本颜色)
    刷新显示 ()

.子程序 刷新显示, , 公开
    SendMessageA (集_目标句柄, #LVM_REDRAWITEMS, 取第一项索引 (), 取可显示数量 ())

.子程序 取第一项索引, 整数型, 公开, 取在当前超级列表框中显示的第一条项目的索引。失败返回-1
    返回 (SendMessageA (集_目标句柄, #LVM_GETTOPINDEX, 0, 0))

.子程序 取可显示数量, 整数型, 公开, 取当前在超级列表框的用户区中大约可以显示的项目数量
    返回 (SendMessageA (集_目标句柄, #LVM_GETCOUNTPERPAGE, 0, 0))

.子程序 取文本背景颜色, 整数型, 公开
    返回 (SendMessageA (集_目标句柄, #LVM_GETTEXTBKCOLOR, 0, 0))

.子程序 置文本背景颜色, , 公开
    .参数 背景颜色, 整数型

    SendMessageA (集_目标句柄, #LVM_SETTEXTBKCOLOR, 0, 背景颜色)
    刷新显示 ()

.子程序 取背景颜色, 整数型, 公开
    返回 (SendMessageA (集_目标句柄, #LVM_GETBKCOLOR, 0, 0))

.子程序 置背景颜色, , 公开
    .参数 背景颜色, 整数型

    SendMessageA (集_目标句柄, #LVM_SETBKCOLOR, 0, 背景颜色)
    刷新显示 ()

.子程序 取类型, 整数型, 公开
    .局部变量 局_类型, 整数型

    局_类型 ＝ SendMessageA (集_目标句柄, #LVM_GETVIEW, 0, 0)
    .判断开始 (局_类型 ＝ 0) ' 0_大图标列表
        返回 (0)
    .判断 (局_类型 ＝ 1) ' 1_报表列表框
        返回 (3)
    .判断 (局_类型 ＝ 2) ' 2_小图标列表
        返回 (1)
    .判断 (局_类型 ＝ 3) ' 3_普通列表框
        返回 (2)
    .判断 (局_类型 ＝ 4)
        返回 (4)
    .默认
        返回 (-1)
    .判断结束
    

.子程序 置类型, , 公开
    .参数 类型, 整数型
    .局部变量 局_类型, 整数型

    .判断开始 (类型 ＝ 0) ' 0_大图标列表
        局_类型 ＝ 0
    .判断 (类型 ＝ 3) ' 1_报表列表框
        局_类型 ＝ 1
    .判断 (类型 ＝ 1) ' 2_小图标列表
        局_类型 ＝ 2
    .判断 (类型 ＝ 2) ' 3_普通列表框
        局_类型 ＝ 3
    .判断 (类型 ＝ 4)
        局_类型 ＝ 4
    .默认
        
    .判断结束
    SendMessageA (集_目标句柄, #LVM_SETVIEW, 局_类型, 0)

.子程序 取背景图片, 文本型, 公开, 返回图片文件名路径
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局_结构1, 超级列表框5
    .局部变量 局_结构2, 超级列表框6
    .局部变量 局_结构3, 字节集

    .判断开始 (集_外部控件)
        局_结构2.ulFlags ＝ 2
        局_结构2.cchImageMax ＝ 256
        临时_项目地址 ＝ 取数据_通用型 (局_结构2)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 24)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 256)
        局_结构2.pszImage ＝ 临时_数据地址2
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 24)
        SendMessageA (集_目标句柄, 4165, 0, 临时_数据地址1)
        局_结构3 ＝ 集_外部控件操作.读外部指针数据 (集_进程句柄, 临时_数据地址2, 256)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
        ' 外部控件操作.结束操作 (集_进程句柄)
        返回 (到文本 (局_结构3))
    .默认
        局_结构1.ulFlags ＝ 2
        局_结构1.图片路径 ＝ 取空白文本 (256)
        局_结构1.最大长度 ＝ 256
        临时_项目地址 ＝ 取数据_通用型 (局_结构1)
        SendMessageA (集_目标句柄, 4165, 0, 临时_项目地址)
        返回 (局_结构1.图片路径)
    .判断结束
    

.子程序 置背景图片, , 公开, 设置背景图片
    .参数 图片文件名, 文本型
    .参数 是否平铺, 逻辑型, 可空, 可以省略。默认为真。
    .局部变量 临时_数据地址1, 整数型
    .局部变量 临时_数据地址2, 整数型
    .局部变量 临时_项目地址, 整数型
    .局部变量 局_结构1, 超级列表框5
    .局部变量 局_结构2, 超级列表框6
    .局部变量 局_结构3, 整数型

    置文本背景颜色 (-1)
    .判断开始 (集_外部控件)
        局_结构2.ulFlags ＝ 2
        局_结构2.cchImageMax ＝ 取文本长度 (图片文件名) ＋ 1
        局_结构2.xOffsetPercent ＝ 100
        局_结构2.yOffsetPercent ＝ 100
        .如果真 (是否平铺 或 是否为空 (是否平铺))
            局_结构2.ulFlags ＝ 位或 (局_结构2.ulFlags, 16)
        .如果真结束
        临时_项目地址 ＝ 取数据_通用型 (局_结构2)
        ' 集_进程句柄 ＝ 外部控件操作.打开进程 (集_目标句柄)
        临时_数据地址1 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 24)
        临时_数据地址2 ＝ 集_外部控件操作.分配外部指针 (集_进程句柄, 取文本长度 (图片文件名) ＋ 1)
        局_结构2.pszImage ＝ 临时_数据地址2
        局_结构3 ＝ lstrcpyn_文本型 (图片文件名, 图片文件名, 0)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址1, 临时_项目地址, 24)
        集_外部控件操作.写进程内存 (集_进程句柄, 临时_数据地址2, 局_结构3, 取文本长度 (图片文件名) ＋ 1)
        SendMessageA (集_目标句柄, 4164, 0, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址1)
        集_外部控件操作.销毁外部指针 (集_进程句柄, 临时_数据地址2)
        ' 外部控件操作.结束操作 (集_进程句柄)
    .默认
        局_结构1.ulFlags ＝ 2
        局_结构1.图片路径 ＝ 图片文件名
        局_结构1.最大长度 ＝ 取文本长度 (图片文件名) ＋ 1
        局_结构1.xOffsetPercent ＝ 100
        局_结构1.yOffsetPercent ＝ 100
        .如果真 (是否平铺 或 是否为空 (是否平铺))
            局_结构1.ulFlags ＝ 位或 (局_结构1.ulFlags, 16)
        .如果真结束
        临时_项目地址 ＝ 取数据_通用型 (局_结构1)
        SendMessageA (集_目标句柄, 4164, 0, 临时_项目地址)
    .判断结束
    

.子程序 取鼠标指针所在的索引, 整数型, 公开, 如果鼠标指针在超级列表框范围内，取当前其指向的项目索引
    返回 (SendMessageA (集_目标句柄, #LVM_GETHOTITEM, 0, 0))

.子程序 取项目行距, 整数型, 公开
    返回 (SendMessageA (集_目标句柄, #LVM_GETITEMSPACING, 0, 0))

.子程序 取编辑框句柄, 整数型, 公开, 当列表框在进行编辑时，返回这个编辑框的句柄
    返回 (SendMessageA (集_目标句柄, #LVM_GETEDITCONTROL, 0, 0))

.子程序 取表项间距, , 公开
    .参数 水平间距, 整数型, 参考, 用于返回表项的水平间距
    .参数 垂直间距, 整数型, 参考, 用于返回表项的垂直间距
    .参数 列表框类型, 逻辑型, 可空, 可以被省略。当为真时取回小图标列表框的项目间距。当为假时取回大图标列表框的项目间距。默认为假。
    .局部变量 局_数据, 字节集

    局_数据 ＝ 到字节集 (SendMessageA (集_目标句柄, 4147, 到数值 (列表框类型), 0))
    水平间距 ＝ 取字节集数据 (局_数据, #短整数型, 1)
    垂直间距 ＝ 取字节集数据 (局_数据, #短整数型, 3)

.子程序 列排序, , 公开, 当超级列表框的类型是报表列表框时才能使用。使用本方法时，请将超级列表框的“排序方式”置为“0.不排序”
    .参数 列索引, 整数型, , 要排序的列索引，从0计起。
    .参数 排序方式, 逻辑型, 可空, 可以被省略。当为真时为正向排序，当为假时为逆向排序。默认为真，正向排序。
    .局部变量 局_列表框信息, 列表框信息, , "0"

    读超级列表框项目数据 (局_列表框信息)
    全部删除 ()
    .判断开始 (是否为空 (排序方式) 或 排序方式 ＝ 真)
        高速插入表项 (0, 局_列表框信息, 2048 ＋ 列索引)
    .默认
        高速插入表项 (0, 局_列表框信息, 位取反 (2048 ＋ 列索引))
    .判断结束
    

.子程序 单一选择, 逻辑型, 公开
    .参数 是否单一选择, 逻辑型, 可空, 可以被省略。当被省略时，只返回当前的单一选择设置状态，而不改变其状态。
    .局部变量 局_数值, 整数型

    局_数值 ＝ GetWindowLongA (集_目标句柄, -16)
    .如果真 (是否为空 (是否单一选择))
        返回 (位与 (局_数值, 4) ＝ 4)
    .如果真结束
    .判断开始 (位与 (局_数值, 4) ＝ 4 且 是否单一选择 ＝ 假)
        局_数值 ＝ 位异或 (局_数值, 4)
        SetWindowLongA (集_目标句柄, -16, 局_数值)
    .默认
        局_数值 ＝ 位或 (局_数值, 4)
        SetWindowLongA (集_目标句柄, -16, 局_数值)
    .判断结束
    局_数值 ＝ GetWindowLongA (集_目标句柄, -16)
    返回 (位与 (局_数值, 4) ＝ 4)

.子程序 取现行选中项, 整数型, 公开
    返回 (SendMessageA (集_目标句柄, #LVM_GETNEXTITEM, -1, 2))
    .判断开始 (单一选择 ())
        返回 (SendMessageA (集_目标句柄, #LVM_GETNEXTITEM, -1, 2))
    .默认
        返回 (-1)
    .判断结束
    

.子程序 置现行选中项, , 公开
    .参数 现行选中项, 整数型

    .如果真 (单一选择 (真))
        选择表项 (现行选中项)
    .如果真结束
    

.子程序 导出Excel, 逻辑型, 公开, 把超级列表框内容导出为Excel表格文件，成功返回真，失败返回假。注：只能导出报表列表框。
    .参数 保存文件, 文本型, , 保存到目标文件，后缀名一般为“xls”。注：如果文件已存在则会重写改文件，如果不能打开文件则返回假。
    .参数 保留表头, 逻辑型, 可空, 是否把超级列表框表头一起导出到表格文件，如果不需要导出表头，请留空此值或者赋值“假”。
    .局部变量 hFile, 整数型
    .局部变量 列数, 整数型
    .局部变量 行数, 整数型
    .局部变量 列索引, 整数型
    .局部变量 表项索引, 整数型
    .局部变量 局_列标题, 文本型
    .局部变量 局_行标题, 文本型

    列数 ＝ 取列数 ()
    行数 ＝ 取表项数 ()
    .如果真 (行数 ＝ 0 或 列数 ＝ 0)
        返回 (假)
    .如果真结束
    hFile ＝ 打开文件 (保存文件, #重写, #无限制)
    .如果真 (hFile ＝ 0)
        返回 (假)
    .如果真结束
    移到文件首 (hFile)
    写文本行 (hFile, “<table border=” ＋ #引号 ＋ “1” ＋ #引号 ＋ “>”)
    .如果真 (保留表头 ＝ 真 且 列数 ＞ 0)
        写文本行 (hFile, “  <tr>”)
        列索引 ＝ 0
        .计次循环首 (列数, )
            局_列标题 ＝ 取列标题 (列索引)
            写文本行 (hFile, “    <th>” ＋ 局_列标题 ＋ “</th>”)
            列索引 ＝ 列索引 ＋ 1
        .计次循环尾 ()
        写文本行 (hFile, “  </tr>”)
    .如果真结束
    .如果真 (行数 ＞ 0)
        表项索引 ＝ 0
        .计次循环首 (行数, )
            写文本行 (hFile, “  <tr>”)
            列索引 ＝ 0
            .计次循环首 (列数, )
                局_行标题 ＝ 取标题 (表项索引, 列索引, 1024)
                写文本行 (hFile, “    <td>” ＋ 局_行标题 ＋ “</td>”)
                列索引 ＝ 列索引 ＋ 1
            .计次循环尾 ()
            写文本行 (hFile, “  </tr>”)
            表项索引 ＝ 表项索引 ＋ 1
        .计次循环尾 ()
    .如果真结束
    写文本行 (hFile, “</table>”)
    关闭文件 (hFile)
    返回 (真)

.子程序 _置外部操作, , 公开, 如果是外部控件，就要用这个方法初始化
    集_外部控件 ＝ 真

.子程序 _销毁外部操作, , 公开, 如果不再需要对外部控件进行初始化，可用这个销毁
    集_外部控件 ＝ 假

.子程序 取表项位置, 整数型, 公开, 取出表项在列表框中的坐标位置
    .参数 索引, 整数型
    .参数 点, POINT, 参考, 用于装载坐标的变量
    .局部变量 _POINT, 整数型
    .局部变量 返回值, 整数型

    .如果 (集_外部控件)
        _POINT ＝ VirtualAllocEx (集_进程句柄, 0, 8, #MEM_COMMIT, #PAGE_READWRITE)
        返回值 ＝ SendMessageA (集_目标句柄, #LVM_GETITEMPOSITION, 索引, _POINT)
        ReadProcessMemory_整数型1 (集_进程句柄, _POINT, 取数据_通用型 (点), 8, 0)
        VirtualFreeEx (集_进程句柄, _POINT, 0, #MEM_RELEASE)
    .否则
        返回值 ＝ SendMessageA (集_目标句柄, #LVM_GETITEMPOSITION, 索引, 取数据_通用型 (点))
    .如果结束
    返回 (返回值)

