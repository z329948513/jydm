.版本 2

.程序集 集_音乐音量, , , 该类仅支持同线程下使用 
.子程序 音乐_时长, 整数型, 公开, 获取音乐文件时长，获取总毫秒数
    .参数 文件路径, 文本型, , 音乐文件的完整路径
    .局部变量 总时间, 文本型

    总时间 ＝ 取空白文本 (255)
    mciSendStringA (“open ” ＋ 文件路径, “0&”, 0, 0)
    mciSendStringA (“status ” ＋ 文件路径 ＋ “ length”, 总时间, 128, 0) ' 取总时间
    返回 (到整数 (总时间))

.子程序 音乐_进度, 整数型, 公开, 播放后用线程或者时钟 实时获取当前播放长度
    .参数 标识, 文本型
    .局部变量 进度, 文本型

    进度 ＝ 取空白文本 (255)
    mciSendStringA (“status ” ＋ 标识 ＋ “ position”, 进度, 256, 0)
    返回 (到整数 (进度))

.子程序 音乐_播放, 逻辑型, 公开, 真=播放成功,假=播放失败。可用于同时播放多首音乐，只要标识不同
    .参数 文件名, 文本型, , 支持格式:mp3,wav,其他格式没有测试..也可以是网络地址
    .参数 标识, 文本型, , 不可以含有空格，用于标识播放了哪个歌曲,可用来控制该音乐。比如:mysong

    mciSendStringA (“close ” ＋ 标识, “0”, 0, 0)
    mciSendStringA (“open ” ＋ #引号 ＋ 文件名 ＋ #引号 ＋ “ alias ” ＋ 标识, “0”, 0, 0)
    返回 (mciSendStringA (“play ” ＋ 标识, “0”, 0, 0) ＝ 0)

.子程序 音乐_暂停, 逻辑型, 公开, 暂时后，可调用 音乐_播放() 继续播放
    .参数 标识, 文本型, , 音乐_播放中使用的标识

    返回 (mciSendStringA (“pause ” ＋ 标识, “0”, 0, 0) ＝ 0)

.子程序 音乐_停止, 逻辑型, 公开, 禁止指定标识正在播放的音乐
    .参数 标识, 文本型, , 音乐_播放中使用的标识，即使播放完毕也记得调用一次

    返回 (mciSendStringA (“close ” ＋ 标识, “0”, 0, 0) ＝ 0)

.子程序 音量_置声道, , 公开, 设置左右声道的音量，从0～100
    .参数 左声道音量, 整数型, , 0～100
    .参数 右声道音量, 整数型, , 0～100
    .局部变量 音量, 整数型
    .局部变量 左, 整数型
    .局部变量 右, 整数型

    左 ＝ (100 － 左声道音量) ÷ 100 × 256
    右 ＝ (100 － 右声道音量) ÷ 100 × 256
    音量 ＝ (257 － 左) × 255 ＋ 16711680 × (257 － 右)
    waveOutSetVolume (0, 音量)

.子程序 音量_取声道, , 公开, 取出左右声道的音量，用参考变量，0～100
    .参数 左声道音量, 整数型, 参考, 装载左声道音量的变量
    .参数 右声道音量, 整数型, 参考, 装载右声道音量的变量
    .局部变量 音量值, 整数型

    .如果真 (waveOutGetVolume (0, 音量值) ＝ 0) ' 成功
        左声道音量 ＝ 位与 (位或 (音量值, 4294901760), 65535) ＼ 256
        左声道音量 ＝ 左声道音量 × 100 ÷ 256
        右声道音量 ＝ 位与 (位或 (音量值, 65535), 4294901760) ÷ 65536 ＼ 256
        右声道音量 ＝ 右声道音量 × 100 ÷ 256
        .如果真 (右声道音量 ＜ 0)
            .如果真 (右声道音量 ＝ -50)
                右声道音量 ＝ 0
            .如果真结束
            右声道音量 ＝ 49 ＋ 取绝对值 (50 ＋ 右声道音量)
        .如果真结束
        
    .如果真结束
    

.子程序 音量_静音, , 公开, 设置静音或解除静音,如果已经静音就解除，否则静音
    .参数 窗口句柄, 整数型, , 可以通过“取窗口句柄（）”获得

    SendMessageA (窗口句柄, 793, 2100912, 524288)

.子程序 音量_增加, , 公开, 控制声卡方式增加音量
    .参数 窗口句柄, 整数型, , 可以通过“取窗口句柄（）”获得

    SendMessageA (窗口句柄, 793, 197266, 655360)

.子程序 音量_减少, , 公开, 控制声卡方式减少音量
    .参数 窗口句柄, 整数型, , 可以通过“取窗口句柄（）”获得

    SendMessageA (窗口句柄, 793, 197266, 589824)
    
    

