.版本 2

.程序集 类_像素组, , 公开
.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    ' 命令收集地址：https://bbs.125.la/forum.php?mod=viewthread&tid=14182970

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 位图_转_像素组, 逻辑型, 公开, 图片宽高小于1才会返回假
    .参数 wt_, 类_位图操作
    .参数 shuj_, 整数型, 数组
    .局部变量 k_, 整数型
    .局部变量 g_, 整数型
    .局部变量 q1, 整数型
    .局部变量 g_s1, 整数型
    .局部变量 y_, 整数型
    .局部变量 k_s1, 整数型
    .局部变量 s1, 整数型

    k_ ＝ wt_.取宽度 ()
    g_ ＝ wt_.取高度 ()
    .如果真 (k_ ＜ 1)
        返回 (假)
    .如果真结束
    
    .如果真 (g_ ＜ 1)
        返回 (假)
    .如果真结束
    
    q1 ＝ k_ × g_
    重定义数组 (shuj_, 假, q1)
    .计次循环首 (g_, g_s1)
        y_ ＝ g_s1 － 1
        .计次循环首 (k_, k_s1)
            s1 ＝ s1 ＋ 1 ' 优化
            shuj_ [s1] ＝ wt_.取某点颜色 (k_s1 － 1, y_)
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (真)

.子程序 像素组_转_位图, 逻辑型, 公开
    .参数 位图_, 类_位图操作, 参考
    .参数 shuj_, 整数型, 数组
    .参数 k_, 整数型
    .参数 g_, 整数型
    .局部变量 q1, 整数型
    .局部变量 k_s1, 整数型
    .局部变量 g_s1, 整数型
    .局部变量 y_, 整数型
    .局部变量 s1, 整数型

    q1 ＝ k_ × g_
    .如果真 (q1 ＜ 1)
        返回 (假)
    .如果真结束
    
    .如果真 (取数组成员数 (shuj_) ＜ q1)
        返回 (假)
    .如果真结束
    位图_.创建空白位图 (k_, g_, 24)
    .计次循环首 (g_, g_s1)
        y_ ＝ g_s1 － 1
        .计次循环首 (k_, k_s1)
            s1 ＝ s1 ＋ 1 ' 优化
            位图_.置某点颜色 (k_s1 － 1, y_, shuj_ [s1])
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (真)

.子程序 像素组_取_某点颜色, 整数型, 公开
    .参数 shuj_, 整数型, 数组
    .参数 k, 整数型
    .参数 x, 整数型
    .参数 y, 整数型
    .局部变量 ls_d, 整数型
    .局部变量 q1, 整数型

    q1 ＝ 取数组成员数 (shuj_)
    .如果真 (q1 ＜ 1)
        返回 (0)
    .如果真结束
    ls_d ＝ (y － 1) × k ＋ x
    
    .如果真 (q1 ≥ ls_d)
        返回 (shuj_ [ls_d])
    .如果真结束
    返回 (0)
    

.子程序 像素组_置_某点颜色, , 公开
    .参数 shuj_, 整数型, 数组
    .参数 k, 整数型
    .参数 x, 整数型
    .参数 y, 整数型
    .参数 置入颜色, 整数型
    .局部变量 ls_d, 整数型
    .局部变量 q1, 整数型

    q1 ＝ 取数组成员数 (shuj_)
    .如果真 (q1 ＜ 1)
        返回 ()
    .如果真结束
    
    ls_d ＝ (y － 1) × k ＋ x
    .如果真 (q1 ≥ ls_d)
        shuj_ [ls_d] ＝ 置入颜色
    .如果真结束
    
    

.子程序 像素组_缩放图片_快速, 逻辑型, 公开, 图片宽高小于1才会返回假
    .参数 shuj_, 整数型, 数组
    .参数 k_, 整数型
    .参数 g_, 整数型
    .参数 缩放后_k, 整数型
    .参数 缩放后_g, 整数型
    .参数 缩放后数据, 整数型, 数组
    .局部变量 q1, 整数型
    .局部变量 q2, 整数型
    .局部变量 x_int, 整数型
    .局部变量 y_int, 整数型
    .局部变量 g_s1, 整数型
    .局部变量 k_s1, 整数型
    .局部变量 shu_1, 整数型
    .局部变量 shu_2, 整数型
    .局部变量 yt_d, 整数型
    .局部变量 sf_d, 整数型

    q1 ＝ k_ × g_
    .如果真 (q1 ＜ 1)
        返回 (假)
    .如果真结束
    
    .如果真 (取数组成员数 (shuj_) ＜ q1)
        返回 (假)
    .如果真结束
    
    q2 ＝ 缩放后_k × 缩放后_g
    .如果真 (q2 ＜ 1)
        返回 (假)
    .如果真结束
    
    重定义数组 (缩放后数据, 假, q2)
    ' ------------------------
    x_int ＝ k_ × 1000 ÷ 缩放后_k
    y_int ＝ g_ × 1000 ÷ 缩放后_g
    
    .计次循环首 (缩放后_g, g_s1)
        .计次循环首 (缩放后_k, k_s1)
            shu_1 ＝ k_s1 × x_int ÷ 1000
            shu_2 ＝ g_s1 × y_int ÷ 1000
            .如果真 (shu_1 ＜ 1)
                shu_1 ＝ 1
            .如果真结束
            
            .如果真 (shu_2 ＜ 1)
                shu_2 ＝ 1
            .如果真结束
            
            yt_d ＝ (shu_2 － 1) × k_ ＋ shu_1
            sf_d ＝ (g_s1 － 1) × 缩放后_k ＋ k_s1
            缩放后数据 [sf_d] ＝ shuj_ [yt_d]
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (真)

.子程序 像素组_缩放图片_无损, 逻辑型, 公开
    .参数 shuj_, 整数型, 数组
    .参数 k_, 整数型
    .参数 g_, 整数型
    .参数 缩放后_k, 整数型
    .参数 缩放后_g, 整数型
    .参数 缩放后数据, 整数型, 数组
    .局部变量 q1, 整数型
    .局部变量 q2, 整数型
    .局部变量 x_int, 整数型
    .局部变量 y_int, 整数型
    .局部变量 xh_x, 整数型
    .局部变量 xh_y, 整数型
    .局部变量 g_s1, 整数型
    .局部变量 k_s1, 整数型
    .局部变量 shu_1, 整数型
    .局部变量 shu_2, 整数型
    .局部变量 N, 整数型
    .局部变量 R, 整数型
    .局部变量 G, 整数型
    .局部变量 B, 整数型
    .局部变量 临时_x, 整数型
    .局部变量 临时_y, 整数型
    .局部变量 临时_颜色, 整数型
    .局部变量 临时_点, 整数型

    q1 ＝ k_ × g_
    .如果真 (q1 ＜ 1)
        返回 (假)
    .如果真结束
    
    .如果真 (取数组成员数 (shuj_) ＜ q1)
        返回 (假)
    .如果真结束
    
    q2 ＝ 缩放后_k × 缩放后_g
    .如果真 (q2 ＜ 1)
        返回 (假)
    .如果真结束
    
    重定义数组 (缩放后数据, 假, q2)
    x_int ＝ k_ × 1000 ÷ 缩放后_k
    y_int ＝ g_ × 1000 ÷ 缩放后_g
    ' ---------------------------
    xh_x ＝ x_int ÷ 1000
    xh_y ＝ y_int ÷ 1000
    ' ----------------------------
    .计次循环首 (缩放后_g, g_s1)
        .计次循环首 (缩放后_k, k_s1)
            shu_1 ＝ x_int × (k_s1 － 1)
            .如果真 (shu_1 ＞ 0)
                shu_1 ＝ shu_1 ÷ 1000
            .如果真结束
            
            .如果真 (shu_1 ＜ 1)
                shu_1 ＝ 0
            .如果真结束
            N ＝ 0
            R ＝ 0
            G ＝ 0
            B ＝ 0
            临时_颜色 ＝ 0
            .计次循环首 (xh_x, )
                shu_1 ＝ shu_1 ＋ 1
                .判断开始 (shu_1 ＜ 1)
                    临时_x ＝ 1
                .默认
                    
                    临时_x ＝ shu_1
                .判断结束
                shu_2 ＝ y_int × (g_s1 － 1)
                .如果真 (shu_2 ＞ 0)
                    shu_2 ＝ shu_2 ÷ 1000
                .如果真结束
                
                .如果真 (shu_2 ＜ 1)
                    shu_2 ＝ 0
                .如果真结束
                
                .计次循环首 (xh_y, )
                    shu_2 ＝ shu_2 ＋ 1
                    .判断开始 (shu_2 ＜ 1)
                        临时_y ＝ 1
                    .默认
                        
                        临时_y ＝ shu_2
                    .判断结束
                    
                    临时_点 ＝ (临时_y － 1) × k_ ＋ 临时_x
                    临时_颜色 ＝ shuj_ [临时_点]
                    加入_RGB (临时_颜色, R, G, B, N)
                .计次循环尾 ()
                临时_y ＝ y_int × g_s1
                .如果真 (临时_y ＞ 0)
                    临时_y ＝ 临时_y ÷ 1000
                .如果真结束
                
                .如果真 (临时_y ＜ 1)
                    临时_y ＝ 1
                .如果真结束
                
                临时_点 ＝ (临时_y － 1) × k_ ＋ 临时_x
                临时_颜色 ＝ shuj_ [临时_点]
                加入_RGB (临时_颜色, R, G, B, N)
            .计次循环尾 ()
            临时_x ＝ x_int × k_s1
            .如果真 (临时_x ＞ 0)
                临时_x ＝ 临时_x ÷ 1000
            .如果真结束
            
            .如果真 (临时_x ＜ 1)
                临时_x ＝ 1
            .如果真结束
            
            shu_2 ＝ y_int × (g_s1 － 1)
            .如果真 (shu_2 ＞ 0)
                shu_2 ＝ shu_2 ÷ 1000
            .如果真结束
            
            .如果真 (shu_2 ＜ 1)
                shu_2 ＝ 0
            .如果真结束
            
            .计次循环首 (xh_y, )
                shu_2 ＝ shu_2 ＋ 1
                .判断开始 (shu_2 ＜ 1)
                    临时_y ＝ 1
                .默认
                    
                    临时_y ＝ shu_2
                .判断结束
                
                临时_点 ＝ (临时_y － 1) × k_ ＋ 临时_x
                临时_颜色 ＝ shuj_ [临时_点]
                加入_RGB (临时_颜色, R, G, B, N)
            .计次循环尾 ()
            临时_y ＝ y_int × g_s1
            .如果真 (临时_y ＞ 0)
                临时_y ＝ 临时_y ÷ 1000
            .如果真结束
            
            .如果真 (临时_y ＜ 1)
                临时_y ＝ 1
            .如果真结束
            
            临时_点 ＝ (临时_y － 1) × k_ ＋ 临时_x
            临时_颜色 ＝ shuj_ [临时_点]
            加入_RGB (临时_颜色, R, G, B, N)
            R ＝ R ÷ N
            G ＝ G ÷ N
            B ＝ B ÷ N
            临时_颜色 ＝ RGB_整数 (R, G, B)
            临时_点 ＝ (g_s1 － 1) × 缩放后_k ＋ k_s1
            缩放后数据 [临时_点] ＝ 临时_颜色
        .计次循环尾 ()
    .计次循环尾 ()
    返回 (真)

