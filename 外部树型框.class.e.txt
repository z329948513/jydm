.版本 2

.程序集 外部树型框, , 公开
.程序集变量 集_句柄组, 整数型, , "0"
.程序集变量 集_父项目句柄组, 整数型, , "0", 有父项目的句柄组
.程序集变量 集_单一项目, TVITEMEX
.程序集变量 集_树型框项目, TVITEMEX2
.程序集变量 集_外部进程句柄, 整数型
.程序集变量 集_外部数据地址1, 整数型
.程序集变量 集_外部数据地址2, 整数型
.程序集变量 集_项目地址, 整数型
.程序集变量 集_树型框句柄, 整数型
.程序集变量 外部操作, 外部控件操作
.程序集变量 集_外部控件, 逻辑型
.程序集变量 _数据地址, 整数型, , , 加入项目1 使用，可避免重复使用一些API函数
.程序集变量 _项目, TVINSERTSTRUCT, , , 加入项目1 使用
.程序集变量 _搜索回调Call, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    集_单一项目.cchTextMax ＝ 260
    集_树型框项目.cchTextMax ＝ 260

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    清除数组 (集_句柄组)

.子程序 初始化, 逻辑型, 公开
    .参数 树型框句柄, 整数型
    .参数 树型框类名, 文本型, 可空
    .参数 是否操作外部控件, 逻辑型, 可空
    .局部变量 项目数, 整数型, 静态

    .如果真 (窗口_句柄是否有效 (树型框句柄) ＝ 假)
        返回 (假)
    .如果真结束
    _数据地址 ＝ 0
    
    ' 相同的不再初始化
    .如果真 (集_树型框句柄 ＝ 树型框句柄 且 项目数 ＝ 取项目数 ())
        返回 (真)
    .如果真结束
    
    .如果真 (是否操作外部控件)
        .如果真 (窗口_句柄是否有效 (集_外部进程句柄))
            外部操作.结束操作 (集_外部进程句柄)
        .如果真结束
        集_外部进程句柄 ＝ 外部操作.打开进程 (树型框句柄)
    .如果真结束
    集_树型框句柄 ＝ 树型框句柄
    .如果真 (是否操作外部控件)
        集_外部控件 ＝ 是否操作外部控件
    .如果真结束
    内部_遍历句柄 ()
    返回 (真)

.子程序 内部_取数值变量地址, 整数型
    .参数 数值变量, 字节型, 参考

    返回 (lstrcpyn_字节型 (数值变量, 数值变量, 0))

.子程序 取项目数, 整数型, 公开, 返回树型框中所有项目的数目。
    返回 (发送消息整数 (集_树型框句柄, 4357, 0, 0))

.子程序 取项目句柄数组, 整数型, 公开
    .参数 句柄组, 整数型, 参考 数组

    句柄组 ＝ 集_句柄组
    返回 (取数组成员数 (句柄组))

.子程序 取缩进层次, 整数型, 公开, 返回指定项目所处缩进层次，层次值从1开始。如果未找到指定项目，返回 0 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 层次
    .局部变量 父项目

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (0)
        
    .默认
        层次 ＝ 1
        父项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 3, 集_句柄组 [项目索引 ＋ 1])
        .判断循环首 (父项目 ≠ 0)
            父项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 3, 父项目)
            层次 ＝ 层次 ＋ 1
        .判断循环尾 ()
        返回 (层次)
    .判断结束
    

.子程序 取缩进层次_句柄, 整数型, 公开
    .参数 项目句现, 整数型
    .局部变量 父项目, 整数型
    .局部变量 层次, 整数型

    层次 ＝ 1
    父项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 3, 项目句现)
    .判断循环首 (父项目 ≠ 0)
        父项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 3, 父项目)
        层次 ＝ 层次 ＋ 1
    .判断循环尾 ()
    返回 (层次)

.子程序 取项目文本, 文本型, 公开, 返回指定项目的文本。如果该项目不存在，将返回空文本。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 缓冲区, 文本型
    .局部变量 TVITEMEX, TVITEMEX2
    .局部变量 局_项目地址, 整数型
    .局部变量 局_缓冲区, 文本型

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (“”)
        
    .判断 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_外部数据地址2 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 120)
        TVITEMEX.pszText ＝ 集_外部数据地址2
        TVITEMEX.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        TVITEMEX.mask ＝ #TVIF_TEXT
        TVITEMEX.cchTextMax ＝ 120
        局_项目地址 ＝ 取内存变量地址_TVITEMEX2 (TVITEMEX, TVITEMEX, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 局_项目地址, 40)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 集_外部数据地址1)
        局_缓冲区 ＝ 到文本 (外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址2, 120))
        
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址2)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        返回 (局_缓冲区)
    .默认
        
        集_单一项目.pszText ＝ 取空白文本 (120)
        集_单一项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_单一项目.mask ＝ #TVIF_TEXT
        集_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 集_项目地址)
        返回 (删首尾空 (集_单一项目.pszText))
    .判断结束
    

.子程序 取项目文本_句柄, 文本型, 公开
    .参数 项目句柄, 整数型
    .局部变量 缓冲区, 文本型

    .局部变量 项目地址, 整数型
    .局部变量 局_缓冲区, 文本型
    .局部变量 项目, TVITEMEX, 静态
    .局部变量 项目1, TVITEMEX2

    .如果真 (项目句柄 ＝ 0)
        返回 (“”)
    .如果真结束
    .判断开始 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_外部数据地址2 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 120)
        
        项目1.pszText ＝ 集_外部数据地址2
        项目1.hItem ＝ 项目句柄
        项目1.mask ＝ #TVIF_TEXT
        项目1.cchTextMax ＝ 120
        项目地址 ＝ 取内存变量地址_TVITEMEX2 (项目1, 项目1, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 项目地址, 40)
        
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 集_外部数据地址1)
        局_缓冲区 ＝ 到文本 (外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址2, 120))
        
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址2)
        返回 (局_缓冲区)
    .默认
        
        .如果真 (项目.mask ＝ 0)
            项目.cchTextMax ＝ 120
            项目.pszText ＝ 取空白文本 (120)
            项目.mask ＝ #TVIF_TEXT
        .如果真结束
        项目.hItem ＝ 项目句柄
        项目地址 ＝ 取内存变量地址_TVITEMEX (项目, 项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 项目地址)
        返回 (删首尾空 (项目.pszText))
    .判断结束
    

.子程序 取项目文本_句柄1, 文本型, 公开, 循环使用
    .参数 项目句柄, 整数型
    .参数 结束操作, 逻辑型, 可空
    .局部变量 缓冲区, 文本型

    .局部变量 项目地址, 整数型
    .局部变量 局_缓冲区, 文本型
    .局部变量 项目, TVITEMEX, 静态
    .局部变量 项目1, TVITEMEX2
    .局部变量 数据地址1, 整数型, 静态

    .如果真 (项目句柄 ＝ 0)
        返回 (“”)
    .如果真结束
    .判断开始 (集_外部控件)
        .如果真 (结束操作)
            外部操作.销毁外部指针 (集_外部进程句柄, 数据地址1)
            数据地址1 ＝ 0
            返回 (“”)
        .如果真结束
        
        .如果 (数据地址1 ＝ 0)
            数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 120 ＋ 40)
            项目1.pszText ＝ 数据地址1 ＋ 40
            项目1.hItem ＝ 项目句柄
            项目1.mask ＝ #TVIF_TEXT
            项目1.cchTextMax ＝ 120
            项目地址 ＝ 取内存变量地址_TVITEMEX2 (项目1, 项目1, 0)
            外部操作.写进程内存 (集_外部进程句柄, 数据地址1, 项目地址, 40)
            
        .否则
            外部操作.写进程内存 (集_外部进程句柄, 数据地址1 ＋ 4, 取指针整数_ (项目句柄), 4)
        .如果结束
        
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 数据地址1)
        局_缓冲区 ＝ 到文本 (外部操作.读外部指针数据 (集_外部进程句柄, 数据地址1 ＋ 40, 120))
        返回 (局_缓冲区)
        
    .默认
        .如果真 (项目.mask ＝ 0)
            项目.cchTextMax ＝ 120
            项目.pszText ＝ 取空白文本 (120)
            项目.mask ＝ #TVIF_TEXT
        .如果真结束
        项目.hItem ＝ 项目句柄
        项目地址 ＝ 取内存变量地址_TVITEMEX (项目, 项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 项目地址)
        返回 (删首尾空 (项目.pszText))
    .判断结束
    

.子程序 取图片, 整数型, 公开, 返回指定项目的图片索引。如果该项目不存在，将返回 -1 
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 数据, 字节集
    .局部变量 局_项目地址, 整数型

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (-1)
        
    .判断 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 2
        局_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 局_项目地址, 40)
        发送消息整数 (集_树型框句柄, 4364, 0, 集_外部数据地址1)
        数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        写到内存 (数据, 局_项目地址, 40)
        返回 (集_树型框项目.iImage)
        
    .默认
        集_单一项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_单一项目.mask ＝ 2
        局_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 局_项目地址)
        返回 (集_单一项目.iImage)
    .判断结束
    

.子程序 取图片_句柄, 整数型, 公开
    .参数 项目句柄, 整数型
    .局部变量 数据, 字节集
    .局部变量 项目地址, 整数型

    .如果真 (项目句柄 ＝ 0)
        返回 (0)
    .如果真结束
    .判断开始 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 项目句柄
        集_树型框项目.mask ＝ 2
        项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 项目地址, 40)
        发送消息整数 (集_树型框句柄, 4364, 0, 集_外部数据地址1)
        
        数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        写到内存 (数据, 项目地址, 40)
        返回 (集_树型框项目.iImage)
        
    .默认
        集_单一项目.hItem ＝ 项目句柄
        集_单一项目.mask ＝ 2
        项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 项目地址)
        返回 (集_单一项目.iImage)
    .判断结束
    

.子程序 取选中图片, 整数型, 公开, 返回指定项目被选中后的图片索引。如果该项目不存在，将返回 -1 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 数据, 字节集

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (-1)
    .判断 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 32
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        发送消息整数 (集_树型框句柄, 4364, 0, 集_外部数据地址1)
        数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        写到内存 (数据, 集_项目地址, 40)
        返回 (集_树型框项目.iSelectedImage)
        
    .默认
        集_单一项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_单一项目.mask ＝ 32
        集_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 集_项目地址)
        返回 (集_单一项目.iSelectedImage)
    .判断结束
    

.子程序 取项目数值, 整数型, 公开, 返回与指定项目相关联的数值。如果该项目不存在，将返回 0 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 局部_数据, 字节集

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (-1)
        
    .判断 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ #TVIF_PARAM
        集_项目地址 ＝ 取内存变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        SendMessageA (集_树型框句柄, #TVM_GETITEMA, 0, 集_外部数据地址1)
        局部_数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        写到内存 (局部_数据, 集_项目地址, 40)
        返回 (集_树型框项目.lParam)
        
    .默认
        集_单一项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_单一项目.mask ＝ #TVIF_PARAM
        集_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 集_项目地址)
        返回 (集_单一项目.lParam)
    .判断结束
    

.子程序 取项目数值_句柄, 整数型, 公开, 返回与指定项目相关联的数值。如果该项目不存在，将返回 0 。
    .参数 项目句柄, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 局部_数据, 字节集
    .局部变量 局_项目地址, 整数型

    .如果真 (项目句柄 ＝ 0)
        返回 (0)
    .如果真结束
    .判断开始 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 项目句柄
        集_树型框项目.mask ＝ #TVIF_PARAM
        局_项目地址 ＝ 取内存变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 局_项目地址, 40)
        SendMessageA (集_树型框句柄, #TVM_GETITEMA, 0, 集_外部数据地址1)
        局部_数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        写到内存 (局部_数据, 局_项目地址, 40)
        返回 (集_树型框项目.lParam)
        
    .默认
        集_单一项目.hItem ＝ 项目句柄
        集_单一项目.mask ＝ #TVIF_PARAM
        局_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 局_项目地址)
        返回 (集_单一项目.lParam)
    .判断结束
    

.子程序 置图片, 逻辑型, 公开, 设置在指定项目首部所显示图片的索引。成功返回真，失败返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 图片索引, 整数型, , 图片索引用于指定图片组属性中的某张图片，从 0 开始。
    .局部变量 当前项目, 整数型

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (假)
    .默认
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 2
        集_树型框项目.iImage ＝ 图片索引
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        当前项目 ＝ 发送消息整数 (集_树型框句柄, 4365, 0, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        ' 写到内存 (TEMP, 集_项目地址, 40)
        返回 (当前项目 ≠ 0)
    .判断结束
    

.子程序 置选中图片, 逻辑型, 公开, 设置指定项目被选中后所显示图片的索引。成功返回真，失败返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 图片索引, 整数型, , 图片索引用于指定图片组属性中的某张图片，从 0 开始。
    .局部变量 当前项目, 整数型

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (假)
    .默认
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 32
        集_树型框项目.iSelectedImage ＝ 图片索引
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        当前项目 ＝ 发送消息整数 (集_树型框句柄, 4365, 0, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        ' 写到内存 (TEMP, 集_项目地址, 40)
        返回 (当前项目 ≠ 0)
    .判断结束
    

.子程序 置项目数值, 逻辑型, 公开, 设置与指定项目相关联的数值。成功返回真，失败返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 欲置入的项目数值, 整数型, 可空, 可以被省略。该数值与指定项目相关联。如果本参数被省略，默认值为 0 。
    .局部变量 当前项目, 整数型

    .判断开始 (项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 项目索引 ≥ 0)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 4
        集_树型框项目.lParam ＝ 欲置入的项目数值
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        当前项目 ＝ 发送消息整数 (集_树型框句柄, 4365, 0, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        ' 写到内存 (TEMP, 集_项目地址, 40)
        返回 (当前项目 ≠ 0)
    .默认
        返回 (假)
    .判断结束
    

.子程序 置项目文本, 逻辑型, 公开, 设置指定项目的文本。成功返回真，失败返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 欲置入的项目文本, 文本型
    .局部变量 当前项目, 整数型

    .判断开始 (项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 项目索引 ≥ 0)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_外部数据地址2 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 取文本长度 (欲置入的项目文本) ＋ 1)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 1
        集_树型框项目.pszText ＝ 集_外部数据地址2
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        当前项目 ＝ lstrcpyn_文本型 (欲置入的项目文本, 欲置入的项目文本, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址2, 当前项目, 取文本长度 (欲置入的项目文本) ＋ 1)
        当前项目 ＝ 发送消息整数 (集_树型框句柄, 4365, 0, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址2)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        ' 写到内存 (TEMP, 集_项目地址, 40)
        返回 (当前项目 ≠ 0)
    .默认
        返回 (假)
    .判断结束
    

.子程序 保证显示, , 公开
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。

    .如果真 (项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 项目索引 ≥ 0)
        发送消息整数 (集_树型框句柄, 4372, 0, 集_句柄组 [项目索引 ＋ 1])
    .如果真结束
    

.子程序 保证显示_句柄, , 公开
    .参数 项目句柄, 整数型, , 0 为项目一，1 为项目二，如此类推。

    发送消息整数 (集_树型框句柄, 4372, 0, 项目句柄)

.子程序 扩展, , 公开, 将指打开指定项目的下属分枝。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。

    .判断开始 (项目索引 ＞ 99999) ' 如果大于9w，表示传进来的是句柄
        发送消息整数 (集_树型框句柄, 4354, 2, 项目索引)
        
    .判断 (项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 项目索引 ≥ 0)
        发送消息整数 (集_树型框句柄, 4354, 2, 集_句柄组 [项目索引 ＋ 1])
    .默认
        
    .判断结束
    

.子程序 扩展全部, , 公开
    .参数 不扩展第一项, 逻辑型, 可空
    .局部变量 i, 整数型

    .如果真 (取数组成员数 (集_父项目句柄组) ＝ 0)
        内部_遍历句柄_父项目 ()
    .如果真结束
    .计次循环首 (取数组成员数 (集_父项目句柄组), i)
        .如果真 (不扩展第一项 且 i ＝ 1)
            到循环尾 ()
        .如果真结束
        发送消息整数 (集_树型框句柄, 4354, 2, 集_父项目句柄组 [i])
    .计次循环尾 ()

.子程序 收缩, , 公开, 缩回指定项目的下属分枝。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。

    .如果真 (项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 项目索引 ≥ 0)
        .判断开始 (项目索引 ＞ 99999) ' 如果大于9w，表示传进来的是句柄
            发送消息整数 (集_树型框句柄, 4354, 1, 项目索引)
        .默认
            发送消息整数 (集_树型框句柄, 4354, 1, 集_句柄组 [项目索引 ＋ 1])
        .判断结束
        
    .如果真结束
    

.子程序 收缩全部, , 公开
    .局部变量 i, 整数型

    .如果真 (取数组成员数 (集_父项目句柄组) ＝ 0)
        内部_遍历句柄_父项目 ()
    .如果真结束
    .计次循环首 (取数组成员数 (集_父项目句柄组), i)
        .如果真 (i ＝ 1)
            到循环尾 ()
        .如果真结束
        发送消息整数 (集_树型框句柄, 4354, 1, 集_父项目句柄组 [i])
    .计次循环尾 ()

.子程序 强制刷新, , 公开
    内部_遍历句柄 ()

.子程序 排序, , 公开
    .参数 排序1级缩进, 逻辑型, 可空, 是否排序缩进层次为1的，可空为不排序。
    .参数 排序指定层次, 整数型, 可空, 可空，0=全部排序(默认)，排序大于为等于此值的缩进层次，如：2，将排序缩进层次≥2的项目，-1=不排序
    .局部变量 i, 整数型

    .如果真 (取数组成员数 (集_父项目句柄组) ＝ 0)
        内部_遍历句柄_父项目 ()
    .如果真结束
    
    .如果真 (排序1级缩进)
        SendMessageA (集_树型框句柄, 4371, 0, 0)
    .如果真结束
    
    .如果真 (排序指定层次 ＝ -1)
        返回 ()
    .如果真结束
    
    .计次循环首 (取数组成员数 (集_父项目句柄组), i)
        .如果 (排序指定层次 ＞ 0)
            .如果真 (取缩进层次 (i) ≥ 排序指定层次)
                SendMessageA (集_树型框句柄, 4371, 0, 集_父项目句柄组 [i])
            .如果真结束
            
        .否则
            ' 全部排序
            SendMessageA (集_树型框句柄, 4371, 0, 集_父项目句柄组 [i])
        .如果结束
        
    .计次循环尾 ()

.子程序 排序_句柄, , 公开
    .参数 项目句柄, 整数型

    SendMessageA (集_树型框句柄, 4371, 0, 项目句柄)

.子程序 加入项目, 整数型, 公开, 将单个或一批项目加入到树型框中，如果仅加入一个项目，成功返回加入后该项目所处的位置，失败返回 -1 。如果成批加入项目，成功返回 0 ，失败返回 -1 。成批加入项目可以用作一次性快速加入大量项目到树型框中。
    .参数 父项目索引, 整数型, 可空, 可以被省略。指定欲加入项目所处的父项目，0 为项目一，1 为项目二，如此类推。如果没有父项目（即欲加入项目为顶层项目），请提供值 -1 。如果本参数被省略，默认值为 -1 。
    .参数 项目数据1, 项目数据, 可空, 用于只加入一个项目，如欲批量加入，请留空
    .参数 项目数据2, 项目数据, 可空 数组, 用于批量加入项目
    .参数 加入方式, 整数型, 可空, 可以被省略。默认为 3
    .参数 加入完成后是否排序, 逻辑型, 可空
    .局部变量 成员数, 整数型
    .局部变量 计次
    .局部变量 数据地址, 整数型
    .局部变量 插入项目, TVINSERTSTRUCT
    .局部变量 插入项目2, TVINSERTSTRUCT2
    .局部变量 当前项目, 整数型
    .局部变量 空白数据, 字节集
    .局部变量 项目句柄, 整数型

    .判断开始 (父项目索引 ＞ 99999) ' 如果大于9w，表示传进来的是句柄
        连续赋值 (父项目索引, 插入项目.hParent, 插入项目2.hParent)
        
    .判断 (是否为空 (父项目索引) ＝ 假 且 父项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 父项目索引 ≥ 0)
        连续赋值 (集_句柄组 [父项目索引 ＋ 1], 插入项目.hParent, 插入项目2.hParent)
    .默认
        
    .判断结束
    
    .如果真 (是否为空 (加入方式) 或 加入方式 ＞ 4 或 加入方式 ＜ 1)
        加入方式 ＝ 3
    .如果真结束
    
    .判断开始 (是否为空 (项目数据1) ＝ 假)
        插入项目.hInsertAfter ＝ 多项选择 (加入方式, #TVI_ROOT, #TVI_FIRST, #TVI_LAST, #TVI_SORT)
        插入项目.pszText ＝ 项目数据1.项目文本
        插入项目.iImage ＝ 项目数据1.图片索引
        插入项目.iSelectedImage ＝ 项目数据1.选中图片索引
        插入项目.lParam ＝ 项目数据1.项目数值
        插入项目.mask ＝ #TVIF_ALL
        数据地址 ＝ 取内存变量地址_TVINSERTSTRUCT (插入项目, 插入项目, 0)
        返回 (发送消息整数 (集_树型框句柄, #TVM_INSERTITEMA, 0, 数据地址))
        
    .判断 (是否为空 (项目数据2) ＝ 假)
        发送消息整数 (集_树型框句柄, #WM_SETREDRAW, 0, 0) ' 是否使用高速插入
        .判断开始 (加入方式 ＝ 2)
            计次 ＝ 成员数
            .判断循环首 (计次 ＞ 0)
                插入项目.hInsertAfter ＝ 多项选择 (加入方式, #TVI_ROOT, #TVI_FIRST, #TVI_LAST, #TVI_SORT)
                插入项目.mask ＝ #TVIF_ALL
                插入项目.pszText ＝ 项目数据2 [计次].项目文本
                插入项目.iImage ＝ 项目数据2 [计次].图片索引
                插入项目.iSelectedImage ＝ 项目数据2 [计次].选中图片索引
                插入项目.lParam ＝ 项目数据2 [计次].项目数值
                数据地址 ＝ 取内存变量地址_TVINSERTSTRUCT (插入项目, 插入项目, 0)
                发送消息整数 (集_树型框句柄, #TVM_INSERTITEMA, 0, 数据地址)
                计次 ＝ 计次 － 1
            .判断循环尾 ()
            
        .默认
            成员数 ＝ 取数组成员数 (项目数据2)
            .判断循环首 (计次 ＜ 成员数)
                计次 ＝ 计次 ＋ 1
                插入项目.hInsertAfter ＝ 多项选择 (加入方式, #TVI_ROOT, #TVI_FIRST, #TVI_LAST, #TVI_SORT)
                插入项目.mask ＝ #TVIF_ALL
                插入项目.pszText ＝ 项目数据2 [计次].项目文本
                插入项目.iImage ＝ 项目数据2 [计次].图片索引
                插入项目.iSelectedImage ＝ 项目数据2 [计次].选中图片索引
                插入项目.lParam ＝ 项目数据2 [计次].项目数值
                数据地址 ＝ 取内存变量地址_TVINSERTSTRUCT (插入项目, 插入项目, 0)
                项目数据2 [计次].项目句柄 ＝ 发送消息整数 (集_树型框句柄, #TVM_INSERTITEMA, 0, 数据地址)
                .如果真 (项目数据2 [计次].是否加粗)
                    加粗_句柄 (项目数据2 [计次].项目句柄)
                .如果真结束
                
            .判断循环尾 ()
        .判断结束
        .如果真 (加入完成后是否排序)
            排序 (真, -1)
        .如果真结束
        发送消息整数 (集_树型框句柄, #WM_SETREDRAW, 1, 0) ' 是否使用高速插入
        返回 (0)
    .默认
        返回 (-1)
    .判断结束
    

.子程序 加入项目1, 整数型, 公开
    .参数 父项目索引, 整数型, 可空, 可以被省略。指定欲加入项目所处的父项目，0 为项目一，1 为项目二，如此类推。如果没有父项目（即欲加入项目为顶层项目），请提供值 -1 。如果本参数被省略，默认值为 -1 。
    .参数 项目文本, 文本型
    .参数 图片索引, 整数型, 可空
    .参数 选中图片索引, 整数型, 可空
    .参数 项目数值, 整数型, 可空
    .参数 是否加粗, 逻辑型, 可空
    .局部变量 局_插入项目2, TVINSERTSTRUCT2
    .局部变量 局_当前项目, 整数型

    .如果真 (是否为空 (父项目索引) ＝ 假 且 父项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 父项目索引 ≥ 0)
        连续赋值 (集_句柄组 [父项目索引 ＋ 1], _项目.hParent, 局_插入项目2.hParent)
    .如果真结束
    
    .判断开始 (集_外部控件)
        局_插入项目2.hInsertAfter ＝ 3
        局_插入项目2.iImage ＝ 图片索引
        局_插入项目2.iSelectedImage ＝ 选中图片索引
        局_插入项目2.lParam ＝ 项目数值
        局_插入项目2.mask ＝ #TVIF_ALL
        .如果真 (是否加粗)
            局_插入项目2.mask ＝ 局_插入项目2.mask ＋ 8
        .如果真结束
        局_插入项目2.state ＝ 16
        局_插入项目2.stateMask ＝ 16
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 52)
        集_外部数据地址2 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 取文本长度 (项目文本) ＋ 1)
        局_插入项目2.pszText ＝ 集_外部数据地址2
        集_项目地址 ＝ 取内存变量地址_TVINSERTSTRUCT2 (局_插入项目2, 局_插入项目2, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 52)
        局_当前项目 ＝ 取数据地址3 (项目文本, 项目文本, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址2, 局_当前项目, 取文本长度 (项目文本) ＋ 1)
        局_当前项目 ＝ SendMessageA (集_树型框句柄, #TVM_INSERTITEMA, 0, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址2)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        返回 (局_当前项目)
        
    .默认
        ' 输出调试文本 (项目文本)
        _项目.hInsertAfter ＝ 3
        _项目.pszText ＝ 项目文本
        _项目.iImage ＝ 图片索引
        _项目.iSelectedImage ＝ 选中图片索引
        _项目.lParam ＝ 项目数值
        _项目.mask ＝ #TVIF_ALL
        _项目.state ＝ 16
        _项目.stateMask ＝ 16
        .如果真 (是否加粗)
            _项目.mask ＝ _项目.mask ＋ 8
        .如果真结束
        
        .如果真 (_数据地址 ＝ 0)
            _数据地址 ＝ 取内存变量地址_TVINSERTSTRUCT (_项目, _项目, 0)
        .如果真结束
        
        返回 (SendMessageA (集_树型框句柄, #TVM_INSERTITEMA, 0, _数据地址))
    .判断结束
    返回 (0)

.子程序 删除项目, 逻辑型, 公开, 删除树型框中的指定项目。成功返回真，失败返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。

    .如果真 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (假)
    .如果真结束
    返回 (发送消息整数 (集_树型框句柄, 4353, 0, 集_句柄组 [项目索引 ＋ 1]) ≠ 0)

.子程序 清空, , 公开, 删除树型框中的所有项目。
    ' 发送消息整数 (集_树型框句柄, #WM_SETREDRAW, 0, 0)'会导致重绘失败
    发送消息整数 (集_树型框句柄, 4353, 0, 0) ' 删除项目
    ' 发送消息整数 (集_树型框句柄, #WM_SETREDRAW, 1, 0)

.子程序 取父项目, 整数型, 公开, 返回指定项目的父项目索引。如果该项目为顶层项目，将返回 -1 。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 计次1, 整数型
    .局部变量 计次2, 整数型
    .局部变量 父项目, 整数型

    计次2 ＝ 取数组成员数 (集_句柄组)
    .如果真 (项目索引 ＋ 1 ＞ 计次2 或 项目索引 ＜ 0)
        返回 (-1)
    .如果真结束
    父项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 3, 集_句柄组 [项目索引 ＋ 1])
    .判断开始 (父项目 ＝ 0)
        返回 (-1)
    .默认
        .判断循环首 (计次1 ＜ 计次2)
            计次1 ＝ 计次1 ＋ 1
            .如果真 (集_句柄组 [计次1] ＝ 父项目)
                返回 (计次1 － 1)
            .如果真结束
            
        .判断循环尾 ()
    .判断结束
    返回 (-1)

.子程序 取父项目文本, 文本型, 公开
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。

    返回 (取项目文本 (取父项目 (项目索引)))

.子程序 是否有子项目, 逻辑型, 公开, 如果指定项目存在子项目，返回真，否则返回假。如果本命令返回真，意味着指定项目的下一项即为此项目的第一个子项目。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 成员数, 整数型
    .局部变量 数据, 字节集

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (假)
        
    .判断 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 64
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        发送消息整数 (集_树型框句柄, 4364, 0, 集_外部数据地址1)
        数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        写到内存 (数据, 集_项目地址, 40)
        返回 (集_树型框项目.cChildren)
        
    .默认
        集_单一项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_单一项目.mask ＝ #TVIF_CHILDREN
        集_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        发送消息整数 (集_树型框句柄, #TVM_GETITEMA, 0, 集_项目地址)
        返回 (集_单一项目.cChildren)
    .判断结束
    

.子程序 是否已扩展, 逻辑型, 公开, 如果树型框中的指定项目具有子项目并已扩展显示，返回真，否则返回假。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 是否自动更新数据, 逻辑型, 可空, 可以被省略。选择是否自动进行数据更新。当为真时，每次读取数据时都会对数据进行刷新，可能会因此而减慢速度。默认值为“置自动更新状态”方法所设置的状态。
    .局部变量 成员数, 整数型

    成员数 ＝ 取数组成员数 (集_句柄组)
    .如果真 (项目索引 ＋ 1 ≥ 成员数 或 项目索引 ＜ 0)
        ' 如果索引超出范围，或索引是最后一个项目
        返回 (假)
    .如果真结束
    成员数 ＝ 发送消息整数 (集_树型框句柄, 4391, 集_句柄组 [项目索引 ＋ 1], 0)
    返回 (位与 (成员数, 32) ＝ 32)

.子程序 置背景色, , 公开
    .参数 背景颜色, 整数型

    发送消息整数 (集_树型框句柄, 4381, 0, 背景颜色)

.子程序 取背景色, 整数型, 公开
    返回 (发送消息整数 (集_树型框句柄, 4383, 0, 0))

.子程序 置项目间隔, , 公开
    .参数 间隔距离, 整数型, 可空, 默认为16

    .如果真 (是否为空 (间隔距离))
        间隔距离 ＝ 16
    .如果真结束
    发送消息整数 (集_树型框句柄, 4379, 间隔距离, 0)

.子程序 取项目间隔, 整数型, 公开
    返回 (发送消息整数 (集_树型框句柄, 4380, 0, 0))

.子程序 置文本颜色, , 公开
    .参数 文本颜色, 整数型

    发送消息整数 (集_树型框句柄, 4382, 0, 文本颜色)

.子程序 取文本颜色, 整数型, 公开
    返回 (发送消息整数 (集_树型框句柄, 4384, 0, 0))

.子程序 置连线颜色, , 公开
    .参数 连线颜色, 整数型

    发送消息整数 (集_树型框句柄, 4392, 0, 连线颜色)

.子程序 取连线颜色, 整数型, 公开
    返回 (发送消息整数 (集_树型框句柄, 4393, 0, 0))

.子程序 使用复选框, , 公开
    .如果真 (位与 (GetWindowLongA (集_树型框句柄, -16), 256) ≠ 256)
        SetWindowLongA (集_树型框句柄, -16, 位或 (GetWindowLongA (集_树型框句柄, -16), 256))
    .如果真结束
    

.子程序 使用热点跟踪, , 公开
    .如果真 (位与 (GetWindowLongA (集_树型框句柄, -16), 512) ≠ 512)
        SetWindowLongA (集_树型框句柄, -16, 位或 (GetWindowLongA (集_树型框句柄, -16), 512))
    .如果真结束
    

.子程序 禁止滚动条, 逻辑型, 公开, 返回禁止的状态
    .参数 禁止, 逻辑型, 可空, 真为禁止，假为解除禁止。默认为真

    .判断开始 (位与 (GetWindowLongA (集_树型框句柄, -16), 8192) ≠ 8192 且 (禁止 或 是否为空 (禁止)))
        SetWindowLongA (集_树型框句柄, -16, 位或 (GetWindowLongA (集_树型框句柄, -16), 8192))
        返回 (真)
    .判断 (位与 (GetWindowLongA (集_树型框句柄, -16), 8192) ＝ 8192 且 取反 (禁止) 且 取反 (是否为空 (禁止)))
        SetWindowLongA (集_树型框句柄, -16, 位异或 (GetWindowLongA (集_树型框句柄, -16), 8192))
        返回 (假)
    .默认
        返回 (位与 (GetWindowLongA (集_树型框句柄, -16), 8192) ≠ 8192)
    .判断结束
    

.子程序 禁止水平滚动条, 逻辑型, 公开, 返回禁止的状态
    .参数 禁止, 逻辑型, 可空, 真为禁止，假为解除禁止。默认为真

    .判断开始 (位与 (GetWindowLongA (集_树型框句柄, -16), 32768) ≠ 32768 且 (禁止 或 是否为空 (禁止)))
        SetWindowLongA (集_树型框句柄, -16, 位或 (GetWindowLongA (集_树型框句柄, -16), 32768))
        返回 (真)
    .判断 (位与 (GetWindowLongA (集_树型框句柄, -16), 32768) ＝ 32768 且 取反 (禁止) 且 取反 (是否为空 (禁止)))
        SetWindowLongA (集_树型框句柄, -16, 位异或 (GetWindowLongA (集_树型框句柄, -16), 32768))
        返回 (假)
    .默认
        返回 (位与 (GetWindowLongA (集_树型框句柄, -16), 32768) ≠ 32768)
    .判断结束
    

.子程序 使用整行选择, 逻辑型, 公开, 要使用本功能，就不能使用显示连线。
    .参数 是否使用, 逻辑型, 可空, 真为使用，假为不使用。默认为真。

    .判断开始 (位与 (GetWindowLongA (集_树型框句柄, -16), 2) ≠ 2)
        .判断开始 (位与 (GetWindowLongA (集_树型框句柄, -16), 4096) ≠ 4096 且 (是否使用 或 是否为空 (是否使用)))
            SetWindowLongA (集_树型框句柄, -16, 位或 (GetWindowLongA (集_树型框句柄, -16), 4096))
            返回 (真)
        .判断 (位与 (GetWindowLongA (集_树型框句柄, -16), 4096) ＝ 4096 且 取反 (是否使用) 且 取反 (是否为空 (是否使用)))
            SetWindowLongA (集_树型框句柄, -16, 位异或 (GetWindowLongA (集_树型框句柄, -16), 4096))
            返回 (假)
        .默认
            返回 (位与 (GetWindowLongA (集_树型框句柄, -16), 4096) ≠ 4096)
        .判断结束
        
    .默认
        暂停 ()
        返回 (假)
    .判断结束
    

.子程序 是否选中, 逻辑型, 公开
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 是否自动更新数据, 逻辑型, 可空, 可以被省略。选择是否自动进行数据更新。当为真时，每次读取数据时都会对数据进行刷新，可能会因此而减慢速度。默认值为“置自动更新状态”方法所设置的状态。
    .局部变量 数据, 字节集

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (假)
    .默认
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 8
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        发送消息整数 (集_树型框句柄, 4364, 0, 集_外部数据地址1)
        数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        写到内存 (数据, 集_项目地址, 40)
        返回 (位与 (集_树型框项目.state, 8192) ＝ 8192)
    .判断结束
    

.子程序 选中项目, 逻辑型, 公开, 操作成功返回真
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .参数 是否选中, 逻辑型, 可空, 可以被省略。设置项目的选中状态，默认为真。

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 (假)
    .默认
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
        集_树型框项目.mask ＝ 8 ＋ 16
        .判断开始 (是否选中 或 是否为空 (是否选中))
            集_树型框项目.state ＝ 8192
        .默认
            集_树型框项目.state ＝ 4096
        .判断结束
        集_树型框项目.stateMask ＝ 61440
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        发送消息整数 (集_树型框句柄, 4365, 0, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        返回 (真)
    .判断结束
    

.子程序 加粗, , 公开
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。

    .如果真 (项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 项目索引 ≥ 0)
        .判断开始 (集_外部控件)
            集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
            集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
            集_树型框项目.mask ＝ 8
            集_树型框项目.state ＝ 16
            集_树型框项目.stateMask ＝ 16
            集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
            外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
            SendMessageA (集_树型框句柄, 4365, 0, 集_外部数据地址1)
            外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
            
        .默认
            集_单一项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
            集_单一项目.mask ＝ 8
            集_单一项目.state ＝ 16
            集_单一项目.stateMask ＝ 16
            集_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
            SendMessageA (集_树型框句柄, #TVM_SETITEMA, 0, 集_项目地址)
        .判断结束
        
    .如果真结束
    

.子程序 加粗_句柄, , 公开
    .参数 项目句柄, 整数型, , 0 为项目一，1 为项目二，如此类推。

    .判断开始 (集_外部控件)
        集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
        集_树型框项目.hItem ＝ 项目句柄
        集_树型框项目.mask ＝ 8
        集_树型框项目.state ＝ 16
        集_树型框项目.stateMask ＝ 16
        集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        SendMessageA (集_树型框句柄, 4365, 0, 集_外部数据地址1)
        外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
        
    .默认
        集_单一项目.hItem ＝ 项目句柄
        集_单一项目.mask ＝ 8
        集_单一项目.state ＝ 16
        集_单一项目.stateMask ＝ 16
        集_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
        SendMessageA (集_树型框句柄, #TVM_SETITEMA, 0, 集_项目地址)
    .判断结束
    

.子程序 取消加粗, , 公开, 将指定项目的标题取消加粗显示。
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。
    .局部变量 当前项目, 整数型
    .局部变量 当前状态, 整数型, , , 0、普通，1、加粗，2、扩展，3、加粗+扩展

    .如果真 (项目索引 ＋ 1 ≤ 取数组成员数 (集_句柄组) 且 项目索引 ≥ 0)
        .判断开始 (集_外部控件)
            集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
            集_树型框项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
            集_树型框项目.mask ＝ 8
            集_树型框项目.state ＝ 位取反 (16)
            集_树型框项目.stateMask ＝ 16
            集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
            外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
            
            发送消息整数 (集_树型框句柄, 4365, 0, 集_外部数据地址1)
            外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
            
        .默认
            集_单一项目.hItem ＝ 集_句柄组 [项目索引 ＋ 1]
            集_单一项目.mask ＝ #TVIF_STATE
            集_单一项目.state ＝ 位取反 (#TVIS_BOLD)
            集_单一项目.stateMask ＝ #TVIS_BOLD
            集_项目地址 ＝ 取内存变量地址_TVITEMEX (集_单一项目, 集_单一项目, 0)
            发送消息整数 (集_树型框句柄, #TVM_SETITEMA, 0, 集_项目地址)
        .判断结束
        
    .如果真结束
    

.子程序 取可显示数量, 整数型, 公开, 取当前在树形框的用户区中大约可以显示的项目数量
    返回 (发送消息整数 (集_树型框句柄, 4368, 0, 0))

.子程序 取第一项索引, 整数型, 公开, 取在当前树形框中显示的第一条项目的索引。失败返回-1
    .局部变量 目标句柄
    .局部变量 成员数, 整数型
    .局部变量 计次, 整数型

    目标句柄 ＝ 发送消息整数 (集_树型框句柄, 4362, 5, 0)
    成员数 ＝ 取数组成员数 (集_句柄组)
    .判断循环首 (计次 ＜ 成员数)
        计次 ＝ 计次 ＋ 1
        .如果真 (目标句柄 ＝ 集_句柄组 [计次])
            返回 (计次 － 1)
        .如果真结束
        
    .判断循环尾 ()
    返回 (-1)

.子程序 取现行选中项, 整数型, 公开
    .局部变量 当前句柄, 整数型
    .局部变量 成员数, 整数型
    .局部变量 计次, 整数型

    当前句柄 ＝ 发送消息整数 (集_树型框句柄, 4362, 9, 0) ' 先取第一个根
    .如果真 (当前句柄 ≠ 0)
        成员数 ＝ 取数组成员数 (集_句柄组)
        .判断循环首 (计次 ＜ 成员数)
            计次 ＝ 计次 ＋ 1
            .如果真 (集_句柄组 [计次] ＝ 当前句柄)
                返回 (计次 － 1)
            .如果真结束
            
        .判断循环尾 ()
    .如果真结束
    返回 (-1)

.子程序 置现行选中项, , 公开
    .参数 项目索引, 整数型, , 0 为项目一，1 为项目二，如此类推。

    .判断开始 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
        返回 ()
    .默认
        发送消息整数 (集_树型框句柄, 4363, 9, 集_句柄组 [项目索引 ＋ 1])
    .判断结束
    

.子程序 置现行选中项_句柄, , 公开
    .参数 项目句柄, 整数型, , 0 为项目一，1 为项目二，如此类推。

    发送消息整数 (集_树型框句柄, 4363, 9, 项目句柄)

.子程序 取全部选中项, 变体型, 公开, 返回选中项索引数组
    .局部变量 选中项索引, 变体型
    .局部变量 选中项索引A, 整数型, , "0"
    .局部变量 成员数, 整数型
    .局部变量 成员数2, 整数型
    .局部变量 计次, 整数型
    .局部变量 数据, 字节集

    选中项索引.置类型 (#变体类型.数值型数组)
    成员数 ＝ 取数组成员数 (集_句柄组)
    集_树型框项目.mask ＝ 8
    集_项目地址 ＝ _取变量地址_TVITEMEX2 (集_树型框项目, 集_树型框项目, 0)
    集_外部数据地址1 ＝ 外部操作.分配外部指针 (集_外部进程句柄, 40)
    .判断循环首 (计次 ＜ 成员数)
        计次 ＝ 计次 ＋ 1
        集_树型框项目.hItem ＝ 集_句柄组 [计次]
        外部操作.写进程内存 (集_外部进程句柄, 集_外部数据地址1, 集_项目地址, 40)
        发送消息整数 (集_树型框句柄, 4364, 0, 集_外部数据地址1)
        数据 ＝ 外部操作.读外部指针数据 (集_外部进程句柄, 集_外部数据地址1, 40)
        写到内存 (数据, 集_项目地址, 40)
        .如果真 (位与 (集_树型框项目.state, 8192) ＝ 8192)
            成员数2 ＝ 取数组成员数 (选中项索引A) ＋ 1
            重定义数组 (选中项索引A, 真, 成员数2)
            选中项索引A [成员数2] ＝ 计次
            ' 加入成员 (选中项索引A, I)
        .如果真结束
        
    .判断循环尾 ()
    外部操作.销毁外部指针 (集_外部进程句柄, 集_外部数据地址1)
    选中项索引.赋值 (选中项索引A, )
    返回 (选中项索引)

.子程序 更新缓存, , 公开, 如果项目增加或减少，需要调用此命令进行更新
    内部_遍历句柄 ()

.子程序 置外部操作, , 公开
    .参数 是否操作外部控件, 逻辑型

    .如果真 (集_外部控件 ＝ 假 且 是否操作外部控件)
        集_外部进程句柄 ＝ 外部操作.打开进程 (集_树型框句柄)
    .如果真结束
    集_外部控件 ＝ 是否操作外部控件

.子程序 内部_遍历句柄
    .局部变量 局_父项目
    .局部变量 局_子项目, 整数型
    .局部变量 局_数值, 整数型
    .局部变量 局_完成, 逻辑型
    .局部变量 局_计次, 整数型

    清除数组 (集_父项目句柄组)
    重定义数组 (集_句柄组, 假, 取项目数 ())
    局_子项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 0, 0) ' 先取第一个根
    .判断循环首 (局_子项目 ≠ 0)
        局_计次 ＝ 局_计次 ＋ 1
        集_句柄组 [局_计次] ＝ 局_子项目
        局_数值 ＝ 局_子项目
        局_子项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 4, 局_子项目) ' 取第一个子
        .如果真 (局_子项目 ＝ 0) ' 取子取不到，就取兄弟
            局_子项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 1, 局_数值) ' 取子的兄弟
            ' 找到了兄弟就直接到循环尾，否则进入下面的循环找父项目
            .判断循环首 (局_子项目 ＝ 0)
                局_父项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 3, 局_数值) ' 退回上一级
                .如果真 (局_父项目 ＝ 0)
                    局_完成 ＝ 真
                    跳出循环 ()
                .如果真结束
                局_数值 ＝ 局_父项目
                局_子项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 1, 局_父项目) ' 取父的兄弟
            .判断循环尾 ()
        .如果真结束
        .如果真 (局_完成)
            跳出循环 ()
        .如果真结束
        
    .判断循环尾 ()

.子程序 内部_遍历句柄_父项目, , , 遍历有子项目的句柄
    .局部变量 i, 整数型
    .局部变量 计次, 整数型

    重定义数组 (集_父项目句柄组, 假, 取数组成员数 (集_句柄组))
    .计次循环首 (取数组成员数 (集_句柄组), i)
        .如果真 (是否有子项目 (i － 1))
            计次 ＝ 计次 ＋ 1
            集_父项目句柄组 [计次] ＝ 集_句柄组 [i]
        .如果真结束
        
    .计次循环尾 ()
    重定义数组 (集_父项目句柄组, 真, 计次)

.子程序 遍历句柄_子项目, 整数型, 公开, 遍历指定父项目下的所有子项目句柄
    .参数 父项目句柄, 整数型
    .参数 项目数据, 项目数据, 参考 可空 数组
    .参数 是否提取项目数值, 逻辑型, 可空
    .局部变量 局_子项目, 整数型
    .局部变量 局_项目数据, 项目数据

    局_子项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 4, 父项目句柄) ' 取第一个子
    .如果真 (局_子项目 ＝ 0)
        返回 (0)
    .如果真结束
    .判断开始 (是否提取项目数值)
        .判断循环首 (局_子项目 ≠ 0)
            ' 局_项目数据.项目数值 ＝ 取项目数值_句柄 (局_子项目)
            局_项目数据.项目文本 ＝ 取项目文本_句柄 (局_子项目)
            局_项目数据.项目句柄 ＝ 局_子项目
            加入成员 (项目数据, 局_项目数据)
            局_子项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 1, 局_子项目) ' 取子的兄弟
        .判断循环尾 ()
        
    .默认
        .判断循环首 (局_子项目 ≠ 0)
            ' 局_项目数据.项目数值 ＝ 取项目数值_句柄 (局_子项目)
            局_项目数据.项目文本 ＝ 取项目文本_句柄 (局_子项目)
            局_项目数据.项目句柄 ＝ 局_子项目
            加入成员 (项目数据, 局_项目数据)
            局_子项目 ＝ 发送消息整数 (集_树型框句柄, 4362, 1, 局_子项目) ' 取子的兄弟
        .判断循环尾 ()
    .判断结束
    返回 (取数组成员数 (项目数据))

.子程序 句柄取索引, 整数型, 公开, 用树型框的项目句柄获取他的项目索引
    .参数 欲查找句柄, 整数型
    .局部变量 计次1, 整数型
    .局部变量 计次2, 整数型

    计次1 ＝ 1
    计次2 ＝ 取数组成员数 (集_句柄组)
    .判断循环首 (计次1 ＜ 计次2) ' 循环直至左右指针相遇
        .如果真 (集_句柄组 [计次1] ＝ 欲查找句柄)
            返回 (计次1 － 1)
        .如果真结束
        
        .如果真 (集_句柄组 [计次2] ＝ 欲查找句柄)
            返回 (计次2 － 1)
        .如果真结束
        .判断开始 (计次1 ＜ 计次2)
            计次1 ＝ 计次1 ＋ 1
            计次2 ＝ 计次2 － 1
        .默认
            返回 (0)
        .判断结束
        
    .判断循环尾 ()
    返回 (0)

.子程序 索引取句柄, 整数型, 公开, 失败返回-1
    .参数 项目索引, 整数型, , 从0开始
    .局部变量 局_索引, 整数型

    局_索引 ＝ 项目索引 ＋ 1
    .如果真 (取数组成员数 (集_句柄组) ＝ 0)
        内部_遍历句柄 ()
    .如果真结束
    .如果真 (局_索引 ＞ 0 且 局_索引 ≤ 取数组成员数 (集_句柄组))
        返回 (集_句柄组 [局_索引])
    .如果真结束
    返回 (-1)

.子程序 查找项目, 整数型, 公开, 成功返回项目索引
    .参数 项目文本, 文本型
    .参数 精确查找, 逻辑型, 可空, 可空，默认为假，真=精确查找
    .参数 项目句柄, 整数型, 参考 可空
    .局部变量 计次1, 整数型
    .局部变量 计次2, 整数型

    内部_遍历句柄 ()
    计次1 ＝ 0
    计次2 ＝ 取数组成员数 (集_句柄组)
    .判断开始 (精确查找)
        .判断循环首 (计次1 ＜ 计次2) ' 循环直至左右指针相遇
            .如果真 (取项目文本 (计次1) ＝ 项目文本)
                .如果真 (是否为空 (项目句柄) ＝ 假)
                    项目句柄 ＝ 集_句柄组 [计次1 ＋ 1]
                .如果真结束
                返回 (计次1)
            .如果真结束
            
            .如果真 (取项目文本 (计次2) ＝ 项目文本)
                .如果真 (是否为空 (项目句柄) ＝ 假)
                    项目句柄 ＝ 集_句柄组 [计次2 ＋ 1]
                .如果真结束
                返回 (计次2)
            .如果真结束
            .判断开始 (计次1 ＜ 计次2)
                计次1 ＝ 计次1 ＋ 1
                计次2 ＝ 计次2 － 1
            .默认
                返回 (-1)
            .判断结束
            
        .判断循环尾 ()
        
    .默认
        .判断循环首 (计次1 ＜ 计次2) ' 循环直至左右指针相遇
            .如果真 (寻找文本 (取项目文本 (计次1), 项目文本, , 假) ≠ -1)
                .如果真 (是否为空 (项目句柄) ＝ 假)
                    项目句柄 ＝ 集_句柄组 [计次1 ＋ 1]
                .如果真结束
                返回 (计次1)
            .如果真结束
            
            .如果真 (寻找文本 (取项目文本 (计次2), 项目文本, , 假) ≠ -1)
                .如果真 (是否为空 (项目句柄) ＝ 假)
                    项目句柄 ＝ 集_句柄组 [计次2 ＋ 1]
                .如果真结束
                返回 (计次2)
            .如果真结束
            .判断开始 (计次1 ＜ 计次2)
                计次1 ＝ 计次1 ＋ 1
                计次2 ＝ 计次2 － 1
            .默认
                返回 (-1)
            .判断结束
            
        .判断循环尾 ()
        
    .判断结束
    返回 (-1)

.子程序 查找项目数值, 整数型, 公开, 成功返回索引
    .参数 项目文本数值, 整数型
    .局部变量 计次1, 整数型
    .局部变量 计次2, 整数型

    计次1 ＝ 0
    计次2 ＝ 取数组成员数 (集_句柄组)
    .判断循环首 (计次1 ＜ 计次2) ' 循环直至左右指针相遇
        .如果真 (取项目数值 (计次1) ＝ 项目文本数值)
            返回 (计次1)
        .如果真结束
        
        .如果真 (取项目数值 (计次2) ＝ 项目文本数值)
            返回 (计次2)
            
        .如果真结束
        .判断开始 (计次1 ＜ 计次2)
            计次1 ＝ 计次1 ＋ 1
            计次2 ＝ 计次2 － 1
        .默认
            返回 (-1)
        .判断结束
        
    .判断循环尾 ()
    返回 (-1)

.子程序 查找项目_外部, 整数型, 公开, 返回结果数量，被查找出来的项目数值，对应目标树型框的项目索引
    .参数 欲查找的文本, 文本型
    .参数 结果树型框句柄, 整数型, , 用于存放结果的树型框句柄
    .参数 查找结果是否扩展, 逻辑型, 可空, 查找后，结果项目是否扩展开
    .参数 是否同步图标, 逻辑型, 可空, 是否同步项目的图标，会影响速度
    .参数 搜索时禁止重绘, 逻辑型, 可空, 禁止重绘，可能会导致未找到时(只有1个项目时，无法重绘)
    .参数 搜索状态回调Call, 整数型, 可空, 共2个参数，参数1:状态文本(文本型)，参数2:备用(整数)
    .局部变量 父项目索引, 整数型
    .局部变量 树型框, 外部树型框
    .局部变量 项目数据, 项目数据
    .局部变量 一级项目, 整数型
    .局部变量 图片索引, 整数型
    .局部变量 索引, 整数型
    .局部变量 被查找索引, 整数型
    .局部变量 搜索结果数量, 整数型

    .如果真 (窗口_句柄是否有效 (结果树型框句柄) ＝ 假)
        输出调试文本 (“结果树型框句柄 - 无效”)
        返回 (0)
    .如果真结束
    树型框.初始化 (结果树型框句柄, , 集_外部控件)
    树型框.清空 ()
    树型框.更新缓存 ()
    .如果 (搜索状态回调Call ＞ 0)
        _搜索回调Call ＝ 搜索状态回调Call
    .否则
        _搜索回调Call ＝ 0
    .如果结束
    搜索结果数量 ＝ 0
    图片索引 ＝ 取图片 (0)
    树型框.加入项目1 (, 取项目文本 (0), 图片索引, 图片索引, 取项目数值 (0), 真)
    树型框.更新缓存 ()
    .如果真 (搜索时禁止重绘)
        发送消息整数 (结果树型框句柄, #WM_SETREDRAW, 0, 0)
    .如果真结束
    一级项目 ＝ SendMessageA (集_树型框句柄, #TVM_GETNEXTITEM, #TVGN_ROOT, 0)
    内部_递归子项目 (集_树型框句柄, 一级项目, 欲查找的文本, 索引, 被查找索引, 父项目索引, 树型框, , 是否同步图标, 搜索结果数量)
    .如果真 (树型框.取子项目数 (0) ＝ 0)
        索引 ＝ 索引 － 1
        被查找索引 ＝ 被查找索引 － 1
        树型框.删除项目 (0)
    .如果真结束
    
    .判断循环首 (一级项目 ＞ 0)
        索引 ＝ 索引 ＋ 1
        被查找索引 ＝ 被查找索引 ＋ 1
        一级项目 ＝ SendMessageA (集_树型框句柄, #TVM_GETNEXTITEM, #TVGN_NEXT, 一级项目) ' 取兄弟
        .如果真 (一级项目 ＝ 0)
            跳出循环 ()
        .如果真结束
        
        图片索引 ＝ 取图片_句柄 (一级项目)
        树型框.加入项目1 (, 取项目文本_句柄 (一级项目), 图片索引, 图片索引, 取项目数值_句柄 (一级项目), 真)
        树型框.更新缓存 ()
        .如果真 (取项目文本_句柄 (一级项目) ＝ “”)
            暂停 ()
        .如果真结束
        
        父项目索引 ＝ 索引
        内部_递归子项目 (集_树型框句柄, 一级项目, 欲查找的文本, 索引, 被查找索引, 父项目索引, 树型框, , 是否同步图标, 搜索结果数量)
        .如果真 (树型框.取子项目数 (父项目索引) ＝ 0)
            树型框.删除项目 (父项目索引)
            索引 ＝ 索引 － 1
        .如果真结束
        
    .判断循环尾 ()
    .如果真 (搜索时禁止重绘)
        发送消息整数 (结果树型框句柄, #WM_SETREDRAW, 1, 0)
    .如果真结束
    .如果真 (查找结果是否扩展)
        树型框.扩展全部 ()
    .如果真结束
    内部_加入父项目 (, -1, , , , )
    树型框.置现行选中项 (0)
    取项目文本_句柄1 (0, 真)
    返回 (搜索结果数量)

.子程序 内部_递归子项目, 逻辑型, , 【查找项目_外部】
    .参数 树型框句柄, 整数型
    .参数 父项目句柄, 整数型, 可空
    .参数 欲查找的文本, 文本型
    .参数 索引, 整数型, 参考
    .参数 被查找索引, 整数型, 参考
    .参数 主父项目索引, 整数型, 可空
    .参数 树型框, 外部树型框, 可空
    .参数 有查找到结果, 逻辑型, 参考 可空
    .参数 是否同步图标, 逻辑型, 可空, 是否同步项目的图标，会影响速度
    .参数 搜索结果数量, 整数型, 参考
    .局部变量 局_子项目, 整数型
    .局部变量 局_文本, 文本型
    .局部变量 内部_父项目索引, 整数型
    .局部变量 图片索引, 整数型
    .局部变量 Temp, 文本型

    局_子项目 ＝ SendMessageA (树型框句柄, #TVM_GETNEXTITEM, #TVGN_CHILD, 父项目句柄) ' 取子项目的句柄
    .如果真 (局_子项目 ＝ 0)
        返回 (假)
    .如果真结束
    .判断循环首 (局_子项目 ＞ 0)
        被查找索引 ＝ 被查找索引 ＋ 1
        局_文本 ＝ 取项目文本_句柄1 (局_子项目, )
        .判断开始 (SendMessageA (树型框句柄, #TVM_GETNEXTITEM, #TVGN_CHILD, 局_子项目) ＞ 0) ' 取子程序下是否有子
            索引 ＝ 索引 ＋ 1
            有查找到结果 ＝ 假
            .如果真 (是否同步图标)
                图片索引 ＝ 取图片_句柄 (局_子项目)
            .如果真结束
            
            ' 搜索回调
            .如果真 (_搜索回调Call ＞ 0)
                Temp ＝ “正在搜索[” ＋ 局_文本 ＋ “]...”
                程序_Call (_搜索回调Call, 取指针文本_ (Temp), 0, , , )
            .如果真结束
            
            内部_父项目索引 ＝ 内部_加入父项目 (树型框, 主父项目索引, 局_文本, 局_子项目, 索引, 图片索引, 被查找索引)
            内部_递归子项目 (树型框句柄, 局_子项目, 欲查找的文本, 索引, 被查找索引, 内部_父项目索引, 树型框, 有查找到结果, 是否同步图标, 搜索结果数量)
            
            .判断开始 (树型框.取子项目数 (内部_父项目索引) ＝ 0 且 寻找文本 (局_文本, 欲查找的文本, , 真) ＝ -1)
                索引 ＝ 索引 － 1
                树型框.删除项目 (内部_父项目索引)
                
            .默认
                搜索结果数量 ＝ 搜索结果数量 ＋ 1
            .判断结束
            
        .判断 (寻找文本 (局_文本, 欲查找的文本, , 真) ≠ -1)
            搜索结果数量 ＝ 搜索结果数量 ＋ 1
            索引 ＝ 索引 ＋ 1
            有查找到结果 ＝ 真
            .如果真 (是否同步图标)
                图片索引 ＝ 取图片_句柄 (局_子项目)
            .如果真结束
            树型框.加入项目1 (主父项目索引, 局_文本, 图片索引, 图片索引, 被查找索引)
        .默认
            
        .判断结束
        局_子项目 ＝ SendMessageA (树型框句柄, #TVM_GETNEXTITEM, #TVGN_NEXT, 局_子项目) ' 取兄弟
    .判断循环尾 ()
    返回 (真)

.子程序 内部_加入父项目, 整数型, , 【查找项目_外部】
    .参数 树型框, 外部树型框, 可空
    .参数 主父项目索引, 整数型, 可空, -1=初始化
    .参数 项目文本, 文本型, 可空
    .参数 父项目句柄, 整数型, 可空
    .参数 索引, 整数型, 可空
    .参数 图片索引, 整数型, 可空
    .参数 被查找索引, 整数型, 可空
    .局部变量 句项数组, 整数型, 静态, "0"
    .局部变量 索引数组, 整数型, 静态, "0"
    .局部变量 i, 整数型

    .如果真 (主父项目索引 ＝ -1)
        清除数组 (句项数组)
        清除数组 (索引数组)
        返回 (0)
    .如果真结束
    .计次循环首 (取数组成员数 (索引数组), i)
        .如果真 (句项数组 [i] ＝ 父项目句柄)
            返回 (索引数组 [i])
        .如果真结束
        
    .计次循环尾 ()
    树型框.加入项目1 (主父项目索引, 项目文本, 图片索引, 图片索引, 被查找索引, 真)
    树型框.更新缓存 ()
    加入成员 (句项数组, 父项目句柄)
    加入成员 (索引数组, 索引)
    返回 (索引)

.子程序 取子项目数, 整数型, 公开, 获取当前选中项目的子项目数量
    .参数 项目索引, 整数型, 可空, 可空，默认取选中项的子项目数
    .局部变量 局_当前项目句柄, 整数型
    .局部变量 局_数量, 整数型
    .局部变量 局_子项目句柄, 整数型

    .如果 (是否为空 (项目索引))
        局_当前项目句柄 ＝ SendMessageA (集_树型框句柄, #TVM_GETNEXTITEM, 9, 0)
        
    .否则
        .如果真 (项目索引 ＋ 1 ＞ 取数组成员数 (集_句柄组) 或 项目索引 ＜ 0)
            返回 (0)
        .如果真结束
        局_当前项目句柄 ＝ 集_句柄组 [项目索引 ＋ 1]
    .如果结束
    
    局_子项目句柄 ＝ SendMessageA (集_树型框句柄, #TVM_GETNEXTITEM, #TVGN_CHILD, 局_当前项目句柄) ' 取第一个子项目的句柄
    局_数量 ＝ 0
    .判断循环首 (局_子项目句柄 ＞ 0)
        局_数量 ＝ 局_数量 ＋ 1
        局_子项目句柄 ＝ SendMessageA (集_树型框句柄, #TVM_GETNEXTITEM, #TVGN_NEXT, 局_子项目句柄) ' 取同级的下一个项目句柄
    .判断循环尾 ()
    返回 (局_数量)

.子程序 双击项目, , 公开
    .参数 项目句柄, 整数型
    .局部变量 精易_矩形, 精易_矩形J

    .计次循环首 (2, )
        精易_矩形.左边 ＝ 项目句柄
        SendMessageA (集_树型框句柄, 4356, 0, 取数据_通用型 (精易_矩形)) ' 取矩形
        SendMessageA (集_树型框句柄, 4372, 0, 项目句柄) ' 保证显示
    .计次循环尾 ()
    鼠标_消息 (集_树型框句柄, 精易_矩形.左边 ＋ 50, 精易_矩形.顶边 ＋ 10, , 2)
    鼠标_移动 (集_树型框句柄, 精易_矩形.左边 ＋ 50, 精易_矩形.顶边 ＋ 10)

.子程序 取所有父项目, 整数型, 公开, 获取所有有子项目的项目索引，成功返回获取到的数量
    .参数 项目索引数组, 整数型, 参考 数组, 用于存放父项目索引的数组
    .参数 排除父项目, 文本型, 可空, 如：类，将不获取类下的所有有子项目的父项目索引
    .参数 缩进层次, 整数型, 可空, 可空，默认为所有。获取小于或等于这个层次的父项目，缩进层次从1开始
    .参数 指定缩进层次, 整数型, 可空, 可空，默认为所有。如果有指定，第23参数将无效，只获取指定缩进层次的父项目
    .局部变量 i, 整数型
    .局部变量 局_缩进层次, 整数型
    .局部变量 局_父项目名称, 文本型

    清除数组 (项目索引数组)
    .计次循环首 (取项目数 (), i)
        .如果真 (是否有子项目 (i － 1) ＝ 假)
            到循环尾 ()
        .如果真结束
        
        .判断开始 (是否为空 (指定缩进层次) ＝ 假)
            局_缩进层次 ＝ 取缩进层次 (i － 1)
            .如果真 (局_缩进层次 ≠ 指定缩进层次)
                到循环尾 ()
            .如果真结束
            
        .默认
            .如果真 (缩进层次 ≠ 0)
                局_缩进层次 ＝ 取缩进层次 (i － 1)
                .如果真 (局_缩进层次 ＞ 缩进层次)
                    到循环尾 ()
                .如果真结束
                
            .如果真结束
            .如果真 (是否为空 (排除父项目) ＝ 假)
                局_父项目名称 ＝ 取项目文本 (取父项目 (i － 1))
                .如果真 (局_父项目名称 ＝ 排除父项目)
                    到循环尾 ()
                .如果真结束
                
            .如果真结束
            
        .判断结束
        加入成员 (项目索引数组, i － 1)
    .计次循环尾 ()
    返回 (取数组成员数 (项目索引数组))

.子程序 取上一个同级项目, 整数型, 公开
    .参数 项目索引, 整数型
    .局部变量 局_成员数, 整数型
    .局部变量 局_项目索引, 整数型
    .局部变量 i, 整数型

    局_成员数 ＝ 取数组成员数 (集_句柄组)
    .如果真 (项目索引 ＜ 0 或 项目索引 ≥ 局_成员数)
        返回 (-1)
    .如果真结束
    ' .常量 TVM_GETNEXTITEM, "4362", , 获取与树视图控件中的指定项目具有指定关系的项目
    ' .常量 TVGN_PREVIOUS, "2", , 获取指定项目的同一组中的上一个项目
    局_项目索引 ＝ 发送消息整数 (集_树型框句柄, 4362, 2, 集_句柄组 [项目索引 ＋ 1])
    .如果真 (局_项目索引 ＝ 0)
        返回 (-1)
    .如果真结束
    .计次循环首 (局_成员数, i)
        .如果真 (集_句柄组 [i] ＝ 局_项目索引)
            返回 (i － 1)
        .如果真结束
        
    .计次循环尾 ()
    返回 (-1)

.子程序 取下一个同级项目, 整数型, 公开
    .参数 项目索引, 整数型
    .局部变量 局_成员数, 整数型
    .局部变量 局_项目索引, 整数型
    .局部变量 i, 整数型

    局_成员数 ＝ 取数组成员数 (集_句柄组)
    .如果真 (项目索引 ＜ 0 或 项目索引 ≥ 局_成员数)
        返回 (-1)
    .如果真结束
    ' .常量 TVM_GETNEXTITEM, "4362", , 获取与树视图控件中的指定项目具有指定关系的项目
    ' .常量 TVGN_NEXT, "1", , 获取与指定项目相同的组中的下一个项目
    局_项目索引 ＝ 发送消息整数 (集_树型框句柄, 4362, 1, 集_句柄组 [项目索引 ＋ 1])
    .如果真 (局_项目索引 ＝ 0)
        返回 (-1)
    .如果真结束
    .计次循环首 (局_成员数, i)
        .如果真 (集_句柄组 [i] ＝ 局_项目索引)
            返回 (i － 1)
        .如果真结束
        
    .计次循环尾 ()
    返回 (-1)

