.版本 2

.程序集 集_对话框
.子程序 对话框_打开文件, 文本型, 公开, 成功打开返回文件路径,失败返回空文本
    .参数 窗口句柄, 整数型, , 拥有者窗口句柄,如启动窗口的句柄
    .参数 初始目录, 文本型, 可空, 指定当打开对话框时所自动跳转到的目录,如果留空将跳转到当前目录
    .参数 对话框标题, 文本型, 可空, 对话框的标题，可空:默认为"打开文件"
    .参数 过滤器, 文本型, 可空, 可空，默认为“所有文件(*.*)|*.*”  过滤器文本由单个或多个成对的文本串组成，每对文本串的第一个描述显示形式，如：“文本文件（*.txt）”；第二个指定实际的过滤匹配符，如：“*.txt”，所有各文本串之间用“|”号隔开
    .参数 修改当前目录, 逻辑型, 可空, 可以为空。默认为假，不改变当前目录。真 = 把初始目录改成当前目录。
    .局部变量 局_结构, OPENFILENAME
    .局部变量 局_计次, 整数型
    .局部变量 局_数组, 文本型, , "0"
    .局部变量 局_当前目录, 文本型

    局_当前目录 ＝ 取当前目录 ()
    .如果真 (是否为空 (对话框标题))
        对话框标题 ＝ “打开文件”
    .如果真结束
    .如果真 (是否为空 (过滤器))
        过滤器 ＝ “所有文件(*.*)|*.*”
    .如果真结束
    局_结构.结构大小 ＝ 76
    局_结构.窗口句柄 ＝ 窗口句柄
    局_结构.实例句柄 ＝ 0
    局_数组 ＝ 分割文本 (过滤器, “|”, )
    .计次循环首 (取数组成员数 (局_数组), 局_计次)
        .如果 (取数组成员数 (局_数组) ＝ 局_计次)
            局_结构.过滤器 ＝ 局_结构.过滤器 ＋ 到字节集 (局_数组 [局_计次]) ＋ { 0, 0 }
        .否则
            局_结构.过滤器 ＝ 局_结构.过滤器 ＋ 到字节集 (局_数组 [局_计次]) ＋ { 0 }
        .如果结束
        
    .计次循环尾 ()
    局_结构.自定义过滤器 ＝ 字符 (0)
    局_结构.自定义过滤器最大长度 ＝ 0
    局_结构.过滤器索引 ＝ 1
    局_结构.文件名 ＝ { 0 } ＋ 取空白字节集 (512)
    局_结构.文件名最大长度 ＝ 512
    局_结构.文件标题 ＝ 取空白文本 (512)
    局_结构.文件标题最大长度 ＝ 512
    局_结构.初始目录 ＝ 选择 (是否为空 (初始目录), 局_当前目录, 初始目录)
    局_结构.标题 ＝ 对话框标题
    局_结构.标志 ＝ 0
    局_结构.文件扩展名 ＝ 0
    .如果 (GetOpenFileNameA (局_结构))
        .如果真 (修改当前目录 ＝ 假)
            改变目录 (局_当前目录)
        .如果真结束
        返回 (到文本 (局_结构.文件名))
    .否则
        .如果真 (修改当前目录 ＝ 假)
            改变目录 (局_当前目录)
        .如果真结束
        返回 (“”)
    .如果结束
    

.子程序 对话框_另存文件, 文本型, 公开, 成功打开返回文件路径,失败返回空文本
    .参数 窗口句柄, 整数型, , 拥有者窗口句柄,如启动窗口的句柄
    .参数 初始目录, 文本型, 可空, 指定当打开对话框时所自动跳转到的目录,如果留空将跳转到当前目录
    .参数 对话框标题, 文本型, 可空, 对话框的标题，可空:默认为"保存文件"
    .参数 默认文件名, 文本型, 可空
    .参数 过滤器, 文本型, 可空, 可空，过滤器文本由单个或多个成对的文本串组成，每对文本串的第一个描述显示形式，如：“文本文件（*.txt）”；第二个指定实际的过滤匹配符，如：“*.txt”，所有各文本串之间用“|”号隔开
    .参数 初始过滤器, 整数型, 可空, 可空，默认为1，指定初始的过滤器，1为第一个过滤器
    .参数 修改当前目录, 逻辑型, 可空, 可以为空。默认为假，不改变当前目录。真 = 把初始目录改成当前目录。
    .局部变量 过滤器索引, 整数型
    .局部变量 局_结构, OPENFILENAME
    .局部变量 局_计次, 整数型
    .局部变量 局_数组, 文本型, , "0"
    .局部变量 局_当前目录, 文本型

    局_当前目录 ＝ 取当前目录 ()
    .如果真 (是否为空 (对话框标题))
        对话框标题 ＝ “保存文件”
    .如果真结束
    .如果真 (是否为空 (过滤器))
        过滤器 ＝ “TXT文档(*.txt)|*.txt|所有文件(*.*)|*.*”
    .如果真结束
    .如果真 (初始过滤器 ＝ 0)
        初始过滤器 ＝ 1
    .如果真结束
    .如果真 (是否为空 (默认文件名))
        默认文件名 ＝ 到文本 ({ 0 })
    .如果真结束
    
    局_结构.结构大小 ＝ 76
    局_结构.窗口句柄 ＝ 窗口句柄
    局_结构.实例句柄 ＝ 0
    局_数组 ＝ 分割文本 (过滤器, “|”, )
    .计次循环首 (取数组成员数 (局_数组), 局_计次)
        .如果 (取数组成员数 (局_数组) ＝ 局_计次)
            局_结构.过滤器 ＝ 局_结构.过滤器 ＋ 到字节集 (局_数组 [局_计次]) ＋ { 0, 0 }
        .否则
            局_结构.过滤器 ＝ 局_结构.过滤器 ＋ 到字节集 (局_数组 [局_计次]) ＋ { 0 }
        .如果结束
        
    .计次循环尾 ()
    局_结构.自定义过滤器 ＝ 字符 (0)
    局_结构.自定义过滤器最大长度 ＝ 0
    局_结构.过滤器索引 ＝ 初始过滤器
    ' 局_结构.文件名 ＝ { 0 } ＋ 取空白字节集 (512)
    ' 局_结构.文件名最大长度 ＝ 512
    局_结构.文件名 ＝ 到字节集 (默认文件名) ＋ 取空白字节集 (512 － 取字节集长度 (到字节集 (默认文件名)))
    局_结构.文件名最大长度 ＝ 512 － 取字节集长度 (到字节集 (默认文件名))
    
    局_结构.文件标题 ＝ 取空白文本 (512)
    局_结构.文件标题最大长度 ＝ 512
    局_结构.初始目录 ＝ 选择 (是否为空 (初始目录), 局_当前目录, 初始目录)
    
    局_结构.标题 ＝ 对话框标题
    局_结构.标志 ＝ 0
    局_结构.文件扩展名 ＝ 0
    局_结构.默认扩展名 ＝ “”
    .如果 (GetSaveFileNameA (局_结构))
        .如果真 (修改当前目录 ＝ 假)
            改变目录 (局_当前目录)
        .如果真结束
        返回 (到文本 (局_结构.文件名))
    .否则
        .如果真 (修改当前目录 ＝ 假)
            改变目录 (局_当前目录)
        .如果真结束
        返回 (“”)
    .如果结束
    

.子程序 对话框_打开颜色选择框, 整数型, 公开, 打开颜色选择框,成功返回指定的颜色值,取消或失败返回-1
    .参数 窗口句柄, 整数型, , 拥有者窗口句柄
    .局部变量 颜色对话框, 精易_颜色对话框
    .局部变量 局_成功, 整数型

    颜色对话框.窗口句柄 ＝ 窗口句柄
    颜色对话框.颜色数长度 ＝ 取空白文本 (15)
    颜色对话框.长度 ＝ 36
    局_成功 ＝ ChooseColorA (颜色对话框)
    .如果真 (局_成功 ＝ 1)
        返回 (颜色对话框.rgbResult)
    .如果真结束
    返回 (-1)

.子程序 对话框_打开文件多选框, 文本型, 公开, 打开多选文件对话框,可单个一个文件,将直接返回文件路径,如果选择多个文件,将以换行符分隔
    .参数 窗口句柄, 整数型, 可空, 拥有者窗口句柄
    .参数 窗口标题, 文本型, 可空, 对话框窗口标题
    .参数 过滤器, 文本型, 可空, 可为空,默认即所有文件,过滤器文本由单个或多个成对的文本串组成，每对文本串的第一个描述显示形式，如：“文本文件（*.txt）”；第二个指定实际的过滤匹配符，如：“*.txt”，所有各文本串之间用“|”号隔开
    .参数 初始目录, 文本型, 可空, 可以为空。如果留空将跳转到当前目录
    .参数 修改当前目录, 逻辑型, 可空, 可以为空。默认为假，不改变当前目录。真 = 把初始目录改成当前目录。
    .局部变量 文件列表, OPENFILENAME
    .局部变量 文件名, 字节集
    .局部变量 文件名数组, 文本型, , "0"
    .局部变量 局_计次, 整数型, , , 记录循环的次数
    .局部变量 多选文件, 文本型
    .局部变量 文件缓冲数, 整数型, , , 决定可以打开文件的个数，原为8192，可打开80个文件
    .局部变量 局_当前目录, 文本型

    局_当前目录 ＝ 取当前目录 ()
    文件缓冲数 ＝ 98192 ' 大小决定打开文件的数量，此可以打开200个文件
    文件列表.结构大小 ＝ 76 ' 文件列表.lStructSize ＝ 76
    文件列表.窗口句柄 ＝ 窗口句柄
    文件列表.过滤器 ＝ 子字节集替换 (到字节集 (过滤器), { 124 }, { 0 }, , ) ＋ { 0 } ＋ { 0 }
    文件列表.文件名 ＝ 取空白字节集 (文件缓冲数)
    文件列表.文件名最大长度 ＝ 文件缓冲数
    文件列表.文件标题 ＝ 取空白文本 (文件缓冲数)
    文件列表.文件标题最大长度 ＝ 文件缓冲数
    文件列表.初始目录 ＝ 选择 (是否为空 (初始目录), 局_当前目录, 初始目录)
    文件列表.标题 ＝ 窗口标题
    文件列表.标志 ＝ 524800
    
    .如果 (GetOpenFileNameA (文件列表)) ' 打开文件成功
        文件名 ＝ 文件列表.文件名 ' 文件名 ＝ 文件列表.lpstrFile
        文件名 ＝ 取字节集左边 (文件名, 寻找字节集 (文件名, { 0, 0 }, ) － 1)
        .如果 (寻找字节集 (文件名, { 0 }, ) ≠ -1) ' 是否选择多个文件
            文件名数组 ＝ 分割文本 (到文本 (子字节集替换 (文件名, { 0 }, { 124 }, , )), “|”, )
            .如果真 (取文本右边 (文件名数组 [1], 1) ≠ “\”) ' 选择多个文件时，第一个数组为路径名，后面为不包括路径的文件名
                文件名数组 [1] ＝ 文件名数组 [1] ＋ “\”
            .如果真结束
            .计次循环首 (取数组成员数 (文件名数组) － 1, 局_计次)
                .如果 (取数组成员数 (文件名数组) － 1 ≠ 局_计次) ' 最后一个文件名不用加分隔字符“；”
                    多选文件 ＝ 多选文件 ＋ 文件名数组 [1] ＋ 文件名数组 [局_计次 ＋ 1] ＋ #换行符
                .否则
                    多选文件 ＝ 多选文件 ＋ 文件名数组 [1] ＋ 文件名数组 [局_计次 ＋ 1]
                .如果结束
                
            .计次循环尾 ()
            .如果真 (修改当前目录 ＝ 假)
                改变目录 (局_当前目录)
            .如果真结束
            返回 (多选文件)
        .否则
            .如果真 (修改当前目录 ＝ 假)
                改变目录 (局_当前目录)
            .如果真结束
            返回 (到文本 (文件名))
        .如果结束
        
    .否则
        返回 (“”)
    .如果结束
    

.子程序 对话框_整理收藏夹, , 公开
    .参数 窗口句柄, 整数型, , 直接用 取窗口句柄() 命令即可

    DoOrganizeFavDlg (窗口句柄, 目录_取特定目录 (2))

.子程序 对话框_添加到收藏夹, , 公开
    .参数 窗口句柄, 整数型, , 直接用 取窗口句柄() 命令即可
    .参数 网页标题, 文本型
    .参数 网页地址, 文本型
    .局部变量 局_pid, 整数型
    .局部变量 局_写入路径, 文本型

    局_写入路径 ＝ 目录_取特定目录 (2)
    .如果真 (网页标题 ＝ “” 或 网页地址 ＝ “about:blank”)
        返回 ()
    .如果真结束
    SHGetSpecialFolderLocation (窗口句柄, 6, 局_pid)
    .如果真 (DoAddToFavDlg (窗口句柄, 局_写入路径, 256, 网页标题, 256, 局_pid))
        .如果真 (写配置项 (局_写入路径, “InternetShortcut”, “URL”, 网页地址))
            提示框J (“已经添加到收藏夹”, #信息图标, , 窗口句柄)
        .如果真结束
        
    .如果真结束
    CoTaskMemFree (局_pid)

