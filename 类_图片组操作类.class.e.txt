.版本 2

.程序集 类_图片组操作类, , 公开
.程序集变量 集_图片组句柄, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用
    

.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用
    

.子程序 销毁图片组, 整数型, 公开, 成功返回非零,失败返回0
    返回 (ImageList_Destroy (集_图片组句柄))

.子程序 创建图片组, 字节集, 公开, 创建一个空的图片组，成功返回所创建图片组的数据，失败返回空字节集
    .参数 参_宽度, 整数型, 可空, 欲创建图片组的宽度,默认为32
    .参数 参_高度, 整数型, 可空, 欲创建图片组的高度,默认为32
    .参数 参_位深, 整数型, 可空, 默认为32
    .参数 图片组背景颜色, 整数型, 可空, 默认为白色

    .如果真 (是否为空 (参_宽度))
        参_宽度 ＝ 32
    .如果真结束
    .如果真 (是否为空 (参_高度))
        参_高度 ＝ 32
    .如果真结束
    .如果真 (是否为空 (参_位深))
        参_位深 ＝ 32
    .如果真结束
    集_图片组句柄 ＝ ImageList_Create (参_宽度, 参_高度, 参_位深, 0, 假)
    .如果 (是否为空 (图片组背景颜色))
        置图片组背景颜色 (#白色)
    .否则
        置图片组背景颜色 (图片组背景颜色)
    .如果结束
    返回 (导出图片组 ())

.子程序 删除图标, 逻辑型, 公开, 删除图片组中指定位置的图标,成功返回真,否则返回假
    .参数 参_索引位置, 整数型, , 要删除的图标位置,索引从0开始

    返回 (ImageList_Remove (集_图片组句柄, 参_索引位置))

.子程序 清空, 逻辑型, 公开, 成功返回真,否则返回假
    返回 (ImageList_Remove (集_图片组句柄, -1))

.子程序 取图片组尺寸, 逻辑型, 公开, 取得图片组的图标尺寸,成功返回真,否则返回假
    .参数 参_宽度, 整数型, 参考, 此变量用于存放图片组图标宽度
    .参数 参_高度, 整数型, 参考, 此变量用于存放图片组图标高度

    返回 (ImageList_GetIconSize (集_图片组句柄, 参_宽度, 参_高度))

.子程序 取图片组图标数, 整数型, 公开, 取出图片组的图标数
    返回 (ImageList_GetImageCount (集_图片组句柄))

.子程序 置图片组图标尺寸, 逻辑型, 公开, 设置图片组的图标大小,成功返回真,否则返回假
    .参数 参_宽度, 整数型, , 要设置的图标组图标宽度
    .参数 参_高度, 整数型, , 要设置的图标组图标高度

    返回 (ImageList_SetIconSize (集_图片组句柄, 参_宽度, 参_高度))

.子程序 置图片组图标数, 逻辑型, 公开, 成功返回真,否则返回假
    .参数 图标数量, 整数型, , 要设置的图片组图标数量

    返回 (ImageList_SetImageCount (集_图片组句柄, 图标数量))

.子程序 取图片组背景颜色, 整数型, 公开, 返回图片组的背景颜色
    返回 (ImageList_GetBkColor (集_图片组句柄))

.子程序 置图片组背景颜色, 逻辑型, 公开, 成功返回真,否则返回假
    .参数 背景颜色值, 整数型, , 要设置的图片组背景颜色值

    返回 (ImageList_SetBkColor (集_图片组句柄, 背景颜色值))

.子程序 取图片组句柄, 整数型, 公开, 返回创建成功后的图片组句柄
    返回 (集_图片组句柄)

.子程序 置图片组句柄, , 公开, 设置要操作的图片组句柄
    .参数 句柄, 整数型, , 要设置要操作的图片组句柄

    集_图片组句柄 ＝ 句柄

.子程序 替换图标, 逻辑型, 公开, 根据图标句柄替换指定索引的图标,成功返回真,失败返回假
    .参数 图标句柄, 整数型, , 欲替换成的ico图标的句柄,可用命令 载入图标_ 返回的句柄
    .参数 替换位置索引, 整数型, , 欲替换图片组的索引位置,0为第一位置,如果为空表示插入到尾部

    返回 (ImageList_ReplaceIcon (集_图片组句柄, 替换位置索引, 图标句柄) ≠ -1)

.子程序 替换位图, 整数型, 公开, 成功返回非0失败返回0
    .参数 位图句柄, 整数型, , 欲替换成的位图的句柄,可用命令 载入位图_ 返回的句柄
    .参数 替换位置索引, 整数型, , 欲替换图片组的索引位置,0为第一位置

    返回 (ImageList_Replace (集_图片组句柄, 替换位置索引, 位图句柄, 0))

.子程序 导出图片组_其它语言, 字节集, , 取回图片组的数据
    .局部变量 字节流, 字节流
    .局部变量 返回值, 逻辑型
    .局部变量 图片组字节集, 字节集

    字节流.创建字节流 ()
    返回值 ＝ ImageList_Write (集_图片组句柄, 字节流.取字节流指针 ())
    .如果真 (返回值)
        图片组字节集 ＝ 字节流.取出字节集 ()
    .如果真结束
    字节流.释放字节流 ()
    返回 (图片组字节集)

.子程序 导出图片组, 字节集, 公开, 取出为易使用的图片组字节集 
    .局部变量 字节流, 字节流
    .局部变量 返回值, 逻辑型
    .局部变量 图片组字节集, 字节集

    字节流.创建字节流 ()
    返回值 ＝ ImageList_Write (集_图片组句柄, 字节流.取字节流指针 ())
    .如果 (返回值)
        图片组字节集 ＝ { 77, 73, 84, 76, 0, 0, 0, 255 } ＋ 字节流.取出字节集 ()
    .否则
        信息框 (“取出失败”, 0, )
    .如果结束
    字节流.释放字节流 ()
    返回 (图片组字节集)

.子程序 导入图片组_其它语言, 逻辑型, , 将图片组字节集数据读入到本类中操作使用,成功返回真,失败返回假
    .参数 图片组字节集, 字节集, , 要读入的图片组数据
    .局部变量 字节流, 字节流

    字节流.创建字节流 (图片组字节集)
    集_图片组句柄 ＝ ImageList_Read (字节流.取字节流指针 ()) ' 将图片组句柄赋值
    字节流.释放字节流 ()
    返回 (集_图片组句柄 ≠ 0) ' 成功返回图片组句柄

.子程序 导入图片组, 逻辑型, 公开, 将易图片组读入为类图片组
    .参数 图片组字节集, 字节集, , 要读入的图片组数据
    .局部变量 字节流, 字节流

    图片组字节集 ＝ 取字节集中间 (图片组字节集, 9, 取字节集长度 (图片组字节集) － 8) ' 前面的八个字节为易图片组标志头  MITL.... 的字节集数据
    字节流.创建字节流 (图片组字节集)
    集_图片组句柄 ＝ ImageList_Read (字节流.取字节流指针 ()) ' 将图片组句柄赋值
    字节流.释放字节流 ()
    返回 (集_图片组句柄 ≠ 0) ' 成功返回图片组句柄

.子程序 取图标句柄_从文件, 整数型, 公开, 载入指定图标,成功返回图标句柄,失败返回0
    .参数 图标文件路径, 文本型, , 欲载入的图标文件全路径

    返回 (LoadImage (0, 图标文件路径, 1, 0, 0, 16))

.子程序 取图片句柄_从文件, 整数型, 公开, 载入指定位图,成功返回位图句柄,失败返回0
    .参数 位图文件路径, 文本型, , 欲载入的位图文件全路径,只能为BMP格式位图文件路径

    返回 (LoadImage (0, 位图文件路径, 0, 0, 0, 16))

.子程序 取图片句柄_从字节集, 整数型, 公开, 载入指定图片,或者图标.成功返回位图句柄,失败返回0
    .参数 图片字节集, 字节集, , 欲载入的bmp,jpg等图片资源或者ico图标字节集
    .局部变量 图片对象, 对象, 静态
    .局部变量 图片句柄, 整数型

    CoInitialize (0)
    图片对象.创建图片对象 (图片字节集)
    图片句柄 ＝ 图片对象.读数值属性 (“Handle”, )
    CoUninitialize ()
    返回 (图片句柄)

.子程序 加入图标_从字节集, 逻辑型, 公开
    .参数 图片字节集, 字节集, , 要加入的bmp,jpg等图片资源或者ico图标字节集

    返回 (加入图标_从句柄 (取图片句柄_从字节集 (图片字节集)) ≠ -1)

.子程序 加入位图_从文件, 整数型, 公开, 成功返回添加位图后的图片索引,失败返回 -1
    .参数 位图文件路径, 文本型, , 要添加的bmp位图文件路径,只能为bmp格式图片,如果这个位图尺寸大于了创建图片组的尺寸,将自动被分割成多个图片,自动添加到图片组中
    .局部变量 位图句柄, 整数型

    位图句柄 ＝ 取图片句柄_从文件 (位图文件路径)
    返回 (ImageList_Add (集_图片组句柄, 位图句柄, 0))

.子程序 加入位图_从句柄, 整数型, 公开, 成功返回添加位图后的图片索引,失败返回 -1
    .参数 位图句柄, 整数型, , 可用命令 载入位图_从字节集_ ,载入位图_从文件_ 返回的句柄,欲添加的bmp位图图片句柄,只能为bmp格式图片,如果这个位图尺寸大于了创建图片组的尺寸,将自动被分割成多个图片,自动添加到图片组中

    返回 (ImageList_Add (集_图片组句柄, 位图句柄, 0))

.子程序 加入图标_从文件, 整数型, 公开, 成功返回添加图标后的图片索引,失败返回 -1
    .参数 图标文件路径, 文本型, , 欲添加的ico图标文件路径
    .局部变量 图标句柄, 整数型

    图标句柄 ＝ 取图标句柄_从文件 (图标文件路径)
    返回 (ImageList_AddIcon (集_图片组句柄, 图标句柄))

.子程序 加入图标_从句柄, 整数型, 公开, 成功返回添加图标后的图片索引,失败返回 -1
    .参数 图标句柄, 整数型, , 欲添加的ico图标的句柄,可用命令 取图标句柄_从文件 返回的句柄

    返回 (ImageList_AddIcon (集_图片组句柄, 图标句柄))

