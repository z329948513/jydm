.版本 2

.程序集 集_网页
.程序集变量 集_屏蔽信息框线程句柄, 整数型, , , 开始屏蔽信息框
.程序集变量 集_点击信息框标识文本, 文本型, , , 点击屏蔽信息框
.程序集变量 集_按钮的标题, 文本型, , , 点击屏蔽信息框

.子程序 网页_取IP地址, 精易_IP归属地, 公开
    .局部变量 局_网址, 文本型
    .局部变量 局_返回, 文本型
    .局部变量 局_Json, 类_json2
    .局部变量 局_归属地, 精易_IP归属地

    局_网址 ＝ “http://ip.taobao.com/service/getIpInfo2.php?ip=myip”
    局_返回 ＝ 编码_Utf8到Ansi (网页_访问J (局_网址))
    局_返回 ＝ 子文本替换 (局_返回, 字符 (10), , , , 真)
    局_Json.解析 (局_返回)
    局_归属地.IP ＝ 局_Json.取通用属性 (“data.ip”)
    局_归属地.国家 ＝ 局_Json.取通用属性 (“data.country”)
    局_归属地.省份 ＝ 局_Json.取通用属性 (“data.region”)
    局_归属地.城市 ＝ 局_Json.取通用属性 (“data.city”)
    局_归属地.运营商 ＝ 局_Json.取通用属性 (“data.isp”)
    返回 (局_归属地)

.子程序 网页_IE浏览器网页跳转静音, , 公开, 默认为真  真=静音
    .参数 参_是否静音, 逻辑型, 可空

    .如果真 (是否为空 (参_是否静音))
        参_是否静音 ＝ 真
    .如果真结束
    CoInternetSetFeatureEnabled (#FEATURE_DISABLE_NAVIGATION_SOUNDS, #SET_FEATURE_ON_PROCESS, 参_是否静音) ' 21

.子程序 网页_协议头_取信息, 文本型, 公开, 取出协议头中的某个值 例如302跳转协议头中的 Location
    .参数 参_源协议头, 文本型
    .参数 参_键名, 文本型, , 例如：Location
    .局部变量 局_数组, 文本型, , "0"
    .局部变量 i, 整数型
    .局部变量 局_数据, 文本型
    .局部变量 局_长度, 整数型

    局_数组 ＝ 分割文本 (参_源协议头, #换行符, )
    局_长度 ＝ 取文本长度 (参_键名)
    .计次循环首 (取数组成员数 (局_数组), i)
        .如果真 (取文本左边 (局_数组 [i], 局_长度) ＝ 参_键名)
            局_数据 ＝ 文本_取右边 (局_数组 [i], 参_键名 ＋ “: ”)
            返回 (局_数据)
        .如果真结束
        
    .计次循环尾 ()
    返回 (“”)
    
    

.子程序 网页_JS格式化_EX, 文本型, 公开
    .参数 参数1, 文本型
    .局部变量 JS命令, 类_脚本组件
    .局部变量 结果, 文本型

    JS命令.执行 (#JS格式化EX)
    结果 ＝ JS命令.运行 (“FJsDecoder”, 参数1)
    返回 (结果)

.子程序 网页_文字翻译, 文本型, 公开, 调用 有道翻译  支持中翻英  或  多语翻中
    .参数 参_待翻译, 文本型
    .局部变量 局_网址, 文本型
    .局部变量 局_方式, 整数型
    .局部变量 ADD_协议头, 类_POST数据类
    .局部变量 局_提交协议头, 文本型
    .局部变量 局_结果, 字节集
    .局部变量 局_返回, 文本型
    .局部变量 局_待翻译, 文本型

    
    局_待翻译 ＝ 编码_URL编码 (参_待翻译, , 真)
    局_网址 ＝ “http://fanyi.youdao.com//translate?i=” ＋ 局_待翻译 ＋ “&type=AUTO&doctype=text&xmlVersion=1.1&keyfrom=360se”
    局_方式 ＝ 0
    ADD_协议头.添加 (“Connection”, “ keep-alive”)
    ADD_协议头.添加 (“Referer”, “ http://fanyi.youdao.com/translate”)
    ADD_协议头.添加 (“User-Agent”, “ Mozilla/5.0 (Windows NT 6.1; WOW64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/55.0.2883.87 Safari/537.36”)
    ADD_协议头.添加 (“Accept-Language”, “ zh-CN,zh;q=0.8”)
    局_提交协议头 ＝ ADD_协议头.获取协议头数据 ()
    局_结果 ＝ 网页_访问_对象 (局_网址, 局_方式, , , , 局_提交协议头, , , , , , , , , , , )
    局_返回 ＝ 到文本 (编码_编码转换对象 (局_结果))
    局_返回 ＝ 文本_取右边 (局_返回, “result=”)
    返回 (局_返回)
    

.子程序 网页_更改IE版本, 逻辑型, 公开, 需要XP以上系统支持，且IE版本不低于8，写入后永久生效。多数情况需获取管理员权限，详情参阅：https://docs.microsoft.com/en-us/previous-versions/windows/internet-explorer/ie-developer/general-info/ee330730(v=vs.85)#browser-emulation
    .参数 类型, 整数型, 可空, 为空或0移除写入的版本  1=ie7  2=ie8   3=ie9   4=ie10   5=ie11
    .参数 是否使用所有用户, 逻辑型, 可空, 默认为假！使用当前用户，为真则使用所有用户。
    .局部变量 局_处理器, 文本型
    .局部变量 参数, 整数型

    ' https://bbs.125.la/thread-14547669-1-1.html
    
    局_处理器 ＝ 读环境变量 (“PROCESSOR_ARCHITECTURE”)
    
    .如果真 (类型 ≤ 0 或 类型 ＞ 5)
        .如果真 (是否使用所有用户)
            .判断开始 (局_处理器 ＝ “x86”)
                返回 (删除注册项 (#本地机器, “SOFTWARE\Microsoft\Internet Explorer\MAIN\FeatureControl\FEATURE_BROWSER_EMULATION\” ＋ 取执行文件名 ()))
            .判断 (局_处理器 ＝ “AMD64”)
                返回 (删除注册项 (#本地机器, “SOFTWARE\Wow6432Node\Microsoft\Internet Explorer\MAIN\FeatureControl\FEATURE_BROWSER_EMULATION\” ＋ 取执行文件名 ()))
            .默认
                
            .判断结束
            返回 (假)
        .如果真结束
        返回 (删除注册项 (#现行用户, “Software\Microsoft\Internet Explorer\Main\FeatureControl\FEATURE_BROWSER_EMULATION\” ＋ 取执行文件名 ()))
    .如果真结束
    
    .判断开始 (类型 ＝ 1)
        参数 ＝ 7000 ' IE 7
    .判断 (类型 ＝ 2)
        参数 ＝ 8000 ' IE 8
    .判断 (类型 ＝ 3)
        参数 ＝ 9000 ' IE 9
    .判断 (类型 ＝ 4)
        参数 ＝ 10000 ' IE 10
    .判断 (类型 ＝ 5)
        参数 ＝ 11000 ' IE 11
    .默认
        
    .判断结束
    
    .如果真 (是否使用所有用户)
        .判断开始 (局_处理器 ＝ “x86”)
            返回 (写注册项 (#本地机器, “SOFTWARE\Microsoft\Internet Explorer\MAIN\FeatureControl\FEATURE_BROWSER_EMULATION\” ＋ 取执行文件名 (), 参数))
        .判断 (局_处理器 ＝ “AMD64”)
            返回 (写注册项 (#本地机器, “SOFTWARE\Wow6432Node\Microsoft\Internet Explorer\MAIN\FeatureControl\FEATURE_BROWSER_EMULATION\” ＋ 取执行文件名 (), 参数))
        .默认
            
        .判断结束
        返回 (假)
    .如果真结束
    
    返回 (写注册项 (#现行用户, “Software\Microsoft\Internet Explorer\Main\FeatureControl\FEATURE_BROWSER_EMULATION\” ＋ 取执行文件名 (), 参数))
    ' 写注册项 (#现行用户, “Software\Microsoft\Internet Explorer\Main\FeatureControl\FEATURE_GPU_RENDERING\” ＋ 取执行文件名 (), 1)

.子程序 网页_eval解密, 文本型, 公开
    .参数 原JS文本, 文本型, , 需要加密\解密的JS源文本
    .局部变量 脚本, 类_脚本组件
    .局部变量 结果, 文本型

    结果 ＝ 脚本.计算表达式 (子文本替换 (原JS文本, “eval”, “”, , , 真))
    返回 (结果)

.子程序 网页_eval加密, 文本型, 公开
    .参数 原JS文本, 文本型, , 需要加密\解密的JS源文本
    .局部变量 脚本, 类_脚本组件
    .局部变量 结果, 文本型

    脚本.执行 (#eval加密)
    结果 ＝ 脚本.运行 (“encode”, 原JS文本)
    返回 (结果)

.子程序 内部_协议头取值, 文本型
    .参数 协议头, 文本型
    .局部变量 位置, 整数型
    .局部变量 结果, 文本型

    位置 ＝ 寻找文本 (协议头, “:”, , 假)
    .如果真 (位置 ≠ -1)
        结果 ＝ 取文本右边 (协议头, 取文本长度 (协议头) － 位置)
    .如果真结束
    返回 (删首尾空 (结果))

.子程序 内部_协议头取名, 文本型
    .参数 协议头, 文本型
    .局部变量 位置, 整数型
    .局部变量 结果, 文本型

    位置 ＝ 寻找文本 (协议头, “:”, , 假)
    .如果真 (位置 ≠ -1)
        结果 ＝ 取文本左边 (协议头, 位置 － 1)
    .如果真结束
    返回 (删首尾空 (结果))

.子程序 内部_设Cookie, , , 设定前，请先清除
    .参数 域名, 文本型, , 只接受域名
    .参数 Cookie内容, 文本型, , 以“;”号结尾
    .局部变量 网址, 文本型
    .局部变量 计次, 整数型
    .局部变量 Cookies, 文本型, , "0"

    网址 ＝ “http://” ＋ 域名
    Cookies ＝ 分割文本 (Cookie内容, “;”, )
    .计次循环首 (取数组成员数 (Cookies), 计次)
        InternetSetCookieA (网址, 内部_Cookie取名 (Cookies [计次]), 内部_Cookie取值 (Cookies [计次]))
    .计次循环尾 ()

.子程序 网页_Cookie合并更新ex, 文本型, 公开, 返回更新后的Cookie 感谢【小小n】提供
    .参数 旧Cookie, 文本型, 参考, a=1; b=2
    .参数 新Cookie, 文本型, , a=1; b=2
    .局部变量 局_新Cookie组, 文本型, , "0"
    .局部变量 局_合并Cookie组, 文本型, , "0"
    .局部变量 局_计次, 整数型
    .局部变量 初步合并, 文本型
    .局部变量 已存在名称, 文本型, , "0"
    .局部变量 C, 整数型
    .局部变量 倒序索引, 整数型
    .局部变量 i, 整数型
    .局部变量 是否存在, 逻辑型
    .局部变量 名称, 文本型
    .局部变量 值, 文本型
    .局部变量 局_合并cookie, 文本型

    
    ' 初步合并 顺序：旧→新
    ' 能处理单次http请求返回多个同名Cookie的情况,例如同时存在 Set-Cookie: a=1;  和 Set-Cookie: a=2; 这种情况以后者为新
    初步合并 ＝ 旧Cookie ＋ 选择 (旧Cookie ≠ “” 且 新Cookie ≠ “”, “; ”, “”) ＋ 新Cookie
    ' 输出调试文本 (“初步合并: ” ＋ 初步合并)
    
    局_合并Cookie组 ＝ 分割文本 (初步合并, “; ”, )
    C ＝ 取数组成员数 (局_合并Cookie组)
    .计次循环首 (C, 局_计次)
        ' 更新 倒序：新→旧，名称已记录的Cookie一定是更加新的Cookie
        倒序索引 ＝ C ＋ 1 － 局_计次
        名称 ＝ 内部_Cookie取名ex (删首尾空 (局_合并Cookie组 [倒序索引]))
        值 ＝ 内部_Cookie取值 (删首尾空 (局_合并Cookie组 [倒序索引]))
        .如果 (名称 ≠ “”) ' 过滤
            是否存在 ＝ 假
            .计次循环首 (取数组成员数 (已存在名称), i)
                .如果真 (已存在名称 [i] ＝ 名称)
                    是否存在 ＝ 真
                    跳出循环 ()
                .如果真结束
                
            .计次循环尾 ()
            .如果 (是否存在 ＝ 假)
                .如果 (值 ≠ “” 且 到小写 (值) ≠ “deleted”) ' 过滤
                    ' 输出调试文本 (“有效[” ＋ 名称 ＋ “=” ＋ 值 ＋ “]”)
                    加入成员 (局_新Cookie组, 局_合并Cookie组 [倒序索引])
                .否则
                    ' 输出调试文本 (“名称[” ＋ 名称 ＋ “]过滤无效值”)
                .如果结束
                加入成员 (已存在名称, 名称)
            .否则
                ' 输出调试文本 (“名称[” ＋ 名称 ＋ “]已存在”)
            .如果结束
            
        .否则
            ' 输出调试文本 (“过滤无效名称[” ＋ 局_合并Cookie组 [倒序索引] ＋ “]”)
        .如果结束
        
    .计次循环尾 ()
    局_合并cookie ＝ “”
    .计次循环首 (取数组成员数 (局_新Cookie组), i)
        .如果真 (局_合并cookie ≠ “”)
            局_合并cookie ＝ “; ” ＋ 局_合并cookie
        .如果真结束
        局_合并cookie ＝ 局_新Cookie组 [i] ＋ 局_合并cookie
    .计次循环尾 ()
    旧Cookie ＝ 局_合并cookie
    返回 (局_合并cookie)

.子程序 内部_Cookie取名ex, 文本型
    .参数 Cookie, 文本型
    .局部变量 位置, 整数型
    .局部变量 结果, 文本型

    位置 ＝ 寻找文本 (Cookie, “=”, , 假)
    .判断开始 (位置 ≠ -1)
        ' 输出调试文本 (Cookie, 位置)
        结果 ＝ 取文本左边 (Cookie, 位置 － 1)
    .默认
        结果 ＝ “” ' 无效名称应返回空
    .判断结束
    返回 (删首尾空 (结果))

.子程序 网页_Cookie合并更新, 文本型, 公开, 返回更新后的Cookie
    .参数 旧Cookie, 文本型, 参考
    .参数 新Cookie, 文本型
    .局部变量 局_旧Cookie组, 文本型, , "0"
    .局部变量 局_新Cookie组, 文本型, , "0"
    .局部变量 局_计次, 整数型

    ' 初步格式化
    旧Cookie ＝ 删首尾空 (旧Cookie)
    .如果真 (取文本右边 (旧Cookie, 1) ＝ “;”)
        旧Cookie ＝ 旧Cookie ＋ 字符 (#空格键)
    .如果真结束
    
    新Cookie ＝ 删首尾空 (新Cookie)
    .如果真 (取文本右边 (新Cookie, 1) ＝ “;”)
        新Cookie ＝ 新Cookie ＋ 字符 (#空格键)
    .如果真结束
    
    ' 开始更新Cookie
    局_旧Cookie组 ＝ 分割文本 (旧Cookie, “; ”, )
    局_新Cookie组 ＝ 分割文本 (新Cookie, “; ”, )
    .计次循环首 (取数组成员数 (局_旧Cookie组), 局_计次)
        .如果真 (内部_数组成员是否存在1 (局_新Cookie组, 内部_Cookie取名 (局_旧Cookie组 [局_计次])) ＝ 假)
            加入成员 (局_新Cookie组, 局_旧Cookie组 [局_计次])
        .如果真结束
        
    .计次循环尾 ()
    ' 去除无效的Cookie
    旧Cookie ＝ “”
    .计次循环首 (取数组成员数 (局_新Cookie组), 局_计次)
        .如果真 (取文本右边 (局_新Cookie组 [局_计次], 8) ≠ “=deleted”)
            旧Cookie ＝ 旧Cookie ＋ 局_新Cookie组 [局_计次] ＋ “; ”
        .如果真结束
        
    .计次循环尾 ()
    旧Cookie ＝ 取文本左边 (旧Cookie, 取文本长度 (旧Cookie) － 2)
    ' 对重复的Cookie进行合并
    旧Cookie ＝ 文本_去重复文本 (旧Cookie, “; ”)
    返回 (旧Cookie)

.子程序 内部_数组成员是否存在1, 逻辑型, , Cookie处理专用
    .参数 数组, 文本型, 数组
    .参数 要判断值, 文本型
    .局部变量 局_计次, 整数型

    .计次循环首 (取数组成员数 (数组), 局_计次)
        .如果真 (内部_Cookie取名 (数组 [局_计次]) ＝ 要判断值)
            返回 (真)
        .如果真结束
        
    .计次循环尾 ()
    返回 (假)

.子程序 内部_Cookie取值, 文本型
    .参数 Cookie, 文本型
    .局部变量 位置, 整数型
    .局部变量 结果, 文本型

    位置 ＝ 寻找文本 (Cookie, “=”, , 假)
    .如果真 (位置 ≠ -1)
        结果 ＝ 取文本右边 (Cookie, 取文本长度 (Cookie) － 位置)
    .如果真结束
    返回 (结果)

.子程序 内部_Cookie取名, 文本型
    .参数 Cookie, 文本型
    .局部变量 位置, 整数型
    .局部变量 结果, 文本型

    位置 ＝ 寻找文本 (Cookie, “=”, , 假)
    .判断开始 (位置 ≠ -1)
        结果 ＝ 取文本左边 (Cookie, 位置 － 1)
    .默认
        结果 ＝ Cookie
    .判断结束
    返回 (删首尾空 (结果))

.子程序 网页_处理协议头, 文本型, 公开, 将协议头中的键名首字母处理为大写
    .参数 参_原始协议头, 文本型
    .局部变量 局_数组, 文本型, , "0"
    .局部变量 i, 整数型
    .局部变量 局_键名, 文本型
    .局部变量 局_键名右, 文本型
    .局部变量 局_键名左, 文本型
    .局部变量 局_协议头, 文本型
    .局部变量 局_键名拼接, 文本型
    .局部变量 局_键名数组, 文本型, , "0"
    .局部变量 局_总数, 整数型
    .局部变量 x, 整数型
    .局部变量 局_键值, 文本型
    .局部变量 局_冒号位置, 整数型

    局_数组 ＝ 分割文本 (参_原始协议头, #换行符, )
    .计次循环首 (取数组成员数 (局_数组), i)
        局_冒号位置 ＝ 寻找文本 (局_数组 [i], “:”, , 假)
        .如果真 (局_冒号位置 ＝ -1)
            到循环尾 ()
        .如果真结束
        
        局_键名 ＝ 取文本中间 (局_数组 [i], 1, 局_冒号位置 － 1)
        .如果 (寻找文本 (局_键名, “-”, , 假) ≠ -1)
            局_键名拼接 ＝ “”
            局_键名数组 ＝ 分割文本 (局_键名, “-”, )
            局_总数 ＝ 取数组成员数 (局_键名数组)
            .计次循环首 (局_总数, x)
                .如果 (x ＝ 局_总数)
                    局_键名拼接 ＝ 局_键名拼接 ＋ 文本_首字母改大写 (局_键名数组 [x])
                .否则
                    局_键名拼接 ＝ 局_键名拼接 ＋ 文本_首字母改大写 (局_键名数组 [x]) ＋ “-”
                .如果结束
                
            .计次循环尾 ()
            
            局_键名 ＝ 局_键名拼接
        .否则
            局_键名 ＝ 文本_首字母改大写 (局_键名)
        .如果结束
        
        局_键值 ＝ 取文本右边 (局_数组 [i], 取文本长度 (局_数组 [i]) － 取文本长度 (局_键名))
        .如果真 (取代码 (取文本左边 (局_键值, 1), ) ＝ 32)
            局_键值 ＝ 取文本右边 (局_键值, 取文本长度 (局_键值) － 1)
        .如果真结束
        
        局_协议头 ＝ 局_协议头 ＋ 局_键名 ＋ 局_键值 ＋ #换行符
        
        
    .计次循环尾 ()
    局_协议头 ＝ 取文本左边 (局_协议头, 取文本长度 (局_协议头) － 取文本长度 (#换行符))
    返回 (局_协议头)

.子程序 网页_处理协议头ex, 文本型, 公开, 将协议头中的键名首字母处理为大写
    .参数 参_原始协yi头, 文本型
    .局部变量 首分, 文本型, , "0"
    .局部变量 次分, 文本型, , "0"
    .局部变量 代码, 整数型
    .局部变量 位置, 整数型
    .局部变量 输出, 文本型
    .局部变量 文件号, 整数型
    .局部变量 i, 整数型

    ' 该命令出自：https://bbs.125.la/thread-14325247-1-1.html
    文件号 ＝ 打开内存文件 ()
    首分 ＝ 分割文本 (参_原始协yi头, #换行符, )
    .计次循环首 (取数组成员数 (首分), i)
        次分 ＝ 分割文本 (首分 [i], “:”, )
        .如果真 (取数组成员数 (次分) ≥ 2)
            代码 ＝ 取代码 (次分 [1], 1)
            .如果真 (代码 ≥ 97 且 代码 ≤ 122)
                次分 [1] ＝ 文本替换 (次分 [1], 1, 1, 字符 (代码 － 32))
                位置 ＝ 寻找文本 (次分 [1], “-”, , 假)
                .判断循环首 (位置 ≠ -1)
                    位置 ＝ 位置 ＋ 1
                    代码 ＝ 取代码 (次分 [1], 位置)
                    .如果真 (代码 ≥ 97 且 代码 ≤ 122)
                        次分 [1] ＝ 文本替换 (次分 [1], 位置, 1, 字符 (代码 － 32))
                    .如果真结束
                    位置 ＝ 寻找文本 (次分 [1], “-”, 位置, 假)
                .判断循环尾 ()
                首分 [i] ＝ 文本替换 (首分 [i], 1, 取文本长度 (次分 [1]), 次分 [1])
            .如果真结束
            代码 ＝ 取代码 (次分 [2], 1)
            .如果真 (代码 ≠ 32)
                首分 [i] ＝ 文本替换 (首分 [i], 取文本长度 (次分 [1]) ＋ 1, 1, “: ”)
            .如果真结束
            
        .如果真结束
        写文本行 (文件号, 首分 [i])
    .计次循环尾 ()
    移到文件首 (文件号)
    输出 ＝ 读入文本 (文件号, )
    关闭文件 (文件号)
    返回 (输出)

.子程序 网页_访问_对象, 字节集, 公开, 使用WinHttp的对象方式访问网页
    .参数 网址, 文本型, , 完整的网页地址,必须包含http://或者https://
    .参数 访问方式, 整数型, 可空, 0=GET 1=POST 2=HEAD 3=PUT  4=OPTIONS  5=DELETE  6=TRACE  7=CONNECT
    .参数 提交信息, 文本型, 可空, "POST"专用   自动UTF8编码
    .参数 提交Cookies, 文本型, 参考 可空, 设置提交时的cookie
    .参数 返回Cookies, 文本型, 参考 可空, 返回的Cookie
    .参数 附加协议头, 文本型, 可空, 一行一个请用换行符隔开
    .参数 返回协议头, 文本型, 参考 可空, 返回的协议头
    .参数 返回状态代码, 整数型, 参考 可空, 网页返回的状态代码，例如：200；302；404等
    .参数 禁止重定向, 逻辑型, 可空, 默认不禁止网页重定向
    .参数 字节集提交, 字节集, 可空, 提交字节集数据
    .参数 代理地址, 文本型, 可空, 代理地址，格式为 8.8.8.8:88
    .参数 超时, 整数型, 可空, 秒|默认为15秒,-1为无限等待
    .参数 代理用户名, 文本型, 可空, 用户名
    .参数 代理密码, 文本型, 可空, 密码
    .参数 代理标识, 整数型, 可空, 代理标识，默认为1，0为路由器
    .参数 对象继承, 对象, 可空, 此处可自行提供对象，不再主动创建
    .参数 是否自动合并更新Cookie, 逻辑型, 可空, 默认为真，自动合并更新
    .参数 是否补全必要协议头, 逻辑型, 可空, 当附件协议头为空时自动添加必要的UA协议头 默认为真，假将不再添加非传入协议头
    .参数 是否处理协议头大小写, 逻辑型, 可空, 将协议头中的键名首字母处理为大写  默认为真
    .局部变量 局_访问方式, 文本型
    .局部变量 局_WinHttp, 对象
    .局部变量 局_发送协议头, 文本型, , "0"
    .局部变量 局_返回协议头, 文本型, , "0"
    .局部变量 局_计次, 整数型
    .局部变量 局_网页数据, 字节集
    .局部变量 局_变体提交, 变体型

    .如果真 (是否为空 (是否自动合并更新Cookie))
        是否自动合并更新Cookie ＝ 真
    .如果真结束
    
    .如果真 (是否为空 (是否补全必要协议头))
        是否补全必要协议头 ＝ 真
    .如果真结束
    
    .如果真 (是否为空 (是否处理协议头大小写))
        是否处理协议头大小写 ＝ 真
    .如果真结束
    
    线程_初始化COM库 ()
    
    .如果真 (访问方式 ＜ 0 或 访问方式 ＞ 7)
        访问方式 ＝ 0
    .如果真结束
    局_访问方式 ＝ 多项选择 (访问方式 ＋ 1, “GET”, “POST”, “HEAD”, “PUT”, “OPTIONS”, “DELETE”, “TRACE”, “CONNECT”)
    
    .判断开始 (是否为空 (对象继承))
        .如果真 (局_WinHttp.创建 (“WinHttp.WinHttpRequest.5.1”, ) ＝ 假)
            返回 ({ })
        .如果真结束
        
    .默认
        局_WinHttp ＝ 对象继承
    .判断结束
    
    .如果真 (超时 ≠ -1)
        .如果 (超时 ＜ 1)
            超时 ＝ 15000
        .否则
            超时 ＝ 超时 × 1000
        .如果结束
        局_WinHttp.方法 (“SetTimeouts”, 超时, 超时, 超时, 超时)
    .如果真结束
    
    .如果真 (代理地址 ≠ “”)
        局_WinHttp.方法 (“SetProxy”, 2, 代理地址)
    .如果真结束
    局_WinHttp.方法 (“Open”, 局_访问方式, 网址, 假)
    .如果真 (禁止重定向)
        局_WinHttp.写属性 (“Option”, 6, 0)
    .如果真结束
    
    局_WinHttp.写属性 (“Option”, 4, 13056) ' 忽略错误标志
    
    
    .如果真 (是否补全必要协议头)
        .如果真 (寻找文本 (附加协议头, “Accept:”, , 真) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Accept: */*”
        .如果真结束
        
        .如果真 (寻找文本 (附加协议头, “Referer:”, , 真) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Referer: ” ＋ 网址
        .如果真结束
        
        .如果真 (寻找文本 (附加协议头, “Accept-Language:”, , 真) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Accept-Language: zh-cn”
        .如果真结束
        
        .如果真 (寻找文本 (附加协议头, “User-Agent:”, , 真) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “User-Agent: Mozilla/4.0 (compatible; MSIE 9.0; Windows NT 6.1)”
        .如果真结束
        
        .如果真 (访问方式 ＝ 1)
            .如果真 (寻找文本 (附加协议头, “Content-Type:”, , 真) ＝ -1)
                附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Content-Type: application/x-www-form-urlencoded”
            .如果真结束
            
        .如果真结束
        
    .如果真结束
    
    .如果真 (是否处理协议头大小写)
        附加协议头 ＝ 网页_处理协议头 (附加协议头)
    .如果真结束
    
    .如果真 (代理地址 ≠ “” 且 代理用户名 ≠ “”)
        .如果真 (是否为空 (代理标识))
            代理标识 ＝ 1
        .如果真结束
        
        局_WinHttp.方法 (“SetCredentials”, 代理用户名, 代理密码, 代理标识)
        .如果真 (代理标识 ＝ 1)
            局_WinHttp.方法 (“SetRequestHeader”, “Proxy-Authorization”, “Basic ” ＋ 编码_BASE64编码 (到字节集 (代理用户名 ＋ “:” ＋ 代理密码)))
        .如果真结束
        
    .如果真结束
    
    .如果真 (取反 (是否为空 (提交Cookies)))
        .如果真 (取文本长度 (提交Cookies) ＞ 0)
            .如果真 (寻找文本 (提交Cookies, “Cookie:”, , 真) ≠ -1)
                提交Cookies ＝ 子文本替换 (提交Cookies, “Cookie:”, “”, , , 假)
            .如果真结束
            
        .如果真结束
        局_WinHttp.方法 (“SetRequestHeader”, “Cookie”, 提交Cookies)
    .如果真结束
    
    
    .判断开始 (寻找文本 (附加协议头, #换行符, , 假) ＝ -1)
        局_WinHttp.方法 (“SetRequestHeader”, 内部_协议头取名 (附加协议头), 内部_协议头取值 (附加协议头))
    .默认
        局_发送协议头 ＝ 分割文本 (附加协议头, #换行符, )
        .计次循环首 (取数组成员数 (局_发送协议头), 局_计次)
            .如果真 (局_发送协议头 [局_计次] ≠ “”)
                .如果真 (内部_协议头取名 (局_发送协议头 [局_计次]) ＝ “Connection”)
                    .如果真 (内部_协议头取值 (局_发送协议头 [局_计次]) ＝ “keep-alive”)
                        到循环尾 () ' 筛选排除此参数 避免协议头有此参数 HTTP对象又自动添加此参数
                    .如果真结束
                    
                .如果真结束
                
                局_WinHttp.方法 (“SetRequestHeader”, 内部_协议头取名 (局_发送协议头 [局_计次]), 内部_协议头取值 (局_发送协议头 [局_计次]))
            .如果真结束
            
        .计次循环尾 ()
    .判断结束
    
    .判断开始 (字节集提交 ＝ { })
        局_WinHttp.方法 (“Send”, 提交信息)
    .默认
        局_变体提交.赋值 (字节集提交, )
        局_WinHttp.方法 (“Send”, 局_变体提交)
    .判断结束
    局_网页数据 ＝ 局_WinHttp.读属性 (“ResponseBody”, ).取字节集 ()
    返回协议头 ＝ 局_WinHttp.读文本属性 (“GetAllResponseHeaders”, )
    返回状态代码 ＝ 局_WinHttp.读数值属性 (“Status”, )
    局_WinHttp.读文本属性 (“StatusText”, )
    局_返回协议头 ＝ 分割文本 (返回协议头, #换行符, )
    返回Cookies ＝ “”
    .计次循环首 (取数组成员数 (局_返回协议头), 局_计次)
        .如果真 (文本_取左边 (局_返回协议头 [局_计次], “: ”) ＝ “Set-Cookie”)
            .判断开始 (寻找文本 (局_返回协议头 [局_计次], “;”, , 假) ≠ -1)
                返回Cookies ＝ 返回Cookies ＋ 删首尾空 (文本_取出中间文本 (局_返回协议头 [局_计次], “Set-Cookie:”, “;”)) ＋ “; ”
            .默认
                返回Cookies ＝ 返回Cookies ＋ 删首尾空 (子文本替换 (局_返回协议头 [局_计次], “Set-Cookie:”, , , , 假)) ＋ “; ”
            .判断结束
            
        .如果真结束
        
    .计次循环尾 ()
    .如果真 (取文本左边 (返回Cookies, 2) ＝ “; ”)
        返回Cookies ＝ 取文本左边 (返回Cookies, 取文本长度 (返回Cookies) － 2)
    .如果真结束
    
    
    .如果真 (是否自动合并更新Cookie)
        .如果真 (取反 (是否为空 (提交Cookies)))
            返回Cookies ＝ 网页_Cookie合并更新 (提交Cookies, 返回Cookies)
        .如果真结束
        
    .如果真结束
    局_WinHttp.清除 ()
    线程_取消COM库 ()
    返回 (局_网页数据)

.子程序 网页_访问J, 字节集, 公开, 使用WinInet的API方式访问网页
    .参数 网址, 文本型, , 完整的网页地址,必须包含http://或者https://
    .参数 访问方式, 整数型, 可空, 0=GET 1=POST 2=HEAD 3=PUT  4=OPTIONS  5=DELETE  6=TRACE  7=CONNECT
    .参数 提交信息, 文本型, 可空, POST专用
    .参数 提交Cookies, 文本型, 参考 可空, 设置提交时的cookie
    .参数 返回Cookies, 文本型, 可空, 返回的Cookie
    .参数 附加协议头, 文本型, 可空, 一行一个请用换行符隔开,建议填写常量值或文本值,防止因传参引发错误
    .参数 返回协议头, 文本型, 参考 可空, 返回的协议头
    .参数 禁止重定向, 逻辑型, 可空, 默认不禁止网页重定向
    .参数 字节集提交, 字节集, 可空, 提交字节集数据
    .参数 代理地址, 文本型, 可空, 代理地址，格式为:ip:port,例如:8.8.8.8:88
    .参数 是否自动合并更新Cookie, 逻辑型, 可空, 默认为真，自动合并更新
    .参数 是否补全必要协议头, 逻辑型, 可空, 当附件协议头为空时自动添加必要的UA协议头 默认为真，假将不再添加非传入协议头
    .参数 是否处理协议头大小写, 逻辑型, 可空, 将协议头中的键名首字母处理为大写  默认为真
    .局部变量 局_Internet句柄, 整数型
    .局部变量 局_Internet连接句柄, 整数型
    .局部变量 局_HTTP请求句柄, 整数型
    .局部变量 局_计次, 整数型
    .局部变量 局_字节集, 字节集
    .局部变量 局_访问方式, 文本型
    .局部变量 局_User_Agent, 文本型
    .局部变量 局_返回协议头, 文本型, , "0"
    .局部变量 局_页面内容, 字节集
    .局部变量 局_请求标记, 整数型
    .局部变量 局_重定向地址, 文本型
    .局部变量 局_HTTPS, 逻辑型
    .局部变量 bAutoDecode, 整数型

    
    .如果真 (是否为空 (是否自动合并更新Cookie))
        是否自动合并更新Cookie ＝ 真
    .如果真结束
    
    .如果真 (是否为空 (是否补全必要协议头))
        是否补全必要协议头 ＝ 真
    .如果真结束
    
    .如果真 (是否为空 (是否处理协议头大小写))
        是否处理协议头大小写 ＝ 真
    .如果真结束
    
    .如果真 (是否处理协议头大小写)
        附加协议头 ＝ 网页_处理协议头 (附加协议头)
    .如果真结束
    
    ' 局_访问方式 ＝ 选择 (访问方式 ＝ 1, “POST”, “GET”)
    .如果真 (访问方式 ＜ 0 或 访问方式 ＞ 7)
        访问方式 ＝ 0
    .如果真结束
    局_访问方式 ＝ 多项选择 (访问方式 ＋ 1, “GET”, “POST”, “HEAD”, “PUT”, “OPTIONS”, “DELETE”, “TRACE”, “CONNECT”)
    
    .如果真 (到小写 (取文本左边 (网址, 8)) ＝ “https://”)
        局_HTTPS ＝ 真
    .如果真结束
    
    .判断开始 (寻找文本 (附加协议头, “User-Agent:”, , 真) ≠ -1)
        局_User_Agent ＝ 删首尾空 (文本_取出中间文本 (附加协议头 ＋ #换行符, “User-Agent:”, #换行符))
        .如果真 (局_User_Agent ＝ “”)
            局_User_Agent ＝ 内部_协议头取值 (附加协议头)
        .如果真结束
        
    .默认
        局_User_Agent ＝ “Mozilla/4.0 (compatible; MSIE 9.0; Windows NT 6.1)”
    .判断结束
    
    .判断开始 (代理地址 ＝ “”)
        局_Internet句柄 ＝ InternetOpenA (局_User_Agent, 1, 字符 (0), 字符 (0), 0)
    .默认
        .判断开始 (局_HTTPS) ' 用于解决win7下https协议代理无效问题
            局_Internet句柄 ＝ InternetOpenA (局_User_Agent, 3, 代理地址, “”, 0)
        .默认
            局_Internet句柄 ＝ InternetOpenA (局_User_Agent, 3, “http=” ＋ 代理地址, “”, 0)
        .判断结束
        
    .判断结束
    
    .如果真 (局_Internet句柄 ＝ 0)
        ' InternetCloseHandle (局_Internet句柄)
        返回 ({ })
    .如果真结束
    局_Internet连接句柄 ＝ InternetConnectA (局_Internet句柄, 网页_取域名 (网址), 网页_取端口 (网址), 字符 (0), 字符 (0), 3, 0, 0)
    .如果真 (局_Internet连接句柄 ＝ 0)
        ' InternetCloseHandle (局_Internet连接句柄)
        InternetCloseHandle (局_Internet句柄)
        返回 ({ })
    .如果真结束
    
    局_请求标记 ＝ 位或 (#INTERNET_FLAG_RELOAD, #INTERNET_COOKIE_THIRD_PARTY)
    .如果真 (取反 (是否为空 (提交Cookies)))
        局_请求标记 ＝ 位或 (局_请求标记, #INTERNET_FLAG_NO_COOKIES)
    .如果真结束
    
    .如果真 (禁止重定向)
        局_请求标记 ＝ 位或 (局_请求标记, #INTERNET_FLAG_NO_AUTO_REDIRECT)
    .如果真结束
    
    .判断开始 (局_HTTPS)
        局_请求标记 ＝ 位或 (局_请求标记, #INTERNET_FLAG_SECURE)
    .默认
        局_请求标记 ＝ 位或 (局_请求标记, #INTERNET_FLAG_IGNORE_REDIRECT_TO_HTTPS)
    .判断结束
    
    局_HTTP请求句柄 ＝ HttpOpenRequestA (局_Internet连接句柄, 局_访问方式, 网页_取页面地址 (网址), “HTTP/1.1”, 字符 (0), 字符 (0), 局_请求标记, 0)
    .如果真 (局_HTTP请求句柄 ＝ 0)
        ' InternetCloseHandle (局_HTTP请求句柄)
        InternetCloseHandle (局_Internet连接句柄)
        InternetCloseHandle (局_Internet句柄)
        返回 ({ })
    .如果真结束
    
    InternetSetOption (局_HTTP请求句柄, #INTERNET_OPTION_SECURITY_FLAGS, 127872, 4)
    
    .如果真 (是否补全必要协议头)
        .如果真 (附加协议头 ＝ “”)
            附加协议头 ＝ “Accept: */*”
        .如果真结束
        
        .如果真 (寻找文本 (附加协议头, “Accept:”, , 真) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Accept: */*”
        .如果真结束
        
        
        .如果真 (寻找文本 (附加协议头, “Referer:”, , 真) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Referer: ” ＋ 网址
        .如果真结束
        
        .如果真 (寻找文本 (附加协议头, “Accept-Language:”, , 真) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Accept-Language: zh-cn”
        .如果真结束
        
        .如果真 (访问方式 ＝ 1)
            .如果真 (寻找文本 (附加协议头, “Content-Type:”, , 真) ＝ -1)
                附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Content-Type: application/x-www-form-urlencoded”
            .如果真结束
            
        .如果真结束
        
    .如果真结束
    
    .如果真 (提交Cookies ≠ “”)
        .判断开始 (寻找文本 (提交Cookies, “Cookie:”, , 假) ＝ -1)
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ “Cookie: ” ＋ 提交Cookies
        .默认
            附加协议头 ＝ 附加协议头 ＋ #换行符 ＋ 提交Cookies
        .判断结束
        
    .如果真结束
    
    
    ' 在Vista之后  可以自动解压Gzip
    ' bAutoDecode ＝ 1
    ' InternetSetOptionA_传址 (局_HTTP请求句柄, #INTERNET_OPTION_HTTP_DECODING, bAutoDecode, 4)
    
    .判断开始 (访问方式 ＝ 0)
        HttpSendRequestA (局_HTTP请求句柄, 附加协议头, 取文本长度 (附加协议头), 0, 0)
    .默认
        .如果真 (字节集提交 ＝ { })
            字节集提交 ＝ 到字节集 (提交信息)
        .如果真结束
        HttpSendRequestA (局_HTTP请求句柄, 附加协议头, 取文本长度 (附加协议头), 取指针字节集_ (字节集提交), 取字节集长度 (字节集提交))
    .判断结束
    
    局_计次 ＝ 0
    局_字节集 ＝ 取空白字节集 (1024)
    .循环判断首 ()
        InternetReadFile (局_HTTP请求句柄, 局_字节集, 1024, 局_计次)
        局_页面内容 ＝ 局_页面内容 ＋ 取字节集左边 (局_字节集, 局_计次)
    .循环判断尾 (局_计次 ≠ 0)
    返回协议头 ＝ 取空白文本 (5000)
    HttpQueryInfoA (局_HTTP请求句柄, 22, 返回协议头, 5000, 0)
    ' 返回协议头 ＝ 子文本替换 (返回协议头, “Set-Cookie”, “Set-Cookie”, , , 假)
    InternetCloseHandle (局_HTTP请求句柄)
    InternetCloseHandle (局_Internet连接句柄)
    InternetCloseHandle (局_Internet句柄)
    局_返回协议头 ＝ 分割文本 (返回协议头, #换行符, )
    返回Cookies ＝ “”
    .计次循环首 (取数组成员数 (局_返回协议头), 局_计次)
        .如果真 (文本_取左边 (局_返回协议头 [局_计次], “: ”) ＝ “Set-Cookie”)
            .判断开始 (寻找文本 (局_返回协议头 [局_计次], “;”, , 假) ≠ -1)
                返回Cookies ＝ 返回Cookies ＋ 删首尾空 (文本_取出中间文本 (局_返回协议头 [局_计次], “Set-Cookie:”, “;”)) ＋ “; ”
            .默认
                返回Cookies ＝ 返回Cookies ＋ 删首尾空 (子文本替换 (局_返回协议头 [局_计次], “Set-Cookie:”, , , , 假)) ＋ “; ”
            .判断结束
            
        .如果真结束
        
    .计次循环尾 ()
    返回Cookies ＝ 取文本左边 (返回Cookies, 取文本长度 (返回Cookies) － 2)
    .如果真 (是否自动合并更新Cookie)
        .如果真 (取反 (是否为空 (提交Cookies)))
            返回Cookies ＝ 网页_Cookie合并更新 (提交Cookies, 返回Cookies)
        .如果真结束
        
    .如果真结束
    返回 (局_页面内容)

.子程序 网页_访问S, 文本型, 公开, 源码由[洫蜘蛛]提供,使用WinHttp的 API方式访问网页,如果返回是“?”请查询返回协议头是否有“Content-Encoding: gzip“表示gzip压缩过网页，可用 网页_GZIP解压()命令解压，如果返回是乱码，则返回原始文本，请自行转换编码，失败返回空文本，请取出状态文本。
    .参数 网址, 文本型, , 完整的网页地址,必须包含http://或者https://
    .参数 访问方式, 整数型, 可空, 0=GET 1=POST 2=HEAD  3=PUT  4=OPTIONS  5=DELETE  6=TRACE  7=CONNECT
    .参数 提交信息, 文本型, 可空, POST专用
    .参数 提交Cookies, 文本型, 参考 可空, 设置提交时的cookie
    .参数 返回Cookies, 文本型, 参考 可空, 返回的Cookie
    .参数 附加协议头, 文本型, 可空, 一行一个请用换行符隔开,建议填写常量值或文本值,防止因传参引发错误
    .参数 返回协议头, 文本型, 参考 可空, 返回的协议头
    .参数 禁止重定向, 逻辑型, 可空, 默认不禁止网页重定向
    .参数 网站登录用户名, 文本型, 可空, 自动登录网页用户名 如路由器
    .参数 网站登录密码, 文本型, 可空
    .参数 代理地址, 文本型, 可空, 代理地址，格式为 8.8.8.8:88
    .参数 代理帐号, 文本型, 可空
    .参数 代理密码, 文本型, 可空
    .参数 超时时间, 整数型, 可空, 可以被省略，单位：秒，默认30秒，如果提供大于0的数值，则修改操作超时时间。
    .参数 返回重定向, 文本型, 参考 可空, 可以被省略，提供参数时只能提供文本型变量，用于当发生重定向时取回页面重定向的地址。
    .参数 返回状态文本, 文本型, 参考 可空
    .参数 返回原始数据, 字节集, 参考 可空, 可以被省略，返回原始网页编码
    .参数 是否自动合并更新Cookie, 逻辑型, 可空, 默认为真，自动合并更新
    .参数 是否补全必要协议头, 逻辑型, 可空, 当附件协议头为空时自动添加必要的UA协议头 默认为真，假将不再添加非传入协议头
    .参数 字节集提交, 字节集, 可空, 提交字节集数据
    .参数 是否处理协议头大小写, 逻辑型, 可空, 将协议头中的键名首字母处理为大写  默认为真
    .局部变量 局_URL结构, URL_COMPONENTS
    .局部变量 局_网页地址, 字节集
    .局部变量 局_域名, 文本型
    .局部变量 局_端口, 整数型
    .局部变量 局_重定向地址, 文本型
    .局部变量 局_HTTPS, 逻辑型
    .局部变量 局_会话句柄, 整数型
    .局部变量 局_User_Agent, 文本型
    .局部变量 局_代理名称, 字节集
    .局部变量 局_会话类型, 整数型
    .局部变量 局_连接句柄, 整数型
    .局部变量 局_请求句柄, 整数型
    .局部变量 局_访问方式, 文本型
    .局部变量 局_页面地址, 字节集
    .局部变量 局_设置超时, 整数型
    .局部变量 局_请求标记, 整数型
    .局部变量 局_请求协议, 文本型
    .局部变量 局_请求报头, 字节集
    .局部变量 局_数据长度, 整数型
    .局部变量 局_提交数据, 字节集
    .局部变量 局_可读长度, 整数型
    .局部变量 局_缓存数据, 字节集
    .局部变量 局_网页数据, 字节集
    .局部变量 局_网页内容, 文本型
    .局部变量 局_文本数组, 文本型, , "0"
    .局部变量 局_编码类型, 文本型
    .局部变量 局_解码对象, 对象
    .局部变量 局_变体型, 变体型
    .局部变量 局_计次, 整数型
    .局部变量 局_正则, 正则表达式类

    .如果真 (是否为空 (是否自动合并更新Cookie))
        是否自动合并更新Cookie ＝ 真
    .如果真结束
    
    .如果真 (是否为空 (是否补全必要协议头))
        是否补全必要协议头 ＝ 真
    .如果真结束
    
    .如果真 (是否为空 (是否处理协议头大小写))
        是否处理协议头大小写 ＝ 真
    .如果真结束
    
    .如果 (WinHttpCheckPlatform ())
        局_URL结构.dwStructSize ＝ 60
        连续赋值 (-1, 局_URL结构.dwSchemeLength, 局_URL结构.dwHostNameLength, 局_URL结构.dwUrlPathLength, 局_URL结构.dwExtraInfoLength)
        局_网页地址 ＝ 编码_Ansi到Unicode (网址)
        .如果 (WinHttpCrackUrl (局_网页地址, 取字节集长度 (局_网页地址), 0, 局_URL结构))
            局_域名 ＝ 编码_Unicode到Ansi (指针到字节集 (局_URL结构.lpszHostName, 局_URL结构.dwHostNameLength × 2))
            局_页面地址 ＝ 选择 (局_URL结构.dwUrlPathLength ＜ 1, 编码_Ansi到Unicode (“/”), 指针到字节集 (局_URL结构.lpszUrlPath, 局_URL结构.dwUrlPathLength × 2) ＋ 指针到字节集 (局_URL结构.lpszExtraInfo, 局_URL结构.dwExtraInfoLength × 2) ＋ { 0, 0 })
            局_端口 ＝ 局_URL结构.nPort
            局_HTTPS ＝ #INTERNET_SCHEME_HTTPS ＝ 局_URL结构.nScheme
            局_会话类型 ＝ 选择 (取文本长度 (代理地址) ＞ 0, #WINHTTP_ACCESS_TYPE_NAMED_PROXY, #WINHTTP_ACCESS_TYPE_NO_PROXY)
            局_代理名称 ＝ 选择 (取文本长度 (代理地址) ＝ 0, { 0, 0 }, 选择 (局_HTTPS, 编码_Ansi到Unicode (代理地址), 编码_Ansi到Unicode (“http=” ＋ 代理地址)))
            局_会话句柄 ＝ WinHttpOpen (0, 局_会话类型, 局_代理名称, 0, 0)
            .如果 (局_会话句柄 ≠ 0)
                局_设置超时 ＝ 选择 (超时时间 ＞ 0, 超时时间 × 1000, 30000)
                .如果 (WinHttpSetTimeouts (局_会话句柄, 局_设置超时, 局_设置超时, 局_设置超时, 局_设置超时))
                    局_连接句柄 ＝ WinHttpConnect (局_会话句柄, 编码_Ansi到Unicode (局_域名, ), 局_端口, 0)
                    .如果 (局_连接句柄 ≠ 0)
                        局_访问方式 ＝ 选择 (访问方式 ＜ 0 或 访问方式 ＞ 7, “GET”, 多项选择 (访问方式 ＋ 1, “GET”, “POST”, “HEAD”, “PUT”, “OPTIONS”, “DELETE”, “TRACE”, “CONNECT”))
                        局_请求句柄 ＝ WinHttpOpenRequest (局_连接句柄, 编码_Ansi到Unicode (局_访问方式, ), 局_页面地址, 0, #WINHTTP_NO_REFERER, #WINHTTP_DEFAULT_ACCEPT_TYPES, 选择 (局_HTTPS, #WINHTTP_FLAG_SECURE, 0))
                        .如果 (局_请求句柄 ≠ 0)
                            .如果真 (取文本长度 (网站登录用户名) ＞ 0 且 取文本长度 (网站登录密码) ＞ 0)
                                .如果真 (WinHttpSetCredentials (局_请求句柄, #WINHTTP_AUTH_TARGET_SERVER, #WINHTTP_AUTH_SCHEME_BASIC, 编码_Ansi到Unicode (网站登录用户名, ), 编码_Ansi到Unicode (网站登录密码, ), 0) ＝ 假)
                                    返回状态文本 ＝ “707 设置代理凭证失败!”
                                    WinHttpCloseHandle (局_请求句柄)
                                    WinHttpCloseHandle (局_连接句柄)
                                    WinHttpCloseHandle (局_会话句柄)
                                    返回 (“”)
                                .如果真结束
                                
                            .如果真结束
                            .如果真 (取文本长度 (代理帐号) ＞ 0 且 取文本长度 (代理密码) ＞ 0)
                                .如果真 (WinHttpSetCredentials (局_请求句柄, #WINHTTP_AUTH_TARGET_PROXY, #WINHTTP_AUTH_SCHEME_BASIC, 编码_Ansi到Unicode (代理帐号, ), 编码_Ansi到Unicode (代理密码, ), 0) ＝ 假)
                                    WinHttpCloseHandle (局_请求句柄)
                                    WinHttpCloseHandle (局_连接句柄)
                                    WinHttpCloseHandle (局_会话句柄)
                                    返回状态文本 ＝ “707 设置代理凭证失败!”
                                    返回 (“”)
                                .如果真结束
                                
                            .如果真结束
                            
                            
                            .如果真 (WinHttpSetOption (局_请求句柄, #WINHTTP_OPTION_SECURITY_FLAGS, 13056, 4) ＝ 假)
                                返回状态文本 ＝ “忽略错误/证书错误  失败！”
                                WinHttpCloseHandle (局_请求句柄)
                                WinHttpCloseHandle (局_连接句柄)
                                WinHttpCloseHandle (局_会话句柄)
                                返回 (“”)
                                
                            .如果真结束
                            
                            .判断开始 (提交Cookies ＝ “”)
                                局_请求标记 ＝ #WINHTTP_OPTION_DISABLE_FEATURE
                            .默认
                                局_请求标记 ＝ #WINHTTP_OPTION_ENABLE_FEATURE
                            .判断结束
                            
                            .如果真 (WinHttpSetOption (局_请求句柄, 局_请求标记, #WINHTTP_DISABLE_COOKIES, 4) ＝ 假)
                                返回状态文本 ＝ “708 禁止自动设置Cookies失败!”
                                WinHttpCloseHandle (局_请求句柄)
                                WinHttpCloseHandle (局_连接句柄)
                                WinHttpCloseHandle (局_会话句柄)
                                返回 (“”)
                            .如果真结束
                            .如果真 (禁止重定向)
                                .如果真 (WinHttpSetOption (局_请求句柄, #WINHTTP_OPTION_DISABLE_FEATURE, #WINHTTP_DISABLE_REDIRECTS, 4) ＝ 假)
                                    返回状态文本 ＝ “709 重定向设置失败!”
                                    WinHttpCloseHandle (局_请求句柄)
                                    WinHttpCloseHandle (局_连接句柄)
                                    WinHttpCloseHandle (局_会话句柄)
                                    返回 (“”)
                                .如果真结束
                                
                            .如果真结束
                            
                            .如果真 (取文本长度 (附加协议头) ＞ 0) ' 先加载用户设置的协议头，后面再进行补充。
                                局_请求协议 ＝ 附加协议头 ＋ #换行符
                            .如果真结束
                            
                            
                            .如果真 (是否补全必要协议头)
                                .如果真 (寻找文本 (局_请求协议, “User-Agent: ”, 1, 真) ＝ -1)
                                    局_请求协议 ＝ 局_请求协议 ＋ “User-Agent: Mozilla/5.0 (compatible; MSIE 9.0; Windows NT 6.1; WOW64; Trident/5.0)” ＋ #换行符
                                .如果真结束
                                .如果真 (寻找文本 (局_请求协议, “Accept: ”, 1, 真) ＝ -1)
                                    局_请求协议 ＝ 局_请求协议 ＋ “Accept: text/html, application/xhtml+xml, */*” ＋ #换行符
                                .如果真结束
                                .如果真 (寻找文本 (局_请求协议, “Accept-Encoding: ”, 1, 真) ＝ -1)
                                    局_请求协议 ＝ 局_请求协议 ＋ “Accept-Encoding: gbk, GB2312” ＋ #换行符
                                .如果真结束
                                .如果真 (寻找文本 (局_请求协议, “Accept-Language: ”, 1, 真) ＝ -1)
                                    局_请求协议 ＝ 局_请求协议 ＋ “Accept-Language: zh-cn” ＋ #换行符
                                .如果真结束
                                
                                .如果真 (访问方式 ＝ 1)
                                    .如果真 (寻找文本 (局_请求协议, “Content-Type:”, , 真) ＝ -1)
                                        局_请求协议 ＝ 局_请求协议 ＋ “Content-Type: application/x-www-form-urlencoded” ＋ #换行符
                                    .如果真结束
                                    
                                .如果真结束
                                .如果真 (寻找文本 (局_请求协议, “Cache-Control: ”, , 真) ＝ -1)
                                    局_请求协议 ＝ 局_请求协议 ＋ “Cache-Control: no-cache” ＋ #换行符
                                .如果真结束
                                
                            .如果真结束
                            
                            .如果真 (是否处理协议头大小写)
                                局_请求协议 ＝ 网页_处理协议头 (局_请求协议)
                            .如果真结束
                            
                            
                            .如果真 (寻找文本 (局_请求协议, “Connection”, , 假) ≠ -1)
                                .如果真 (网页_协议头_取信息 (局_请求协议, “Connection”) ＝ “keep-alive”)
                                    局_请求协议 ＝ 子文本替换 (局_请求协议, “Connection: keep-alive” ＋ #换行符, “”, , , 真) ' 筛选排除此参数 避免协议头有此参数 HTTP对象又自动添加此参数
                                .如果真结束
                                
                            .如果真结束
                            
                            .如果真 (寻找文本 (局_请求协议, “Cookie:”, , 真) ＝ -1)
                                .如果真 (取文本长度 (提交Cookies) ＞ 0)
                                    .判断开始 (寻找文本 (提交Cookies, “Cookie:”, , 真) ＝ -1)
                                        局_请求协议 ＝ 局_请求协议 ＋ #换行符 ＋ “Cookie: ” ＋ 提交Cookies ＋ #换行符
                                    .默认
                                        局_请求协议 ＝ 局_请求协议 ＋ #换行符 ＋ 提交Cookies ＋ #换行符
                                    .判断结束
                                    
                                .如果真结束
                                
                            .如果真结束
                            
                            局_请求报头 ＝ 编码_Ansi到Unicode (局_请求协议, )
                            局_数据长度 ＝ 取字节集长度 (局_请求报头) ÷ 2 － 1
                            .如果 (WinHttpAddRequestHeaders (局_请求句柄, 局_请求报头, 局_数据长度, #WINHTTP_ADDREQ_FLAG_ADD))
                                .如果 (字节集提交 ＝ { })
                                    局_提交数据 ＝ 到字节集 (提交信息)
                                .否则
                                    局_提交数据 ＝ 字节集提交
                                .如果结束
                                
                                局_数据长度 ＝ 取字节集长度 (局_提交数据)
                                .如果 (WinHttpSendRequest (局_请求句柄, 0, 0, 局_提交数据, 局_数据长度, 局_数据长度, 0))
                                    .如果 (WinHttpReceiveResponse (局_请求句柄, 0))
                                        局_缓存数据 ＝ { }
                                        局_网页数据 ＝ { }
                                        .判断循环首 (WinHttpQueryDataAvailable (局_请求句柄, 局_可读长度))
                                            .如果真 (局_可读长度 ＜ 1)
                                                跳出循环 ()
                                            .如果真结束
                                            局_缓存数据 ＝ 取空白字节集 (局_可读长度)
                                            .如果真 (WinHttpReadData (局_请求句柄, 局_缓存数据, 局_可读长度, 局_数据长度) ＝ 假)
                                                返回状态文本 ＝ “713 读取数据失败!”
                                                跳出循环 ()
                                            .如果真结束
                                            局_网页数据 ＝ 局_网页数据 ＋ 取字节集左边 (局_缓存数据, 局_数据长度)
                                        .判断循环尾 ()
                                        局_缓存数据 ＝ { }
                                        局_数据长度 ＝ 0
                                        WinHttpQueryHeaders (局_请求句柄, #WINHTTP_QUERY_RAW_HEADERS_CRLF, 0, 局_缓存数据, 局_数据长度, #WINHTTP_NO_HEADER_INDEX)
                                        局_缓存数据 ＝ 取空白字节集 (局_数据长度)
                                        .如果真 (WinHttpQueryHeaders (局_请求句柄, #WINHTTP_QUERY_RAW_HEADERS_CRLF, 0, 局_缓存数据, 局_数据长度, #WINHTTP_NO_HEADER_INDEX))
                                            返回协议头 ＝ 编码_Unicode到Ansi (局_缓存数据)
                                            返回状态文本 ＝ 取文本中间 (返回协议头, 10, 寻找文本 (返回协议头, #换行符, 10, 假) － 9)
                                            
                                            局_文本数组 ＝ 分割文本 (返回协议头, #换行符, )
                                            .计次循环首 (取数组成员数 (局_文本数组), 局_计次)
                                                .如果真 (文本_取左边 (局_文本数组 [局_计次], “: ”, , ) ＝ “Set-Cookie”)
                                                    .判断开始 (寻找文本 (局_文本数组 [局_计次], “;”, , 假) ≠ -1)
                                                        返回Cookies ＝ 返回Cookies ＋ 删首尾空 (文本_取出中间文本 (局_文本数组 [局_计次], “Set-Cookie:”, “;”, , )) ＋ “; ”
                                                    .默认
                                                        返回Cookies ＝ 返回Cookies ＋ 删首尾空 (子文本替换 (局_文本数组 [局_计次], “Set-Cookie:”, , , , 假)) ＋ “; ”
                                                    .判断结束
                                                    
                                                .如果真结束
                                                
                                                .如果真 (文本比较 (取文本左边 (局_文本数组 [局_计次], 10), “Location: ”, 假) ＝ 0)
                                                    返回重定向 ＝ 取文本中间 (局_文本数组 [局_计次], 11, 取文本长度 (局_文本数组 [局_计次]) － 10)
                                                .如果真结束
                                                处理事件 ()
                                            .计次循环尾 ()
                                            返回Cookies ＝ 取文本左边 (返回Cookies, 取文本长度 (返回Cookies))
                                            .如果真 (取文本左边 (返回Cookies, 2) ＝ “; ”)
                                                返回Cookies ＝ 取文本左边 (返回Cookies, 取文本长度 (返回Cookies) － 2)
                                            .如果真结束
                                            
                                            .如果真 (是否自动合并更新Cookie)
                                                .如果真 (取反 (提交Cookies ＝ “”))
                                                    返回Cookies ＝ 网页_Cookie合并更新 (提交Cookies, 返回Cookies)
                                                .如果真结束
                                                
                                            .如果真结束
                                            ' -------------------提取网页编码，并转码
                                            返回原始数据 ＝ 局_网页数据
                                            局_网页内容 ＝ 到文本 (返回原始数据)
                                            CoInitialize (0)
                                            .如果真 (局_正则.创建 (“<meta.+?charset=[^\w]?([-\w]+)”, 取文本左边 (局_网页内容, 1024), , , , ))
                                                局_编码类型 ＝ 局_正则.取子匹配文本 (1, 1, )
                                                .如果真 (取文本长度 (局_编码类型) ＞ 0)
                                                    .如果真 (局_编码类型 ≠ “gbk” 或 局_编码类型 ≠ “gb2312”) ' 如果为中文就不处理
                                                        .如果真 (局_解码对象.创建 (“Adodb.Stream”, ))
                                                            局_变体型.赋值 (局_网页数据, )
                                                            局_解码对象.写属性 (“Type”, 1)
                                                            局_解码对象.写属性 (“Mode”, 3)
                                                            局_解码对象.方法 (“Open”, )
                                                            局_解码对象.方法 (“Write”, 局_变体型)
                                                            局_解码对象.写属性 (“Position”, 0)
                                                            局_解码对象.写属性 (“Type”, 2)
                                                            局_解码对象.写属性 (“Charset”, 局_编码类型)
                                                            局_网页内容 ＝ 局_解码对象.文本方法 (“ReadText”, )
                                                            局_解码对象.方法 (“Close”, )
                                                            局_解码对象.清除 ()
                                                            局_变体型.清除 ()
                                                        .如果真结束
                                                        
                                                    .如果真结束
                                                    
                                                .如果真结束
                                                处理事件 ()
                                            .如果真结束
                                            CoUninitialize ()
                                        .如果真结束
                                        处理事件 ()
                                    .否则
                                        返回状态文本 ＝ “712 服务器未响应!”
                                    .如果结束
                                    处理事件 ()
                                .否则
                                    返回状态文本 ＝ “711 发送请求失败!”
                                .如果结束
                                处理事件 ()
                            .否则
                                返回状态文本 ＝ “710 设置协议头失败!”
                            .如果结束
                            处理事件 ()
                        .否则
                            返回状态文本 ＝ “704 创建WinHttp请求失败!”
                        .如果结束
                        处理事件 ()
                    .否则
                        返回状态文本 ＝ “703 创建WinHttp连接失败!”
                    .如果结束
                    处理事件 ()
                    
                .否则
                    返回状态文本 ＝ “705 设置超时时间失败!”
                .如果结束
                处理事件 ()
                
            .否则
                返回状态文本 ＝ “702 创建WinHttp会话失败!”
            .如果结束
            处理事件 ()
        .否则
            返回状态文本 ＝ “701 解析URL地址失败!”
        .如果结束
        处理事件 ()
    .否则
        返回状态文本 ＝ “700 当前系统不支持WinHttp服务!”
    .如果结束
    WinHttpCloseHandle (局_请求句柄)
    WinHttpCloseHandle (局_连接句柄)
    WinHttpCloseHandle (局_会话句柄)
    返回 (局_网页内容)

.子程序 网页_URL取本地地址, 文本型, 公开, 通过文件的URL地址，获取缓存在本地的文件地址，成功返回本地文件地址，失败或不存在返回空文本。目前知道不支持搜狗浏览器。
    .参数 文件地址, 文本型, , 如：http://bbs.125.la/static/image/common/logo.png
    .局部变量 缓存信息, INTERNET_CACHE_ENTRY_INFO
    .局部变量 dwbuffer, 整数型
    .局部变量 memaddr, 整数型

    GetUrlCacheEntryInfo (文件地址, 0, dwbuffer) ' 获取dwbuffer大小
    .判断开始 (GetLastError () ＝ 2) ' 指定的缓存条目在缓存中没有找到。
        输出调试文本 (“指定的缓存条目在缓存中没有找到。”)
        返回 (“”)
        
    .判断 (GetLastError () ＝ 122) ' 有缓存文件
        memaddr ＝ LocalAlloc (0, dwbuffer) ' 申请内存
        GetUrlCacheEntryInfo (文件地址, memaddr, dwbuffer)
        RtlMoveMemory_GetUrlCache (缓存信息, memaddr, 80)
        LocalFree (memaddr) ' 释放内存
        返回 (缓存信息.lpszLocalFileName)
    .默认
        
    .判断结束
    返回 (“”)

.子程序 网页_GZIP压缩_文本, 文本型, 公开, 返回 Base64编码 感谢【@surplus 】提供源码
    .参数 Text, 文本型
    .参数 是否线程, 逻辑型, 可空
    .局部变量 对象, 对象
    .局部变量 eval, 变体型
    .局部变量 ret, 文本型

    .如果真 (是否线程)
        线程_初始化COM库 ()
    .如果真结束
    Text ＝ 编码_URL编码 (Text, 真, 真)
    对象.创建 (“ScriptControl”, )
    对象.写属性 (“Language”, “JScript”)
    对象.逻辑方法 (“AddCode”, #Gzip)
    eval ＝ 对象.通用方法 (“Eval”, “Gzip_Encrypt('” ＋ Text ＋ “')”) ' 这里要填写单引号， 也就是 Gzip_Encrypt('1233')  否则遇到 易语言的引号 会冲突 导致 压缩失败
    ret ＝ eval.取文本 ()
    对象.清除 ()
    .如果真 (是否线程)
        线程_取消COM库 ()
    .如果真结束
    返回 (到文本 (编码_BASE64编码A (字节集_文本转字节集 (“{” ＋ ret ＋ “}”))))

.子程序 网页_GZIP解压_文本, 文本型, 公开, 传递 Base64编码 感谢【@surplus 】提供源码
    .参数 Text, 文本型
    .参数 是否线程, 逻辑型, 可空
    .局部变量 对象, 对象
    .局部变量 eval, 变体型
    .局部变量 ret, 文本型
    .局部变量 局_转码内容, 字节集

    .如果真 (是否线程)
        线程_初始化COM库 ()
    .如果真结束
    Text ＝ 字节集_字节集转文本ex (编码_BASE64解码A (到字节集 (Text)))
    Text ＝ 取文本左边 (取文本右边 (Text, 取文本长度 (Text) － 1), 取文本长度 (Text) － 2)
    对象.创建 (“ScriptControl”, )
    对象.写属性 (“Language”, “JScript”)
    对象.逻辑方法 (“AddCode”, #Gzip)
    eval ＝ 对象.通用方法 (“Eval”, “Gzip_Decrypt('” ＋ Text ＋ “')”) ' 这里要填写单引号， 也就是 Gzip_Decrypt('1233')  否则遇到 易语言的引号 会冲突 导致 压缩失败
    ret ＝ eval.取文本 ()
    对象.清除 ()
    .如果真 (是否线程)
        线程_取消COM库 ()
    .如果真结束
    局_转码内容 ＝ 字节集_文本转字节集ex (“{” ＋ ret ＋ “}”)
    返回 (编码_URL解码_快速 (编码_Utf8到Ansi (局_转码内容), ))

.子程序 网页_GZIP压缩, 字节集, 公开, 感谢小磊提供，gzip.dll文件请自行到这里来下载并放到程序根目录下即可。http://pan.baidu.com/share/link?shareid=222122&uk=2013335237
    .参数 inData, 字节集
    .局部变量 context, 整数型
    .局部变量 inData2, 字节集
    .局部变量 inLength, 整数型
    .局部变量 buffer, 字节集
    .局部变量 inUsed, 整数型
    .局部变量 outUsed, 整数型
    .局部变量 hResult, 整数型
    .局部变量 retBytes, 字节集
    .局部变量 upRetByts, 字节集
    .局部变量 压缩位置, 整数型

    buffer ＝ 取空白字节集 (8192) ' #MAX_BUF
    InitCompression ()
    CreateCompression (context, 3) ' #GZIP_LVL
    .循环判断首 ()
        inData2 ＝ 取字节集中间 (inData, 压缩位置 ＋ 1, 8192) ' #MAX_BUF
        inLength ＝ 取字节集长度 (inData2)
        hResult ＝ Compress (context, inData2, inLength, buffer, 8192, inUsed, outUsed, 3)
        .如果真 (0 ≠ outUsed)
            upRetByts ＝ upRetByts ＋ 取字节集左边 (buffer, outUsed)
            压缩位置 ＝ 压缩位置 ＋ inUsed
        .如果真结束
        
    .循环判断尾 (hResult ＝ 0)
    DestroyCompression (context)
    返回 (upRetByts)

.子程序 网页_GZIP解压, 字节集, 公开, 感谢小磊提供，gzip.dll文件请自行到这里来下载并放到程序根目录下即可。http://pan.baidu.com/share/link?shareid=222122&uk=2013335237
    .参数 inData, 字节集
    .局部变量 context, 整数型
    .局部变量 inData2, 字节集
    .局部变量 inLength, 整数型
    .局部变量 buffer, 字节集
    .局部变量 inUsed, 整数型
    .局部变量 outUsed, 整数型
    .局部变量 hResult, 整数型
    .局部变量 retBytes, 字节集
    .局部变量 upRetByts, 字节集
    .局部变量 解压位置, 整数型

    buffer ＝ 取空白字节集 (8192)
    InitDecompression ()
    CreateDecompression (context, 3)
    .循环判断首 ()
        inData2 ＝ 取字节集中间 (inData, 解压位置 ＋ 1, 8192)
        inLength ＝ 取字节集长度 (inData2)
        hResult ＝ Decompress (context, inData2, inLength, buffer, 8192, inUsed, outUsed)
        .如果真 (0 ≠ outUsed)
            upRetByts ＝ upRetByts ＋ 取字节集左边 (buffer, outUsed)
            解压位置 ＝ 解压位置 ＋ inUsed
        .如果真结束
        
    .循环判断尾 (hResult ＝ 0)
    DestroyDecompression (context)
    返回 (upRetByts)

.子程序 网页_JS格式化, 文本型, 公开, 感谢小磊提供，格式化未处理的JS文本
    .参数 js代码, 文本型, , 需要加密\解密的源码
    .参数 压缩模式, 逻辑型, 可空, 假为格式化代码,真为压缩代码
    .局部变量 js, 类_脚本组件
    .局部变量 str, 文本型

    js.执行 (#JS格式化)
    .判断开始 (压缩模式)
        返回 (js.运行 (“ysjs”, js代码))
    .默认
        返回 (js.运行 (“js_beautify”, js代码))
    .判断结束
    

.子程序 网页_置浏览器代理, , 公开, 设置当前程序中浏览器的代理,不影响外部浏览器使用
    .参数 代理IP, 文本型, 可空, 格式ip地址+端口，例如【192.168.1.10:80】,留空为取消代理
    .局部变量 op, INTERNET_PROXY_INFO

    .如果 (是否为空 (代理IP) ＝ 假)
        op.dwAccessType ＝ #INTERNET_OPEN_TYPE_PROXY
        op.lpszProxy ＝ “ftp= ” ＋ 代理IP ＋ “;http= ” ＋ 代理IP ＋ “;socks= ” ＋ 代理IP
        op.lpszProxyBypass ＝ “”
        InternetSetOptionA2 (0, #INTERNET_OPTION_PROXY, op, 12)
        InternetSetOptionA_传址 (0, #INTERNET_OPTION_SETTINGS_CHANGED, 0, 0)
    .否则
        
        op.dwAccessType ＝ #INTERNET_OPEN_TYPE_DIRECT
        op.lpszProxy ＝ “”
        op.lpszProxyBypass ＝ “”
        InternetSetOptionA2 (0, #INTERNET_OPTION_PROXY, op, 12)
        InternetSetOptionA_传址 (0, #INTERNET_OPTION_SETTINGS_CHANGED, 0, 0)
    .如果结束
    

.子程序 网页_置浏览器UA, 逻辑型, 公开, 设置当前程序中浏览器的UserAgent,不影响外部浏览器使用
    .参数 UserAgent, 文本型

    返回 (UrlMkSetSessionOption (#URLMON_OPTION_USERAGENT, UserAgent, 取文本长度 (UserAgent), 0) ＝ 0)

.子程序 网页_取网页源码, 文本型, 公开, 获取指定网址的网页源码文本
    .参数 网址, 文本型, , 请提供要取源码的网址

    返回 (到文本 (网页_访问J (网址)))

.子程序 网页_取文本_reg, 文本型, 公开, 去掉网页源码中的HTML标签，保留纯文本(正则方式)
    .参数 网页源码, 文本型, , 网页源码如果含中文且乱码的,请先解码
    .局部变量 正则, 正则表达式类
    .局部变量 局_源码, 文本型

    局_源码 ＝ 网页源码
    正则.创建 (“<script[\s\S]*?</script>|<style[\s\S]*?</style>|<[^>]+>|&.{1,8};”, 局_源码)
    局_源码 ＝ 正则.替换 ()
    返回 (局_源码)

.子程序 网页_取文本_dom, 文本型, 公开, 去掉网页源码中的HTML标签，保留纯文本(DOM方式)
    .参数 网页源码, 文本型, , 网页源码如果含中文且乱码的,请先解码
    .局部变量 dom, 对象
    .局部变量 局_源码, 文本型

    局_源码 ＝ 网页源码
    CoInitialize (0)
    dom.创建 (“{25336920-03F9-11CF-8FD0-00AA00686F13}”, )
    dom.方法 (“write”, “<HTML><BODY><div></div></BODY></HTML>”)
    dom.对象型方法 (“body”, ).写属性 (“innerHTML”, 局_源码)
    dom.方法 (“close”, )
    局_源码 ＝ dom.对象型方法 (“body”, ).读文本属性 (“outerText”, )
    CoUninitialize ()
    返回 (局_源码)

.子程序 网页_取端口, 整数型, 公开, 取出网站服务器的默认端口
    .参数 网址, 文本型
    .局部变量 端口, 文本型
    .局部变量 位置, 整数型

    端口 ＝ 网页_取域名 (网址, , 真)
    位置 ＝ 倒找文本 (端口, “:”, , 假)
    .判断开始 (位置 ≠ -1)
        返回 (到整数 (取文本右边 (端口, 取文本长度 (端口) － 位置)))
    .判断 (取文本左边 (到小写 (网址), 5) ＝ “https”)
        返回 (443)
    .默认
        返回 (80)
    .判断结束
    

.子程序 网页_取域名, 文本型, 公开, 通过“/”或者“.”来获取网址中的域名，默认使用“/”方式
    .参数 网址, 文本型
    .参数 去除二级域名, 逻辑型, 可空
    .参数 不过滤端口, 逻辑型, 可空
    .参数 后缀点数量, 整数型, 可空, 不留空表示通过“.”的方式取域名 。如：.com后缀点数量为1 ,.com.cn后缀点数量为2
    .局部变量 域名, 文本型
    .局部变量 位置, 整数型
    .局部变量 二级, 文本型, , "0"
    .局部变量 域名长度, 整数型
    .局部变量 前点开始, 整数型, , , 如www.bbs.125.la   www后面.的位置
    .局部变量 前点结束, 整数型, , , 如www.bbs.125.la   bbs后面.的位置
    .局部变量 后点开始, 整数型, , , 如www.bbs.125.la   125后面.的位置

    域名 ＝ 到小写 (网址)
    .判断开始 (是否为空 (后缀点数量)) ' 使用“/”方式获取域名，网页_访问() 内部 必须使用这个
        .如果真 (取文本右边 (域名, 1) ≠ “/”)
            域名 ＝ 域名 ＋ “/”
        .如果真结束
        
        .判断开始 (取文本左边 (域名, 8) ＝ “https://”)
            域名 ＝ 文本_取出中间文本 (域名, “https://”, “/”)
        .默认
            .如果真 (寻找文本 (域名, “http://”, , 假) ＝ -1)
                域名 ＝ “http://” ＋ 域名
            .如果真结束
            域名 ＝ 文本_取出中间文本 (域名, “http://”, “/”)
        .判断结束
        
        .如果真 (去除二级域名)
            二级 ＝ 分割文本 (域名, “.”, )
            位置 ＝ 取数组成员数 (二级)
            .如果真 (位置 ＞ 1)
                域名 ＝ 二级 [位置 － 1] ＋ “.” ＋ 二级 [位置]
            .如果真结束
            
        .如果真结束
        
    .默认
        ' 通过“.”的方式取域名，支持任何后缀域名
        域名 ＝ 子文本替换 (域名, “http://”, “”, , , 真)
        域名 ＝ 子文本替换 (域名, “https://”, “”, , , 真)
        位置 ＝ 寻找文本 (域名, “/”, , 假) ' 一撇位置
        .如果真 (位置 ≠ -1)
            域名 ＝ 取文本左边 (域名, 位置 － 1)
        .如果真结束
        连续赋值 (取文本长度 (域名), 域名长度, 后点开始)
        .计次循环首 (后缀点数量, )
            后点开始 ＝ 倒找文本 (域名, “.”, 后点开始, 假)
        .计次循环尾 ()
        前点结束 ＝ 倒找文本 (域名, “.”, 后点开始, 假)
        .判断开始 (去除二级域名)
            域名 ＝ 取文本右边 (域名, 域名长度 － 前点结束)
        .默认
            ' 保留二级域名---------------------------------------
            .如果真 (前点结束 ≠ -1) ' 至少是二级域名
                前点开始 ＝ 倒找文本 (域名, “.”, 前点结束, 假)
                .如果真 (前点开始 ≠ -1) ' 至少是三级域名
                    域名 ＝ 取文本右边 (域名, 域名长度 － 前点开始)
                .如果真结束
                
            .如果真结束
            
        .判断结束
        
    .判断结束
    ' 端口过滤处理--------------------------------------------------------
    .判断开始 (不过滤端口)
        返回 (域名)
    .默认
        位置 ＝ 寻找文本 (域名, “:”, , 假) ' 冒号位置
        .判断开始 (位置 ≠ -1)
            返回 (取文本左边 (域名, 位置 － 1))
        .默认
            返回 (域名)
        .判断结束
        
    .判断结束
    

.子程序 网页_取外网IP, 文本型, 公开, 取本机的外网IP
    .参数 所在省市, 文本型, 可空, 可空。请提供一个文本变量，用于存放您所在的省市，注：有些接口无法取出城市
    .参数 接口, 整数型, 可空, 可空。0、ip138；1、sohu；2、ttt.sh；3、ip.cn；4、taobao；5、uc；6、ip.sb；7、ipinfo
    .局部变量 局_结果, 字节集
    .局部变量 局_网址, 文本型
    .局部变量 局_返回, 文本型
    .局部变量 局_IP, 文本型

    .判断开始 (接口 ＝ 1)
        局_网址 ＝ “http://pv.sohu.com/cityjson”
        局_返回 ＝ 到文本 (网页_访问_对象 (局_网址, , , , , , , , , , , , , , , , , , ))
        局_IP ＝ 文本_取出中间文本 (局_返回, “cip” ＋ #引号 ＋ “: ” ＋ #引号, #引号 ＋ “, ” ＋ #引号 ＋ “cid”, , )
        所在省市 ＝ 文本_取出中间文本 (局_返回, “cname” ＋ #引号 ＋ “: ” ＋ #引号, #引号 ＋ “};”, , )
    .判断 (接口 ＝ 2)
        局_网址 ＝ “https://api.ttt.sh/ip/qqwry/”
        局_返回 ＝ 编码_Utf8到Ansi (网页_访问_对象 (局_网址, , , , , , , , , , , , , , , , , , ))
        局_返回 ＝ 编码_usc2到ansi (局_返回)
        局_IP ＝ 文本_取出中间文本 (局_返回, “ip” ＋ #引号 ＋ “:” ＋ #引号, #引号, , )
        所在省市 ＝ 文本_取出中间文本 (局_返回, “address” ＋ #引号 ＋ “:” ＋ #引号, #引号, , )
    .判断 (接口 ＝ 3)
        局_网址 ＝ “https://ip.cn/index.php”
        局_返回 ＝ 编码_Utf8到Ansi (网页_访问_对象 (局_网址, , , , , “User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/47.0.2526.80 Safari/537.36 Core/1.47.277.400 QQBrowser/9.4.7658.400”, , , , , , , , , , , , , ))
        局_IP ＝ 文本_取出中间文本 (局_返回, “您现在的 IP：<code>”, “</code>”, , )
        所在省市 ＝ 文本_取出中间文本 (局_返回, “所在地理位置：<code>”, “</code>”, , )
    .判断 (接口 ＝ 4)
        局_网址 ＝ “http://ip.taobao.com/service/getIpInfo2.php?ip=myip” ' 较慢且不一定可以取出
        局_返回 ＝ 编码_Utf8到Ansi (网页_访问_对象 (局_网址, , , , , , , , , , , , , , , , , , ))
        局_IP ＝ 文本_取出中间文本 (局_返回, “ip” ＋ #引号 ＋ “:” ＋ #引号, #引号, , )
        所在省市 ＝ 文本_取出中间文本 (局_返回, “region” ＋ #引号 ＋ “:” ＋ #引号, #引号, , ) ＋ “ ” ＋ 文本_取出中间文本 (局_返回, “city” ＋ #引号 ＋ “:” ＋ #引号, #引号, , )
    .判断 (接口 ＝ 5)
        局_网址 ＝ “https://www.uc.cn/ip”
        局_返回 ＝ 编码_Utf8到Ansi (网页_访问_对象 (局_网址, , , , , , , , , , , , , , , , , , ))
        局_IP ＝ 文本_取右边 (局_返回, “IP:”, , )
        所在省市 ＝ “” ' 返回无省市
    .判断 (接口 ＝ 6)
        局_网址 ＝ “https://api.ip.sb/ip”
        局_返回 ＝ 到文本 (网页_访问_对象 (局_网址, , , , , , , , , , , , , , , , , , ))
        局_IP ＝ 文本_取左边 (局_返回, 字符 (10), , )
        所在省市 ＝ “” ' 返回无省市
    .判断 (接口 ＝ 7)
        局_网址 ＝ “https://ipinfo.io/json”
        局_返回 ＝ 网页_访问S (局_网址, , , , , , , , , , , , , , , , , , , , )
        局_IP ＝ 文本_取出中间文本 (局_返回, “ip” ＋ #引号 ＋ “: ” ＋ #引号, #引号, , )
        所在省市 ＝ “” ' 返回无省市
    .默认
        局_网址 ＝ “http://www.ip138.com”
        局_结果 ＝ 网页_访问_对象 (局_网址, , , , , , , , , , , , , , , , , , )
        局_网址 ＝ 到文本 (字节集_取中间 (局_结果, 到字节集 (“<iframe src=”) ＋ { 34 }, { 34 }, )) ' 引号
        局_返回 ＝ 编码_Utf8到Ansi (网页_访问_对象 (局_网址, , , , , , , , , , , , , , , , , , ))
        局_IP ＝ 文本_取出中间文本 (局_返回, “您的IP地址是：”, “</title>”, , )
        所在省市 ＝ 文本_取出中间文本 (局_返回, “来自：”, #换行符 ＋ “</p>”, , )
    .判断结束
    返回 (局_IP)

.子程序 网页_置Cookie, 逻辑型, 公开, 设置本地IE的cookie
    .参数 网址, 文本型
    .参数 Cookie名称, 文本型, 可空, 为空清除所有
    .参数 Cookie内容, 文本型, 可空, 为空清除
    .参数 有效期, 日期时间型, 可空, 为空不保存
    .局部变量 域名, 文本型
    .局部变量 GMT时间, 文本型
    .局部变量 现有Cookie, 文本型
    .局部变量 整数容器, 整数型
    .局部变量 名称, 文本型
    .局部变量 CookieStr, 文本型

    域名 ＝ 网页_取域名 (网址)
    CookieStr ＝ Cookie内容
    .如果 (Cookie名称 ≠ “”)
        .如果真 (Cookie内容 ＝ “”)
            CookieStr ＝ “delete”
            有效期 ＝ 指定时间 (1999, 1, 1, 1, 1, 1)
        .如果真结束
        .如果真 (有效期 ≠ 指定时间 (1899, 12, 30, 0, 0, 0))
            GMT时间 ＝ 时间_转为GMT格式 (有效期)
            CookieStr ＝ CookieStr ＋ “; expires=” ＋ GMT时间
        .如果真结束
        CookieStr ＝ CookieStr ＋ “; path=/”
        InternetSetCookieA (“http://” ＋ 域名, Cookie名称, CookieStr)
        CookieStr ＝ CookieStr ＋ “; domain=.” ＋ 网页_取域名 (域名, 真, )
        返回 (InternetSetCookieA (“http://” ＋ 域名, Cookie名称, CookieStr))
    .否则
        现有Cookie ＝ 网页_取Cookie (“http://” ＋ 域名)
        .如果真 (现有Cookie ≠ “”)
            整数容器 ＝ 寻找文本 (现有Cookie, “=”, 1, 真)
            .如果真 (整数容器 ＞ 0)
                名称 ＝ 取文本左边 (现有Cookie, 整数容器 － 1)
                网页_置Cookie (“http://” ＋ 域名, 名称)
                网页_置Cookie (“http://” ＋ 域名)
            .如果真结束
            
        .如果真结束
        返回 (真)
    .如果结束
    

.子程序 网页_置Cookie1, , 公开
    .参数 网站域名, 文本型
    .参数 Cookie值, 文本型, , 整个Cookie值
    .参数 有效期, 日期时间型, 可空, 可空。默认为2088年8月8日8时
    .局部变量 局_数组, 文本型, , "0"
    .局部变量 i, 整数型
    .局部变量 局_C名称, 文本型
    .局部变量 局_C内容, 文本型
    .局部变量 局_位置, 整数型

    .如果真 (是否为空 (有效期))
        有效期 ＝ [2088年8月8日8时]
    .如果真结束
    局_数组 ＝ 分割文本 (Cookie值, “;”, )
    .计次循环首 (取数组成员数 (局_数组), i)
        局_位置 ＝ 寻找文本 (局_数组 [i], “=”, , 假)
        .如果真 (局_位置 ＝ -1)
            到循环尾 ()
        .如果真结束
        局_C名称 ＝ 删首尾空 (取文本左边 (局_数组 [i], 局_位置 － 1))
        局_C内容 ＝ 删首尾空 (取文本右边 (局_数组 [i], 取文本长度 (局_数组 [i]) － 局_位置))
        网页_置Cookie (网站域名, 局_C名称, 局_C内容, 有效期)
    .计次循环尾 ()

.子程序 网页_取Cookie, 文本型, 公开, 取出指定域名在本机的cookie
    .参数 域名, 文本型, , 只接受域名
    .参数 Cookie名称, 文本型, 可空, 为空返回全部Cookie
    .局部变量 网址, 文本型
    .局部变量 读取内容, 文本型
    .局部变量 读取内容1, 文本型

    网址 ＝ “http://” ＋ 域名
    连续赋值 (取空白文本 (3000), 读取内容, 读取内容1)
    InternetGetCookieA (网址, 字符 (0), 读取内容, 3000)
    InternetGetCookieExA (网址, 字符 (0), 读取内容1, 3000, 8192, 0)
    读取内容 ＝ 网页_Cookie合并更新 (读取内容, 读取内容1)
    .如果真 (Cookie名称 ≠ “”)
        读取内容 ＝ 网页_取单条Cookie (读取内容, Cookie名称)
    .如果真结束
    返回 (读取内容)
    

.子程序 网页_取Cookie2, 文本型, 公开, [Mr.Yang]
    .参数 域名, 文本型, , 欲取出cookie域名 例:<a href="http://baidu.com或baidu.com" target="_blank">http://baidu.com或baidu.com</a>
    .参数 Path, 文本型, 可空, 取出存放在path的cookie
    .参数 Cookie名称, 文本型, 可空, 为空返回全部Cookie
    .局部变量 _域名, 文本型
    .局部变量 _Path, 文本型
    .局部变量 读取内容, 文本型
    .局部变量 读取内容1, 文本型

    .如果 (寻找文本 (域名, “://”, , 假) ＝ -1)
        _域名 ＝ “http://” ＋ 域名
    .否则
        _域名 ＝ 域名
    .如果结束
    
    .如果真 (取文本右边 (_域名, 1) ＝ “/”)
        _域名 ＝ 文本_删右边 (_域名, 1)
    .如果真结束
    .如果真 (Path ≠ “”)
        _Path ＝ Path
        .如果真 (取文本左边 (_Path, 1) ≠ “/”)
            _Path ＝ “/” ＋ _Path
        .如果真结束
        .如果真 (取文本右边 (_Path, 1) ≠ “/”)
            _Path ＝ _Path ＋ “/”
        .如果真结束
        _域名 ＝ _域名 ＋ _Path
    .如果真结束
    连续赋值 (取空白文本 (3000), 读取内容, 读取内容1)
    InternetGetCookieA (_域名, 字符 (0), 读取内容, 3000)
    InternetGetCookieExA (_域名, 字符 (0), 读取内容1, 3000, 8192, 0)
    网页_Cookie合并更新 (读取内容, 读取内容1)
    .如果真 (Cookie名称 ≠ “”)
        读取内容 ＝ 网页_取单条Cookie (读取内容, Cookie名称)
    .如果真结束
    返回 (读取内容)

.子程序 网页_取单条Cookie, 文本型, 公开, 从左往右获取
    .参数 Cookie, 文本型
    .参数 名称, 文本型
    .参数 不附带名称, 逻辑型, 可空
    .局部变量 t, 文本型

    Cookie ＝ 删首尾空 (Cookie)
    .如果真 (取文本右边 (Cookie, 1) ≠ “;”)
        Cookie ＝ Cookie ＋ “; ”
    .如果真结束
    t ＝ 取单条Cookie (Cookie, 名称)
    .判断开始 (不附带名称 或 t ＝ “”)
        返回 (t)
    .默认
        返回 (名称 ＋ “=” ＋ t)
    .判断结束
    

.子程序 取单条Cookie, 文本型
    .参数 原Cookies, 文本型
    .参数 单条Cookie名称, 文本型
    .局部变量 Cookies, 文本型, , "0"
    .局部变量 i, 整数型
    .局部变量 Name, 文本型

    Cookies ＝ 分割文本 (原Cookies, “;”, )
    .计次循环首 (取数组成员数 (Cookies), i)
        Name ＝ 文本_取左边 (删首尾空 (Cookies [i]), “=”, , )
        .如果真 (到小写 (Name) ＝ 到小写 (单条Cookie名称))
            .如果真 (寻找文本 (删首尾空 (Cookies [i]), “:”, , 假) ≠ -1)
                返回 (文本_取出中间文本 (删首尾空 (Cookies [i]), “=”, “:”, , ))
            .如果真结束
            返回 (文本_取右边 (删首尾空 (Cookies [i]), “=”, -1, ))
        .如果真结束
        
    .计次循环尾 ()
    返回 (“”)

.子程序 网页_清除Cookie, , 公开, 清除本机IE里的cookie
    .参数 域名, 文本型, , 只接受域名
    .局部变量 Cookie值1, 文本型
    .局部变量 Cookie值2, 文本型
    .局部变量 Cookie, 文本型, , "0"
    .局部变量 计次, 整数型
    .局部变量 最终Cookie, 文本型
    .局部变量 Cookie名, 文本型

    Cookie值1 ＝ “deleted; expires=Fri, 1-Jan-1999 1:1:1 GMT; path=/;”
    Cookie值2 ＝ Cookie值1 ＋ “ domain=” ＋ 网页_取域名 (域名, 真)
    最终Cookie ＝ 网页_取Cookie (域名)
    Cookie ＝ 分割文本 (最终Cookie, “;”, )
    .计次循环首 (取数组成员数 (Cookie), 计次)
        Cookie名 ＝ 内部_Cookie取名 (Cookie [计次])
        InternetSetCookieA (“http://” ＋ 域名, Cookie名, Cookie值1)
        InternetSetCookieA (“http://” ＋ 域名, Cookie名, Cookie值2)
    .计次循环尾 ()

.子程序 网页_清除Cookie文件, 逻辑型, 公开, 【此命令由小磊提供】，删除本地IE缓存的cookie文件
    .参数 关键字, 文本型, 可空, 输入Cookie文件的关键字即可,空则清除全部
    .参数 Cookie路径, 文本型, 参考 可空, 传递Cookie路径
    .局部变量 文件名, 文本型
    .局部变量 已删除, 逻辑型

    Cookie路径 ＝ 取文本注册项 (3, “Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders\Cookies”, )
    文件名 ＝ 寻找文件 (Cookie路径 ＋ “\*.txt”, )
    .判断循环首 (文件名 ≠ “”)
        .如果真 (寻找文本 (文件名, 关键字, 1, 假) ≠ -1 或 关键字 ＝ “”)
            .如果真 (删除文件 (Cookie路径 ＋ “\” ＋ 文件名))
                已删除 ＝ 真
            .如果真结束
            
        .如果真结束
        文件名 ＝ 寻找文件 (, )
    .判断循环尾 ()
    返回 (已删除)

.子程序 网页_删除IE缓存文件, 逻辑型, 公开, 【此命令由小磊提供】，删除本地IE的缓存文件
    .参数 缓存文件路径, 文本型, 参考 可空, 传递缓存文件路径
    .局部变量 文件名, 文本型
    .局部变量 已删除, 逻辑型

    缓存文件路径 ＝ 取文本注册项 (3, “Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders\Cache”, )
    文件名 ＝ 寻找文件 (缓存文件路径 ＋ “\*.*”, )
    .判断循环首 (文件名 ≠ “”)
        .如果真 (删除文件 (缓存文件路径 ＋ “\” ＋ 文件名))
            已删除 ＝ 真
        .如果真结束
        文件名 ＝ 寻找文件 (, )
    .判断循环尾 ()
    返回 (已删除)

.子程序 网页_保存, , 公开, 保存网页
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 258)

.子程序 网页_打印, , 公开, 打印
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 260) ' #define ID_IE_FILE_PRINT 260

.子程序 网页_全选, , 公开, 全选
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 31)

.子程序 网页_打印预览, , 公开, 打印预览
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 93)

.子程序 网页_查找关键字, , , 查找关键字
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 67)

.子程序 网页_保存html文档, , 公开, 保存html文档
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 71)

.子程序 网页_查看网页属性, , 公开, 查看网页属性
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 28)

.子程序 网页_设置网页字体, , 公开, 设置网页字体
    .参数 浏览器句柄, 整数型

    网页_发送命令 (浏览器句柄, 0, 90)

.子程序 网页_发送命令, , , 发送IE命令
    .参数 窗口句柄, 整数型, , 网页框句柄
    .参数 命令类型, 整数型, , 命令类型
    .参数 命令ID, 整数型, , 命令ID
    .局部变量 hwndSDV, 整数型, , , Shell DocObject View
    .局部变量 hwndIES, 整数型, , , Internet Explorer_Server

    窗口_查找网页句柄 (窗口句柄, hwndSDV, hwndIES)
    .如果真 (hwndSDV ≠ 0 且 hwndIES ≠ 0)
        .判断开始 (命令类型 ＝ 1)
            发送消息整数 (hwndIES, 273, 命令ID, 0)
        .默认
            发送消息整数 (hwndSDV, 273, 命令ID, 0)
        .判断结束
        
    .如果真结束
    

.子程序 窗口_查找网页句柄, , , 取网页框(WebBrowser)的子窗口句柄
    .参数 Hwnd, 整数型
    .参数 hwndSDV, 整数型, 参考, Shell DocObject View
    .参数 hwndIES, 整数型, 参考, Internet Explorer_Server
    .局部变量 窗口句柄, 整数型
    .局部变量 窗口类名, 文本型

    窗口句柄 ＝ FindWindowExA (Hwnd, 0, 0, 0)
    .判断循环首 (窗口句柄 ≠ 0)
        窗口类名 ＝ 窗口_取类名J (窗口句柄)
        .如果 (窗口类名 ＝ “Shell DocObject View”)
            hwndSDV ＝ 窗口句柄
            跳出循环 ()
        .否则
            窗口_查找网页句柄 (窗口句柄, hwndSDV, hwndIES)
        .如果结束
        窗口句柄 ＝ FindWindowExA (Hwnd, 窗口句柄, 0, 0)
    .判断循环尾 ()
    .如果真 (hwndSDV ≠ 0)
        窗口类名 ＝ 窗口_取类名J (窗口句柄)
        .如果真 (窗口类名 ＝ “Shell DocObject View”)
            hwndIES ＝ FindWindowExA (hwndSDV, 0, 0, 0)
        .如果真结束
        
    .如果真结束
    

.子程序 网页_取网址引用, 文本型, 公开, 返回指定网址的引用信息
    .参数 网址, 文本型
    .局部变量 pwszUrl1, 字节集
    .局部变量 urlComp, URL_COMPONENTS
    .局部变量 bool, 逻辑型
    .局部变量 lpszScheme, 文本型
    .局部变量 lpszHostName, 文本型
    .局部变量 Agreement, 文本型

    urlComp.dwStructSize ＝ 60
    urlComp.dwSchemeLength ＝ -1
    urlComp.dwHostNameLength ＝ -1
    urlComp.dwUrlPathLength ＝ -1
    urlComp.dwExtraInfoLength ＝ -1
    
    Agreement ＝ 文本_取左边 (网址, “//”)
    .如果真 (Agreement ＝ “mms:”)
        网址 ＝ 子文本替换 (网址, Agreement, “http:”, , , 真)
    .如果真结束
    pwszUrl1 ＝ 编码_Ansi到Unicode (网址)
    bool ＝ WinHttpCrackUrl (pwszUrl1, 取字节集长度 (pwszUrl1), 0, urlComp)
    .如果真 (bool)
        lpszScheme ＝ 编码_Unicode到Ansi (指针到字节集 (urlComp.lpszScheme, urlComp.dwSchemeLength × 2))
        lpszHostName ＝ 编码_Unicode到Ansi (指针到字节集 (urlComp.lpszHostName, urlComp.dwHostNameLength × 2))
        .如果真 (Agreement ＝ “mms:”)
            返回 (Agreement ＋ “//” ＋ lpszHostName)
        .如果真结束
        返回 (lpszScheme ＋ “://” ＋ lpszHostName)
    .如果真结束
    返回 (“”)

.子程序 网页_取页面地址, 文本型, 公开, 返回指定网址的页面地址
    .参数 网址, 文本型
    .局部变量 页面地址, 文本型
    .局部变量 符号位置, 整数型
    .局部变量 域名, 文本型

    域名 ＝ 网页_取域名 (网址)
    符号位置 ＝ 寻找文本 (网址, “/”, 寻找文本 (网址, 域名, 1, 真), 真)
    .如果 (符号位置 ＞ 0)
        页面地址 ＝ 取文本右边 (网址, 取文本长度 (网址) － 符号位置 ＋ 1)
    .否则
        页面地址 ＝ “/”
    .如果结束
    返回 (页面地址)

.子程序 网页_取网址文件名, 文本型, 公开, 取一个网站地址中的文件名
    .参数 网址, 文本型
    .局部变量 分隔块, 文本型, , "0"
    .局部变量 块数, 整数型
    .局部变量 文件名, 文本型

    分隔块 ＝ 分割文本 (网址, “/”, )
    块数 ＝ 取数组成员数 (分隔块)
    .如果真 (块数 ＝ 0)
        返回 (“”)
    .如果真结束
    返回 (分隔块 [块数])

.子程序 网页_取网络文件名, 文本型, 公开, 取特殊网络文件名，如{35419830/13139680144000128.mp3?xcode=91d07544}，取出文件名为“我可以抱你吗.mp3”
    .参数 网址, 文本型
    .局部变量 name, 文本型
    .局部变量 hOpen, 整数型
    .局部变量 hOpenUrl
    .局部变量 Length
    .局部变量 bool, 逻辑型
    .局部变量 sBuffer, 文本型

    hOpen ＝ InternetOpenA (“”, 0, 字符 (0), 字符 (0), 0)
    .如果真 (hOpen ≠ 0)
        hOpenUrl ＝ InternetOpenUrlA (hOpen, 网址, 字符 (0), 0, 2147483648, 0)
        .如果真 (hOpenUrl ≠ 0)
            bool ＝ HttpQueryInfoA (hOpenUrl, 22, “”, Length, 0) ' #HTTP_QUERY_RAW_HEADERS_CRLF
            sBuffer ＝ 取空白文本 (Length)
            bool ＝ HttpQueryInfoA (hOpenUrl, 22, sBuffer, Length, 0)
            .如果真 (bool)
                name ＝ 文本_取出中间文本 (sBuffer, “filename=” ＋ #引号, #引号)
            .如果真结束
            InternetCloseHandle (hOpenUrl)
        .如果真结束
        InternetCloseHandle (hOpen)
    .如果真结束
    返回 (name)

.子程序 网页_是否为网页, 逻辑型, 公开, 判断输入的地址是否为URL地址。
    .参数 网页地址, 文本型

    返回 (PathIsURL (网页地址))

.子程序 网页_还原网页颜色, 文本型, 公开, 转换颜色值到网页颜色的代码
    .参数 颜色值, 整数型
    .局部变量 结果文本, 文本型

    结果文本 ＝ 取十六进制文本 (颜色值)
    结果文本 ＝ 取文本中间 (结果文本, 5, 2) ＋ 取文本中间 (结果文本, 3, 2) ＋ 取文本左边 (结果文本, 2)
    .如果 (取文本长度 (结果文本) ＜ 6)
        结果文本 ＝ 取文本左边 (“#” ＋ 结果文本 ＋ “000000”, 7)
    .否则
        结果文本 ＝ “#” ＋ 结果文本
    .如果结束
    返回 (结果文本)

.子程序 网页_转换为HTML色, 文本型, 公开, 将RGB色转换为HTML色
    .参数 RGB颜色值, 整数型
    .局部变量 HTML颜色值, 文本型
    .局部变量 字节集, 字节集
    .局部变量 局_文本, 文本型

    字节集 ＝ 到字节集 (RGB颜色值)
    
    局_文本 ＝ 取十六进制文本 (字节集 [1])
    HTML颜色值 ＝ HTML颜色值 ＋ 选择 (取文本长度 (局_文本) ＝ 1, 局_文本 ＋ “0”, 局_文本)
    
    局_文本 ＝ 取十六进制文本 (字节集 [3])
    HTML颜色值 ＝ HTML颜色值 ＋ 选择 (取文本长度 (局_文本) ＝ 1, 局_文本 ＋ “0”, 局_文本)
    
    局_文本 ＝ 取十六进制文本 (字节集 [2])
    HTML颜色值 ＝ HTML颜色值 ＋ 选择 (取文本长度 (局_文本) ＝ 1, 局_文本 ＋ “0”, 局_文本)
    返回 (HTML颜色值)

.子程序 网页_取十进制颜色值, 整数型, 公开, 把十六进制的网页颜色值转换成十进制的颜色值    
    .参数 十六进制的颜色值, 文本型, , 234A97
    .局部变量 字节集, 字节集
    .局部变量 str, 文本型

    str ＝ 子文本替换 (十六进制的颜色值, “#”, “”, , , 真)
    字节集 ＝ 取空白字节集 (4)
    字节集 [1] ＝ 进制_十六到十 (取文本中间 (str, 1, 2))
    字节集 [2] ＝ 进制_十六到十 (取文本中间 (str, 3, 2))
    字节集 [3] ＝ 进制_十六到十 (取文本中间 (str, 5, 2))
    返回 (取字节集数据 (字节集, #整数型, ))

.子程序 网页_打开指定网址, , 公开, 使用系统默认的浏览器打开一个网址
    .参数 网页地址, 文本型, , 要打开的网页地址

    ShellExecuteA (0, “open”, 网页地址, “”, “”, 1)

.子程序 网页_创建快捷方式, 逻辑型, 公开, 创建网页快捷方式
    .参数 快捷方式文件名, 文本型, , 扩展名为.url全路径
    .参数 网页地址, 文本型
    .参数 ico图标, 文本型, 可空
    .参数 快捷键, 文本型, 可空, 如"CTRL+SHIFT+F"
    .局部变量 Wshom, 对象
    .局部变量 oUrlLink, 对象

    CoInitialize (0)
    .如果真 (Wshom.创建 (“{F935DC22-1CF0-11d0-ADB9-00C04FD58A0B}”, ))
        快捷方式文件名 ＝ 选择 (文件_取扩展名 (快捷方式文件名) ≠ “.url”, 快捷方式文件名 ＋ “.url”, 快捷方式文件名)
        oUrlLink ＝ Wshom.对象型方法 (“CreateShortcut”, 快捷方式文件名)
        oUrlLink.写属性 (“TargetPath”, 网页地址)
        oUrlLink.写属性 (“WindowStyle”, 2)
        oUrlLink.写属性 (“IconLocation”, ico图标)
        写配置项 (快捷方式文件名, “InternetShortcut”, “IconFile”, ico图标)
        写配置项 (快捷方式文件名, “InternetShortcut”, “IconIndex”, “0”)
        oUrlLink.写属性 (“Hotkey”, 快捷键)
        oUrlLink.数值方法 (“Save”, )
        Wshom.清除 ()
    .如果真结束
    CoUninitialize ()
    返回 (文件_是否存在 (快捷方式文件名))

.子程序 网页_取快捷方式地址, 文本型, 公开, 取网页快捷方式的地址；
    .参数 文件路径, 文本型

    返回 (读配置项 (文件路径, “InternetShortcut”, “URL”, ))

.子程序 网页_网络文件是否存在, 逻辑型, 公开, 判断网络上的文件是否存在，存在返回真，否则返回假；
    .参数 网络文件名, 文本型
    .局部变量 IXMLHTTP, 对象
    .局部变量 局_返回, 逻辑型

    CoInitialize (0)
    IXMLHTTP.创建 (“msxml2.XMLHTTP”, )
    IXMLHTTP.方法 (“open”, “HEAD”, 网络文件名, 假)
    IXMLHTTP.方法 (“send”, )
    局_返回 ＝ IXMLHTTP.读数值属性 (“Status”, ) ＝ 200
    IXMLHTTP.清除 ()
    CoUninitialize ()
    返回 (局_返回)

.子程序 网页_开始屏蔽信息框, 逻辑型, 公开, 屏蔽掉电脑上把有页页信息框，成功开始屏蔽返回真，失败返回假；
    .如果真 (集_屏蔽信息框线程句柄 ＝ 0)
        集_屏蔽信息框线程句柄 ＝ 线程_启动J (&内部_屏蔽网页信息框)
        返回 (真)
    .如果真结束
    .如果真 (集_屏蔽信息框线程句柄 ≠ 0)
        输出调试文本 (“已经开始了屏蔽网页信息框，请不要重复使用！”)
    .如果真结束
    返回 (假)

.子程序 网页_停止屏蔽信息框, , 公开, 停止屏蔽掉电脑上把有页页信息框
    线程_销毁 (集_屏蔽信息框线程句柄)
    线程_关闭句柄 (集_屏蔽信息框线程句柄)
    集_屏蔽信息框线程句柄 ＝ 0

.子程序 内部_屏蔽网页信息框, 整数型
    .参数 整数, 整数型
    .局部变量 窗口句柄, 整数型
    .局部变量 窗口标题, 文本型
    .局部变量 静态, 整数型
    .局部变量 子窗口数组, 整数型, , "0"
    .局部变量 i, 整数型

    静态 ＝ 1
    .判断循环首 (静态 ＝ 1) ' 构造死循环.又不让编译器不通过
        .计次循环首 (窗口_枚举所有子窗口J (, 子窗口数组, “#32770”), i)
            窗口标题 ＝ 窗口_取标题J (子窗口数组 [i])
            .如果真 (窗口标题 ＝ “来自网页的消息”) ' WIN7是IE9+提示这个
                发送消息整数 (子窗口数组 [i], 16, 0, 0)
            .如果真结束
            .如果真 (窗口标题 ＝ “Microsoft Internet Explorer”) ' ie6
                发送消息整数 (子窗口数组 [i], 16, 0, 0)
            .如果真结束
            
        .计次循环尾 ()
        延时 (500)
    .判断循环尾 ()
    返回 (0)

.子程序 网页_自动点击网页信息框, , 公开, 自动点击网页上弹出的消息框
    .参数 窗口句柄, 整数型, , 程序本身的窗口句柄,直接用 取窗口句柄() 命令即可
    .参数 网页信息框的标题, 文本型, 可空, 可空，网页信息框的标题,默认为【Microsoft Internet Explorer】
    .参数 按钮的标题, 文本型, 可空, 可空，消息框上要点击的按钮标题,默认为【确定】
    .参数 停止, 逻辑型, 可空, 可空，如果将此参数设置为真，将停止自动点击信息框
    .参数 监控间隔, 整数型, 可空, 可空，默认为300毫秒。

    .如果真 (停止)
        时钟_销毁 (窗口句柄, 100)
        返回 ()
    .如果真结束
    
    .如果真 (是否为空 (监控间隔) 或 监控间隔 ＜ 50)
        监控间隔 ＝ 300
    .如果真结束
    .如果真 (是否为空 (网页信息框的标题))
        网页信息框的标题 ＝ “Microsoft Internet Explorer”
    .如果真结束
    .如果真 (是否为空 (按钮的标题))
        按钮的标题 ＝ “确定”
    .如果真结束
    集_按钮的标题 ＝ 网页信息框的标题 ＋ “|” ＋ 按钮的标题
    时钟_创建 (窗口句柄, 100, 监控间隔, &开始自动点击网页信息框)

.子程序 开始自动点击网页信息框
    .局部变量 窗口句柄, 整数型
    .局部变量 子窗口句柄, 整数型

    窗口句柄 ＝ 窗口_取句柄J (, , “#32770”, 文本_取左边 (集_按钮的标题, “|”))
    子窗口句柄 ＝ 窗口_取句柄J (窗口句柄, , “Button”, 文本_取右边 (集_按钮的标题, “|”))
    .如果真 (子窗口句柄 ＞ 0)
        鼠标_消息 (子窗口句柄, 5, 5)
    .如果真结束
    

.子程序 网页_自动点击网页信息框_带返回值J, 逻辑型, 公开, 成功返回真，失败返回假。
    .参数 网页信息框的标题, 文本型, 可空, 网页信息框的标题,默认为【Microsoft Internet Explorer】
    .参数 按钮的标题, 文本型, 可空, 消息框上要点击的按钮标题,默认为【确定】
    .参数 超时返回, 整数型, 可空, 可空，单位为毫秒，默认为5000毫秒。
    .参数 监控间隔, 整数型, 可空, 可空，默认为300毫秒。
    .局部变量 窗口句柄, 整数型
    .局部变量 子窗口句柄, 整数型
    .局部变量 局_超时, 整数型
    .局部变量 局_线程句柄, 整数型
    .局部变量 局_返回值, 整数型
    .局部变量 局_时间, 整数型

    
    
    .如果真 (是否为空 (网页信息框的标题))
        网页信息框的标题 ＝ “Microsoft Internet Explorer”
    .如果真结束
    .如果真 (是否为空 (按钮的标题))
        按钮的标题 ＝ “确定”
    .如果真结束
    .如果真 (是否为空 (超时返回) 或 超时返回 ＜ 1000)
        超时返回 ＝ 5000
    .如果真结束
    .如果真 (是否为空 (监控间隔) 或 监控间隔 ＜ 50)
        监控间隔 ＝ 300
    .如果真结束
    
    集_按钮的标题 ＝ 网页信息框的标题 ＋ “|” ＋ 按钮的标题
    局_线程句柄 ＝ 线程_启动J (&开始自动点击网页信息框_带返回值J, 监控间隔)
    局_时间 ＝ 取启动时间 ()
    
    .循环判断首 ()
        局_返回值 ＝ MsgWaitForMultipleObjects (1, 局_线程句柄, 假, 超时返回, 1215)
        .如果真 (取启动时间 () － 局_时间 ≥ 超时返回)
            局_返回值 ＝ 258
        .如果真结束
        程序_延时 (300)
    .循环判断尾 (局_返回值 ≠ 0 且 局_返回值 ≠ 258) ' 258=已超时,0=线程正常结束
    .如果真 (局_返回值 ＝ 258) ' 线程_销毁 (局_线程句柄)
        线程_销毁 (局_线程句柄)
        CloseHandle (局_线程句柄)
    .如果真结束
    返回 (局_返回值 ≠ 258) ' 如果不是超时,则为真
    

.子程序 开始自动点击网页信息框_带返回值J
    .参数 监控间隔, 整数型
    .局部变量 窗口句柄, 整数型
    .局部变量 子窗口句柄, 整数型

    .判断循环首 (真)
        窗口句柄 ＝ 窗口_取句柄J (, , “#32770”, 文本_取左边 (集_按钮的标题, “|”))
        子窗口句柄 ＝ 窗口_取句柄J (窗口句柄, , “Button”, 文本_取右边 (集_按钮的标题, “|”))
        .如果真 (子窗口句柄 ＞ 0)
            鼠标_消息 (子窗口句柄, 5, 5)
            鼠标_消息 (子窗口句柄, 5, 5)
            返回 ()
        .如果真结束
        程序_延时 (监控间隔)
    .判断循环尾 ()

.子程序 立即刷新注册表
    发送消息整数 (GetDesktopWindow (), 26, 0, 0)

.子程序 网页_屏蔽网页复制提示, , 公开, 目前只测试了IE8
    .参数 是否屏蔽, 逻辑型, 可空, 默认为真，允许网页复制，假为不允许。
    .局部变量 注册表, 注册表操作

    .如果 (是否为空 (是否屏蔽) 或 是否屏蔽 ＝ 真)
        注册表.写DWORD值 (“HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Internet Settings\Zones\3”, “1407”, 0)
    .否则
        注册表.写DWORD值 (“HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Internet Settings\Zones\3”, “1407”, 1)
    .如果结束
    立即刷新注册表 ()

.子程序 网页_禁止允许一键操作, , 公开, 一键操作所有设置（写注册项，影响全局，不仅仅是软件的自带浏览器）
    .参数 禁止允许gif图片, 逻辑型, 可空, 禁止或允许 播放GIF动画图片，可以加快网页的显示速度。默认为假，允许。
    .参数 禁止允许显示图片, 逻辑型, 可空, 禁止或允许 显示网页中的图片，可以加快网页的显示速度。默认为假，允许。
    .参数 禁止允许背景声音, 逻辑型, 可空, 禁止或允许 播放网页背景声音，如：mid、mp3等等。默认为假，允许。
    .参数 禁止允许点击声音, 逻辑型, 可空, 禁止或允许 在点击网页链接或网页跳转时,会有一个点击的声音,用此命令可以禁止或恢复它。默认为假，允许。
    .参数 禁止允许播放网页视频, 逻辑型, 可空, 禁止或允许 播放网页中的视频，可以加快网页的显示速度。默认为假，允许。

    网页_禁止允许gif图片 (禁止允许gif图片)
    网页_禁止允许显示图片 (禁止允许显示图片)
    网页_禁止允许背景声音 (禁止允许背景声音)
    网页_禁止允许点击声音 (禁止允许点击声音)
    网页_禁止允许播放网页视频 (禁止允许播放网页视频)

.子程序 网页_禁止允许gif图片, 逻辑型, 公开, 禁止或允许 播放GIF动画图片，可以加快网页的显示速度。默认为假，允许。（写注册项，影响全局，不仅仅是软件的自带浏览器）
    .参数 禁止或允许, 逻辑型, 可空, 可空：默认为假 真=禁止 假=允许
    .局部变量 局_文本, 文本型

    局_文本 ＝ 选择 (禁止或允许, “no”, “yes”)
    禁止或允许 ＝ 写注册项 (#现行用户, “Software\Microsoft\Internet Explorer\Main\Play_Animations”, 局_文本)
    立即刷新注册表 ()
    返回 (禁止或允许)

.子程序 网页_禁止允许背景声音, 逻辑型, 公开, 禁止或允许 播放网页背景声音，如：mid、mp3等等。默认为假，允许。（写注册项，影响全局，不仅仅是软件的自带浏览器）
    .参数 禁止或允许, 逻辑型, 可空, 可空：默认为假 真=禁止 假=允许
    .局部变量 局_文本, 文本型

    局_文本 ＝ 选择 (禁止或允许, “no”, “yes”)
    禁止或允许 ＝ 写注册项 (#现行用户, “Software\Microsoft\Internet Explorer\Main\Play_Background_Sounds”, 局_文本)
    立即刷新注册表 ()
    返回 (禁止或允许)

.子程序 网页_禁止允许显示图片, 逻辑型, 公开, 禁止或允许 显示网页中的图片，可以加快网页的显示速度。默认为假，允许。（写注册项，影响全局，不仅仅是软件的自带浏览器）
    .参数 禁止或允许, 逻辑型, 可空, 可空：默认为假 真=禁止 假=允许
    .局部变量 局_文本, 文本型

    局_文本 ＝ 选择 (禁止或允许, “no”, “yes”)
    禁止或允许 ＝ 写注册项 (#现行用户, “Software\Microsoft\Internet Explorer\Main\Display Inline Images”, 局_文本)
    立即刷新注册表 ()
    返回 (禁止或允许)

.子程序 网页_禁止允许点击声音, 逻辑型, 公开, 禁止或允许 在点击网页链接或网页跳转时,会有一个点击的声音,用此命令可以禁止或恢复它。默认为假，允许。（写注册项，影响全局，不仅仅是软件的自带浏览器）
    .参数 禁止或允许, 逻辑型, 可空, 可空：默认为假 真=禁止 假=允许
    .局部变量 局_声音路径, 文本型
    .局部变量 局_REG路径, 文本型
    .局部变量 注册表, 注册表操作

    局_REG路径 ＝ “HKEY_CURRENT_USER\AppEvents\Schemes\Apps\Explorer\Navigating\.Current”
    局_声音路径 ＝ 到文本 (注册表.取字节集 (局_REG路径, “”, 到数值 (文本_取右边 (注册表.取键名类型 (局_REG路径), “：”))))
    .如果真 (删首尾空 (局_声音路径) ＝ “”)
        返回 (假)
    .如果真结束
    .判断开始 (禁止或允许)
        .如果真 (取文本右边 (局_声音路径, 1) ＝ “1”)
            返回 (真)
        .如果真结束
        返回 (注册表.写字节集 (局_REG路径, “”, 到字节集 (局_声音路径 ＋ “1”), 到数值 (文本_取右边 (注册表.取键名类型 (局_REG路径), “：”))))
    .判断 (禁止或允许 ＝ 假)
        .如果真 (取文本右边 (局_声音路径, 1) ≠ “1”)
            返回 (真)
        .如果真结束
        局_声音路径 ＝ 取文本左边 (局_声音路径, 取文本长度 (局_声音路径) － 1)
        返回 (注册表.写字节集 (局_REG路径, “”, 到字节集 (局_声音路径), 到数值 (文本_取右边 (注册表.取键名类型 (局_REG路径), “：”))))
    .默认
        
    .判断结束
    返回 (假)

.子程序 网页_禁止允许播放网页视频, 逻辑型, , 禁止或允许 播放网页中的视频，可以加快网页的显示速度。默认为假，允许。（写注册项，影响全局，不仅仅是软件的自带浏览器）
    .参数 禁止或允许, 逻辑型, 可空, 可空：默认为假 真=禁止 假=允许
    .局部变量 局_文本, 文本型

    局_文本 ＝ 选择 (禁止或允许, “no”, “yes”)
    禁止或允许 ＝ 写注册项 (#现行用户, “Software\Microsoft\Internet Explorer\Main\Display Inline Videos”, 局_文本)
    立即刷新注册表 ()
    返回 (禁止或允许)

.子程序 网页_取编码, 文本型, 公开, 用于取出指定网页地址的编码
    .参数 网页地址, 文本型
    .局部变量 局_网页内容, 文本型
    .局部变量 局_正则, 正则表达式类
    .局部变量 局_编码类型, 文本型

    局_网页内容 ＝ 到文本 (网页_访问J (网页地址))
    .如果真 (取文本长度 (局_网页内容) ＞ 0)
        .如果真 (局_正则.创建 (“<meta.+?charset=[^\w]?([-\w]+)”, 取文本左边 (局_网页内容, 1024)))
            局_编码类型 ＝ 局_正则.取子匹配文本 (1, 1)
        .如果真结束
        返回 (局_编码类型)
    .如果真结束
    返回 (“”)

.子程序 网页_取网络文件尺寸, 长整数型, 公开
    .参数 欲取网络文件地址, 文本型
    .局部变量 hOpen
    .局部变量 hOpenUrl
    .局部变量 sbuffer, 文本型
    .局部变量 Length, 整数型

    hOpen ＝ InternetOpenA (“”, 0, 字符 (0), 字符 (0), 0)
    .如果真 (hOpen ≠ 0)
        hOpenUrl ＝ InternetOpenUrlA (hOpen, 欲取网络文件地址, 字符 (0), 0, 2147483648, 0)
        .如果真 (hOpenUrl ≠ 0)
            HttpQueryInfoA (hOpenUrl, 5, “”, Length, 0)
            sbuffer ＝ 取空白文本 (Length)
            HttpQueryInfoA (hOpenUrl, 5, sbuffer, Length, 0)
            InternetCloseHandle (hOpenUrl)
        .如果真结束
        InternetCloseHandle (hOpen)
    .如果真结束
    返回 (到长整数 (sbuffer))

.子程序 网络_取网络文件大小_对象版, 长整数型, 公开
    .参数 url, 文本型
    .参数 代理IP, 文本型, 可空
    .局部变量 局_http, 对象
    .局部变量 局_结果, 变体型
    .局部变量 局_返回, 长整数型

    ' 该例程收录自：https://bbs.125.la/thread-14380851-1-1.html
    CoInitialize (0)
    .如果 (局_http.创建 (“WinHttp.WinHttpRequest.5.1”, ))
        局_http.方法 (“SetTimeouts”, 5000, 5000, 10000, 5000)
        局_http.方法 (“open”, “HEAD”, url, 假)
        局_http.方法 (“setRequestHeader”, “User-Agent”, “Mozilla/4.0 (compatible; MSIE 6.0; Windows NT 5.1; SV1)”)
        局_http.方法 (“setRequestHeader”, “Accept-Language”, “zh-cn”)
        局_http.方法 (“setRequestHeader”, “Connection”, “Keep-Alive”)
        .如果真 (是否为空 (代理IP) ＝ 假)
            局_http.方法 (“SetProxy”, 代理IP)
        .如果真结束
        局_http.方法 (“send”, )
        局_结果 ＝ 局_http.读属性 (“status”, )
        .如果 (到整数 (局_结果.取数值 ()) ＝ 200)
            局_结果 ＝ 局_http.读属性 (“GetResponseHeader”, “Content-Length”)
            局_返回 ＝ 到长整数 (局_结果.取数值 ())
            CoUninitialize ()
            返回 (局_返回)
        .否则
            CoUninitialize ()
            返回 (-1)
        .如果结束
        
    .否则
        CoUninitialize ()
        返回 (-2)
    .如果结束
    

.子程序 网址_取所有参数, 整数型, 公开, 取出指定网址中所有的键名和键值，返回取到的数量。
    .参数 参_网址, 文本型
    .参数 参_名称, 文本型, 参考 可空, 返回名称；如：http://www.baidu.com/nihao?id=123456&name=hello，其中nihao就是名称
    .参数 参_键名数组, 文本型, 参考 可空 数组
    .参数 参_键值数组, 文本型, 参考 可空 数组
    .局部变量 局_临时url, 文本型
    .局部变量 局_最后位置, 整数型
    .局部变量 局_参数文本, 文本型
    .局部变量 局_返回位置1, 整数型
    .局部变量 局_起始位置1, 整数型
    .局部变量 局_返回位置2, 整数型
    .局部变量 局_参数名, 文本型
    .局部变量 局_返回位置3, 整数型
    .局部变量 局_参数值, 文本型

    ' https://bbs.125.la/thread-14496162-1-1.html
    
    局_临时url ＝ 参_网址
    
    局_最后位置 ＝ 寻找文本 (局_临时url, “?”, , 假) ' 针对url中参数部分包含url，会导致解析最后一个url，所以要从第一个?位置开始寻找
    
    .判断开始 (局_最后位置 ＝ -1)
        局_最后位置 ＝ 倒找文本 (局_临时url, “/”, , 假)
    .默认
        局_最后位置 ＝ 倒找文本 (局_临时url, “/”, 局_最后位置, 假) ' 通常url参数部分都是从最后一个/符号开始的
    .判断结束
    
    局_参数文本 ＝ 取文本右边 (局_临时url, 取文本长度 (局_临时url) － 局_最后位置)
    
    局_返回位置1 ＝ 寻找文本 (局_参数文本, “?”, , 假)
    .判断开始 (局_返回位置1 ≠ -1)
        参_名称 ＝ 取文本左边 (局_参数文本, 局_返回位置1 － 1)
    .默认
        参_名称 ＝ “”
    .判断结束
    
    ' 替换掉第一次出现的?符号，部分url第一个参数是用?号开始的
    局_参数文本 ＝ 子文本替换 (局_参数文本, “?”, “&”, , 1, 真)
    清除数组 (参_键名数组)
    清除数组 (参_键值数组)
    .判断循环首 (真)
        
        ' 寻找参数名，&和=直接的就是参数名
        局_返回位置1 ＝ 寻找文本 (局_参数文本, “&”, 局_起始位置1, 假)
        
        .如果真 (局_返回位置1 ＝ -1)
            跳出循环 ()
        .如果真结束
        
        局_返回位置2 ＝ 寻找文本 (局_参数文本, “=”, 局_返回位置1, 假)
        .如果真 (局_返回位置2 ＝ -1)
            跳出循环 ()
        .如果真结束
        
        ' 如果寻找到&和?就取出两个符号之间的参数名
        局_参数名 ＝ 取文本中间 (局_参数文本, 局_返回位置1 ＋ 1, 局_返回位置2 － 局_返回位置1 － 1)
        加入成员 (参_键名数组, 局_参数名)
        
        ' 寻找参数值，从上一次的=号位置开始寻找&号，中间的内容就是参数值了
        局_返回位置3 ＝ 寻找文本 (局_参数文本, “&”, 局_返回位置2, 假)
        .判断开始 (局_返回位置3 ＝ -1) ' 如果找不到&符号，表示到url尾部了，取出尾部所有内容即可
            局_参数值 ＝ 取文本右边 (局_参数文本, 取文本长度 (局_参数文本) － 局_返回位置2)
        .默认
            局_参数值 ＝ 取文本中间 (局_参数文本, 局_返回位置2 ＋ 1, 局_返回位置3 － 局_返回位置2 － 1)
        .判断结束
        加入成员 (参_键值数组, 局_参数值)
        
        ' 最后一次找到的=符号的位置要赋值给起始位置
        局_起始位置1 ＝ 局_返回位置2
        
    .判断循环尾 ()
    返回 (取数组成员数 (参_键名数组))

.子程序 网页_参数Ascii排序, 文本型, 公开, 对传入的请求参数按参数名的字典序（ASCII码)排序
    .参数 请求参数, 文本型, , url键值对格式(即key1=value1&key2=value2)
    .参数 排序方式, 整数型, 可空, 0.升序排序 1.降序排序  留空默认 0.升序
    .局部变量 局_参数名, 文本型, , "0"
    .局部变量 局_字符串, 文本型
    .局部变量 len, 整数型
    .局部变量 i, 整数型

    ' https://bbs.125.la/forum.php?mod=viewthread&tid=14511499
    .如果真 (排序方式 ＜ 0 或 排序方式 ＞ 1)
        排序方式 ＝ 0
    .如果真结束
    局_参数名 ＝ 分割文本 (请求参数, “&”, )
    数组_排序 (局_参数名, 排序方式, 真)
    len ＝ 取数组成员数 (局_参数名)
    .计次循环首 (len, i)
        局_字符串 ＝ 局_字符串 ＋ 局_参数名 [i]
        .如果真 (i ＜ len)
            局_字符串 ＝ 局_字符串 ＋ “&”
        .如果真结束
        
    .计次循环尾 ()
    返回 (局_字符串)

